@LIFE_BAR
#DIM DYNAMIC 체력値

PRINTFORM 체력
체력値 = BASE:0
SIF BASE:0 < 0
	체력値 = 0
BAR 체력値,MAXBASE:0,32
PRINTFORML ({체력値}/{MAXBASE:0})

@STAIN_INFO

;主人の汚れ
PRINTFORML 【 %CALLNAME:MASTER%의 얼룩】
REPEAT 6
	SIF COUNT == 2 && HAS_PENIS(MASTER) == 0
		CONTINUE
	SIF COUNT == 3 && !(TALENT:MASTER:성별 & 1)
		CONTINUE
	SIF COUNT == 5 && !(TALENT:MASTER:성별 & 1)
		CONTINUE
	IF COUNT == 0
		PRINTFORM 　입：
	ELSEIF COUNT == 1
		PRINTFORM 　손：
	ELSEIF COUNT == 2
		PRINTFORM 　Ｐ：
	ELSEIF COUNT == 3
		PRINTFORM 　Ｖ：
	ELSEIF COUNT == 4
		PRINTFORM 　Ａ：
	ELSEIF COUNT == 5
		PRINTFORM 　Ｂ：
	ENDIF
	SIF STAIN:MASTER:COUNT & 1
		PRINTFORM <애액>
	SIF STAIN:MASTER:COUNT & 2
		PRINTFORM <페니스>
	SIF STAIN:MASTER:COUNT & 4
		PRINTFORM <정액>
	SIF STAIN:MASTER:COUNT & 8
		PRINTFORM <애널>
	SIF STAIN:MASTER:COUNT & 16
		PRINTFORM <모유>
	SIF STAIN:MASTER:COUNT & 32
		PRINTFORM <점액>
	SIF STAIN:MASTER:COUNT & 64
		PRINTFORM <파과의 혈>
	PRINTL 
REND
	IF TALENT:MASTER:성별 != 2
		PRINTFORML 질내상황：［{TCVAR:TARGET:질내사정횟수}ml］
		SELECTCASE TCVAR:MASTER:질내사정횟수
			CASE 0
				PRINTFORM 
			CASE 1 to 5 
				PRINTFORM %CALLNAME:TARGET%의 질 내에 정액이 쏟아지고 있다。
			CASE 6 to 15
				PRINTFORM %CALLNAME:TARGET%의 질 내에 다량의 정자가 꿈틀거리고 있다。
			CASE 16 to 30
				PRINTFORM %CALLNAME:TARGET%의 질 내는 정액으로 빵빵하다。
			CASE 30 to 50
				PRINTFORM %CALLNAME:TARGET%의 배는 정액으로 크게 부풀어 올랐다。
			CASE 100 to 300
				PRINTFORM %CALLNAME:TARGET%는 약간 고통스러워하며 정액으로 부풀어 오른 배를 안고 있었다。
			CASEELSE	
				PRINTFORM %CALLNAME:TARGET%의 배는、치 만삭의 임산부처럼 부풀어 올라、고통스러워 보인다。
		ENDSELECT
	PRINTL 
	ENDIF
	PRINTL 
IF TARGET > 0
	;奴隷の汚れ
	PRINTFORML 【 %CALLNAME:TARGET%의 얼룩】
	REPEAT 6
		SIF COUNT == 2 && HAS_PENIS(TARGET) == 0
			CONTINUE
		SIF COUNT == 3 && !(TALENT:2 & 1)
			CONTINUE
		SIF COUNT == 5 && !(TALENT:2 & 1)
			CONTINUE
		IF COUNT == 0
			PRINTFORM 　입：
		ELSEIF COUNT == 1
			PRINTFORM 　손：
		ELSEIF COUNT == 2
			PRINTFORM 　Ｐ：
		ELSEIF COUNT == 3
			PRINTFORM 　Ｖ：
		ELSEIF COUNT == 4
			PRINTFORM 　Ａ：
		ELSEIF COUNT == 5
			PRINTFORM 　Ｂ：
		ENDIF

		SIF STAIN:COUNT & 1
			PRINTFORM <애액>
		SIF STAIN:COUNT & 2
			PRINTFORM <페니스>
		SIF STAIN:COUNT & 4
			PRINTFORM <정액>
		SIF STAIN:COUNT & 8
			PRINTFORM <애널>
		SIF STAIN:COUNT & 16
			PRINTFORM <모유>
		SIF STAIN:COUNT & 32
			PRINTFORM <점액>
		SIF STAIN:COUNT & 64
			PRINTFORM <파과의 혈>
		PRINTL 
	REND
	IF TALENT:TARGET:2 != 2
		PRINTFORML 부어진 양 ［{TCVAR:TARGET:질내사정횟수}ml］
		PRINTFORM 질 내 상황：
		SELECTCASE TCVAR:TARGET:질내사정횟수
			CASE 0
				PRINTFORM 
			CASE 1 to 5 
				PRINTFORM %CALLNAME:TARGET%의 질 내에는 정액이 쏟아지고 있다。
			CASE 6 to 15
				PRINTFORM %CALLNAME:TARGET%의 질 내에 다량의 정자가 꿈틀거리고 있다。
			CASE 16 to 30
				PRINTFORM %CALLNAME:TARGET%의 질 내는 정액으로 빵빵하다。
			CASE 30 to 50
				PRINTFORM %CALLNAME:TARGET%의 배는 정액으로 크게 부풀어 올랐다。
			CASE 100 to 300
				PRINTFORM %CALLNAME:TARGET%는 약간 고통스러워하며 정액으로 부풀어 오른 배를 안고 있었다。
			CASEELSE	
				PRINTFORM %CALLNAME:TARGET%의 배는、마치 만삭의 임산부처럼 부풀어 올라、고통스러워 보인다。
		ENDSELECT
	PRINTL 
	ENDIF
	PRINTL 
ENDIF



@STAIN_SIDE(캐릭터_ID, 汚名称)
#DIM DYNAMIC 캐릭터_ID
#DIMS DYNAMIC 汚名称
#DIM DYNAMIC 汚箇所
#DIM DYNAMIC 汚れ표시

SELECTCASE 汚名称
CASE "口"
	汚箇所 = 0
CASE "手"
	汚箇所 = 1
CASE "Ｐ"
	汚箇所 = 2
CASE "Ｖ"
	汚箇所 = 3
CASE "Ａ"
	汚箇所 = 4
CASE "Ｂ"
	汚箇所 = 5
CASE "질"
	PRINTFORM 질［{TCVAR:TARGET:질내사정횟수,10,RIGHT}ml］%"",20%
	RETURN
ENDSELECT



PRINTFORM %汚名称%［
IF STAIN:캐릭터_ID:汚箇所 & 1
	PRINTFORM <애>
	汚れ표시 ++
ENDIF
IF STAIN:캐릭터_ID:汚箇所 & 2
	PRINTFORM <페>
	汚れ표시 ++
ENDIF
IF STAIN:캐릭터_ID:汚箇所 & 4
	PRINTFORM <정>
	汚れ표시 ++
ENDIF

IF STAIN:캐릭터_ID:汚箇所 & 8
	PRINTFORM <애>
	汚れ표시 ++
ENDIF
IF STAIN:캐릭터_ID:汚箇所 & 16
	PRINTFORM <유>
	汚れ표시 ++
ENDIF
IF STAIN:캐릭터_ID:汚箇所 & 32
	PRINTFORM <점>
	汚れ표시 ++
ENDIF
IF STAIN:캐릭터_ID:汚箇所 & 64
	PRINTFORM <혈>
	汚れ표시 ++
ENDIF

REPEAT 8 - 汚れ표시
	PRINTFORM %"",4%
REND
PRINTFORM ］
;PRINTFORM %"",4%

@SAVEINFO

PUTFORM {DAY}日目

@SHOW_EQUIP_2
;상황の표시
;조교대상には使わない、특수な着脱式の道具を추가した場合はここに추가をしてください
;（場所변경、마스터や助手が使用する道具等）

SETCOLOR C_PINK
SIF TEQUIP:28
	PRINTFORM 【비디오촬영중(테이프 잔량{TEQUIP:28})】
IF TEQUIP:야외플레이 == 2
	PRINTFORM  【야외플레이중】
ELSEIF TEQUIP:야외플레이 == 1
	PRINTFORM  【관내노출플레이중】
ENDIF
SIF TEQUIP:수치플레이 == 1
	PRINTFORM  【수치심플레이중】
SIF TEQUIP:목욕탕플레이
	PRINTFORM  【목욕탕플레이중】
SIF TEQUIP:새댁플레이
	PRINTFORM  【새댁플레이중】
SIF CFLAG:TARGET:최면상태 == 1
	PRINTFORM  【최면상태중】
RESETCOLOR
PRINTL 

@SHOW_EQUIP_1
;사용중の道具を표시する
;一度使用したら解除するまで止まらない道具や조교を
;추가した場合はここに추가をしてください
SIF TEQUIP:TARGET:클리캡
	PRINTFORM 【클리캡장착중】
SIF TEQUIP:TARGET:오나홀
	PRINTFORM 【오나홀장착중】
SIF TEQUIP:TARGET:바이브
	PRINTFORM 【바이브삽입중】
SIF TEQUIP:TARGET:애널바이브
	PRINTFORM 【A바이브삽입중】
SIF TEQUIP:TARGET:애널비즈
	PRINTFORM 【애널비즈삽입중】
SIF TEQUIP:TARGET:니플캡
	PRINTFORM 【니플캡장착】
SIF TEQUIP:TARGET:착유기
	PRINTFORM 【착유기부착삽입중】
SIF TEQUIP:TARGET:딜도
	PRINTFORM 【딜도삽입중】
SIF TEQUIP:TARGET:딜도
	PRINTFORM 【딜도삽입중】
SIF TEQUIP:TARGET:로터
	PRINTFORM 【로터삽입중】
	SIF CFLAG:TARGET:로터삽입 >= 1
		PRINTFORM ({CFLAG:TARGET:로터삽입}분)


SIF TEQUIP:TARGET:아이마스크
	PRINTFORM 【아이마스크】
SIF TEQUIP:TARGET:밧줄
	PRINTFORM 【밧줄로결박】
SIF TEQUIP:TARGET:볼개그
	PRINTFORM 【볼개그】
SIF TEQUIP:TARGET:관장
	PRINTFORM 【관장기＋애널플러그】
SIF TEQUIP:TARGET:확장벌룬
	PRINTFORM 【확장 풍선】
SIF TEQUIP:TARGET:애널전극
	PRINTFORM 【애널 전극】
SIF TEQUIP:TARGET:유방전극
	PRINTFORM 【유방전극장착중】
SIF TEQUIP:TARGET:미약
	PRINTFORM 【최음제효과발휘중】
SIF TEQUIP:TARGET:이뇨제
	PRINTFORM 【이뇨제효과발휘중】
SIF TEQUIP:TARGET:사지없음
	PRINTFORM 【사지탈착중】
SIF TEQUIP:TARGET:앞치마
	PRINTFORM 【에이프런】
SIF TEQUIP:TARGET:콘돔
	PRINTFORM 【콘돔장착중】

SIF TEQUIP:TARGET:샤워
	PRINTFORM 【샤워】
SIF TCVAR:TARGET:도게자중
	PRINTFORM 【도게자중】
SIF TEQUIP:TARGET:절정금지
	PRINTFORM 【절정금지】
SIF TEQUIP:TARGET:Ｖ섹스 != -1
	PRINTFORM 【V삽입중】
SIF TEQUIP:TARGET:Ａ섹스 != -1
	PRINTFORM 【A삽입중】

;ココより↑にTEQUIPに登録したものを書き込む


@SHOW_INFO_PALAM
#DIM DYNAMIC パラメータ種
#DIM DYNAMIC 改行カウンタ

PRINTL
FOR パラメータ種,4,100
	SIF !STRLENS(PALAMNAME:パラメータ種)
		CONTINUE
	PRINTFORM  %PALAMNAME:パラメータ種%Lv{GETPALAMLV(PALAM:パラメータ種,15),2}
	CALL PRINT_COLORBAR, (PALAM:TARGET:パラメータ種), (PALAMLV:(GETPALAMLV((PALAM:TARGET:パラメータ種), 10) + 1)), 10, UNICODE(0x2584), UNICODE(0x2584), BARCOLORSET("青"), RESULT:1
    PRINTFORM |
	改行カウンタ ++
	IF 改行カウンタ == 5
		PRINTL 
		改行カウンタ = 0
	ENDIF
NEXT
PRINTL

;-------------------------------------------------
;関数名:SORT_CFLAG
;CFLAG:ARGの昇順でRESULT:1に캐릭터番号を代入する
;ARG CFLAGの番号
;-------------------------------------------------
@SORT_CFLAG(ARG)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 캐릭터_ID_2
#DIM DYNAMIC ソート値
#DIM DYNAMIC 並び替え後

VARSET RESULT
FOR 캐릭터_ID,1,CHARANUM
	ソート値 = 0
	FOR 캐릭터_ID_2,1,CHARANUM
		SIF MATCH(RESULT,캐릭터_ID_2)
			CONTINUE
		IF CFLAG:캐릭터_ID:ARG >= ソート値
			ソート値 = CFLAG:캐릭터_ID_2:ARG
			並び替え後 = 캐릭터_ID_2
		ENDIF
	NEXT
	RESULT:캐릭터_ID = 並び替え後
NEXT

;캐릭터の태도
@CHARA_ATTITUDE
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC 판정値


FOR 캐릭터_ID,1,CHARANUM
	판정値 += TALENT:캐릭터_ID:연모 * 10
	판정値 += TALENT:캐릭터_ID:굴복됨 * 20
	판정値 += TALENT:캐릭터_ID:음란 * 30
	판정値 += MARK:캐릭터_ID:굴복각인 * 3 
	판정値 += MARK:캐릭터_ID:쾌락각인 * 3
	판정値 += MIN(ABL:캐릭터_ID:친밀 * 10,50)
	판정値 += MIN(ABL:캐릭터_ID:욕망 * 10,50) 
	판정値 += GETPALAMLV(PALAM:캐릭터_ID:욕정,5) * 5
	판정値 += GETPALAMLV(PALAM:캐릭터_ID:호의,5) * 5 
	판정値 += BASE:캐릭터_ID:무드 / 50 
	판정値 += (1000 - BASE:캐릭터_ID:이성) / 30
	;성별嗜好
	IF TALENT:캐릭터_ID:성별기호 == 1 && TALENT:MASTER:성별 == 2
		판정値 = 판정値 / 2
	ELSEIF TALENT:캐릭터_ID:성별기호 == 2 && TALENT:MASTER:성별 != 2
		판정値 = 판정値 / 2
	ELSEIF TALENT:캐릭터_ID:성별기호 == 3
		판정値 = 판정値 / 2
	ENDIF

	;기정사실
	SIF GETBIT(CFLAG:캐릭터_ID:기정사실 ,1)
		판정値 += 5
	;약점
	SIF CFLAG:캐릭터_ID:약점취득
		판정値 += 5
	;약점がなければ욕정보정
	SIF !CFLAG:캐릭터_ID:약점취득
		판정値 = 판정値 * (5000 + MIN(PALAM:캐릭터_ID:욕정,5000)) / 10000

	SELECTCASE 판정値
		CASE IS >= 150
			CFLAG:캐릭터_ID:태도 = 3
		CASE IS >= 100 + TALENT:캐릭터_ID:일선넘지않음 * 20
			CFLAG:캐릭터_ID:태도 = 2
		CASE IS >= 50 + TALENT:캐릭터_ID:일선넘지않음 * 10
			CFLAG:캐릭터_ID:태도 = 1
		CASEELSE
			CFLAG:캐릭터_ID:태도 = 0
	ENDSELECT

	SIF CFLAG:캐릭터_ID:호감도 < 500
		CFLAG:캐릭터_ID:태도 = 0
	SIF CFLAG:캐릭터_ID:극대노
		CFLAG:캐릭터_ID:태도 = 0
	SIF CFLAG:캐릭터_ID:수면
		CFLAG:캐릭터_ID:태도 = 0
NEXT

;情事の発覚
@AFFAIR_DISCLOSURE(入室캐릭터_ID, 같은방플래그)
#DIM DYNAMIC 入室캐릭터_ID
#DIM DYNAMIC 情事캐릭터_ID
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 情事終了
#DIM DYNAMIC 같은방플래그
#DIM DYNAMIC 入室플래그
;ARG 入室してきた캐릭터の番号　같은방플래그 1なら같은방、0なら覗き
VARSET LOCAL

;バレ遭遇
情事캐릭터_ID = IN_ROOM("MAX", CFLAG:MASTER:현재위치, "CFLAG", GETNUM(CFLAG, "우후후"))
IF 情事캐릭터_ID && CFLAG:情事캐릭터_ID:우후후 && 情事캐릭터_ID != 入室캐릭터_ID
	CUSTOMDRAWLINE —
	PRINTFORMW %CALLNAME:入室캐릭터_ID%에게 %CALLNAME:情事캐릭터_ID%와의 불륜을 들키고 말았다！
	IF 같은방플래그 && (TALENT:入室캐릭터_ID:연모 >= 1 || TALENT:入室캐릭터_ID:포식자 == 1)
		;この後乱入する場合は弱味関連は発生しない
	ELSEIF !GETBIT(CFLAG:入室캐릭터_ID:10 , 1) && TALENT:入室캐릭터_ID:무관심 != 1
		PRINTFORMW %CALLNAME:入室캐릭터_ID%가 약점을 잡았지만
		SETBIT CFLAG:入室캐릭터_ID:약점취득 ,1
	ELSEIF TALENT:入室캐릭터_ID:무관심 == 1
		PRINTFORMW …… %CALLNAME:入室캐릭터_ID%는 약점에는 무관심한 것 같다.……。
	ENDIF
	IF 같은방플래그
		IF TALENT:入室캐릭터_ID:연모 >= 1 || TALENT:入室캐릭터_ID:포식자 == 1
			PRINTFORMW %CALLNAME:入室캐릭터_ID%는 질세라 %CALLNAME:MASTER%에게 몸을 기대어 왔다。
			CFLAG:入室캐릭터_ID:우후후 = CFLAG:(情事캐릭터_ID):우후후
			;見た人
			CALL KOJO_MESSAGE_SEND("EVENT", 入室캐릭터_ID, 6, 情事캐릭터_ID,2)
			;見られた人（最も땅位の高いTARGETで代表）
			CALL KOJO_MESSAGE_SEND("EVENT", 情事캐릭터_ID, 7, 入室캐릭터_ID,2)
			CUSTOMDRAWLINE —
		ELSE
			;臆病だったら그대로 去る、それ以外は半々の確立くらいで
			IF TALENT:入室캐릭터_ID:배짱 == -1 || RAND:2 == 0
				PRINTFORMW %CALLNAME:入室캐릭터_ID%는 %CALLNAME:MASTER%와 눈이 마주치자 허둥지둥 그 자리를 떠났다…。
				CFLAG:入室캐릭터_ID:현재위치 = CFLAG:入室캐릭터_ID:전턴위치
				;見た人
				CALL KOJO_MESSAGE_SEND("EVENT", 入室캐릭터_ID, 6, 情事캐릭터_ID,3)
				;見られた人（最も땅位の高いTARGETで代表）
				CALL KOJO_MESSAGE_SEND("EVENT", 情事캐릭터_ID, 7, 入室캐릭터_ID,3)
				CUSTOMDRAWLINE —
			ELSE
				;見た人
				CALL KOJO_MESSAGE_SEND("EVENT", 入室캐릭터_ID, 6, 情事캐릭터_ID,1)
				;見られた人（最も땅位の高いTARGETで代表）
				CALL KOJO_MESSAGE_SEND("EVENT", 情事캐릭터_ID, 7, 入室캐릭터_ID,1)
				CUSTOMDRAWLINE —
				CALL TOUCH_SET(0,0,0,1)
				情事終了 = 1
			ENDIF
		ENDIF
	ENDIF
ENDIF

IF 情事終了
	FOR カウンタ,0,CHARANUM
		CALL RESET_SEXCON(カウンタ)
	NEXT
	RETURN 1
ENDIF


;通常遭遇
;こちらからも見える위치にいる、もしくは같은방
IF !CFLAG:入室캐릭터_ID:수면 && (같은방플래그 || CAN_MOVE(CFLAG:MASTER:현재위치, CFLAG:入室캐릭터_ID:현재위치) == 2)
	;TARGETが入ってきた
	IF CFLAG:入室캐릭터_ID:전턴위치 != CFLAG:MASTER:전턴위치
		PRINTFORMW %CALLNAME:入室캐릭터_ID%가%GETPLACENAME(CFLAG:入室캐릭터_ID:현재위치)%에게 왔다
		;외출先で遭遇した
		IF CFLAG:MASTER:현재위치 >= 自宅部屋最大値
			;데이트중
			IF CFLAG:MASTER:데이트중 == 1
				入室플래그 = 7
			ELSE
				;외출先での挨拶
				入室플래그 = 6
			ENDIF
		ELSE
			IF BATHROOM(CFLAG:MASTER:현재위치)
				IF (CFLAG:入室캐릭터_ID:호감도 >= 2500 || TALENT:入室캐릭터_ID:무지 || TALENT:入室캐릭터_ID:무관심) && RAND:10 != 0
					入室플래그 = 3
				ELSE
					IF TALENT:入室캐릭터_ID:배짱 == -1 || RAND:2 == 0
						CFLAG:入室캐릭터_ID:현재위치 = CFLAG:入室캐릭터_ID:전턴위치
						PRINTFORMW %CALLNAME:入室캐릭터_ID%는 %CALLNAME:MASTER%가 있다는 것에 놀라、욕실에서 나가버렸다。
						入室플래그 = 4
					ELSE
						CFLAG:MASTER:현재위치 = CFLAG:入室캐릭터_ID:전턴위치
						PRINTFORMW %CALLNAME:入室캐릭터_ID%는 %CALLNAME:MASTER%를 쫓아냈다。
						入室플래그 = 5
					ENDIF
				ENDIF
			ELSE
				;家での挨拶
				入室플래그 = 2
			ENDIF
		ENDIF
	;MASTERが入ってきた
	ELSEIF CFLAG:入室캐릭터_ID:전턴위치 == CFLAG:入室캐릭터_ID:현재위치
		PRINTFORMW %CALLNAME:入室캐릭터_ID%가%GETPLACENAME(CFLAG:入室캐릭터_ID:현재위치)%에\@CAN_MOVE(CFLAG:MASTER:전턴위치, CFLAG:入室캐릭터_ID:현재위치) == 2 ? 있다 # 있었다\@
		入室플래그 = 1
	ENDIF

	IF 같은방플래그 && 入室플래그
		IF CFLAG:入室캐릭터_ID:면식 == 0 && !INRANGE(入室플래그,3,5)
			CALL SYOTAIMEN(入室캐릭터_ID)
		ELSEIF CFLAG:入室캐릭터_ID:정사목격 && CFLAG:(CFLAG:入室캐릭터_ID:정사목격):현재위치 != CFLAG:入室캐릭터_ID:현재위치 && !INRANGE(入室플래그,3,5)
			PRINTFORMW %CALLNAME:入室캐릭터_ID%에게 먼저 %CALLNAME:(CFLAG:入室캐릭터_ID:정사목격)%와의 일건에 대해 추궁당했다…
			CFLAG:入室캐릭터_ID:정사목격 = 0
		ELSE
			CALL KOJO_MESSAGE_SEND("EVENT", 入室캐릭터_ID, 1, 入室플래그)
		ENDIF
		CFLAG:入室캐릭터_ID:인사했음 = 1
	ENDIF
ENDIF

;요리鮮度チェック
@CEACK_COOKING
IF TIME_PROGRESS(TCVAR:MASTER:305) > 720
	PRINTFORMW %GETDISHNAME(FLAG:20)%는 아주 아니지만 먹을 수 없다！
	PRINTL
	PRINTFORMW %CALLNAME:MASTER%는 %GETDISHNAME(FLAG:20)%를 폐기했다
	FLAG:20 = 0
	TCVAR:MASTER:305 = 0
	TCVAR:MASTER:306 = 0
ELSEIF TIME_PROGRESS(TCVAR:MASTER:305) > 360 && TCVAR:MASTER:306 < 2
	PRINTFORMW %GETDISHNAME(FLAG:20)%에서 역겨운 냄새가 나기 시작했다…
	TCVAR:MASTER:306 = 2
ELSEIF TIME_PROGRESS(TCVAR:MASTER:305) > 60 && TCVAR:MASTER:306 < 1 && FLAG:20 < 50
	PRINTFORMW %GETDISHNAME(FLAG:20)%는 식어버렸다…
	TCVAR:MASTER:306 = 1

ENDIF




@SYOTAIMEN(캐릭터_ID,シチュエーション)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC シチュエーション

SIF CFLAG:캐릭터_ID:면식
	RETURN

IF TALENT:MASTER:용모 == -1 
	PRINTFORMW %CALLNAME:캐릭터_ID%는 %CALLNAME:MASTER%의 정돈되지 않은 태도를 보고 나쁜 인상을 품었다。
	CALL COLORMESSAGE(@"개막호감도가 내려갔다",C_YELLOW,2,1)
	CFLAG:캐릭터_ID:호감도 -= 500
	;eraneko추가
	IF MAXBASE:MASTER:체력 > 9999
	PRINTFORMW %CALLNAME:캐릭터_ID%는 %CALLNAME:MASTER%의 악마 같은 근육을 보고、더 나쁜 인상을 받았다。
	CALL COLORMESSAGE(@"개막호감도가 내려갔다",C_YELLOW,2,1)
	CFLAG:캐릭터_ID:호감도 -= (MAXBASE:MASTER:체력 / 100)
	ENDIF
ENDIF

IF TALENT:MASTER:용모 == 1
	PRINTFORMW %CALLNAME:캐릭터_ID%는 %CALLNAME:MASTER%의 정돈된 모습을 보고、좋은 인상을 받았다。
	CALL COLORMESSAGE(@"호감도가 올라갔다",C_YELLOW,2,1)
	CFLAG:캐릭터_ID:호감도 += 300
	;eraneko추가
	IF MAXBASE:MASTER:체력 > 9999
	PRINTFORMW %CALLNAME:캐릭터_ID%는 %CALLNAME:MASTER%의 단련된 근육을 보고、더욱 좋은 인상을 받았다。
	CALL COLORMESSAGE(@"호감도가 올라갔다",C_YELLOW,2,1)
	CFLAG:캐릭터_ID:호감도 += (MAXBASE:MASTER:체력 / 100)
	ENDIF
ENDIF


IF TALENT:MASTER:매혹
	PRINTFORMW %CALLNAME:캐릭터_ID%는 %CALLNAME:MASTER%에게【매혹】당했다。
	CALL COLORMESSAGE(@"호감도와 친밀이 올라갔다",C_YELLOW,2,1)
	ABL:캐릭터_ID:친밀 += 1
	CFLAG:캐릭터_ID:호감도 += 300
ENDIF
IF TALENT:MASTER:수수께끼의매력
	PRINTFORMW %CALLNAME:캐릭터_ID%는 %CALLNAME:MASTER%의【수수께끼의 매력】에 가슴이 두근거리는 것을 느꼈다
	CALL COLORMESSAGE(@"호감도와 친밀이 올라갔다",C_YELLOW,2,1)
	ABL:캐릭터_ID:친밀 += 1
	CFLAG:캐릭터_ID:호감도 += 500
ENDIF
;Anonym추가
IF オンラインレート >= 100 && TALENT:캐릭터_ID:듀얼리스트 == 1
	PRINTFORMW %CALLNAME:캐릭터_ID%는 %CALLNAME:MASTER%의 높은 온라인 레이트에 동경을 품은 것 같다。
	CALL COLORMESSAGE(@"호감도가 올라갔다",C_YELLOW,2,1)
	IF INRANGE(オンラインレート,100,199)
		CFLAG:캐릭터_ID:호감도 += 100
	ELSEIF INRANGE(オンラインレート,200,299)
		CFLAG:캐릭터_ID:호감도 += 200
	ELSEIF INRANGE(オンラインレート,300,399)
		CFLAG:캐릭터_ID:호감도 += 300
	ELSEIF INRANGE(オンラインレート,400,499)
		CFLAG:캐릭터_ID:호감도 += 400
	ELSEIF オンラインレート >= 500
		CFLAG:캐릭터_ID:호감도 += 500
	ENDIF
ENDIF

IF MAXBASE:캐릭터_ID:체력 <= 500
	MAXBASE:캐릭터_ID:체력 += 1000
	BASE:캐릭터_ID:체력 += 1000
ENDIF
PRINTFORM %CALLNAME:캐릭터_ID%는 처음 만난 %CALLNAME:MASTER%에게 인사
IF !TALENT:캐릭터_ID:펫
	PRINTFORMW 했다
ELSE
	PRINTFORMW 하는 것처럼 보였다。
ENDIF
CALL KOJO_MESSAGE_SEND("ENCOUNTER",캐릭터_ID,シチュエーション)
CFLAG:캐릭터_ID:면식 = 1



@HORNY_CHECK(캐릭터_ID,バトル플래그)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC カウンタ
#DIM DYNAMIC バトル플래그

VARSET RESULT
;押し倒され拒否ならBACK
SIF オプション_押し倒され拒否 == 1
	RETURN 0

SIF CFLAG:캐릭터_ID:조르기허가 == 1
	RETURN 0

IF バトル플래그 == 1
	SIF TALENT:TARGET:반응 == -1 || TALENT:TARGET:성적흥미 == -1
		RETURN 0
	SIF TCVAR:MASTER:절정여운 == 0
		RETURN 0
	SIF TCVAR:TARGET:절정여운 != 0
		RETURN 0
	IF TALENT:캐릭터_ID:즉시타락
		IF RAND:(5 * TALENT:캐릭터_ID:즉시타락) != 0
			PRINTFORML 건방지게도 %CALLNAME:캐릭터_ID%는 공수 역전을 노리지만、실패하고 말았다。
			RETURN 0
		ENDIF
	ELSEIF TALENT:캐릭터_ID:포식자
		SIF RAND:5 == 0
			RETURN 0
	ELSE
		SIF RAND:2 == 0
			RETURN 0
	ENDIF
	RETURN 1
ENDIF

;수면中の캐릭터が같은방にいるなら拒否
FOR カウンタ, 0, GET_TARGETNUM()
	SIF CFLAG:(TARGET:カウンタ):수면 == 1
		RETURN 0
NEXT

;開けたところはNG
SIF OPENPLACE(CFLAG:MASTER:현재위치)
	RETURN 0

SIF BASE:캐릭터_ID:만족 >= 400
	RETURN 0
SIF BASE:캐릭터_ID:이성 >= 300
	RETURN 0
IF TCVAR:캐릭터_ID:친밀감 > 2
	RETURN 1
ELSEIF TCVAR:캐릭터_ID:친밀감 > 1
	;처녀でない포식자は、ムラっと来たら襲い掛かる
	IF TALENT:캐릭터_ID:포식자
		SIF TALENT:TARGET:처녀 != 0
			RETURN 1
	ENDIF
ENDIF

RETURN 0



@CHANGE_TIMEZONE
#DIM 時間帯변경

SELECTCASE TIME
	;AM4~6　早朝
	CASE 240 TO 359
		時間帯변경 = 1
	;AM6~9　朝
	CASE 360 TO 539
		時間帯변경 = 2
	;AM9~12　正午前
	CASE 540 TO 719
		時間帯변경 = 3
	;PM12~3　昼
	CASE 720 TO 899
		時間帯변경 = 4
	;PM3~6　昼下がり
	CASE 900 TO 1079
		時間帯변경 = 5
	;PM6~8　夕
	CASE 1080 TO 1199
		時間帯변경 = 6
	;PM8~11　夜
	CASE 1200 TO 1379
		時間帯변경 = 7
	;PM11~AM4　深夜
	CASE 0 TO 239, IS >= 1380
		時間帯변경 = 8
ENDSELECT

IF 時間帯변경 != TIME:2
	TIME:2 = 時間帯변경
	CALL CHANGE_WEATHER
	CALL CHARA_MOVE_OUTSIDE(時間帯변경)
	;우후후中じゃない＆＆듀얼モードじゃない
	IF 듀얼モード == 0 && CFLAG:MASTER:우후후 == 0
		IF !GROUPMATCH(GET_DAY(), "土", "日") && 時間帯변경 == 3 && オプション_自動通勤 == 1
			CALL COM465
			PRINTW 몸이……몸이！　마음대로 움직여버려！！			
			PRINTW ・・・・・・・・・・・・
			PRINTW ・・・・・・
			PRINTW ・・・・
			PRINTW ・・
			PRINTL
			IF TALENT:MASTER:연령 >= 0
				PRINTW 일이 끝났다。 돌아갈까！
				CALL RECOVER_PERMIL(MASTER,-400,"체력")
				CALL RECOVER_PERMIL(MASTER,-800,"기력")
				仕事_工数 = 8
				仕事_出席 = 2
				CFLAG:MASTER:애처도시락 = 0
				TIME += 420
			ELSE
				PRINTW 학교가 종료되었다。 돌아갈까！
				CALL RECOVER_PERMIL(MASTER,-200,"체력")
				CALL RECOVER_PERMIL(MASTER,-400,"기력")
				CFLAG:MASTER:애처도시락 = 0
				TIME += 360
			ENDIF
			RESTART
		ENDIF
	ENDIF
ENDIF

@TIME_ZONE(時間帯플래그)
#DIM DYNAMIC 時間帯플래그
#FUNCTION

;営業時間
IF 時間帯플래그 == 1
	SIF GROUPMATCH(TIME:2,3,4,5,6)
		RETURNF 1
	RETURNF 0
ENDIF

SIF GROUPMATCH(TIME:2,2,3)
	RETURNF T_朝時間

SIF GROUPMATCH(TIME:2,4,5)
	RETURNF T_昼時間

SIF GROUPMATCH(TIME:2,6)
	RETURNF T_夕時間

SIF GROUPMATCH(TIME:2,7,8,1)
	RETURNF T_夜時間



@CHANGE_WEATHER
#DIM DYNAMIC ランダム설정



;折りたたみ傘を持っておらず、外にいたら、바람引くよ
IF 천후설정 == 천후_雨天 && ITEM:접이식우산 == 0 && OPENPLACE(CFLAG:MASTER:현재위치) == 2
	IF RAND:5 == 0
		PRINTFORMW 엣취！　에？　이런……감기에 걸렸을지도……
		CFLAG:MASTER:감기플래그 = 3
	ENDIF
;豪雨は全てを破壊する
ELSEIF GROUPMATCH(천후설정,천후_豪雨,천후_吹雪) && OPENPLACE(CFLAG:MASTER:현재위치) == 2
	PRINTFORMW 엣취！　에？　이런……감기에 걸렸을지도……
	CFLAG:MASTER:감기플래그 = 5
ENDIF


ランダム설정 = RAND:100
SELECTCASE 천후설정
CASE 천후_快晴
	;20%で晴れに移行
	IF INRANGE(ランダム설정,0,29)
		천후설정 = 천후_晴れ
		PRINTW 약간 날씨가 가라앉았다。
	;20%で曇りに移行
	ELSEIF INRANGE(ランダム설정,30,40)
		천후설정 = 천후_曇り
		PRINTW 조금 흐려졌다。
	;上記確率に引っかからなかったら그대로 
	ELSE
		천후설정 = 천후_快晴
	ENDIF
CASE 천후_晴れ
	;20%で晴天に移行
	IF INRANGE(ランダム설정,0,19)
		천후설정 = 천후_快晴
		PRINTW 구름이 걷혔다。
	;10%で曇りに移行
	ELSEIF INRANGE(ランダム설정,20,39)
		천후설정 = 천후_曇り
		PRINTW 조금 흐려졌다。
	;5%で雨
	ELSEIF INRANGE(ランダム설정,95,99)
		천후설정 = 천후_雨天
		PRINTW 비가 오기 시작했다。
	;上記確率に引っかからなかったら그대로 
	ELSE
		천후설정 = 천후_晴れ
	ENDIF
CASE 천후_曇り
	;20%で晴天に移行
	IF INRANGE(ランダム설정,0,29)
		천후설정 = 천후_晴れ
		PRINTW 약간 날씨가 가라앉았다。
	;20%で雨天に移行
	ELSEIF INRANGE(ランダム설정,30,49)
		천후설정 = 천후_雨天
		PRINTW 비가 오기 시작했다！
	;20%、冬で降雪に移行
	ELSEIF INRANGE(ランダム설정,50,79) && GET_MONTH() == "冬の月"
		천후설정 = 천후_降雪
		PRINTW 눈이 내리기 시작했다！

	;上記確率に引っかからなかったら그대로 
	ELSE
		천후설정 = 천후_曇り
	ENDIF
CASE 천후_雨天
	;40%で曇りに移行
	IF INRANGE(ランダム설정,0,39)
		천후설정 = 천후_曇り
		PRINTW 비가 그치기 시작했다。
	;15%で晴れに移行
	ELSEIF INRANGE(ランダム설정,40,54)
		천후설정 = 천후_晴れ
		PRINTW 비와 함께 구름이 사라졌다。
	;20%、冬で降雪に移行
	ELSEIF INRANGE(ランダム설정,55,74) && GET_MONTH() == "冬の月"
		천후설정 = 천후_降雪
		PRINTW 눈이 내리기 시작했다！
	;5%で豪雨に移行
	ELSEIF INRANGE(ランダム설정,95,99)
		천후설정 = 천후_豪雨
		PRINTW 비가 억수같이 내린다！！
	;上記確率に引っかからなかったら그대로 
	ELSE
		천후설정 = 천후_雨天
	ENDIF

CASE 천후_豪雨
	;20%で雨に
	IF INRANGE(ランダム설정,0,19)
		천후설정 = 천후_雨天
		PRINTW 비가 조금 진정되었다
	;10%で曇りに
	ELSEIF INRANGE(ランダム설정,20,39)
		천후설정 = 천후_曇り
		PRINTW 비가 그쳤다。
	;上記確率に引っかからなかったら그대로 
	ELSE
		천후설정 = 천후_豪雨
	ENDIF
CASE 천후_降雪
	;50%で曇りに移行
	IF INRANGE(ランダム설정,0,49)
		천후설정 = 천후_曇り
		PRINTW 눈이 그치기 시작했다。
	;50%で曇りに移行
	ELSEIF INRANGE(ランダム설정,50,74)
		천후설정 = 천후_晴れ
		PRINTW 눈과 함께 구름이 사라졌다。
	;上記確率に引っかからなかったら그대로 
	ELSE
		천후설정 = 천후_雨天
	ENDIF

CASE 천후_吹雪
ENDSELECT



@CHARA_MOVE_OUTSIDE(時間帯변경)
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC バイト開始
#DIM DYNAMIC バイト終了
#DIM DYNAMIC 時間帯변경
#DIM DYNAMIC 개시시간

IF GROUPMATCH(GET_DAY(), "土", "日")
	개시시간 = 3
ELSE
	개시시간 = 5
ENDIF

IF RAND:3 == 0
	IF 訪問캐릭터_ID >= 0 && INRANGE(時間帯변경,개시시간,6) && CFLAG:MASTER:현재위치 <= 自宅部屋最大値 && CFLAG:訪問캐릭터_ID:현재위치 >= 自宅部屋最大値
		PRINTFORMW %CALLNAME:訪問캐릭터_ID%가 방문한 것 같다。
		IF CFLAG:MASTER:우후후 == 0
			CALL KOJO_MESSAGE_SEND("EVENT", 訪問캐릭터_ID, 11, 5)
			PRINTFORMW 들여보낼까요？
			PRINTL [0] - 예
			PRINTL [1] - 아니오
			BINPUT 
			IF RESULT == 0
				PRINTFORMW 일단 들어가서 천천히 쉬기로 했다。
				CALL KOJO_MESSAGE_SEND("EVENT", 訪問캐릭터_ID, 11, 5, 1)
				CFLAG:訪問캐릭터_ID:현재위치 = CFLAG:MASTER:현재위치
			ELSE
				PRINTFORMW 지금은 잠시 손을 뗄 수 없다…라고 말하고 오늘은 돌아갔다。
				CALL KOJO_MESSAGE_SEND("EVENT", 訪問캐릭터_ID, 11, 5, 2)
			ENDIF
		ELSE
			PRINTFORMW 몇번이나 초인종을 눌러도 답장이 돌아오지 않기 때문에、
			IF RAND:3 == 0
				CFLAG:訪問캐릭터_ID:현재위치 = 場所_ダイニング
				PRINTFORMW 멋대로 집에 들어가 버렸다……
				CALL KOJO_MESSAGE_SEND("EVENT", 訪問캐릭터_ID, 11, 5, 4)
			ELSE
				PRINTFORMW 오늘은 돌아온 것 같다。
				CALL KOJO_MESSAGE_SEND("EVENT", 訪問캐릭터_ID, 11, 5, 3)
			ENDIF
		ENDIF
		ARRAYREMOVE 訪問캐릭터_ID, 0, 1
	ENDIF
ENDIF

FOR 캐릭터_ID,1,CHARANUM
	SIF 캐릭터_ID == MASTER
		CONTINUE
	;현재위치が自宅な人は動かない
	SIF CFLAG:캐릭터_ID:현재위치 <= 自宅部屋最大値
		CONTINUE
	;현재위치が100なら動かない
	SIF CFLAG:캐릭터_ID:현재위치 == 場所_VOID
		CONTINUE
	;데이트중の子は動かない
	SIF CFLAG:캐릭터_ID:데이트중 == 1
		CONTINUE


	;알바중の子は動かない
	IF CFLAG:캐릭터_ID:아르바이트 >= 1
		IF CFLAG:캐릭터_ID:알바중 == 1
			CONTINUE
		ENDIF
	ENDIF
	;현재위치がプレイヤーと同じである場合
	IF CFLAG:캐릭터_ID:현재위치 == CFLAG:MASTER:현재위치
		;호감도1500以上は動かない
		IF CFLAG:캐릭터_ID:호감도 >= 1500
			CONTINUE
		ENDIF
	ENDIF

;―――ここから이동が開始する―――――――――――――――――――――――――――――――――――――――――
	;現在が平日で、위치が学校なら、終了までは動かない
	IF !GROUPMATCH(GET_DAY(), "土", "日")
		IF TIME < 900
			SIF CFLAG:캐릭터_ID:현재위치 == 場所_学校内
				CONTINUE
		ELSE
			IF CFLAG:캐릭터_ID:현재위치 == 場所_学校内
				IF RAND:4 == 0
					CFLAG:캐릭터_ID:현재위치 = 場所_VOID
				ELSEIF RAND:3 == 0
						CFLAG:캐릭터_ID:현재위치 = RAND:15 + 50
				ELSEIF RAND:2 == 0
					CFLAG:캐릭터_ID:현재위치 = RAND:10 + 110
				ELSE
					CFLAG:캐릭터_ID:현재위치 = 場所_学校内
				ENDIF
			ENDIF
		ENDIF
	ENDIF
	;終了は上の条件で行うので、それでもいる子は多分部活とかかな
	SIF CFLAG:캐릭터_ID:현재위치 == 場所_学校内
		CONTINUE
	;後は適当にランダム이동したりのこったり
	IF RAND:3 == 0
		CFLAG:캐릭터_ID:현재위치 = CFLAG:캐릭터_ID:현재위치 + 1
	ELSEIF RAND:2 == 0
		CFLAG:캐릭터_ID:현재위치 = CFLAG:캐릭터_ID:현재위치 - 1
	ELSE
		CFLAG:캐릭터_ID:현재위치 = CFLAG:캐릭터_ID:현재위치
	ENDIF

	;変な場所に行った人はVOID行き
	SIF GETPLACENAME(CFLAG:캐릭터_ID:현재위치) == ""
		CFLAG:캐릭터_ID:현재위치 = 場所_VOID
NEXT

;帰宅の処理とかいろいろ
;やりたいこと、데이트や家に遊びに来テイル場合でも、時間になったら帰るよう로 한다。
FOR 캐릭터_ID,1,CHARANUM
	;MASTER以外
	SIF 캐릭터_ID == MASTER
		CONTINUE
	;MASTER의家に住んでる人は대상外
	SIF CFLAG:캐릭터_ID:시작위치 <= 自宅部屋最大値
		CONTINUE
	CALL CHARA_MOVEMENT_GOHOME_TEST(캐릭터_ID)
	IF RESULT == 1
		;현재위치が一緒/こちらの家にいる場合
		IF CFLAG:캐릭터_ID:현재위치 == CFLAG:MASTER:시작위치 && CFLAG:캐릭터_ID:현재위치 <= 20
			CALL KOJO_MESSAGE_SEND("EVENT", 캐릭터_ID, 11, 6, 1)
			PRINTFORMW %CALLNAME:캐릭터_ID%는 작별 인사를 나누고、귀가했다。
		ENDIF
		CFLAG:캐릭터_ID:현재위치 = 場所_VOID
	ENDIF
NEXT
