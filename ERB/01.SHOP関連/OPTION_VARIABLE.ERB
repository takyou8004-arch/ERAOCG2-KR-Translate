
@HAN_KOJO_OPTION(캐릭터_ID)
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタ2
#DIM DYNAMIC INT_R = 192
#DIM DYNAMIC INT_G = 192
#DIM DYNAMIC INT_B = 192
#DIM DYNAMIC DEFAULT
#DIM DYNAMIC 대상
#DIM DYNAMIC 選択肢
#DIM 표시サイズ
#DIM 服装타입

표시サイズ = 20
SIF CFLAG:캐릭터_ID:문자색설정 == 0
	CFLAG:캐릭터_ID:문자색설정 = COLOR_FROMRGB(INT_R, INT_G, INT_B)

CUSTOMDRAWLINE ―
PRINTL 
PRINTL ＞＞어떤 구상을 설정하시겠습니까？
PRINTL 
CUSTOMDRAWLINE ―


PRINTL ―범용구상―
FOR カウンタ,1,20
	VARSET RESULT
	TRYCALLFORM M_HNYO_K{カウンタ}()
	IF RESULT == 1
		SIF TALENT:캐릭터_ID:구상성격 == カウンタ
			SETCOLOR C_GREEN
		PRINTFORM 　[{カウンタ,2}] - %RESULTS,25,LEFT%
		RESETCOLOR
	ELSE
		CONTINUE
	ENDIF
	SIF カウンタ % 4 == 0
		PRINTL
NEXT
PRINTL 
PRINTL 
PRINTL 　[ 0] - 설정없음（구상설정을취소）
CUSTOMDRAWLINE ―
PRINTFORM 　
PRINTBUTTON "[일인칭]", 1500
PRINTFORM 　：%CSTR:캐릭터_ID:SELFCALL,30,LEFT%　　
PRINTBUTTON "[플레이어호칭]", 1501
PRINTFORM 　：%CSTR:캐릭터_ID:MASCALL,30,LEFT%

PRINTL 
CUSTOMDRAWLINE ―

PRINT 　
PRINTBUTTON "[자동 신음]", 50
IF CFLAG:캐릭터_ID:자동신음 == 0
	PRINTFORM (없음)　
ELSE
	PRINTFORM (있음)　
ENDIF

PRINTL
CUSTOMDRAWLINE ―

GDISPOSE 1000
GDISPOSE 1001
GDISPOSE 1002
GDISPOSE 1003

REDRAW 0



PRINTL □샘플색
GCREATE 1003, 256, 17
GCLEAR 1003, 0xFF000000+COLOR_FROMRGB(INT_R, INT_G, INT_B)
SPRITECREATE "SAMPLEBAR", 1003

PRINT 　[
PRINT_IMG "SAMPLEBAR"
PRINT ]
PRINTL

GCREATE 1000, 256, 17
FOR カウンタ, 0, 256
	FOR カウンタ2, 0, 17
		IF INT_R == カウンタ
			GSETCOLOR 1000, 0xFF000000+COLOR_FROMRGB(255, 255, 255), カウンタ, カウンタ2
		ELSE
			GSETCOLOR 1000, 0xFF000000+COLOR_FROMRGB(カウンタ, 0, 0), カウンタ, カウンタ2
		ENDIF
	NEXT
NEXT
SPRITECREATE "RBAR", 1000

GCREATE 1001, 256, 17
FOR カウンタ, 0, 256
	FOR カウンタ2, 0, 17
		IF INT_G == カウンタ
			GSETCOLOR 1001, 0xFF000000+COLOR_FROMRGB(255, 255, 255), カウンタ, カウンタ2
		ELSE
			GSETCOLOR 1001, 0xFF000000+COLOR_FROMRGB(0, カウンタ, 0), カウンタ, カウンタ2
		ENDIF
	NEXT
NEXT
SPRITECREATE "GBAR", 1001

GCREATE 1002, 256, 17
FOR カウンタ, 0, 256
	FOR カウンタ2, 0, 17
		IF INT_B == カウンタ
			GSETCOLOR 1002, 0xFF000000+COLOR_FROMRGB(255, 255, 255), カウンタ, カウンタ2
		ELSE
			GSETCOLOR 1002, 0xFF000000+COLOR_FROMRGB(0, 0, カウンタ), カウンタ, カウンタ2
		ENDIF
	NEXT
NEXT
SPRITECREATE "BBAR", 1002

PRINTBUTTON "[-E]", 101
PRINTBUTTON "[E]", 100
PRINT_IMG "RBAR"
PRINTBUTTON "[R]", 150
PRINTBUTTON "[+R]", 151

PRINTBUTTON @" R値:{INT_R,3} [변경]", 120
PRINTL 

PRINTBUTTON "[-F]", 201
PRINTBUTTON "[F]", 200
PRINT_IMG "GBAR"
PRINTBUTTON "[G]", 250
PRINTBUTTON "[+G]", 251
PRINTBUTTON @" G値:{INT_G,3} [변경]", 220
PRINTL 

PRINTBUTTON "[-V]", 301
PRINTBUTTON "[V]", 300
PRINT_IMG "BBAR"
PRINTBUTTON "[B]", 350
PRINTBUTTON "[+B]", 351
PRINTBUTTON @" B値:{INT_B,3} [변경]", 320
PRINTL 
CUSTOMDRAWLINE ―


SETCOLOR 0xFF000000 + COLOR_FROMRGB(INT_R, INT_G, INT_B)
PRINTPLAIN 　샘플문자　「샘플문자」　
PRINT [8888] [샘플문자]　
PRINTL
RESETCOLOR
CUSTOMDRAWLINE ―
PRINTBUTTON "[기본색]　", 888
PRINTBUTTON "[랜덤색]　", 999
PRINTBUTTON "[샘플 대사 출력]　", 777

SETCOLOR C_GREEN
PRINTBUTTON "[문자색 결정]", 1000
RESETCOLOR
PRINTL 
CUSTOMDRAWLINE ―


PRINTL [9999] - 뒤로
ONEINPUT
;BINPUT
選択肢 = RESULT
SELECTCASE 選択肢
	CASE 9999
		GDISPOSE 0
		GDISPOSE 1000
		GDISPOSE 1001
		GDISPOSE 1002
		GDISPOSE 1003
		RETURN 0
	CASE 0 to 20
		IF 選択肢 != 0
			VARSET RESULT
			TRYCALLFORM M_KOJO_K{NO:캐릭터_ID}
			IF RESULT == 1 && CFLAG:캐릭터_ID:개인구상무효 == 0
				PRINTFORML %CALLNAME:캐릭터_ID%에는 이미 개인 구상이 있습니다。
				PRINTFORML 개인 구상을 OFF로 하고 일반 구상을 사용합니까？
				PRINTL [0] - 예
				PRINTL [1] - 아니오
				BINPUT 
				SIF RESULT == 1
					RESTART
				CFLAG:캐릭터_ID:개인구상무효 = 1
			ENDIF
		ELSEIF 選択肢 == 0
			IF CFLAG:캐릭터_ID:개인구상무효 == 1
				PRINTFORML %CALLNAME:캐릭터_ID%의 범용구상은 무효로 되어 있습니다
				PRINTFORML 다시 사용하시겠습니까？
				PRINTL [0] - 예
				PRINTL [1] - 아니오
				BINPUT 
				SIF RESULT == 1
					RESTART
				CFLAG:캐릭터_ID:개인구상무효 = 0
			ENDIF
		ENDIF
		TALENT:캐릭터_ID:구상성격 = 選択肢
		CSTR:캐릭터_ID:SELFCALL =
		CSTR:캐릭터_ID:MASCALL =
		TRYCALLFORM M_HNYO_SETTINGS_K{TALENT:캐릭터_ID:구상성격}(캐릭터_ID)
	CASE 50
		CFLAG:캐릭터_ID:자동신음 ^= 1
	CASE 1000
		CFLAG:캐릭터_ID:문자색설정 = COLOR_FROMRGB(INT_R, INT_G, INT_B)
	CASE 100
		INT_R --
	CASE 150
		INT_R ++
	CASE 200
		INT_G --
	CASE 250
		INT_G ++
	CASE 300
		INT_B --
	CASE 350
		INT_B ++
	CASE 101
		INT_R -= 5
	CASE 151
		INT_R += 5
	CASE 201
		INT_G -= 5
	CASE 251
		INT_G += 5
	CASE 301
		INT_B -= 5
	CASE 351
		INT_B += 5
	CASE 120, 220, 320
		PRINTL 수치를 입력해주세요（0～255）
		INPUT
		SIF RESULT >= 255
			RESULT = 255
		SIF RESULT <= 0
			RESULT = 0
		SELECTCASE 選択肢
			CASE 120
				INT_R = RESULT
			CASE 220
				INT_G = RESULT
			CASE 320
				INT_B = RESULT
		ENDSELECT
	CASE 777
		CUSTOMDRAWLINE ―
		SETCOLOR CFLAG:캐릭터_ID:문자색설정
		TRYCALLFORM M_HNYO_MESSAGE_COM_K{TALENT:캐릭터_ID:구상성격}_300_1
		RESETCOLOR
		CUSTOMDRAWLINE ―	


	CASE 888
		INT_R = 192
		INT_G = 192
		INT_B = 192
	CASE 999
		INT_R = RAND:256
		INT_G = RAND:256
		INT_B = RAND:256
	CASE 1500
		PRINTFORM 현재의 1인칭：%CSTR:캐릭터_ID:SELFCALL%      
		PRINTBUTTON "[변경하지않는다]",""
		PRINTL
		INPUTS
		IF RESULTS != ""
			SUBSTRING RESULTS, 0, 20
			CSTR:캐릭터_ID:SELFCALL = %RESULTS%
		ENDIF	
	CASE 1501
		PRINTL "（플레이어이름）○○"등으로하시겠습니까？（상/군/선배 등등）
		PRINTL [0] - 예 
		PRINTL [1] - 아니오
		BINPUT
		IF RESULT == 0
			PRINTFORM 현재의호칭：%CSTR:캐릭터_ID:MASCALL%
			PRINTBUTTON "[변경하지않는다]",""
			PRINTL
			INPUTS
			IF RESULTS != ""
				SUBSTRING RESULTS, 0, 20
				CSTR:캐릭터_ID:MASCALL = %CALLNAME:MASTER%%RESULTS%
			ENDIF	
		ELSE
			PRINTFORM 현재의호칭：%CSTR:캐릭터_ID:MASCALL%      
			PRINTBUTTON "[변경하지않는다]",""
			PRINTL
			INPUTS
			IF RESULTS != ""
				SUBSTRING RESULTS, 0, 20
				CSTR:캐릭터_ID:MASCALL = %RESULTS%
			ENDIF	
		ENDIF
ENDSELECT
INT_R = LIMIT(INT_R, 0, 255)
INT_G = LIMIT(INT_G, 0, 255)
INT_B = LIMIT(INT_B, 0, 255)

RESTART






@OPTION_KOJO_DEBUG
#DIM DYNAMIC 選択コマンド
#DIM DYNAMIC 캐릭터_ID
#DIM DYNAMIC カウンタ

캐릭터_ID = 1
TARGET = 1
$INPUT_LOOP
CUSTOMDRAWLINE ―
PRINTL 　＜구상디버깅＞
CUSTOMDRAWLINE ―


FOR カウンタ,0,500
	SIF INRANGE(カウンタ,200,230) || GROUPMATCH(カウンタ,490,491)
		CONTINUE
	IF カウンタ == 0
		SETCOLOR C_YELLOW
		PRINTL 　――조교계커맨드――――――――
	ELSEIF カウンタ == 100
		SETCOLOR C_YELLOW
		SIF カウンタ % 6 == 0
			PRINTL
		PRINTL 
		PRINTL 　――조교계커맨드(하드)――――
	ELSEIF カウンタ == 300
		SETCOLOR C_YELLOW
		SIF カウンタ % 6 == 0
			PRINTL
		PRINTL 
		PRINTL 　――일상계커맨드A（회화등）――
	ELSEIF カウンタ == 400
		SETCOLOR C_YELLOW
		SIF カウンタ % 6 == 0
			PRINTL
		PRINTL 
		PRINTL 　――일상계커맨드B（이동등）――
	ENDIF
	RESETCOLOR

;	TRYCALLFORM M_KOJO_MESSAGE_COM_K{NO:캐릭터_ID}_{カウンタ}_1

	SIF STRLENS(TRAINNAME:カウンタ)
		PRINTFORMC %TRAINNAME:カウンタ%[{カウンタ,3}]

	RESETCOLOR
NEXT
PRINTL
CALL COLOR_LINE(C_ROYAL_BLUE)
PRINTFORML 캐릭터ID：
RESETCOLOR
PRINTBUTTON @"　＞[8888] - [%NAME:캐릭터_ID%]",8888
PRINTFORML 
CALL COLOR_LINE(C_ROYAL_BLUE)
PRINTFORML
PRINTL 　[9999] - 뒤로

BINPUT
PRINTFORML RESULT:{RESULT}
選択コマンド = RESULT
SELECTCASE 選択コマンド
CASE 9999
	RETURN 0
CASE 8888
	CALL MONSTER_LIST(31)
	SIF RESULT == -1
		RESTART
	캐릭터_ID = RESULT
	TARGET = RESULT
CASE 0 TO 500
	CUSTOMDRAWLINE ―
;	SETCOLOR CFLAG:캐릭터_ID:문자색설정
	TRYCALLFORM M_KOJO_MESSAGE_COM_K{NO:캐릭터_ID}_{選択コマンド}_1
;	RESETCOLOR
	CUSTOMDRAWLINE ―
ENDSELECT
GOTO INPUT_LOOP