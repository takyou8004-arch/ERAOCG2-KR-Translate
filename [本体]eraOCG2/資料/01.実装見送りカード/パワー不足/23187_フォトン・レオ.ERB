@CARDNAME_23187,参照先
;ココで指定カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = フォトン・レオ
	CASE "略称"
		RESULTS = Ｐｔレオ
	CASE "カテゴリ"
		RESULTS:0 = フォトン
ENDSELECT


@CARD_23187(参照先)
#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN 効果モン
	CASE "属性"
		RETURN 光属性
	CASE "種族"
		RETURN 獣族
	CASE "レベル"
		RETURN 6
	CASE "攻撃力"
		RETURN 2100
	CASE "守備力"
		RETURN 1100
ENDSELECT

RETURN 0

@CARD_EXPLANATION_23187(種類)
#DIM DYNAMIC 種類

CALLFORM CARD_ABLE(0,手札起動)
PRINTL ①：自分フィールドの 몬스터 １장을 リリースして発動できる。
PRINTL このカードを手札から特殊召喚する。
CALLFORM CARD_ABLE(0,召喚時 ,特殊召喚時)
PRINTL ②：このカードが召喚・特殊召喚した場合に発動できる。
PRINTL 手札があるプレイヤーは、その手札を全てデッキに戻す。
PRINTL その後、それぞれ自身が戻した枚数分デッキからドローする。

@CARDCAN_23187(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,23187)
SIF RESULT == 1
	RETURN 0


IF 種類 == 手札起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL CARD_SS_CHECK(決闘者,23187,"手札",1)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	効果範囲戦場 = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 種類 == 召喚時 || 種類 == 特殊召喚時
	CALL FULLRESET
	効果範囲手札 = 1
	効果範囲相手手札 = 1
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_23187(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC ドロー枚数

CALL 対面者判定(決闘者)
対面者 = %RESULTS%


IF 種類 == 手札起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,23187)
	IF RESULT == 0
		CALL FULLRESET
		効果範囲戦場 = 1
		CALL CARD_ADD_GY_BASIC(決闘者,"戦場",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(決闘者, "手札", 場所)
		ENDIF
	ENDIF
ELSEIF 種類 == 召喚時 || 種類 == 特殊召喚時
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,23187)
	IF RESULT == 0
		FOR カウンタ,0,紙束最大値
			SIF GETVAR(@"%決闘者%_手札:カウンタ") == -1
				CONTINUE
			CALL CARD_DISPOSE(決闘者,"手札",カウンタ,デッキ行き)
			ドロー枚数 ++
		NEXT
		CALL CARDDRAW_BASIC(決闘者,ドロー枚数)
		VARSET ドロー枚数
		FOR カウンタ,0,紙束最大値
			SIF GETVAR(@"%対面者%_手札:カウンタ") == -1
				CONTINUE
			CALL CARD_DISPOSE(対面者,"手札",カウンタ,デッキ行き)
			ドロー枚数 ++
		NEXT
		CALL CARDDRAW_BASIC(対面者,ドロー枚数)
	ENDIF
ENDIF

