@CARDNAME_77510,参照先
;ココで指定卡片の名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 惑乱魔狩猎园
	CASE "略称"
		RESULTS = 惑乱魔狩猎园
	CASE "カテゴリ"
		RESULTS:0 = 惑乱魔
		RESULTS:1 = 惑乱魔狩猎园
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_77510(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN フィールド
	CASE "唯一存在"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_77510(種類)
#DIM DYNAMIC 種類

CALL TEXT_DECORATION("ROGUE")
PRINTL このカード名の①②③の効果はそれぞれ1ターンに1度しか使用できない。
PRINTL 自分フィールドは「惑乱魔狩猎园」となる。
CALL CARD_ABLE(1,手札起動)
PRINTL ①：このカードの発動時の効果処理として。
PRINTL デッキから「惑乱魔」카드 １枚を手札に加える。
CALLFORM CARD_ABLE(2,魔罠起動)
PRINTL ②：自分メインフェイズに発動できる,自分·相手フィールドの「蜜罐」１枚を対象として発動できる
PRINTL そのカードを墓地へ送る,自分の墓地の「惑乱魔」モンスターを１장選び、そのモンスターを特殊召喚する。
CALLFORM CARD_ABLE(3,魔罠起動)
PRINTL ③：相手戦闘阶段,相手魔法＆罠ゾに「蜜罐」ーが存在する場合に発動できる。
PRINTL 相手はその中から攻撃表示を1枚選んで特殊召喚する,この効果で特殊召喚したモンスターの効果は無効化され,召喚の素材にもできない,攻撃力と守備力が0になる。
CALL TEXT_DECORATION("ROGUE")


@CARDCAN_77510(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,77510)
SIF RESULT == 1
	RETURN 0

IF 種類 == 魔罠起動 ;&& GETVAR(@"%決闘者%_魔_表:場所") == 1 || 種類 == 戦場起動 || 種類 == フリー誘発
	IF メインフェイズ == 1
		SIF ターンプレイヤー != 決闘者
			RETURN 0
		SIF 発動済_2(決闘者,77510)
			RETURN 0
		SIF 発動済_3(決闘者,77510)
			RETURN 0
		SIF ゾーン != "魔罠"
			RETURN 0
		SIF モンスター数(決闘者) >= 5
			RETURN 0

		CALL FULLRESET
		効果範囲戦場 = 1
		効果範囲相手戦場 = 1
		効果範囲魔罠 = 1
		効果範囲相手魔罠 = 1
		効果条件カテゴリ = 蜜罐
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	
	ELSEIF バトルフェイズ == 1
		SIF ターンプレイヤー == 決闘者
			RETURN 0
		SIF 発動済_2(決闘者,77510)
			RETURN 0
		SIF 発動済_3(決闘者,77510)
			RETURN 0
		SIF ゾーン != "魔罠"
			RETURN 0
		SIF モンスター数(対面者) >= 5
			RETURN 0

		CALL FULLRESET
		効果範囲相手魔罠 = 1
		効果条件カテゴリ = 蜜罐
		効果条件種類 = モン全般
		CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ENDIF
	RETURN 1

ELSEIF 種類 == 魔罠起動 || 種類 == 手札起動
	IF 種類 == 魔罠起動
		SIF GETVAR(@"%決闘者%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF 発動済_1(決闘者,77510)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 決闘者
		RETURN 0
	CALL FULLRESET
	効果範囲魔罠 = 1
	効果条件表示 = 表側表示
	効果条件名前 = 惑乱魔狩猎园
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1

ELSEIF 種類 == 永続効果
	SIF ゾーン != "魔罠"
		RETURN 0
	SIF GETVAR(@"%決闘者%_魔_表:場所") != 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_77510(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 选择卡片
#DIM DYNAMIC 条件達成
#DIM DYNAMIC 調教対象 = -1
#DIM DYNAMIC 调教强度
#DIM DYNAMIC 戦場单体

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 魔罠起動 && GETVAR(@"%決闘者%_魔_表:場所") == 1 || 種類 == 戦場起動 || 種類 == フリー誘発
	IF メインフェイズ == 1
		CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,77510,2)
		IF RESULT == 0
			CALL FULLRESET
			効果範囲戦場 = 1
			効果範囲相手戦場 = 1
			効果範囲魔罠 = 1
			効果範囲相手魔罠 = 1
			効果条件カテゴリ = 蜜罐
			CALL CARD_ADD_GY_ADV(決闘者,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				効果範囲墓地 = 1
				効果条件カテゴリ = 惑乱魔
				効果条件種類 = モン全般
				CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
			ENDIF
		ENDIF

	ELSEIF バトルフェイズ == 1
		CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,77510,3)
		IF RESULT == 0
			CALL FULLRESET
			効果範囲魔罠 = 1
			効果条件カテゴリ = 蜜罐
			効果追加カテ = 蜜罐
			効果追加種類:0 = 1
			効果追加種類:1 = 21
			効果追加種類:2 = 190
			CALL CARD_SP_SUMMON_ADV(対面者,ゾーン,場所,"攻撃表示")
			SETCOLOR C_PINK
			PRINTFORML 
			PRINTFORMW 精疲力尽的蜜罐作为玩乐的工具被惑乱魔再次推到了戦場。
			PRINTFORML 
			RESETCOLOR
		ENDIF
	ENDIF

ELSEIF 種類 == 魔罠起動 || 種類 == 手札起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,77510,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所,魔罠行き)
		CALL FULLRESET
		効果条件カテゴリ = 惑乱魔
		効果条件種類 = モン全般
		CALL CARD_ADD_BASIC(決闘者,"デッキ",ゾーン,場所)
	ENDIF

ELSEIF 種類 == 永続効果
	CALL FULLRESET
	CALL SET_FIELD_MAGIC_INFO(決闘者, "惑乱魔狩猎园")
ENDIF




