@CARDNAME_40075,参照先
;ココで指定卡片の名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 惑乱魔塞拉
	CASE "略称"
		RESULTS = 塞拉(惑)
	CASE "カテゴリ"
		RESULTS:0 = 惑乱魔
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_40075(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
		RETURN リンク
	CASE "属性"
		RETURN 地属性
	CASE "種族"
		RETURN 植物族
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 400
	CASE "守備力"
		RETURN 2400
	CASE "性別"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARDSUMMON_40075(決闘者,召喚カード,EX参照)
#DIMS DYNAMIC 決闘者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 召喚カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要リンク数 = 1
素材条件種類 = リンク以外
素材条件カテゴリ:0 = 蟲惑魔
素材条件カテゴリ:1 = 惑乱魔

IF EX参照 == 1
	CALL LINCAN(決闘者,召喚カード,40075) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_40075(種類)
#DIM DYNAMIC 種類

CALL TEXT_DECORATION("ROGUE")
PRINTL Lモンスター以外の「蟲惑魔」モンスターまたは「惑乱魔」モンスター1体
PRINTL このカード名の②③④の効果はそれぞれ1ターンに1度しか使用できない。
CALLFORM CARD_ABLE(0,永続効果)
PRINTL ①：このカードは罠耐性を持つ。
PRINTL 自分フィールドにこのカード以外の「惑乱魔」と名のついたモンスターが存在する場合,このカードは攻撃対象にならず。
CALLFORM CARD_ABLE(1,効果後誘発)
PRINTL ②：通常罠カードが発動した場合に発動できる。
PRINTL 「惑乱魔」モンスター1体をデッキから特殊召喚する。
CALLFORM CARD_ABLE(2,効果後誘発)
PRINTL ③：このカード以外の自分の「惑乱魔」モンスターの効果が発動した場合に発動できる。
PRINTL デッキから「落とし穴」通常罠カードか「蜜穴」通常罠カード1枚を選んで自分フィールドにセットする。
CALLFORM CARD_ABLE(3,戦場起動)
PRINTL ④：このカード以外の自分フィールドの「惑乱魔」と名のついたモンスター1体を対象として発動できる。
PRINTL そのモンスタの表示形式を変更,那之后,可以选相手フィールド1只モンスタ的攻撃力下降自己选择モンスタ守備力的数值,この効果は相手ターンでも発動できる。
CALL TEXT_DECORATION("ROGUE")



@CARDCAN_40075(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIM DYNAMIC 条件達成

CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,40075)
SIF RESULT == 1
	RETURN 0

IF 種類 == 永続効果
	SIF ゾーン != "戦場"
		RETURN 0
	RETURN 1

ELSEIF 種類 == 効果後誘発
	SIF ゾーン != "戦場"
		RETURN 0
		
	;通常罠カード发动时,从デッキ特殊召唤惑乱魔怪兽的效果检测
	CALL FULLRESET
	効果条件種類 = 通常罠
	CALL CARD_CHECK_FIN_DATA(決闘者,"効果発動",2)
	SIF RESULT == 1 && 発動済_1(決闘者,40075) == 0 && モンスター数(決闘者) < 5
		条件達成 = 1

	;惑乱魔怪兽发动效果时,从デッキ盖放陷阱卡的效果检测	
	CALL FULLRESET
	効果条件固名前 =  惑乱魔塞拉
	効果条件カテゴリ = 惑乱魔
	効果条件種類 = モン全般
	CALL CARD_CHECK_FIN_DATA(決闘者,"効果発動")
	SIF RESULT == 1 && 発動済_2(決闘者,40075) == 0 && 魔罠数(決闘者) < 5
		条件達成 = 1
	SIF	条件達成 != 1
		RETURN 0

	RETURN 1

ELSEIF 種類 == 戦場起動 || 種類 == フリー誘発
	SIF 発動済_3(決闘者,40075)
		RETURN 0
	SIF ゾーン != "戦場"
		RETURN 0
		
	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件表示 = 表側表示
	効果条件カテゴリ = 惑乱魔
	効果条件固名前 =  惑乱魔塞拉
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_40075(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 选择卡片
#DIM DYNAMIC 选择对方卡片
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 条件達成

CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 永続効果
	CALL STATCHANGE_EFFECT(決闘者,"自身",場所,"罠耐")
	CALL FULLRESET
	効果範囲戦場 = 1
	効果条件カテゴリ = 惑乱魔
	効果条件固名前 = 惑乱魔塞拉
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT == 1
	CALL STATCHANGE_EFFECT(決闘者,"自身",場所,"攻避")

ELSEIF 種類 == 効果後誘発
	;将惑乱魔怪兽特殊召唤
	CALL FULLRESET
	効果条件種類 = 通常罠
	CALL CARD_CHECK_FIN_DATA(決闘者,"効果発動",2)
	IF RESULT == 1 && 発動済_1(決闘者,40075) == 0
		CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,40075,1)
		IF RESULT == 0
			CALL FULLRESET
			効果範囲デッキ = 1
			効果条件カテゴリ = 惑乱魔
			効果条件種類 = モン全般
			CALL CARD_SP_SUMMON_ADV(決闘者,ゾーン,場所)
		ENDIF
	ENDIF

	;将落とし穴或蜜穴通常罠カード盖放
	CALL FULLRESET
	効果条件固名前 =  惑乱魔塞拉
	効果条件カテゴリ = 惑乱魔
	効果条件種類 = モン全般
	CALL CARD_CHECK_FIN_DATA(決闘者,"効果発動")
	IF RESULT == 1 && 発動済_2(決闘者,40075) == 0
		CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,40075,2)
		IF RESULT == 0
			CALL FULLRESET
			効果条件カテゴリ:0 = 落とし穴
			効果条件カテゴリ:1 = 蜜穴
			効果条件種類 = 通常罠
			CALL CARD_SET_BASIC(決闘者,"デッキ",ゾーン,場所,"裏側表示")
		ENDIF
	ENDIF

ELSEIF 種類 == 戦場起動 || 種類 == フリー誘発
	;确认效果的发动
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,40075,3)
	IF RESULT == 0
		;选择自己戦場要变更表示形式的惑乱魔
		CALL FULLRESET
		効果条件表示 = 表側表示
		効果条件カテゴリ = 惑乱魔
		効果条件固名前 =  惑乱魔塞拉
		CALL CARD_CHOOSE_BASIC(決闘者,"戦場",ゾーン,場所)
		;定义选定到的卡片
		选择卡片 = RESULT
		;如果选择到了卡片
		IF 选择卡片 != 9999
			;获取选择卡片的守備力
			攻撃力修正 = GETVAR(@"%決闘者%_MZ_守:选择卡片")

			;根据怪兽的表示形式变更成另1种表示形式。
			IF GETVAR(@"%決闘者%_MZ_表:RESULT") == 1
				CALL SET_FIELD_INFO(決闘者,"戦場",选择卡片,"表",2,1)
			ELSEIF GETVAR(@"%決闘者%_MZ_表:RESULT") == 2
				CALL SET_FIELD_INFO(決闘者,"戦場",选择卡片,"表",1,1)
			ENDIF
			;确认是否要降低对手戦場怪兽的攻撃力。
			CALL FULLRESET
			PRINTL 要让相手戦場1只怪兽的攻撃力降低吗？
			PRINTL [1] - 是
			PRINTL [100] - 不需要
			BINPUT
			IF RESULT != 100
				CALL FULLRESET
				効果範囲相手戦場 = 1
				CALL CARD_CHOOSE_BASIC(決闘者,"相手戦場",ゾーン,場所)
				选择对方卡片 = RESULT
				IF RESULT != 9999
					CALL CARD_REMOVAL_BASIC(決闘者,"自動処理",选择对方卡片,"永攻DOWN",攻撃力修正)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF

