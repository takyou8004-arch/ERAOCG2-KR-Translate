@CARDNAME_40083,参照先
;ココで指定卡片の名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 惑乱魔吉娜
	CASE "略称"
		RESULTS = 吉娜(惑)
	CASE "カテゴリ"
		RESULTS:0 = 惑乱魔
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_40083(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "種類"
	
		RETURN 効果モン
	CASE "属性"
		RETURN 地属性
	CASE "種族"
		RETURN 植物族
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 700
	CASE "守備力"
		RETURN 2100
	CASE "性別"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_40083(種類)
#DIM DYNAMIC 種類

CALL TEXT_DECORATION("ROGUE")
PRINTL このカード名の①②の効果は1ターンに1度、いずれか1つしか使用できない。
CALLFORM CARD_ABLE(1,手札起動)
PRINTL ①：このカードは手札から「惑乱魔」モンスタ·通常罠カード１枚を捨てに発動できる。
PRINTL このカードを手札から特殊召喚する,その後,デッキからこのカード名以外の「惑乱魔」モンスター１体を墓地に送る。
CALLFORM CARD_ABLE(2,墓地起動,フリー誘発)
PRINTL ②：墓地のこのカードを除外し,相手墓地の「性别を有する怪兽」モンスター１体を対象として発動できる。
PRINTL そのカードを持ち主の魔法＆罠ゾーンに「蜜罐」にして表側表示で置く事ができる,この効果は相手ターンでも発動できる。
SETCOLOR C_PINK
CALLFORM CARD_ABLE(3,攻撃時誘発)
PRINTL ③：攻撃表示のこのカードが攻撃対象に選択された場合に発動する。
PRINTL 攻撃モンスターの攻撃力は800ポイントダウンする,このカードを守備表示にする。
RESETCOLOR
CALLFORM CARD_ABLE(0,永続効果)
PRINTL ④：このカードは罠耐性を持つ。
CALL TEXT_DECORATION("ROGUE")


@CARDCAN_40083(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件達成

CALL 対面者判定(決闘者)
対面者 = %RESULTS%


CALL CARD_NEGATE(決闘者,種類,ゾーン,場所,40083)
SIF RESULT == 1
	RETURN 0

IF 種類 == 手札起動
	SIF ゾーン != "手札"
    	RETURN 0
    SIF 発動済_1(決闘者,40083)
    	RETURN 0
    SIF 発動済_2(決闘者,40083)
    	RETURN 0
    SIF ターンプレイヤー != 決闘者
    	RETURN 0
    SIF メインフェイズ != 1
    	RETURN 0

    CALL FULLRESET
    効果範囲手札 = 1
    効果条件種類 = モン全般
    効果条件カテゴリ = 惑乱魔
    効果条件固名前 = 惑乱魔吉娜
    CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
    SIF RESULT == 1
    	条件達成++

    CALL FULLRESET
    効果範囲手札 = 1
    効果条件種類 = 通常罠
	効果条件カテゴリ:0 = 落とし穴
	効果条件カテゴリ:1 = 蜜穴
    CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
    SIF RESULT == 1
    	条件達成++
    SIF 条件達成 == 0
		RETURN 0
    RETURN 1

ELSEIF 種類 == 墓地起動 || (種類 == フリー誘発 && ゾーン == "墓地")
	SIF ゾーン != "墓地"
    	RETURN 0
    SIF 発動済_1(決闘者,40083)
    	RETURN 0
    SIF 発動済_2(決闘者,40083)
    	RETURN 0
	SIF GETVAR(@"%決闘者%_除外NG置換") >= 1
		RETURN 0

    CALL FULLRESET
	効果範囲相手墓地 = 1
	効果条件種類 = モン全般
	効果条件性別 = 性別在
	CALL CARD_CHECK_ADV(決闘者,種類,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
    
ELSEIF 種類 == 攻撃時誘発
	SIF ゾーン != "戦場"
		RETURN 0
	SIF 戦闘場所(決闘者) != 場所
		RETURN 0
	SIF ターンプレイヤー == 決闘者
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_表:場所") == 2
		RETURN 0
	RETURN 1

ELSEIF 種類 == 攻撃時強制誘発
	SIF ゾーン != "戦場"
		RETURN 0
	SIF 戦闘場所(決闘者) != 場所
		RETURN 0
	SIF ターンプレイヤー == 決闘者
		RETURN 0
	SIF GETVAR(@"%決闘者%_MZ_表:場所") == 2
		RETURN 0
	RETURN 1

ELSEIF 種類 == 永続効果
	SIF ゾーン != "戦場"
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_40083(決闘者,種類,ゾーン,場所)
#DIMS DYNAMIC 決闘者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 種類
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 自己卡片
#DIM DYNAMIC 自己守備力修正
#DIM DYNAMIC 对方卡片
#DIM DYNAMIC 对方攻撃力修正
#DIM DYNAMIC 选择墓地卡片
#DIM DYNAMIC 調教対象


CALL 対面者判定(決闘者)
対面者 = %RESULTS%

IF 種類 == 手札起動
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,40083,1)
	IF RESULT == 0
    	CALL FULLRESET
        効果範囲手札 = 1
        効果条件種類:0 = モン全般
        効果条件種類:1 = 通常罠
        効果条件カテゴリ:0 = 落とし穴
        効果条件カテゴリ:1 = 蜜穴
        効果条件カテゴリ:2 = 惑乱魔
        効果条件固名前 = 惑乱魔吉娜
        CALL CARD_ADD_GY_BASIC(決闘者,"手札",ゾーン,場所)
        IF RESULT == 1
        	CALL FULLRESET
            CALL CARD_SP_SUMMON(決闘者,ゾーン,場所)
            CALL FULLRESET
            効果条件種類 = モン全般
            効果条件カテゴリ = 惑乱魔
            効果条件固名前 = 惑乱魔吉娜
            CALL CARD_ADD_GY_BASIC(決闘者,"デッキ",ゾーン,場所)
        ENDIF
    ENDIF

ELSEIF 種類 == 墓地起動 || (種類 == フリー誘発 && ゾーン == "墓地")
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,40083,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(決闘者,ゾーン,場所)
		CALL FULLRESET
		効果条件種類 = モン全般
		効果条件性別 = 性別在
		CALL CARD_CHOOSE_BASIC(決闘者,"相手墓地",ゾーン,場所)
		选择墓地卡片 = RESULT
		IF 选择墓地卡片 != 9999
			CALL FULLRESET
			効果追加カテ = 蜜罐
			CALL CARD_ADD(対面者,"墓地","魔罠",选择墓地卡片,"表側表示")
		ENDIF
    ENDIF

ELSEIF 種類 == 攻撃時誘発
	CALL CARD_CHOICE(決闘者,種類,ゾーン,場所,40083)
	IF RESULT == 0
		自己卡片 = 戦闘場所(決闘者)
		自己守備力修正 = GETVAR(@"%決闘者%_MZ_守:自己卡片")
		对方卡片 = 戦闘場所(決闘者,1)
		对方攻撃力修正 = GETVAR(@"%対面者%_MZ_攻:对方卡片")
		TRYCALLFORM CARD_{戦闘カードID(決闘者,1)}("性別")
		IF RESULT == 牝性
			調教対象 = GETVAR(@"%対面者%_戦場:对方卡片")
			CALL CARD_EROTICS(調教対象)
			IF RESULT != -1
				調教対象 = RESULT
				SETCOLOR C_PINK
				PRINTFORMW %CALLNAME:調教対象%的身体接受调教的同时被压在半透明的叶片上,屈辱的暴露在众人的视线之中。
				RESETCOLOR
				CALL EROTIC_MOVE("拘束晒し",調教対象)
			ENDIF
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(決闘者,"自動処理",对方卡片,"永攻DOWN",800)

		ELSEIF RESULT == 雄性 || RESULT == ふたなり && (对方攻撃力修正 - 800 ) - 自己守備力修正 >= 0
			調教対象 = GETVAR(@"%対面者%_戦場:对方卡片")
			CALL CARD_EROTICS(調教対象)
			IF RESULT != -1
				調教対象 = RESULT
				SETCOLOR C_PINK
				PRINTFORMW 吉娜沉溺于交欢的快感，被%CALLNAME:調教対象%的肉棒彻底征服了……！
				RESETCOLOR
				CALL EROTIC_MOVE("前穴凌辱",NO_TO_CHARANUM(1379), -1)
				IF 魔罠数(対面者) < 5
					SETCOLOR C_PINK
					PRINTFORMW 随后就那样被%CALLNAME:調教対象%当作肉铠带走了……
					RESETCOLOR
					CALL CARD_EQUIP_MONSTER(対面者,对方卡片,"相手戦場",自己卡片,(1 + 2))
				ENDIF
			ELSE
				CALLFORM CARDNAME_{GETVAR(@"%対面者%_戦場:对方卡片")}("名前")
				SETCOLOR C_PINK
				PRINTFORMW 吉娜沉溺于交欢的快感，被%RESULTS%的肉棒彻底征服了……！
				RESETCOLOR
				CALL EROTIC_MOVE("前穴凌辱",NO_TO_CHARANUM(1379), -1)
				IF 魔罠数(対面者) < 5
					SETCOLOR C_PINK
					PRINTFORMW 随后就那样被%RESULTS%当作肉铠带走了……
					RESETCOLOR
					CALL CARD_EQUIP_MONSTER(対面者,对方卡片,"相手戦場",自己卡片,(1 + 2))
				ENDIF
			ENDIF

		ELSEIF RESULT == 雄性 || RESULT == ふたなり
			調教対象 = GETVAR(@"%対面者%_戦場:对方卡片")
			CALL CARD_EROTICS(調教対象)
			IF RESULT != -1
				調教対象 = RESULT
				SETCOLOR C_PINK
				PRINTFORMW %CALLNAME:調教対象%的身体接受调教的同时被压在半透明的叶片上,屈辱的暴露在众人的视线之中。
				RESETCOLOR
				CALL EROTIC_MOVE("全身愛撫",調教対象,NO_TO_CHARANUM(1379))
			ENDIF
			CALL CARD_REMOVAL_BASIC(決闘者,"自動処理",对方卡片,"永攻DOWN",800)
			
		ELSE
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(決闘者,"自動処理",对方卡片,"永攻DOWN",800)
		ENDIF
		CALL SET_FIELD_INFO(決闘者,"戦場",場所,"表",2,1)
	ENDIF

ELSEIF 種類 == 攻撃時強制誘発
	CALL FULLRESET
	IF RESULT == 0
		IF 戦闘場所(決闘者,1) != 5
			CALL CARD_EROTICS(戦闘カードID(決闘者,1))
			IF RESULT != 0
				調教対象 = RESULT
			ENDIF
		ENDIF
		IF 調教対象 > 0
			PRINTFORML
			SETCOLOR C_PINK 
			PRINTFORML %CALLNAME:調教対象%触碰到绒毛的瞬间就被关了起来。
			RESETCOLOR
		ENDIF		
	ENDIF		

ELSEIF 種類 == 永続効果
	CALL STATCHANGE_EFFECT(決闘者,"自身",場所,"罠耐")
ENDIF
