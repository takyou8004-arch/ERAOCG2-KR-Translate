@CARDNAME_99556,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

;カード制作者：xonng
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 【乳圧】ホルスタインプレッション
	CASE "略称"
		RESULTS = ﾌﾟﾚｯｼｮﾝ【乳圧】
	CASE "カテゴリ"
		RESULTS:0 = ホルスタイン
		RESULTS:1 = E.R.A
ENDSELECT

@CARD_99556(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
	CASE "추가日"
		RETURN 250329
ENDSELECT

RETURN 0

@CARD_EXPLANATION_99556(종류)
#DIM DYNAMIC 종류
#DIMS DYNAMIC 듀얼리스트

CALL TEXT_DECORATION("ROGUE")
PRINTL このカード名の②の효과は１ターンに１度しか발동できない。
CALLFORM CARD_ABLE(0,마함起動,손패起動)
PRINTL ①：자신の마법＆함정ゾーンに「ホルスタイン」カードの효과によって置かれたカードが存在する場合、バトルフェイズ・자신メインフェイズに발동できる。
PRINTL 「ホルスタイン」カード以外の、자신フィールドの表側표시のカードを묘지に送る。
PRINTL その後、選択したカードが牝性モンスターだった場合、以下の효과を適用できる。
PRINTL ●：손패・묘지・덱・EX덱・상대の묘지から、묘지に送ったカードと同じカードを지속마법カード扱いで자신の마법＆함정ゾーンに表側표시で置き
PRINTL 　　この듀얼中、まだ발동していない「ホルスタイン」通常마법を손패・덱から제외し、その효과を全て適用する。
PRINTL 자신の마법＆함정ゾーンにセットされたカードが存在しない場合、このカードは손패から捨てて발동でき
PRINTL １ターンに１度、자신の마법＆함정ゾーンにカードが２枚以上存在する場合、상대フィールドのカードも選択することができる。
CALLFORM CARD_ABLE(1,묘지起動)
PRINTL ②：자신メインフェイズに발동できる。
PRINTL 묘지のこのカードを제외し、묘지の「ホルスタイン」カードを손패に加える。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_99556(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 마함数

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,99556)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動 
	SIF !(メインフェイズ == 1 && ターンプレイヤー == 듀얼리스트) && バトルフェイズ != 1
		RETURN 0
	
	IF ゾーン == "손패"
		;セットカードがあれば使えない
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件표시 = 裏側표시
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT > 0
			RETURN 0
	ELSEIF ゾーン == "마함"
		;セットしたターンには使えない
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 2
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	
	;자신필드에 ホルスタインカードの효과で置いたカードが無ければ使えない。
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件カテゴリ:0 = 登録情報：ホルスタイン-美乳
	효과条件カテゴリ:1 = 登録情報：ホルスタイン-愛乳
	효과条件カテゴリ:2 = 登録情報：ホルスタイン-旨乳
	효과条件カテゴリ:3 = 登録情報：ホルスタイン-EX乳
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 0
		RETURN 0
	
	;필드에 「ホルスタイン」以外の表側のカードがなければ使えない
	;마법함정ゾーンにカードが2枚上あれば상대のカードも대상にできる。
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	마함数 = RESULT
	
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	IF 마함数 >= 2 && !발동済_1(듀얼리스트,99556)
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
	ENDIF
	효과条件표시 = 表側표시
	효과条件固カテゴリ:0 = ホルスタイン
	효과条件固カテゴリ:1 = 登録情報：ホルスタイン-美乳
	효과条件固カテゴリ:2 = 登録情報：ホルスタイン-愛乳
	효과条件固カテゴリ:3 = 登録情報：ホルスタイン-旨乳
	효과条件固カテゴリ:4 = 登録情報：ホルスタイン-EX乳
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT <= 0
		RETURN 0
	
	RETURN 1
ELSEIF 종류 == 묘지起動
	
	SIF 발동済_2(듀얼리스트,99556)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	
	RETURN 1
ENDIF

@CARDEFFECT_99556(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 参照場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 마함数
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 書換先カードID
#DIM DYNAMIC 攻め암소参照カードID
#DIM DYNAMIC 参照カードID
#DIM DYNAMIC 암소대상
#DIMS DYNAMIC 암소名称
#DIM DYNAMIC 암소乳サイズ
#DIM DYNAMIC 被암소대상
#DIMS DYNAMIC 被암소名称
#DIM DYNAMIC 被암소乳サイズ
#DIMS DYNAMIC カテゴリ名称
#DIM DYNAMIC 美乳チェック
#DIM DYNAMIC 愛乳チェック
#DIM DYNAMIC 旨乳チェック
#DIM DYNAMIC EX乳チェック


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99556)
	IF RESULT == 0
		
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件カテゴリ:0 = 登録情報：ホルスタイン-美乳
		효과条件カテゴリ:1 = 登録情報：ホルスタイン-愛乳
		효과条件カテゴリ:2 = 登録情報：ホルスタイン-旨乳
		효과条件カテゴリ:3 = 登録情報：ホルスタイン-EX乳
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			;選択肢なし
			RESULT = 0
		ELSE
			PRINTFORML 誰が乳圧を掛けますか？
			PRINTFORML 
				
			美乳チェック = -1
			愛乳チェック = -1
			旨乳チェック = -1
			EX乳チェック = -1
			
			FOR カウンタ,0,5
				IF GETVARS(@"%듀얼리스트%_魔_追カテ:カウンタ") == "登録情報：ホルスタイン-美乳"
					CALL HORUSTEIN_GET_DATA(듀얼리스트,-1,179556)
					암소名称 = %RESULTS:0%
					PRINTFORML [{カウンタ}] - 【美乳】%RESULTS:0%
					美乳チェック = カウンタ
					CONTINUE
				ENDIF
				IF GETVARS(@"%듀얼리스트%_魔_追カテ:カウンタ") == "登録情報：ホルスタイン-愛乳"
					CALL HORUSTEIN_GET_DATA(듀얼리스트,-1,179557)
					암소名称 = %RESULTS:0%
					PRINTFORML [{カウンタ}] - 【愛乳】%RESULTS:0%
					愛乳チェック = カウンタ
					CONTINUE
				ENDIF
				IF GETVARS(@"%듀얼리스트%_魔_追カテ:カウンタ") == "登録情報：ホルスタイン-旨乳"
					CALL HORUSTEIN_GET_DATA(듀얼리스트,-1,179558)
					암소名称 = %RESULTS:0%
					PRINTFORML [{カウンタ}] - 【旨乳】%RESULTS:0%
					旨乳チェック = カウンタ
					CONTINUE
				ENDIF
				IF GETVARS(@"%듀얼리스트%_魔_追カテ:カウンタ") == "登録情報：ホルスタイン-EX乳"
					CALL HORUSTEIN_GET_DATA(듀얼리스트,-1,179559)
					암소名称 = %RESULTS:0%
					PRINTFORML [{カウンタ}] - 【EX乳】%RESULTS:0%
					EX乳チェック = カウンタ
					CONTINUE
				ENDIF
				
			NEXT
			
			PRINTFORML 
			PRINTFORML [9] - おまかせ
			IF 듀얼리스트 == "자신"
				BINPUT
			ELSE
				RESULT = 9
			ENDIF
		ENDIF
		
		IF RESULT == 9
			SIF 美乳チェック != -1
				RESULT = 美乳チェック
			SIF 愛乳チェック != -1
				RESULT = 愛乳チェック
			SIF 旨乳チェック != -1
				RESULT = 旨乳チェック
			SIF EX乳チェック != -1
				RESULT = EX乳チェック
			
			;雑ランダム処理
			SIF 旨乳チェック != -1 && RAND:2 == 1
				RESULT = 旨乳チェック
			SIF 愛乳チェック != -1 && RAND:2 == 1
				RESULT = 愛乳チェック
			SIF 美乳チェック != -1 && RAND:2 == 1
				RESULT = 美乳チェック
		ENDIF
		
		FOR カウンタ,RESULT,5
			IF GETVARS(@"%듀얼리스트%_魔_追カテ:カウンタ") == "登録情報：ホルスタイン-美乳"
				RESULT = カウンタ
				攻め암소参照カードID = 179556
				BREAK
			ENDIF
			IF GETVARS(@"%듀얼리스트%_魔_追カテ:カウンタ") == "登録情報：ホルスタイン-愛乳"
				RESULT = カウンタ
				攻め암소参照カードID = 179557
				BREAK
			ENDIF
			IF GETVARS(@"%듀얼리스트%_魔_追カテ:カウンタ") == "登録情報：ホルスタイン-旨乳"
				RESULT = カウンタ
				攻め암소参照カードID = 179558
				BREAK
			ENDIF
			IF GETVARS(@"%듀얼리스트%_魔_追カテ:カウンタ") == "登録情報：ホルスタイン-EX乳"
				RESULT = カウンタ
				攻め암소参照カードID = 179559
				BREAK
			ENDIF
		NEXT
		
		;ホルスタイン情報の취득
		CALL HORUSTEIN_GET_DATA(듀얼리스트,-1,攻め암소参照カードID)
		암소名称 = %RESULTS:0%
		参照場所 = RESULT:0
		암소대상 = RESULT:1
		암소乳サイズ = GETVAR(@"%듀얼리스트%_魔_個Ｃ:参照場所");마법함정の乳サイズ
		CALL HORUSTEIN_GET_BUST(RESULT:99,암소乳サイズ)
		암소乳サイズ = RESULT:2
		
		美乳チェック = 0
		愛乳チェック = 0
		旨乳チェック = 0
		EX乳チェック = 0
		
		;묘지送り
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		마함数 = RESULT
		
		효과範囲필드 = 1
		효과範囲마함 = 1
		IF 마함数 >= 2 && !발동済_1(듀얼리스트,99556)
			효과範囲상대필드 = 1
			효과範囲상대마함 = 1
		ENDIF
		효과条件표시 = 表側표시
		효과条件固カテゴリ:0 = ホルスタイン
		효과条件固カテゴリ:1 = 登録情報：ホルスタイン-美乳
		효과条件固カテゴリ:2 = 登録情報：ホルスタイン-愛乳
		효과条件固カテゴリ:3 = 登録情報：ホルスタイン-旨乳
		효과条件固カテゴリ:4 = 登録情報：ホルスタイン-EX乳
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
	
		IF RESULT != -1
			選択カードID = RESULT:1
			
			;상대のカードを選んだら、使用済みにする
			SIF 듀얼리스트 != 대상者（CS）
				CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99556,1)
			
			;ホルスタイン情報の취득(カードIDから名前を취득)
			CALL HORUSTEIN_GET_DATA(듀얼리스트,-1,選択カードID)
			被암소名称 = %RESULTS:0%
			
			;今回は乳の大きさは마법함정を参照して出している。
			;MZの乳は「ホルスタイン」以外のカードの효과で変動してれば値がずれてしまうが、面倒なので今回はふわっとした表現に
			;貧乳(-1)以下、巨乳(1)以下、巨乳より大きい
			
			PRINTFORM %암소名称%は%被암소名称%の上にのしかかると、
			IF 암소乳サイズ <= -1
				PRINTFORMW 可愛らしい乳で一生懸命%被암소名称%を押しつぶした…
			ELSEIF 암소乳サイズ <= 1
				;愛乳以外は巨乳以上になる
				PRINTFORMW 大きな乳で%被암소名称%を押しつぶしてしまった…
			ELSE
				PRINTFORMW とても大きな乳で%被암소名称%を押しつぶしてしまった…
			ENDIF
			PRINTFORML
			
			;選択カードIDが자신の덱・EX덱・손패に存在する場合
			;かつ、마법함정ゾーンに空きがある場合
			
			CALL FULLRESET
			효과範囲상대묘지 = 1
			효과範囲손패 = 1
			효과範囲묘지 = 1
			효과範囲덱 = 1
			효과範囲EX덱 = 1
			효과条件성별 = 牝性
			효과条件ID = 選択カードID
			효과条件CNT = 1
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT != 0 && 마함数(듀얼리스트) < 5
				;存在する
				
				FOR カウンタ,0,5
					;손패と덱に발동していない〇〇登録があるか
					CALL FULLRESET
					효과範囲손패 = 1
					효과範囲덱 = 1
					효과条件ID = 79556 + カウンタ
					CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
					
					;발동可能ﾁｪｯｸと存在チェック
					IF RESULT != -1
						SELECTCASE 79556 + カウンタ
						CASE 79556
							美乳チェック = 1
							SIF 발동済_D1(듀얼리스트,79556)
								美乳チェック = -1
						CASE 79557
							愛乳チェック = 1
							SIF 발동済_D1(듀얼리스트,79557)
								愛乳チェック = -1
						CASE 79558
							旨乳チェック = 1
							SIF 발동済_D1(듀얼리스트,79558)
								旨乳チェック = -1
						CASE 79559
							EX乳チェック = 1
							SIF 발동済_D1(듀얼리스트,79559)
								EX乳チェック = -1
						ENDSELECT
					ENDIF
				NEXT
				
				IF 美乳チェック == 1 || 愛乳チェック == 1 || 旨乳チェック == 1 || EX乳チェック == 1
					
					;발동可能な登録カードがある
					PRINTFORML 
					PRINTFORML 손패・덱・EX덱に암소に登録可能なカードが存在します。登録しますか？
					PRINTFORML 
					IF  美乳チェック == 1
						PRINTFORML [0] - 【美乳】登録
					ELSE
						SETCOLOR C_GRAY
						PRINTFORML     - 【美乳】登録
						RESETCOLOR
					ENDIF
					IF  愛乳チェック == 1
						PRINTFORML [1] - 【愛乳】登録
					ELSE
						SETCOLOR C_GRAY
						PRINTFORML     - 【愛乳】登録
						RESETCOLOR
					ENDIF
					IF  旨乳チェック == 1
						PRINTFORML [2] - 【旨乳】登録
					ELSE
						SETCOLOR C_GRAY
						PRINTFORML     - 【旨乳】登録
						RESETCOLOR
					ENDIF
					IF  EX乳チェック == 1
						PRINTFORML [3] - 【EX乳】登録
					ELSE
						SETCOLOR C_GRAY
						PRINTFORML     - 【EX乳】登録
						RESETCOLOR
					ENDIF
					PRINTFORML 
					PRINTFORML [9] - 登録しない
							
					IF 듀얼리스트 == "자신"
						BINPUT
					ELSE
						SIF  EX乳チェック == 1
							RESULT = 3
						SIF  旨乳チェック == 1
							RESULT = 2
						SIF  愛乳チェック == 1
							RESULT = 1
						SIF  美乳チェック == 1
							RESULT = 0
					ENDIF
				ELSE
					;選択肢が出なければ終わり
					RESULT = -1
				ENDIF
			ELSE 
				;자신の손패덱EX덱にカードが無ければ、カードを潰して終わり
				RESULT = -1
			ENDIF
		ELSE
			;潰すのに失敗したら終わり
			RESULT = -1
		ENDIF
		
		SELECTCASE RESULT
		CASE 0
			美乳チェック = 2
			書換先カードID = 179556
			カテゴリ名称 = 登録情報：ホルスタイン-美乳
		CASE 1
			愛乳チェック = 2
			書換先カードID = 179557
			カテゴリ名称 = 登録情報：ホルスタイン-愛乳
		CASE 2
			旨乳チェック = 2
			書換先カードID = 179558
			カテゴリ名称 = 登録情報：ホルスタイン-旨乳
		CASE 3
			EX乳チェック = 2
			書換先カードID = 179559
			カテゴリ名称 = 登録情報：ホルスタイン-EX乳
		CASEELSE
			RESULT = -1
		ENDSELECT
		
		IF RESULT != -1 && (美乳チェック == 2 || 愛乳チェック == 2 || 旨乳チェック == 2 || EX乳チェック == 2 )
			;何かしら選択したなら登録開始
			
			;選んだカードを덱か손패から제외する(덱優先)
			RESULT = -1
			;덱のカード
			FOR カウンタ,0,現山札数(듀얼리스트)
				SIF GETVAR(@"%듀얼리스트%_덱:カウンタ") != 書換先カードID - 100000
					CONTINUE
				;見つかったら抜ける
				CALL CARD_DISPOSE(듀얼리스트,"덱",カウンタ,제외行き)
				RESULT = 1
				PRINTFORML 덱
				BREAK
			NEXT
			IF RESULT == -1
				;손패のカード
				FOR カウンタ,0,現손패(듀얼리스트)
					SIF GETVAR(@"%듀얼리스트%_손패:カウンタ") != 書換先カードID - 100000
						CONTINUE
					;見つかったら抜ける
					CALL CARD_DISPOSE(듀얼리스트,"손패",カウンタ,제외行き)
					PRINTFORML 덱
					BREAK
				NEXT
			ENDIF
			
			CALL FULLRESET
			SIF 마함数 >= 2
				효과範囲상대묘지 = 1
			효과範囲손패 = 1
			효과範囲묘지 = 1
			효과範囲덱 = 1
			효과範囲EX덱 = 1
			효과条件성별 = 牝性
			효과条件固カテゴリ = ホルスタイン
			효과条件ID = 選択カードID
			효과추가カテ = %カテゴリ名称%
			CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")
			
			;ホルスタイン情報の취득(カードIDから各種情報を취득)
			CALL HORUSTEIN_GET_DATA(듀얼리스트,-1,書換先カードID)
			被암소名称 = %RESULTS:0%
			参照場所 = RESULT:0
			被암소대상 = RESULT:1
			参照カードID = RESULT:99
			CALL HORUSTEIN_GET_BUST(参照カードID)
			被암소乳サイズ = RESULT:2
			
			;耐性と발동無効の付与
			CALL SET_FIELD_INFO(듀얼리스트,"마함",参照場所,"破耐",2)
			CALL SET_FIELD_INFO(듀얼리스트,"마함",参照場所,"対耐",2)
			CALL SET_FIELD_INFO(듀얼리스트,"마함",参照場所,"発無",2)
			
			;---손패に加わった等の표시をスキップ
			SKIPDISP 1
			
			;書き換え前のカードを一旦손패に加える。
			CALL CARD_GENERATE_HAND(듀얼리스트,選択カードID)
			
			;カードの書き換え
			;필드のカード
			FOR カウンタ,0,5
				SIF GETVAR(@"%듀얼리스트%_MZ_ID:カウンタ") != 選択カードID
					CONTINUE
				CALL CARD_RECONSTRUCT(듀얼리스트,"필드",カウンタ,書換先カードID)
				SIF EX乳チェック == 2
					CALL CARD_ADD(듀얼리스트, "필드", "EX덱",カウンタ)
			NEXT
			
			;손패のカード
			FOR カウンタ,0,現손패(듀얼리스트)
				SIF GETVAR(@"%듀얼리스트%_손패:カウンタ") != 選択カードID
					CONTINUE
				CALL CARD_RECONSTRUCT(듀얼리스트,"손패",カウンタ,書換先カードID)
				SIF EX乳チェック == 2
					CALL CARD_ADD(듀얼리스트, "손패", "EX덱",カウンタ)
			NEXT
			
			;덱のカード
			FOR カウンタ,0,現山札数(듀얼리스트)
				SIF GETVAR(@"%듀얼리스트%_덱:カウンタ") != 選択カードID
					CONTINUE
				CALL CARD_RECONSTRUCT(듀얼리스트,"덱",カウンタ,書換先カードID)
				SIF EX乳チェック == 2
					CALL CARD_ADD(듀얼리스트, "덱", "EX덱",カウンタ)
			NEXT
			
			;묘지のカード
			FOR カウンタ,0,現묘지数(듀얼리스트)
				SIF GETVAR(@"%듀얼리스트%_묘지:カウンタ") != 選択カードID
					CONTINUE
				CALL CARD_RECONSTRUCT(듀얼리스트,"묘지",カウンタ,書換先カードID)
			NEXT
			
			;EX덱のカード
			FOR カウンタ,0,現EX山札数(듀얼리스트)
				SIF GETVAR(@"%듀얼리스트%_EX덱:カウンタ") != 選択カードID
					CONTINUE
				CALL CARD_RECONSTRUCT(듀얼리스트,"EX덱",カウンタ,書換先カードID)
				SIF EX乳チェック != 2
					CALL CARD_ADD(듀얼리스트, "EX덱", "덱",カウンタ)
			NEXT
			;---スキップここまで
			SKIPDISP 0
			
			PRINTFORML
			PRINTFORMW %암소名称%は자신の乳をきゅっと搾り、溢れたミルクを手で受け止めると、それをするすると자신の口元に流し込んでいく…
			PRINTFORMW 口元からつぅ…と白いミルクを垂らすと、動けなくなった%被암소名称%に口移しで飲ませ始めた…
			
			IF 美乳チェック == 2
				PRINTFORMW %암소名称%は%被암소名称%にパチン、とピアスを取り付けると、자신がそうされたように%被암소名称%も암소に相応しい"商品"へと"調整"していく…
				PRINTFORML
				PRINTFORM %被암소名称%の乳は
				SETCOLOR C_PINK
				PRINTFORM  [%GET_TALENTNAME(121,被암소乳サイズ)%] 
				RESETCOLOR
				PRINTFORM から
				SETCOLOR C_H_PINK
				PRINTFORM  [%GET_TALENTNAME(121,被암소乳サイズ+1)%] 
				RESETCOLOR
				PRINTFORMW になってしまった…
				PRINTFORML
				PRINTFORMW %암소名称%は%被암소名称%の乳を満足げに撫でると、相応しい服装へと整えていく…
				被암소乳サイズ++ 
			ELSEIF 愛乳チェック == 2
				
				IF 被암소乳サイズ >= 0
					
					SETCOLOR C_GRAY
					PRINTFORML %암소名称%は%被암소名称%の乳を眺めている… 
					PRINTFORML
					RESETCOLOR
					CALL YN_CHOICE(듀얼리스트,"「貧乳」にする","乳のサイズを변경しない")
				ENDIF

				PRINTFORMW %암소名称%は意識の無い%被암소名称%にカウベルの付いた首輪を取り付けると、자신がそうされたように%被암소名称%も암소に相応しい"商品"へと"調整"していく…
				PRINTFORML
				
				IF 被암소乳サイズ <= -1 || RESULT != はい
					;元々貧乳未満の場合、変わらなかった…
					
					PRINTFORM %被암소名称%の乳は
					IF 被암소乳サイズ <= -1
						SETCOLOR C_L_BLUE
					ELSE
						SETCOLOR C_PINK
					ENDIF
					PRINTFORM  [%GET_TALENTNAME(121,被암소乳サイズ)%] 
					RESETCOLOR
					PRINTFORM から……特に変動はなかったようだ……
					PRINTFORMW
					
					被암소乳サイズ = 0
					;着替え
					PRINTFORMW %암소名称%は%被암소名称%の熱を持った身体を冷ますべく、服を次々と脱がせ始めた…
				ELSE
					;貧乳より大きい場合、貧乳にする
					PRINTFORM %被암소名称%の乳は
					SETCOLOR C_PINK
					PRINTFORM  [%GET_TALENTNAME(121,被암소乳サイズ)%] 
					RESETCOLOR
					PRINTFORM から
					SETCOLOR C_L_BLUE
					PRINTFORM  [%GET_TALENTNAME(121,-1)%] 
					RESETCOLOR
					PRINTFORML になってしまった…
					PRINTFORMW
					
					;着替え
					PRINTFORMW %암소名称%はサイズの合わなくなった服を%被암소名称%から剥ぎ取っていく…
					
					;元々が小柄体型じゃなければ표시(実際には변경しない)
					IF 被암소대상 != -1 && TALENT:被암소대상:100 != -1
						PRINTFORML
						PRINTFORMW %被암소名称%は[小柄体型]になってしまったようだ…
					ENDIF
					
					被암소乳サイズ = -1 -(被암소乳サイズ)
				ENDIF
				
				
			ELSEIF 旨乳チェック == 2
				
				PRINTFORML 意識の無い%被암소名称%は抵抗することもできず、身体に管理タグを印字されてしまった…
				PRINTFORMW %암소名称%は자신がそうされたように%被암소名称%も암소に相応しい"商品"へと"調整"していく…
				
				IF 被암소乳サイズ >= 0
					;元々普乳以上の場合、1サイズアップ
					
					PRINTFORM %被암소名称%の乳は
					SETCOLOR C_PINK
					PRINTFORM  [%GET_TALENTNAME(121,被암소乳サイズ)%] 
					RESETCOLOR
					PRINTFORM から
					SETCOLOR C_H_PINK
					PRINTFORM  [%GET_TALENTNAME(121,被암소乳サイズ+1)%] 
					RESETCOLOR
					PRINTFORML になってしまった…
					PRINTFORMW
					
					;着替え
					PRINTFORMW %암소名称%は%被암소名称%の濡れた身体に張り付いていた服を脱がせ始めた…
				ELSE
					;普乳未満の場合、巨乳にする
					;一気に2サイズ以上アップする
					
					PRINTFORM %被암소名称%の乳は
					SETCOLOR C_PINK
					PRINTFORM  [%GET_TALENTNAME(121,被암소乳サイズ)%] 
					RESETCOLOR
					PRINTFORM から
					SETCOLOR C_H_PINK
					PRINTFORM  [%GET_TALENTNAME(121,1)%] 
					RESETCOLOR
					PRINTFORML になってしまった…
					PRINTFORMW
					
					;着替え
					PRINTFORMW %암소名称%は%被암소名称%の一気に大きくなった乳を逃がすように服を脱がせ始めた…
				ENDIF
				
				;旨乳は카운터を巨乳以上になるように調整してスタート(乳サイズ：UP)
				IF 被암소乳サイズ <= 0
					;普乳以下なら巨乳にする
					被암소乳サイズ = 1 -(被암소乳サイズ)
				ELSE
					;巨乳以上なら1サイズUpする
					被암소乳サイズ = 1
				ENDIF
				
			ELSEIF EX乳チェック == 2
				
				PRINTFORML 意識の無い%被암소名称%は抵抗することもできず、
				PRINTFORMW %암소名称%にピアスにカウベルの付いた首輪を取り付けられ、身体に管理タグを印字されてしまった…
				PRINTFORMW %被암소名称%が암소に相応しい"商品"へと"調整"さていく過程を楽しむかのように、%암소名称%は丁寧に乳を揉みしだき始めた…
				
				IF 被암소乳サイズ >= 4
					;魔乳以上の場合、1サイズアップする
					
					PRINTFORM %被암소名称%の乳は
					SETCOLOR C_PINK
					PRINTFORM  [%GET_TALENTNAME(121,被암소乳サイズ)%] 
					RESETCOLOR
					PRINTFORM から
					SETCOLOR C_H_PINK
					PRINTFORM  [%GET_TALENTNAME(121,被암소乳サイズ+1)%] 
					RESETCOLOR
					PRINTFORML になってしまった…
					PRINTFORMW
					
				ELSE
					;魔乳未満の場合、規格外にする
					PRINTFORM %被암소名称%の乳は
					SETCOLOR C_PINK
					PRINTFORM  [%GET_TALENTNAME(121,被암소乳サイズ)%] 
					RESETCOLOR
					PRINTFORM から
					SETCOLOR C_H_PINK
					PRINTFORM  [%GET_TALENTNAME(121,5)%] 
					RESETCOLOR
					PRINTFORML になってしまった…
					PRINTFORMW
				ENDIF
			
				PRINTFORML 
				;着替え
				PRINTFORMW %암소名称%は%被암소名称%のデカすぎる乳を、満足げに指で感触を確かめている…
				
				;EX乳は카운터を規格外以上になるように調整してスタート(乳サイズ：UP)
				IF 被암소乳サイズ < 5
					;規格外未満なら最低限として規格外にする
					被암소乳サイズ = 5 -(被암소乳サイズ)
				ELSE
					;規格外なら+1する
					被암소乳サイズ = 1
				ENDIF
			ENDIF
			
			PRINTFORML 
			CALL SET_FIELD_INFO(듀얼리스트,"마함",参照場所,"個Ｃ",被암소乳サイズ)
			
			
			;服装の변경(牛柄ビキニと角)
			IF 被암소대상 != -1 
				;アクセサリ&帽子：つけ角・小(2枠目を변경)
				EQUIP:被암소대상:2 = 20
				CFLAG:被암소대상:(200 + 2) = 20
				;靴、靴下は触らない
				;ズボン～インナーまでは全剥き
				FOR カウンタ,6,13
					SIF EQUIP:被암소대상:カウンタ != 0
						PRINTFORML %암소名称%は%被암소名称%の【%CLOTHNAME(カウンタ,EQUIP:被암소대상:カウンタ)%】を脱がせた…
					EQUIP:被암소대상:カウンタ = 0
					CFLAG:被암소대상:(200 + カウンタ) = 0
				NEXT
				
				PRINTFORML
				;ワンピース：牛柄ビキニ(マイクロビキニは無かった)
				IF EQUIP:被암소대상:カウンタ != 0
					PRINTFORML %암소名称%は%被암소名称%の【%CLOTHNAME(カウンタ,EQUIP:被암소대상:カウンタ)%】を脱がせ【牛柄ビキニ】を着せた…
				ELSE
					PRINTFORML %암소名称%は%被암소名称%に【牛柄ビキニ】を着せた…
				ENDIF
				EQUIP:被암소대상:15 = 33
				CFLAG:被암소대상:(200 + 15) = 33
				
				;조교
				IF 암소대상 != -1 
					CALL EROTIC_MOVE("Ｂ레즈愛撫",被암소대상,암소대상,300)
				ELSE
					CALL EROTIC_MOVE("Ｂ레즈愛撫",被암소대상,-1,300)
				ENDIF
			ENDIF
			
			PRINTFORML %암소名称%は仕上がりつつある%被암소名称%の乳を眺めると、
			PRINTFORMW 乳をなぞるように혀を這わせ、乳首からトロトロと溢れ始めた白い液体を、その出来栄えを確かめるように一番搾りを味わい始めた…
			PRINTFORMW 何もわからず乳を商品にされてしまった%被암소名称%は無意識にも関わらず、乳を吸われる度にぴくんっぴくんっと身体を震わせている。
			PRINTFORML 
			PRINTFORMW 頼りになる"先輩"も居ることだ…%被암소名称%が目を覚ました頃には立派な雌牛に教育されていることだろう。
			
			;対応する紋章と○○登録のデュエル中１度효과を使用済みにする
			SKIPDISP 1
			CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,書換先カードID - 100000,20)
			CALL CARD_RESERV(듀얼리스트,書換先カードID - 100000,永紋章予約)
			CALL CARD_RESERV(듀얼리스트,書換先カードID - 100000,永攻紋章予約)
			SKIPDISP 0
			
			SETCOLOR C_YELLOW
			PRINTFORML
			PRINTFORM 「【
			IF 美乳チェック == 2
				PRINTFORM 美乳
			ELSEIF 愛乳チェック == 2
				PRINTFORM 愛乳
			ELSEIF 旨乳チェック == 2
				PRINTFORM 旨乳
			ELSEIF EX乳チェック == 2
				PRINTFORM EX乳
			ENDIF
			
			PRINTFORML 】ホルスタイン」は필드에 存在する場合「%被암소名称%」として扱われます。
			RESETCOLOR
			PRINTFORMW
			
			;손패に加える효과
			
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = ホルスタイン
			SIF 美乳チェック == 2
				효과条件ID = 79555
			SIF 愛乳チェック == 2
				효과条件종류 = 通常마법
			SIF 旨乳チェック == 2
				효과条件종류 = 함정全般
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 1
				효과範囲덱 = 1
				효과条件カテゴリ = ホルスタイン
				SIF 美乳チェック == 2
					효과条件ID = 79555
				SIF 愛乳チェック == 2
					효과条件종류 = 通常마법
				SIF 旨乳チェック == 2
					효과条件종류 = 함정全般
				CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
			ELSE
				PRINTFORML
				PRINTFORMW 덱から손패に加えられる「ホルスタイン」カードは無いようだ…
				PRINTFORML
			ENDIF
			
			;특수소환효과
			IF 美乳チェック == 2 || 愛乳チェック == 2 || 旨乳チェック == 2
				;손패から특수소환
				;選択時に【〇乳】ホルスタイン…と표시されるとｷﾞｮｯっとするので、손패から探して특수소환する
				FOR カウンタ,0,現손패(듀얼리스트)
					SIF GETVAR(@"%듀얼리스트%_손패:カウンタ") != 書換先カードID
						CONTINUE
					CALL CARD_SP_SUMMON(듀얼리스트,"손패",カウンタ)
					BREAK
				NEXT
				
				IF RESULT != 0
					IF 美乳チェック == 2
						PRINTFORMW %被암소名称%の美しい乳がしなやかに揺れた…
						CALL HORUSTEIN_RAND_COW_TXT(1,0)
					ELSEIF 愛乳チェック == 2
						PRINTFORMW %被암소名称%の可愛らしい乳首がぷるんと震えた…
						CALL HORUSTEIN_RAND_COW_TXT(1,1)
					ELSEIF 旨乳チェック == 2
						PRINTFORMW %被암소名称%の思わず吸い付きたくなる乳がしっとりと艶めいている…
						CALL HORUSTEIN_RAND_COW_TXT(1,2)
					ENDIF
				ENDIF
			ELSEIF EX乳チェック == 2
				CALL FULLRESET
				효과範囲덱 = 1
				효과範囲묘지 = 1
				효과条件カテゴリ = ホルスタイン
				효과条件종류 = モン全般
				CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
				
				IF RESULT == 1
					;덱・묘지から「ホルスタイン」モンスターを특수소환する
					CALL FULLRESET
					효과範囲덱 = 1
					효과範囲묘지 = 1
					효과条件カテゴリ = ホルスタイン
					CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
					
					IF RESULT != -1
						選択カードID = RESULT:1
						CALL HORUSTEIN_GET_DATA(듀얼리스트,カード소환先,選択カードID)
						;◆小さな胸でも違和感のない描写にする
						SELECTCASE 選択カードID
						CASE 179556
							;美乳
							PRINTFORMW %RESULTS:0%の美しい乳がしなやかに揺れた…
							CALL HORUSTEIN_RAND_COW_TXT(1,0)
						CASE 179557
							;愛乳
							PRINTFORMW %RESULTS:0%の可愛らしい乳首がぷるんと震えた…
							CALL HORUSTEIN_RAND_COW_TXT(1,1)
						CASE 179558
							;旨乳
							PRINTFORMW %RESULTS:0%の思わず吸い付きたくなる乳がしっとりと艶めいている…
							CALL HORUSTEIN_RAND_COW_TXT(1,2)
						ENDSELECT
						PRINTFORMW 
					ENDIF
				ENDIF
			ENDIF
			
		ENDIF
		;テスト時はコメント化
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 묘지起動
	
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99556,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,제외行き)
		
		CALL FULLRESET
		효과条件カテゴリ = ホルスタイン
		CALL CARD_ADD_BASIC(듀얼리스트,"묘지",場所)
	ENDIF
ENDIF
