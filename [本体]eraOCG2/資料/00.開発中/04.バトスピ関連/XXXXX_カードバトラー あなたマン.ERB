@CARDNAME_XXXXX,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = カードバトラー %CALLNAME:MASTER%
	CASE "略称"
		IF TALENT:MASTER:성별 == 1
			RESULTS = ＠걸
		ELSEIF TALENT:MASTER:성별 == 2
			RESULTS = ＠マン
		ELSEIF TALENT:MASTER:성별 == 3
			RESULTS = ＠マン걸
		ELSE
	RESULTS = ＠マン
	ENDIF
	CASE "カテゴリ"
		RESULTS:0 = 듀얼리스트
		RESULTS:1 = 創界神
		RESULTS:4 = 배틀 스피리츠
ENDSELECT




@CARD_XXXXX(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
;	CASE "외부자"
;		RETURN 1
	CASE "종류"
		RETURN 초차원
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 전사족
	CASE "レベル"
		SIF TALENT:MASTER:형태 >= 8
			RETURN 10
		SIF TALENT:MASTER:형태 == 7
			RETURN 8
		SIF TALENT:MASTER:형태 == 6
			RETURN 6
		SIF TALENT:MASTER:형태 == 5
			RETURN 4
		SIF TALENT:MASTER:형태 == 4
			RETURN 3
		SIF TALENT:MASTER:형태 == 3
			RETURN 2
		SIF TALENT:MASTER:형태 == 2
			RETURN 1
		SIF TALENT:MASTER:형태 <= 1
			RETURN 1
	CASE "攻撃力"
		SIF TALENT:MASTER:형태 >= 8
			RETURN 4000
		SIF TALENT:MASTER:형태 == 7
			RETURN 3000
		SIF TALENT:MASTER:형태 == 6
			RETURN 2500
		SIF TALENT:MASTER:형태 == 5
			RETURN 2000
		SIF TALENT:MASTER:형태 == 4
			RETURN 1500
		SIF TALENT:MASTER:형태 == 3
			RETURN 1000
		SIF TALENT:MASTER:형태 == 2
			RETURN 500
		SIF TALENT:MASTER:형태 <= 1
			RETURN 0
	CASE "守備力"
		RETURN 0
	CASE "성별"
		IF TALENT:MASTER:성별 == 1
			RETURN 牝性
		ELSEIF TALENT:MASTER:성별 == 2
			RETURN 雄性
		ELSEIF TALENT:MASTER:성별 == 3
			RETURN ふたなり
		ELSE
			RETURN 雄性
		ENDIF
	CASE "生産불가"
		RETURN 1
	CASE "특수불가"
		RETURN 1
	CASE "連動ID"
		RETURN 9990
ENDSELECT

RETURN 0


@CARD_EXPLANATION_XXXXX(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드는 이 카드의 효과로 のみ특수소환할 수 있다。
CALLFORM CARD_ABLE(20,ターン開始時強制)
PRINTL ①：デュエル開始時に자신의 패에 「계약」카드가 존재하지 않고、자신의 덱に「계약」카드가１종류のみの경우에 발동한다。
PRINTL 덱에서 「계약」카드 １장을 패에 넣는다。그 후、손패１장을 덱に加えてシャッフルする。
CALLFORM CARD_ABLE(0,EX덱起動)
PRINTL ②：자신의 EX덱に「創界神」が１종류のみで、
PRINTL 자신필드에 「創界神」が존재하지 않는 경우에 발동할 수 있다。
PRINTL 손패１장을 덱に加え、EX덱의 이 카드를 자신의 마법＆함정 존에 앞면 표시로 세트한다。
CALLFORM CARD_ABLE(0,소환強制誘発)
PRINTL ③：이 카드가 마법 / 함정 존에 존재하는 한、
PRINTL 자신의 몬스터가 EX덱에서 특수소환される度、이 카드에 コア카운터를 １개 놓는다。
CALLFORM CARD_ABLE(0,마함起動)
PRINTL ④：이 카드에 コア카운터が４つ以上乗っている경우에 발동할 수 있다。
PRINTL 이 카드의 コア카운터를 전부 제거하고、이 카드를 턴 종료시까지 마법 / 함정ゾーンから특수 소환한다。
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ⑤：필드의 이 카드는 대상耐性・破壊내성을 持ち、
PRINTL 이 카드가 몬스터 존에 존재하는 한、카드명을 マスターとしても扱う。





@CARDSUMMON_XXXXX(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

RETURN 0


@CARDCAN_XXXXX(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,XXXXX)
SIF RESULT == 1
	RETURN 0

IF 종류 == ターン開始時強制
	SIF 발동済_D1(듀얼리스트,XXXXX)
		RETURN 0
	SIF ゾーン != "EX덱"
		RETURN 0
	SIF ターン数 > 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲손패 = 1
	효과条件カテゴリ = 계약
	효과条件固カテゴリ = 시희
	효과条件固名前起動 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT >= 2
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件カテゴリ = 계약
	효과条件固カテゴリ = 시희
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 계약
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == EX덱起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 손패数(듀얼리스트) == 0
		RETURN 0
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 創界神
	효과条件固名前起動 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT >= 2
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 創界神
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 소환強制誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	SIF 最終소환決者 != 듀얼리스트
		RETURN 0
	SIF 最終소환타입 == ""
		RETURN 0
	SIF 最終소환出処 != "EX덱"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_個Ｃ:場所") < 4
		RETURN 0
	RETURN 1
ELSEIF 종류 == ターン終了時予約
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件名前 = カードバトラー %CALLNAME:MASTER%
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF !GROUPMATCH(ゾーン, "마함","필드")
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_XXXXX(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 카운터数

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == ターン開始時強制
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,XXXXX,20)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 계약
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"손패",ゾーン,場所)	
			選択カード = RESULT
			IF 選択カード != 9999
				CALL CARD_DISPOSE(듀얼리스트,"손패",選択カード,덱行き)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == EX덱起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,XXXXX)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_ADD(듀얼리스트,"EX덱", "마함", 場所,"表側표시")
		ENDIF
	ENDIF
ELSEIF 종류 == 소환強制誘発
	PRINTFORML コアチャージ！あなたにコア카운터が１つ乗りました。
	CALL SET_FIELD_INFO(듀얼리스트,"마함",場所,"個Ｃ",1)
ELSEIF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,XXXXX)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
		CALL CARD_RESERV(듀얼리스트,XXXXX)
	ENDIF
ELSEIF 종류 == ターン終了時予約
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,XXXXX)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件名前 = カードバトラー %CALLNAME:MASTER%
		효과条件표시 = 表側표시
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL CARD_DISPOSE(듀얼리스트,"필드",選択カード,마함行き)
		ENDIF
	ENDIF
ELSEIF 종류 == 지속효과
	IF ゾーン == "마함"
		CALL STATCHANGE_EFFECT(듀얼리스트,"魔自身",場所,"対耐")
		CALL STATCHANGE_EFFECT(듀얼리스트,"魔自身",場所,"破耐")
	ELSEIF ゾーン == "필드"
		CALL FULLRESET
		CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"変化元ID",9990)
		CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"対耐")
		CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"破耐")
	ENDIF
ENDIF

