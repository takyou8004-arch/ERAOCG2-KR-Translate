;簡易블랙잭
;スキルとか使わない完全に運ゲーです, 시간정지でホールカード覗いたりできてもいいかもね
;勝率はルールが本家と違うんでどうなんでしょう……。テストプレイではゲーム内で195分やって-250とかでした（雑魚）
;カードの추가が常に１回しかできないので더블のデメリットがないのはプレイヤーに有利な気がする
;P_HAND   ×広域変数化、プレイヤーの손패, 7まであるけど現状3までしか使いません
;D_HAND   ×広域変数化、딜러の손패, 上に同じく
;DECK     ×広域変数化、52장의 카드를 무작위로 並べてます, でも使うのは上から6枚だけ, 下の카드를 持ってきたりとイカサマに応用できるかも

;카지노作者（morph）による改造
;・HITの可能횟수を増やした
;・カード関連変数の広域変数化＆処理を独立させて汎用化
;　（판정関連を別関数に任せられるのでこの方が便利、ポーカー実装されたし）
;・カウンティングを可能に、プレイヤースキルが影響するよ！
;・仕様上はジョーカーも混ぜられるようになったけど処理側で対応してない（計算めんどいのよ）


;3500
;1200
;200
;50

@CASINO_BLACKJACK
#DIM P_BET        ;賭け金
#DIM ORI_BET      ;最初の賭け金, 더블を選択するとP_BETが2倍になり、続行すると次のゲームでも2倍になったままなので보존用です
#DIM INS          ;インシュアランスの保険金, 賭けてると再度インシュアランスできない
#DIM LPCOUNT      ;ループカウント
#DIM TOTAL, 3     ;
                  ;TOTAL:1  賭け金合計, カモは마지막으로まとめて精算しました
                  ;TOTAL:2  配当合計
#DIM P_WIN        ;プレイヤーの勝利판정用
#DIM CONST DL = 0 ;定数、딜러のカードプレイヤー番号
#DIM CONST PL = 1 ;定数、プレイヤーのカードプレイヤー番号
;参加플레이어는 ふたり
NUM_CARD_PL = 2

PRINTFORML 
IF FLAG:듀얼포인트 <= 0
	PRINTFORML 딜러
	PRINTFORML 　손님, 본 카지노 내에서는 전용 통화 'DP'만 사용하실 수 있습니다
	PRINTFORMW 　번거로우시겠지만 필요 없는 카드를 분해해서 DP를 확보해주시기 바랍니다
	RETURN
ELSE
	PRINTFORML 딜러
	PRINTFORML 　어서 오세요
	PRINTFORML 　여기는 블랙잭 코너입니다
	PRINTFORML 　잔꾀나 속임수는 전혀 통하지 않는, 순수하게 카드 실력으로 승부하는 게임입니다
	PRINTFORMW 　먼저 게임의 베팅 금액을 제시해 주세요
	PRINTFORML 
ENDIF
CALL CASINO_INPUTLOOP("カード賭け金")
P_BET = RESULT
ORI_BET = RESULT
VARSET TOTAL
IF P_BET
	CALL CASINO_MONEY("ＤＰ", -P_BET)
	TOTAL:1 += P_BET
	CALL PLAY_CARDS("SHUFFLE")
	PRINTFORML 
	PRINTFORMW 딜러는 새로운 덱을 꺼내서 셔플했다
ENDIF

WHILE P_BET
	IF NEW_CARD > 44
		CALL PLAY_CARDS("SHUFFLE")
		PRINTFORML 
		PRINTFORML 딜러
		PRINTFORMW 　카드가 얼마 남지 않았으니 셔플하겠습니다
	ENDIF
	TIME += 5
	;패를 リセットして2枚ずつ引きます,INSもリセット
	CALLF CARD_PLAYER("CLEAR_BJ_ALL")
	CALL PLAY_CARDS("RESET_HAND")
	CALL PLAY_CARDS("HIT", DL)
	CALL PLAY_CARDS("HIT", PL)
	CALL PLAY_CARDS("HIT", DL)
	CALL PLAY_CARDS("HIT", PL)
	CALLF CARD_PLAYER("RENEW_BJ_POINT_ALL")
	INS = 0
	P_WIN = 0
	;初期盤面, お互い손패は２장, ホール카드 １장
	DO
		CALL CASINO_DISP("BJ_CLOSE")
		SIF NEW_CARD < NUM_CARD
			PRINTL [1] - 히트
		PRINTL [2] - 스탠드
		SIF NEW_CARD < NUM_CARD && NUM_HAND:PL == 2 && FLAG:듀얼포인트 >= P_BET
			PRINTL [3] - 더블
		IF NUM_HAND:PL == 2 && CARD_CHECK("RANK", CARD_HAND:DL:0) == 1
			IF CARD_PLAYER("BJ_BJ", PL)
				PRINTL [4] - 이븐머니
			ELSEIF !INS && FLAG:듀얼포인트 >= P_BET / 2
				PRINTL [4] - 인슈어런스
			ENDIF
		ENDIF
		PRINTL [0] - 서렌더
		BINPUT
		DRAWLINEFORM ━
		PRINTFORML 딜러
		SELECTCASE RESULT
		CASE 1
		;ヒット枚数を無制限に변경
			SIF NEW_CARD >= NUM_CARD
				GOTO SURRENDER
			PRINTFORMW 　그럼 카드를 받으세요
			CALL PLAY_CARDS("HIT_BJ", PL)
			SIF CARD_PLAYER("GET_BJ_POINT", PL) >= 21
				BREAK
			CONTINUE
		CASE 2
		;スタンド, 即勝負します
		CASE 3
		;더블, 賭け金を２倍にして카드 １장추가, 賭け金を用意できるときのみ
			SIF FLAG:듀얼포인트 < P_BET || NUM_HAND:PL > 2 || NEW_CARD >= NUM_CARD
				GOTO SURRENDER
			PRINTFORML 　알겠습니다
			PRINTFORML 　베팅금은 {P_BET * 2}DP가 됩니다
			PRINTFORMW 　그럼 카드를 받으세요
			CALL PLAY_CARDS("HIT_BJ", PL)
			TOTAL:1 += P_BET
			CALL CASINO_MONEY("ＤＰ", -P_BET)
			P_BET = P_BET * 2
		CASE 4
			IF NUM_HAND:PL == 2 && CARD_CHECK("RANK", CARD_HAND:DL:0) == 1
				IF CARD_PLAYER("GET_BJ_BJ", PL)
				;イーブンマネー, 引き分けが怖いので２倍の配当で我慢する, 気になるのでホール카드를 見せてくれる
				;表の카드가 Ａであなたの패가 ２１点の時のみ
					PRINTFORML 　알겠습니다
					PRINTFORML 　베팅금은 {P_BET * 2}DP가 됩니다
					PRINTFORMW 　그럼 카드를 받으세요
					TOTAL:2 += P_BET * 2
					CALL CASINO_MONEY("ＤＰ", P_BET * 2)
					PRINTFORMW 　참고로 홀 카드는……
					CALL CARD_DISP("CARD", CARD_HAND:DL:1)
					PRINTW
					IF CARD_PLAYER("GET_BJ_BJ", DL)
						PRINTFORMW 후후후、현명한 판단이셨네요
					ELSE
						PRINTFORMW 어머어머、유감이었네요
					ENDIF
					GOTO CHECK_NEXTGAME
				ELSEIF !INS && FLAG:듀얼포인트 >= P_BET / 2
				;インシュアランス, 블랙잭が怖いので保険金を추가BETする
				;表の카드가 ＡでINSをしておらず保険金分のＤＰが用意できるときのみ
				;その後再度選択肢を選ぶ
					PRINTFORML 　알겠습니다
					PRINTFORMW 　내기금의 반、{P_BET / 2}ＤＰ를 보험금으로 추가합니다
					INS = P_BET / 2
					TOTAL:1 += P_BET / 2
					CALL CASINO_MONEY("ＤＰ", -P_BET / 2)
					CONTINUE
				ELSE
					GOTO SURRENDER
				ENDIF
			ELSE
				GOTO SURRENDER
			ENDIF
		CASEELSE
		;手入力したりするとサレンダー, ゲームを降りて半額返してもらう
			$SURRENDER
			PRINTFORML 　어머、내리시는건가요
			PRINTFORMW 　그럼 배당의 {P_BET / 2}ＤＰ를 받아주세요
			TOTAL:2 += P_BET / 2
			CALL CASINO_MONEY("ＤＰ", P_BET / 2)
			GOTO CHECK_NEXTGAME
		ENDSELECT
		BREAK
	LOOP 1
	
	IF NUM_HAND:PL > 2
		CALL CASINO_DISP("BJ_CLOSE")
		IF CARD_PLAYER("GET_BJ_BUST", PL)
			PRINTW 
			PRINTFORML 딜러
			PRINTFORML 　21을 넘겨버리셨네요
			GOTO YOULOSE
		ENDIF
	ENDIF

	;ホール카드를 公開して盤面を見る
	PRINTW 
	PRINTFORML 딜러
	PRINTFORMW 　では카드를 めくります
	PRINTFORML 
	WHILE CARD_PLAYER("GET_BJ_POINT", DL) < 17
		IF NEW_CARD >= 52
			PRINTFORML 딜러
			PRINTFORMW 　이제 카드가 없네요
			BREAK
		ENDIF
		CALL CASINO_DISP("BJ_OPEN")
		PRINTFORML 딜러
		PRINTFORMW 　17점 미만이니 카드를 추가합니다
		CALL PLAY_CARDS("HIT_BJ", DL)
	WEND

	CALL CASINO_DISP("BJ_OPEN")
	;結果の판정
	IF CARD_PLAYER("GET_BJ_BJ", DL)
	;딜러のBJ
		PRINTFORML 딜러
		PRINTFORML 　블랙잭 이었습니다
		IF INS
		;INSしてたらセーフ
			PRINTFORMW 　인슈어런스를 걸고 계셨기에 배당인 {P_BET + INS}ＤＰ를 받아주세요
			TOTAL:2 += P_BET + INS
			CALL CASINO_MONEY("ＤＰ", P_BET + INS)
		ELSEIF CARD_PLAYER("GET_BJ_BJ", PL)
			PRINTFORML 　둘다 블랙잭이기에 무승부입니다
			PRINTFORMW 　배당인 {P_BET}ＤＰ를 받아주세요
			TOTAL:2 += P_BET
			CALL CASINO_MONEY("ＤＰ", P_BET)
		ELSEIF CARD_PLAYER("GET_BJ_POINT", PL) == 21
		;２１でもBJには勝てない
			PRINTFORML 　이쪽은 블랙잭이기에 당신의 패배입니
			PRINTFORMW 　내기금은 모두 이쪽이 가져가겠습니다
		ELSE
			GOTO YOULOSE
		ENDIF
	ELSEIF CARD_PLAYER("GET_BJ_BUST", DL)
	;딜러BUST（ここまで来た時点であなたはBUSTしていない）
		PRINTFORML 딜러
		PRINTFORML 　21을 넘겨버렸기에 당신의 승리입니다
		P_WIN = 1
	ELSE
	;普通の結果
		PRINTFORML 딜러
		IF CARD_PLAYER("GET_BJ_BJ", PL)
		;21POINTだがPL側のみBJが成立した
			PRINTFORML 　그쪽이 블랙잭 이기에 당신의 승리입니다
			P_WIN = 1
		ELSEIF CARD_PLAYER("GET_BJ_POINT", PL) > CARD_PLAYER("GET_BJ_POINT", DL)
			PRINTFORML 　당신의 승리입니다
			P_WIN = 1
		ELSEIF CARD_PLAYER("GET_BJ_POINT", PL) == CARD_PLAYER("GET_BJ_POINT", DL)
		;그 밖의 引き分け
			PRINTFORML 　비겼습니다
			PRINTFORMW 　배당인 {P_BET}ＤＰ를 받아주세요
			TOTAL:2 += P_BET
			CALL CASINO_MONEY("ＤＰ", P_BET)
		ELSE
		;負けはここ
			$YOULOSE
			PRINTFORML 　당신의 패배입니다
			PRINTFORMW 　내기금은 전부 이쪽이 가져가겠습니다
		ENDIF
	ENDIF

	IF P_WIN
		IF CARD_PLAYER("GET_BJ_BJ", PL)
			PRINTFORML 　블랙잭이었기에 배당은 {P_BET * 5 / 2}ＤＰ가 됩니다
			PRINTFORMW 　받아주세요
			TOTAL:2 += P_BET * 5 / 2
			CALL CASINO_MONEY("ＤＰ", P_BET * 5 / 2)
		ELSE
			PRINTFORML 　배당은 {P_BET * 2}ＤＰ가 됩니다
			PRINTFORMW 　받아주세요
			TOTAL:2 += P_BET * 2
			CALL CASINO_MONEY("ＤＰ", P_BET * 2)
		ENDIF
	ENDIF
	
	;ゲームの続行판정
	$CHECK_NEXTGAME
	IF NEMUKE() >= 700
		P_BET = 0
	ELSEIF FLAG:듀얼포인트 <= 0
		P_BET = 0
	ELSE
		PRINTFORML 　자、승부를 계속할까요？
		PRINTFORML 
		CALL CASINO_INPUTLOOP("カード続行")
		SELECTCASE RESULT
		CASE 0
			P_BET = 0
		CASE 1
			IF FLAG:듀얼포인트 < P_BET
				PRINTFORML 딜러
				PRINTFORML 　소지한 ＤＰ가 부족한것같네요
				PRINTFORML 　내기금을 재설정해주세요
				PRINTFORML 
				CALL CASINO_INPUTLOOP("カード賭け金")
				P_BET = RESULT
				ORI_BET = RESULT
				SIF RESULT
					PRINTFORML 
			ELSE
				P_BET = ORI_BET
			ENDIF
		CASE 2
			PRINTFORML 딜러
			PRINTFORML 　그럼 내기금을 입력해주세요
			PRINTFORML 
			CALL CASINO_INPUTLOOP("カード賭け金")
			P_BET = RESULT
			ORI_BET = RESULT
			SIF RESULT
				PRINTFORML 
		ENDSELECT
	ENDIF

	IF P_BET
		TOTAL:1 += P_BET
		CALL CASINO_MONEY("ＤＰ", -P_BET)
		PRINTFORML 딜러
		PRINTFORML 　네、내기금 {P_BET}ＤＰ 확실히 받았습니다
		PRINTFORMW 　그러면 계속해서 잘 부탁드리겠습니다
	ENDIF
WEND

PRINTFORML 
IF NEMUKE() >= 700
	PRINTFORML 딜러
	PRINTFORML 　저, 손님?
	PRINTFORML 　꽤 피곤해 보이시네요
	PRINTFORML 　오늘은 게임을 여기까지 하시는 게 어떨까요
	PRINTFORMW 　다음에 또 오시길 기다리겠습니다
ELSEIF FLAG:듀얼포인트 <= 0
	PRINTFORML 딜러
	PRINTFORML 　DP가 다 떨어지신 것 같네요
	PRINTFORMW 　다음에 또 오시길 기다리겠습니다
ELSEIF P_BET
;지급에 가게가 버틸 수 없을 때
	PRINTFORML 딜러
	PRINTFORML 　죄송하지만 곧 영업 종료입니다
	PRINTFORMW 　다음에 또 오시길 기다리겠습니다
ELSE
	PRINTFORML 딜러
	PRINTFORML 　알겠습니다
	PRINTFORMW 　다음에 또 오시길 기다리겠습니다
ENDIF
CALLF CARD_PLAYER("CLEAR_BJ_ALL")

IF TOTAL:1
	CALL CASINO_MONEY("カモ", TOTAL:1)
	CALL CASINO_DISP("RESULT", TOTAL:1, TOTAL:2)
ENDIF
VARSET TOTAL
