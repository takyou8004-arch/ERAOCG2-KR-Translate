@XYZSUMMON(듀얼리스트,소환カード,표시形式)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 대상者

#DIMS DYNAMIC カード名
#DIMS DYNAMIC 参照先
#DIMS DYNAMIC 표시形式
#DIM DYNAMIC 上級値
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
#DIM DYNAMIC カウンタC
#DIM DYNAMIC EX参照
#DIM DYNAMIC 選択カード,10
#DIM DYNAMIC 選択カード_ID,10
#DIM DYNAMIC 소환カード
#DIM DYNAMIC 소환カード_ID
#DIM DYNAMIC 素材数
#DIM DYNAMIC 重素材数
#DIM DYNAMIC 選択済カード
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 条件完全달성
#DIM DYNAMIC 상대지정カード
#DIM DYNAMIC 現在最小攻撃力
#DIMS DYNAMIC 使用素材名前,99
#DIM DYNAMIC 素材条件レベル설정,99
#DIM DYNAMIC 検出종류,99


#DIM DYNAMIC 素材数起動
#DIM DYNAMIC 효과추가종류보존,20
#DIM DYNAMIC 素材ID起動 ;Anonym추가
#DIM DYNAMIC 보존素材ID

;엑시즈소환

SIF 素材条件素材数起動 >= 1
	素材数起動 = 1
SIF 素材条件素材ID起動 >= 1 ;Anonym추가
	素材ID起動 = 素材条件素材ID起動

FOR カウンタ,0,10
	효과추가종류보존:カウンタ = 효과추가종류:カウンタ
NEXT


VARSET 選択カード, -1
VARSET 使用素材名前

소환カード_ID = GETVAR(@"%듀얼리스트%_EX덱:{소환カード}")

$CHOICE_LOOP
VARSET 条件完全달성

IF 素材条件同レベル起動 >= 1 && 素材数 == 0
	CALL SETSAME_INFO(듀얼리스트,"レベル",素材条件同レベル起動,1)
ELSEIF 素材条件同レベル起動 >= 1 && 素材数 >= 1
	VARSET 素材条件レベル, -1
	素材条件レベル = 素材条件レベル설정
ENDIF


IF 듀얼리스트 == "자신"
	TRYCALLFORM CARDNAME_{GETVAR(@"%듀얼리스트%_EX덱:{소환カード}")}, "名前"
	カード名 = %RESULTS%
	PRINTFORML 소환카드：《%カード名%》

	CUSTOMDRAWLINE —
	PRINTW 소재가 될 몬스터를 골라주세요：
	PRINTFORML 필요소재 수：{素材数}/{必要素材数}
	CUSTOMDRAWLINE —
	PRINTL
ENDIF

CALL CARD_FILTER_EXSS(듀얼리스트,소환カード,엑시즈,選択カード,"EX덱",素材数)
IF 듀얼리스트 == "자신"
	PRINTL
	PRINTFORM 　%"[9999] - 돌아간다",41,LEFT%
	SIF 素材数 >= 2 && 素材条件数自由 == 1
		PRINTL 　[8888] - 결정（이 소재수로 X소환）
	PRINTL
	BINPUT
	選択カード:素材数 = RESULT
ELSE
	選択カード:素材数 = RESULT
	SIF 素材条件数自由 == 1 && 素材数 >= 2 && 選択カード:素材数 == 9999
		選択カード:素材数 = 8888
ENDIF

IF 選択カード:素材数 == 9999
	PRINTFORMW 돌아갑니다。
	VARSET ALT条件
	RETURN 0
ELSEIF INRANGE (選択カード:素材数, 0, 5)
	選択カード_ID:素材数 = GETVAR(@"%듀얼리스트%_필드:(選択カード:素材数)")	
	IF 重ねてＸ == 1
		重素材数 = GETVAR(@"%듀얼리스트%_MZ_Ｘ:(選択カード:素材数)") + 1
	ENDIF
	TRYCALLFORM CARDNAME_{GETVAR(@"選択カード_ID:{素材数}")}, "名前"
	使用素材名前:素材数 = %RESULTS%
	PRINTFORMW %使用素材名前:素材数%를 소재로합니다
	;({素材数+1}体目)

	IF 素材条件同レベル起動 >= 1
		CALL CARD_SELECT(듀얼리스트,選択カード:素材数)
		素材条件レベル설정 = GETVAR(@"%대상者（CS）%_MZ_星:RESULT")
	ENDIF

	IF 必要素材数 >= 3
		VARSET RESULT
		TRYCALLFORM CARD_{GETVAR(@"選択カード_ID:{素材数}")}, "XYZ代用"
		SIF RESULT == 1
			代替素材数 = 必要素材数 - 1
	ENDIF
	IF 素材ID起動 == 素材数 + 1
		CALL CARD_SELECT(듀얼리스트,選択カード:素材数)
		보존素材ID = GETVAR(@"%대상者（CS）%_%参照先（CS）%:{RESULT}")
	ENDIF

	素材数 ++
ENDIF
PRINTFORML
IF 素材数 >= 必要素材数 || (素材数 >= 代替素材数 && 代替素材数 != 0) || 選択カード:素材数 == 8888
	SIF 選択カード:素材数 == 8888
		選択カード:素材数 = -1
	VARSET 代替素材数
	FOR カウンタ,0,素材数
		IF 지정素材名前 != ""
			TRYCALLFORM CARDNAME_{選択カード_ID:カウンタ}, "名前"
			IF MATCH(지정素材名前, RESULTS)
				条件달성 = 1
			ENDIF
		ENDIF
		IF 지정素材カテゴリ != ""
			TRYCALLFORM CARDNAME_{選択カード_ID:カウンタ}, "カテゴリ"
			FOR カウンタC,0,5
				IF RESULTS:カウンタC != ""
					SIF MATCH(RESULTS:カウンタC, 지정素材カテゴリ)
						条件달성 = 1
					SIF MATCH(지정素材カテゴリ, RESULTS:カウンタC)
						条件달성 = 1
				ENDIF
			NEXT
		ENDIF
		IF 지정素材속성 != 0
			TRYCALLFORM CARD_{選択カード_ID:カウンタ}, "속성"
			IF MATCH(지정素材속성, RESULT)
				条件달성 = 1
			ENDIF
		ENDIF
		IF 지정素材종족 != 0
			TRYCALLFORM CARD_{選択カード_ID:カウンタ}, "종족"
			IF MATCH(지정素材종족, RESULT)
				条件달성 = 1
			ENDIF
		ENDIF

		IF 지정素材종류 != 0
			VARSET 検出종류, -1
			TRYCALLFORM CARD_{選択カード_ID:カウンタ}, "종류"
			FOR カウンタC,0,5
				SIF RESULT:カウンタC <= 0
					CONTINUE
				検出종류:カウンタC = RESULT:カウンタC
			NEXT
			FOR カウンタC,0,5
				SIF MATCH(検出종류:カウンタC, 지정素材종류)
					条件달성 = 1
				SIF MATCH(지정素材종류, 検出종류:カウンタC)
					条件달성 = 1
			NEXT
		ENDIF
		CALL CARD_DISPOSE(듀얼리스트,"필드", (選択カード:カウンタ),完全묘지行き)
		CALLFORM CARD_STANBY(選択カード_ID:カウンタ,듀얼리스트,X素材時효과,"묘지")
	NEXT


	PRINTFORML {素材数}체의 몬스터를 오버레이！
	IF 重ねてＸ == 1
		SETVAR @"%듀얼리스트%_MZ_Ｘ:現필드(듀얼리스트)", 重素材数
	ELSE
		SETVAR @"%듀얼리스트%_MZ_Ｘ:現필드(듀얼리스트)", 素材数 - 1
	ENDIF
	SIF 条件달성
		SETVAR @"%듀얼리스트%_MZ_条達:現필드(듀얼리스트)", 2

	;ザポルグ追記20250309
	TRYCALLFORM CARD_FINISHER_SHOKO(듀얼리스트,소환カード_ID)

	CALL MAT_EVENT_CHECK(듀얼리스트,カード名,使用素材名前) 
	TRYCALLFORM CARD_{GETVAR(@"%듀얼리스트%_EX덱:소환カード")}, "レベル"
	PRINTFORMW 엑시즈 소환！랭크{RESULT}！%カード名%！！
	IF 素材数起動 == 1
		SETVAR @"%듀얼리스트%_MZ_汎数:現필드(듀얼리스트)", 素材数
	ENDIF
	IF 보존素材ID > 0 ;Anonym추가
		SETVAR @"%듀얼리스트%_MZ_汎数:現필드(듀얼리스트)", 보존素材ID
	ENDIF
	VARSET 효과추가종류
	FOR カウンタ,0,10
		효과추가종류:カウンタ = 효과추가종류보존:カウンタ
	NEXT

	CALL CARD_EX_SUMMON(듀얼리스트, "EX덱", 소환カード,표시形式)
	TRYCALLFORM CARDSUMMON_AA_{소환カード_ID}
	IF 듀얼리스트 == "자신"
		リザルト：XYZ소환 = 1
	ENDIF
	VARSET ALT条件
	RETURN 1
ELSE
	GOTO CHOICE_LOOP
ENDIF



@XYZCAN(듀얼리스트,소환カード,カードID) 
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カードID
#DIM DYNAMIC 소환カード
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 素材数
#DIM DYNAMIC 選択カード,10

VARSET 選択カード, -1

CALL CARD_EX_CHECK(듀얼리스트,カードID)
SIF RESULT == 1
	RETURN 0

IF 듀얼리스트 == "상대"
	CALL ENEMY_RUTIN_EXSUM(소환カード,カードID,엑시즈) 
	SIF RESULT == 1
		RETURN 0
ENDIF


$CHOICE_LOOP
IF 素材条件同レベル起動 >= 1
	CALL SETSAME_INFO(듀얼리스트,"レベル",素材条件同レベル起動,1)
ENDIF

CALL CARD_FILTER_EXSS(듀얼리스트,소환カード,엑시즈,選択カード,"EX덱",素材数,1)
IF INRANGE(RESULT,1,3) 
	選択カード:素材数 = RESULT:1
	素材数 += RESULT
	IF 素材数 >= 必要素材数
		RETURN 1
	ELSEIF 素材数 >= 2 && 素材条件数自由
		RETURN 1
	ELSE
		GOTO CHOICE_LOOP
	ENDIF
ELSEIF RESULT == 0 || RESULT == 9999
	RETURN 0
ENDIF

