@CARDNAME_1659,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 빛 없는 그림자 아=바오 아 쿠
	CASE "略称"
RESULTS = A Bao A Qu
	CASE "カテゴリ"
RESULTS:0 = 없음
ENDSELECT


@CARD_1659(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 링크
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 2800
	CASE "守備力"
		RETURN 2100
	CASE "성별"
		RETURN 牝性
ENDSELECT


RETURN 0


@CARD_EXPLANATION_1659(종류)
#DIM DYNAMIC 종류

PRINTL 악마족 몬스터를 포함하는 몬스터 2장 이상
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,필드起動,フリー誘発)
PRINTL ①: 자신 상대의 메인 페이즈에, 손패 1장을 버리고, 이하의 효과에서 1개를 선택하여 발동할 수 있다.
PRINTL ●필드의 카드 1장을 파괴한다.
PRINTL ●이 카드를 엔드 페이즈까지 제외하고, 자신의 묘지에서 빛 어둠 속성 몬스터 1장을 특수 소환한다.
CALLFORM CARD_ABLE(2,ターン開始時)
PRINTL ②: 자신 스탠바이 페이즈에 발동할 수 있다.
PRINTL 자신 묘지의 몬스터의 종족의 종류 수만큼 자신은 드로우한다.
PRINTL 그 후, 드로우한 수만큼 자신의 손패를 고르고 좋아하는 순서대로 덱의 아래로 되돌린다.

@CARDSUMMON_1659(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要링크数 = 4

IF EX参照 == 0 || EX参照 == 1
	素材条件종족 = 악마족
ELSE
	PRINT
ENDIF


IF EX参照 == 1
	CALL LINCAN(듀얼리스트,소환カード,1659) 
	RETURN RESULT
ENDIF



@CARDCAN_1659(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,1659)
SIF RESULT == 1
	RETURN 0



IF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_1(듀얼리스트,1659)
		RETURN 0
	SIF 손패数(듀얼리스트) < 1
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	
	IF 듀얼리스트 != "자신"
		IF ターンプレイヤー == 듀얼리스트
			SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
				RETURN 0
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件속성:0 = 어둠속성
			효과条件속성:1 = 빛속성
			효과条件종류 = モン全般
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT != 1
				RETURN 0
		ELSE
			CALL FULLRESET
			효과範囲상대필드 = 1
			효과範囲상대마함 = 1
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT != 1
				RETURN 0
		ENDIF
	ENDIF
	RETURN 1

ELSEIF 종류 == ターン開始時
	SIF 발동済_2(듀얼리스트,1659)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_1659(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC ドロー数
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1659,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件속성:0 = 어둠속성
		효과条件속성:1 = 빛속성
		효과条件종류 = モン全般
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			条件달성 ++
		
		SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
			条件달성 ++
		
		CALL FULLRESET
		효과範囲손패 = 1
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			IF 듀얼리스트 == "자신"
				IF 条件달성 != 0
					CALL YN_CHOICE(듀얼리스트,"●필드의 카드 １장을 파괴한다","●이 카드를 소실시켜、자신의 묘지에서 光 어둠속성 몬스터 １장을 특수 소환한다")
				ELSE
					RESULT = 0
				ENDIF
			ELSE
				IF 条件달성 != 0 && ターンプレイヤー == 듀얼리스트
					RESULT = 1
				ELSE
					RESULT = 0
				ENDIF
			ENDIF

			IF RESULT == 0
				CALL FULLRESET
				CALL CARD_REMOVAL_BASIC(듀얼리스트,"両全場単体",場所,"묘지")
			ELSEIF RESULT == 1
				CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,제외行き,1)
				CALL FULLRESET
				효과範囲묘지 = 1
				효과条件속성:0 = 빛속성
				효과条件속성:1 = 어둠속성
				효과条件종류 = モン全般
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == ターン開始時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1659,2)
	IF RESULT == 0
		FOR カウンタ,0,20
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件종족 = カウンタ
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				ドロー数 ++
		NEXT
		CALL CARDDRAW_BASIC(듀얼리스트,ドロー数)
		;うららとかで止められたらおわり
		SIF 손패数(듀얼리스트) < ドロー数
			RETURN 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件複数 = ドロー数
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF
