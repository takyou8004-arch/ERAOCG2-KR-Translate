@CARDNAME_54080,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 지박계례 지오 그렘린
	CASE "略称"
RESULTS = 지오 그렘린
	CASE "カテゴリ"
RESULTS:0 = 지박
ENDSELECT


@CARD_54080(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 싱크로
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 악마족 
	CASE "レベル"
		RETURN 6
	CASE "攻撃力"
		RETURN 2000
	CASE "守備力"
		RETURN 1000
	CASE "성별"
		RETURN 雄性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_54080(종류)
#DIM DYNAMIC 종류

PRINTL 어둠속성튜너 ＋ 튜너 이외의 몬스터 1체 이상
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,필드起動,フリー誘発)
PRINTL ①: 자신 / 상대 메인 페이즈에, 상대 필드의 앞면 표시 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 상대는 이하에서 1개를 고르고, 자신은 그 효과를 적용한다.
PRINTL ●대상 몬스터의 공격력만큼 자신의 LP를 회복한다.
PRINTL ●대상의 몬스터를 파괴한다.
CALLFORM CARD_ABLE(2,필드起動,フリー誘発)
PRINTL ②: 자신 / 상대 배틀 페이즈에 발동할 수 있다.
PRINTL 자신의 패 / 필드 / 묘지의 몬스터를 융합 소재로서 제외하고,
PRINTL "지박" 융합 몬스터 1장을 융합 소환한다.

@CARDSUMMON_54080(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要レベル数 = 6

IF EX参照 == 0 || EX参照 == 1
	素材条件튜너 = 1
	素材条件속성 = 어둠속성
ELSEIF EX参照 >= 10
	素材条件튜너 = -1
ENDIF

IF EX参照 == 1
	CALL SYNCAN(듀얼리스트,소환カード,54080) 
	RETURN RESULT
ENDIF


@CARDCAN_54080(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,54080)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	IF メインフェイズ == 1
		SIF 발동済_1(듀얼리스트,54080)
			RETURN 0
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과条件표시 = 表側표시
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
		RETURN 1

	ELSEIF バトルフェイズ == 1
		SIF 발동済_2(듀얼리스트,54080)
			RETURN 0
		SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
			RETURN 0
		CALL RESETAREA_MAT
		素材範囲손패 = 1
		素材範囲필드 = 1
		素材範囲묘지 = 1
		CALL FULLRESET
		효과範囲EX덱 = 1
		효과条件종류 = 융합
		효과条件カテゴリ = 지박
		효과条件소환可 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
		RETURN 1
	ENDIF
ENDIF

@CARDEFFECT_54080(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 回復量

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動 || 종류 == フリー誘発
	IF メインフェイズ == 1
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,54080,1)
		IF RESULT == 0
			CALL FULLRESET
			효과条件표시 = 表側표시
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"選択")
			IF RESULT:1 != 9999
				選択カード = RESULT:1
				CALL YN_CHOICE(対面者,"●ＬＰを回復させる","●파괴한다")
				IF RESULT == 0
					回復量 = GETVAR(@"%듀얼리스트%_MZ_攻:選択カード")
					CALL LIFE_CHANGE(듀얼리스트,"増加","효과",回復量)
				ELSEIF RESULT == 1
					CALL CARD_DISPOSE(対面者,"필드",選択カード,破壊行き)
				ENDIF
			ENDIF
		ENDIF

	ELSEIF バトルフェイズ == 1
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,54080,2)
		IF RESULT == 0
			CALL RESETAREA_MAT
			素材範囲손패 = 1
			素材範囲필드 = 1
			素材範囲묘지 = 1
			CALL FULLRESET
			효과条件종류 = 융합
			효과条件カテゴリ = 지박
			효과条件소환可 = 1
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
				CALL FUSSUMMON(듀얼리스트,選択カード,제외行き)
			ENDIF
		ENDIF
	ENDIF
ENDIF



