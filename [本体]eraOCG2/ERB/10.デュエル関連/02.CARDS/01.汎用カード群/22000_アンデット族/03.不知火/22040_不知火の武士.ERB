@CARDNAME_22040,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 시라누이의 무사
	CASE "略称"
RESULTS = 시라누이의 무사
	CASE "カテゴリ"
RESULTS:0 = 시라누이
ENDSELECT


@CARD_22040(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 언데드족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1800
	CASE "守備力"
		RETURN 0
	CASE "성별"
		RETURN 雄性
ENDSELECT
RETURN 0

@CARD_EXPLANATION_22040(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①② 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,필드起動,フリー誘発)
PRINTL ①: 자신 / 상대 턴에, 자신의 묘지의 언데드족 몬스터 1장을 제외하고 발동할 수 있다.
PRINTL 이 카드의 공격력은 턴 종료시까지 600 올리고,
CALLFORM CARD_ABLE(10,攻撃時誘発)
PRINTL 이 턴 이 카드가 몬스터와 전투를 실행할 경우 1번만, 그 상대 몬스터를 제외할 수 있다.
CALLFORM CARD_ABLE(2,제외時)
PRINTL ②: 이 카드가 제외되었을 경우,
PRINTL "시라누이의 무사" 이외의, 자신의 묘지에서 제외된 상태의 "시라누이" 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 몬스터를 손패에 넣는다.


@CARDCAN_22040(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,22040)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_1(듀얼리스트,22040)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종족 = 언데드족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻撃時誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_条達:場所") == 0
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	RETURN 1

ELSEIF 종류 == 제외時
	SIF 발동済_2(듀얼리스트,22040)
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과範囲제외 = 1
	효과条件カテゴリ = 시라누이
	효과条件固名前 = 시라누이의 무사
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_22040(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 제외カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,22040,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件종족 = 언데드족
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"一攻",600)
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"条達",1)
		ENDIF
	ENDIF

ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,22040,10)
	IF RESULT == 0
		제외カード = 戦闘場所(듀얼리스트,1)
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",제외カード,"제외")
	ENDIF

ELSEIF 종류 == 제외時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,22040,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과範囲제외 = 1
		효과条件カテゴリ = 시라누이
		효과条件固名前 = 시라누이의 무사
		효과条件종류 = モン全般
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF
