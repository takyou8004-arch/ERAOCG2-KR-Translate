@CARDNAME_70557,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = RUM－아르젠트 카오스 포스
	CASE "略称"
RESULTS = RUM-아르젠트포스
	CASE "カテゴリ"
RESULTS:0 = RUM
ENDSELECT


@CARD_70557(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_70557(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ②의 효과는 듀얼 중에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(0,마함起動 ,손패起動)
PRINTL ①: 자신 필드의 랭크 5 이상의 엑시즈 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 자신 몬스터보다 랭크가 1개 높은, "카오스" 엑시즈 몬스터 1장을,
PRINTL 대상 몬스터 위에 겹쳐서 엑시즈 소환 취급으로 EX 덱에서 특수 소환한다.
CALLFORM CARD_ABLE(20,효과後誘発)
PRINTL ②: 이 카드가 묘지에 존재하는 상태에서, 자신 필드에 랭크 5 이상의 엑시즈 몬스터가 특수 소환되었을 때에 발동할 수 있다.
PRINTL 묘지에 존재하는 "RUM－아르젠트 카오스 포스" 1장을 패에 넣는다.

@CARDCAN_70557(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,70557)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = 엑시즈
	CALL 효과条件レベル_ADV(5,"以上")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF 발동済_D1(듀얼리스트,70557)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	CALL FULLRESET
	효과条件종류 = 엑시즈
	CALL 효과条件レベル_ADV(5,"以上")
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"특수소환")
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_70557(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 설정レベル
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードB
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,70557)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		;通常마법汎用処理
		CALL FULLRESET
		효과条件종류 = 엑시즈
		CALL 효과条件レベル_ADV(5,"以上")
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			설정レベル = GETVAR(@"%듀얼리스트%_MZ_星:選択カード") + 1

			CALL FULLRESET
			효과条件レベル = 설정レベル
			효과条件カテゴリ = 카오스
			효과条件종류 = 엑시즈	
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)

			選択カードB = RESULT
			;戻る
			IF 選択カードB != 9999
				TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カードB}")}(듀얼리스트,選択カードB)
				CALL RESETCONDITION_MAT(1)
				代替素材数 = 1
				重ねてＸ = 1
				素材条件固定 = 選択カード
				TRYCALLFORM XYZSUMMON(듀얼리스트,選択カードB)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 효과後誘発
;	PRINTFORML ここ通過～
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,70557,20)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
효과条件名前 = RUM－아르젠트 카오스 포스
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF
