@CARDNAME_52460,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = D 이어폰
	CASE "略称"
RESULTS = D 이어폰
	CASE "カテゴリ"
RESULTS:0 = 디포머
ENDSELECT


@CARD_52460(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
	
		RETURN 싱크로
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1200
	CASE "守備力"
		RETURN 1800
ENDSELECT

RETURN 0

@CARD_EXPLANATION_52460(종류)
#DIM DYNAMIC 종류

PRINTL 튜너 ＋ 튜너 이외의 몬스터 1체 이상
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,특수소환時 ,EX소환時)
PRINTL ①: 이 카드가 특수 소환했을 경우에 발동할 수 있다.
PRINTL 필드의 몬스터 1체를 고르고, 그 몬스터를 이 턴 튜너로 취급한다.

CALLFORM CARD_ABLE(2,필드起動,묘지起動)
PRINTL ②: 자신 필드의 "D 이어폰" 이외의 싱크로 몬스터 1체를 대상으로 하여 발동할 수 있다.
PRINTL 필드 / 묘지의 이 카드를 그 카드에 장비한다.
CALLFORM CARD_ABLE(10,손패起動)
PRINTL ③: 이 카드를 장비하고 있는 몬스터가 전투로 상대 몬스터를 파괴했을 경우,
PRINTL 그 몬스터는 이 턴, 1번 더 공격할 수 있다.


@CARDSUMMON_52460(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要レベル数 = 4
IF EX参照 == 0 || EX参照 == 1
	素材条件튜너 = 1
ELSEIF EX参照 >= 10
	素材条件튜너 = -1
ENDIF

IF EX参照 == 1
	CALL SYNCAN(듀얼리스트,소환カード,52460) 
	RETURN RESULT
ENDIF


@CARDCAN_52460(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,52460)
SIF RESULT == 1
	RETURN 0

IF 종류 == 특수소환時 || 종류 == EX소환時
	SIF 발동済_1(듀얼리스트,52460)
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,52460)
		RETURN 0
	SIF ゾーン != "필드" && ゾーン != "묘지"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = 싱크로
	효과条件他所 = 1
	효과条件固名前 = D 이어폰
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃後強制誘発
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%装備者%_필드:装備先") == -1
		RETURN 0
	SIF GETVAR(@"%装備者%_戦闘カード") != 装備先
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_52460(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

IF 종류 == 특수소환時 || 종류 == EX소환時
	;ありがとうロ-リエ
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52460,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_CHOOSE_BASIC(듀얼리스트, "필드",ゾーン,場所)	
		選択カード = RESULT
		IF 選択カード != 9999
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"一Ｔ",1,1)
		ENDIF
	ENDIF
		
ELSEIF 종류 == 필드起動 || 종류 == 묘지起動
	;参考先：ブラメ
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52460,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件종류 = 싱크로
		효과条件固名前 = D 이어폰
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL CARD_EQUIP_MONSTER(듀얼리스트,選択カード,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 攻撃後強制誘発
	;参考先：トルピ-ド、ニンアルル
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52460,10)
	IF RESULT == 0
		CALL STATCHANGE_EFFECT(듀얼리스트,"装備",場所,"戦",1)
	ENDIF
ENDIF



