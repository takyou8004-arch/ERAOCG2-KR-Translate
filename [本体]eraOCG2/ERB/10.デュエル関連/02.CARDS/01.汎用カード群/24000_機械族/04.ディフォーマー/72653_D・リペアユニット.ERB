@CARDNAME_72653,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = D·리페어유니트
	CASE "略称"
RESULTS = D·리페어
	CASE "カテゴリ"
RESULTS:0 = 디포머-
ENDSELECT


@CARD_72653(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 装備마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_72653(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ③의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(0,마함起動,손패起動)
PRINTL ①: 패에서 「디포머-」 카드 1장을 묘지로 보내고,
PRINTL 자신 묘지의 기계족 몬스터 1장을 대상으로 발동할 수 있다.
PRINTL 그 몬스터를 특수 소환하고, 이 카드를 장착한다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ②: 이 카드를 장착하고 있는 몬스터는 표시 형식을 변경할 수 없다.
CALLFORM CARD_ABLE(1,戦破壊→묘지)
PRINTL ③: 이 카드가 필드에서 묘지로 보내졌을 경우에 발동할 수 있다.
PRINTL 자신 제외 상태인 「디포머-」 몬스터 1장을 특수 소환한다.

@CARDCAN_72653(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,72653)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종족 = 기계족
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件カテゴリ = 디포머
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 戦破壊→묘지
	SIF !GROUPMATCH(묘지送り종류,"リリース","破壊","戦闘破壊")
		RETURN 0
	;参考先：성검
	SIF 발동済_1(듀얼리스트,72653)
		RETURN 0
	CALL FULLRESET
	효과範囲제외 = 1
	효과条件カテゴリ = 디포머
	효과条件종류 = モン全般
	효과条件소환可 = 2
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_72653(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者

#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先
#DIM DYNAMIC 破壊カード
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備先 = RESULT	
装備者 = %RESULTS%


IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,72653)
	IF RESULT == 0
		;패에서 발동後、마법トラップ 존에 カード이동
		;패를 捨てる
PRINTL 버릴 손패를 선택해 주세요
		CALL FULLRESET
		효과条件カテゴリ = 디포머
		효과条件他所 = 1
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		IF RESULT == 1
			;D특수소환
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件종족 = 기계족
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)

			;装備、フル-レから引用
			CALL FULLRESET
			효과範囲필드 = 1
			효과条件固定 = カード소환先
			CALLFORM CARD_EQUIP_ADV(듀얼리스트,場所,ゾーン)
		ENDIF
	ENDIF

ELSEIF 종류 == 戦破壊→묘지
	;参考先：성검、이차원으로부터의 귀환
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,72653,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲제외 = 1
		효과条件カテゴリ = 디포머
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"装備",場所,"表固")
ENDIF

