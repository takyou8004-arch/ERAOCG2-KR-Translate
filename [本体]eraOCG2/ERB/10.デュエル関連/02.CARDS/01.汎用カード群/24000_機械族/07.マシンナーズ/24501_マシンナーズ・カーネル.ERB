@CARDNAME_24501,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 머시너즈 카넬
	CASE "略称"
		RESULTS = 기갑 카넬
	CASE "カテゴリ"
		RESULTS:0 = 머시너즈
ENDSELECT


@CARD_24501(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
	
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 2500
	CASE "소환불가"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_24501(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드는 일반 소환할 수 없으며, 카드 효과로만 특수 소환할 수 있다.
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(2,필드起動 ,フリー誘発)
PRINTL ①: 자신 / 상대 턴에, 자신 필드의 기계족 몬스터 1장을 대상으로 하고 발동할 수 있다.
PRINTL 그 기계족 몬스터와, 그 공격력 이하의 공격력을 가진 상대 필드의 몬스터를 전부 파괴한다.
CALLFORM CARD_ABLE(1,효과後誘発)
PRINTL ②: 이 카드가 묘지에 존재하는 상태에서, "머시너즈 카넬" 이외의
PRINTL 자신 필드의 앞면 표시 기계족 땅 속성 몬스터가 전투 / 효과로 파괴되었을 경우에 발동할 수 있다.
PRINTL 이 카드를 특수 소환한다.

@CARDCAN_24501(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,24501)
SIF RESULT == 1
	RETURN 0

IF 종류 == 효과後誘発
	SIF 발동済_1(듀얼리스트,24501)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,24501,ゾーン)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과条件속성 = 땅속성
	효과条件종족 = 기계족
	효과条件固名前 = 머시너즈 카넬
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"破壊全般")
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,24501)
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	RETURN 1
ENDIF

@CARDEFFECT_24501(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24501,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24501,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件종족 = 기계족
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			攻撃力修正 = GETVAR(@"%듀얼리스트%_MZ_攻:選択カード")
			CALL CARD_DISPOSE(듀얼리스트,"필드",選択カード,10)
			CALL FULLRESET
			효과条件攻撃力 = 攻撃力修正
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드全体",場所,"묘지")
		ENDIF
	ENDIF
ENDIF


