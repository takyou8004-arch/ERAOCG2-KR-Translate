@CARDNAME_24237,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 기황제 그란엘∞
	CASE "略称"
RESULTS = 그란엘∞
	CASE "カテゴリ"
RESULTS:0 = 기황
		RESULTS:1 = 기황제
		RESULTS:2 = 그란엘
ENDSELECT


@CARD_24237(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
	CASE "소환불가"
		RETURN 1
	CASE "추가日"
		RETURN 250602
ENDSELECT
RETURN 0

@CARD_EXPLANATION_24237(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드는 일반 소환할 수 없다.
PRINTL 이 카드명의 ②의 효과는 1턴에 1번밖에 사용할 수 없다.
CALL CARD_ABLE(0,효과後誘発)
PRINTL ①: 자신 몬스터가 효과로 파괴되었을 경우 발동할 수 있다.
PRINTL 패의 이 카드를 특수 소환한다.
CALLFORM CARD_ABLE(1,필드起動,フリー誘発)
PRINTL ②: 자신 / 상대 턴에, 상대 필드의 싱크로 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 상대 싱크로 몬스터를 이 카드에 장착한다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ③: 이 카드는 일시적인 파괴 내성을 가지며, 공격력 / 수비력은 자신 LP의 절반만큼 올리고,
PRINTL 공격력은 자신의 효과로 장착한 몬스터의 공격력만큼 올린다.
CALLFORM CARD_ABLE(10,발동誘発)
PRINTL ④: 1턴에 1번, 자신의 마법 & 함정 존의 싱크로 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 몬스터를 특수 소환한다.

@CARDCAN_24237(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 효과使用불가,3

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,24237)
SIF RESULT == 1
	RETURN 0

IF 종류 == 효과後誘発
	SIF ゾーン != "손패"
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,24237,"손패")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과条件종류 = モン全般
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"효과破壊")
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_1(듀얼리스트,24237)
		효과使用불가:0 = 1
	SIF 마함数(듀얼리스트) >= 5
		효과使用불가:0 = 1
	CALL FULLRESET
	효과条件종류 = 싱크로
	효과条件표시 = 表側표시
	효과範囲상대필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		효과使用불가:0 = 1

	SIF 종류 == フリー誘発
		효과使用불가:1 = 1
	SIF メインフェイズ != 1
		효과使用불가:1 = 1
	SIF ターンプレイヤー != 듀얼리스트
		효과使用불가:1 = 1
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		효과使用불가:1 = 1
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件종류 = 싱크로
	효과条件표시 = 表側표시
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		효과使用불가:1 = 1

	SIF 효과使用불가:0 == 1 && 효과使用불가:1 == 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과 || 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_24237(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 효과使用불가,3

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24237)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,24237)
		효과使用불가:0 = 1
	SIF 마함数(듀얼리스트) >= 5
		효과使用불가:0 = 1
	CALL FULLRESET
	효과条件종류 = 싱크로
	효과範囲상대필드 = 1
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		효과使用불가:0 = 1

	SIF 종류 == フリー誘発
		효과使用불가:1 = 1

	SIF メインフェイズ != 1
		효과使用불가:1 = 1

	SIF ターンプレイヤー != 듀얼리스트
		효과使用불가:1 = 1

	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		효과使用불가:1 = 1

	CALL FULLRESET
	효과範囲마함 = 1
	효과条件종류 = 싱크로
	효과条件소환可 = 1
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		효과使用불가:1 = 1

	IF 효과使用불가:0 == 0 && 효과使用불가:1 == 0
		CUSTOMDRAWLINE —
PRINTL ＞싱크로 몬스터를 재생하시겠습니까?
		CUSTOMDRAWLINE —
		CALL YN_CHOICE(듀얼리스트)
	ELSEIF 효과使用불가:0 == 1
		CUSTOMDRAWLINE —
PRINTL ＞싱크로 몬스터를 재생합니다
		CUSTOMDRAWLINE —
		WAIT
		RESULT = はい
	ELSEIF 효과使用불가:1 == 1
		RESULT = いいえ
	ENDIF

	IF RESULT == いいえ
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24237,1)
		IF RESULT == 0
			CALL FULLRESET
			효과範囲상대필드 = 1
			효과条件종류 = 싱크로
			효과条件표시 = 表側표시
			CALL CARD_EQUIP_MONSTER_ADV(듀얼리스트,ゾーン,場所,1,24237)
		ENDIF

	ELSEIF RESULT == はい
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24237,10)
		IF RESULT == 0
			CALL FULLRESET
			효과範囲마함 = 1
			효과条件종류 = 싱크로
			효과条件표시 = 表側표시
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF

ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"자신",場所,"一破")

ELSEIF 종류 == 攻守変動
	攻撃力修正 = GETVAR(@"ライフ_%듀얼리스트%") / 2
	CALL STATCHANGE_BASIC(듀얼리스트,"自身",場所,攻撃力修正,攻撃力修正)
ENDIF

