@CARDNAME_25504,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 무사신-토리후네
	CASE "略称"
RESULTS = 무사신 토리후네
	CASE "カテゴリ"
RESULTS:0 = 무사신
ENDSELECT


@CARD_25504(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 야수족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1500
	CASE "守備力"
		RETURN 500
ENDSELECT
RETURN 0

@CARD_EXPLANATION_25504(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,필드起動)
PRINTL ①: 이 카드를 릴리스하고 발동할 수 있다.
PRINTL "무사신-토리후네"를 제외한, "무사신""무사신기" 몬스터 2장을 덱에서 수비 표시로 특수 소환한다.
PRINTL 이 효과로 "무사신" 몬스터는 1장밖에 특수 소환할 수 없으며,
PRINTL "무사신기" 몬스터 2장을 특수 소환할 경우, 그 2장은 종족이 달라야 한다.
CALLFORM CARD_ABLE(2,효과後誘発)
PRINTL ②: 이 카드가 묘지에 존재하고, 자신이 "무사신" 엑시즈 몬스터의 엑시즈 소환에 성공했을 때 발동할 수 있다.
PRINTL 이 카드를 장착 카드로서 그 몬스터에 장착한다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ③: 장착 몬스터가 전투로 파괴하고 묘지로 보낸 몬스터는, 묘지로 가지 않고 제외된다.


@CARDCAN_25504(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,25504)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動
	SIF 발동済_1(듀얼리스트,25504)
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0

	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ:0 = 무사신
	효과条件カテゴリ:1 = 무사신기
	효과条件固名前 = 무사신-토리후네
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 효과後誘発
	SIF ゾーン != "묘지"
		RETURN 0
	SIF 발동済_2(듀얼리스트,25504)
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과条件カテゴリ = 무사신
	효과条件종류 = 엑시즈
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"EX특수소환")
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%装備者%_필드:装備先") == -1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_25504(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード_ID
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先
#DIM DYNAMIC 選択종족
#DIMS DYNAMIC 破壊カード
#DIMS DYNAMIC 選択カテゴリ

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,25504,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ:0 = 무사신
		효과条件カテゴリ:1 = 무사신기
		효과条件固名前 = 무사신-토리후네
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"守備표시")
		IF RESULT == 1
			選択カード_ID = RESULT:1
			TRYCALLFORM CARD_{選択カード_ID},"종족"
			選択종족 = RESULT
			TRYCALLFORM CARDNAME_{選択カード_ID},"カテゴリ"
			選択カテゴリ = %RESULTS%

			IF 選択カテゴリ == "무사신"
				CALL FULLRESET
				효과範囲덱 = 1
				효과条件カテゴリ = 무사신기
				효과条件固名前 = 무사신-토리후네
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"守備표시")
			ELSE
				CALL FULLRESET
				효과範囲덱 = 1
				효과条件スーパーカテゴリ = 무사신
				효과条件カテゴリ = 무사신기
				SIF 選択종족 == 비행야수족
					효과条件종족 = 야수족
				SIF 選択종족 == 야수족
					효과条件종족 = 비행야수족
				효과条件固名前 = 무사신-토리후네
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"守備표시")
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,25504,2)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_EQUIP_MONSTER(듀얼리스트,カード소환先,"묘지",場所)
	ENDIF

ELSEIF 종류 == 지속효과
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트,"装備",場所,"戦除")
ENDIF

