@CARDNAME_21039,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 궁극보옥수 레인보우 드래곤
	CASE "略称"
RESULTS = 궁극보옥수 R D
	CASE "カテゴリ"
RESULTS:0 = 보옥수
ENDSELECT


@CARD_21039(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 0
	CASE "추가日"
		RETURN 250401
ENDSELECT

@CARD_EXPLANATION_21039(종류)
#DIM DYNAMIC 종류


PRINTL 이 카드명의 ①③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,攻撃時誘発)
PRINTL ①: "보옥수" 몬스터가 전투를 실행하는 공격 선언 시에 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환한다.
CALLFORM CARD_ABLE(0,身代효과)
PRINTL ②: 이 카드가 몬스터 존에서 파괴되었을 경우,
PRINTL 묘지로 보내지 않고 자신의 마법 & 함정 존에 앞면 표시로 놓을 수 있다.
CALLFORM CARD_ABLE(3,마함起動)
PRINTL ③: 마법 & 함정 존의 이 카드를 제외하고 발동할 수 있다.
PRINTL 덱에서 레벨 4 이하의 "보옥수" 몬스터 1장을 효과를 무효로 하고 특수 소환하며,
PRINTL 덱에서 "궁극보옥신" 몬스터 1장까지 패에 추가한다.


@CARDCAN_21039(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 효과先
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,21039)
SIF RESULT == 1
	RETURN 0

IF 종류 == 攻撃時誘発
	SIF 발동済_1(듀얼리스트,21039)
		RETURN 0
	SIF ゾーン != "손패"
		RETURN 0
	TRYCALLFORM CARDNAME_{戦闘カードID(듀얼리스트)}("カテゴリ")
	SIF !MATCH(RESULTS,"보옥수")
		RETURN 0
	RETURN 1
ELSEIF 종류 == 身代효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF 破壊대상ゾーン != "필드"
		RETURN 0
	SIF 破壊대상場所 != 場所
		RETURN 0
	SIF !GROUPMATCH(破壊종류,"효과","戦闘")
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動
	SIF 발동済_3(듀얼리스트,21039)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종류 = モン全般
	효과条件カテゴリ = 보옥수
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_21039(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 효과先

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21039,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21039)
	IF RESULT == 0
		IF 破壊종류 == "戦闘"
			CALL CARD_FIN_ID_SET(듀얼리스트,21039,"戦闘破壊")
		ELSE
			CALL CARD_FIN_ID_SET(듀얼리스트,21039,"효과破壊")
		ENDIF
		CALL CARD_DISPOSE(듀얼리스트,"필드",場所,마함行き)
		RETURN 1
	ENDIF
	RETURN 0
ELSEIF 종류 == 마함起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21039,3)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,"마함",場所,제외行き)
		CALL FULLRESET
		효과範囲덱 = 1
		CALLFORM 효과条件レベル_ADV(4,"以下")
		효과条件カテゴリ = 보옥수
		효과추가종류 = 効追無効
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件종류 = モン全般
			효과条件カテゴリ = 궁극보옥신
			효과条件任意 = 1
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ENDIF
