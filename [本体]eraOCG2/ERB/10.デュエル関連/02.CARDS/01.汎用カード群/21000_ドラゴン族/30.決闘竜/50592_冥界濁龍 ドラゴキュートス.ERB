@CARDNAME_50592,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 명계탁룡 드래고큐토스
	CASE "略称"
RESULTS = 드래고큐토스
	CASE "カテゴリ"
RESULTS:0 = 결투룡
ENDSELECT


@CARD_50592(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 싱크로
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 4000
	CASE "守備力"
		RETURN 2000
ENDSELECT

RETURN 0

@CARDSUMMON_50592(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要レベル数 = 10
IF EX参照 == 0 || EX参照 == 1
	素材条件속성 = 어둠속성
	素材条件튜너 = 1
ELSEIF EX参照 >= 10
	素材条件종족:0 = 드래곤족
	素材条件종족:1 = 언데드족
	素材条件튜너 = -1
ENDIF

IF EX参照 == 1
	CALL SYNCAN(듀얼리스트,소환カード,50592) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_50592(종류)
#DIM DYNAMIC 종류

PRINTL 어둠속성튜너＋튜너 이외의 드래곤족 언데드족 몬스터 1체
CALL CARD_ABLE(0,지속효과)
PRINTL ①: 이 카드는 전투로는 파괴되지 않는다.
CALL CARD_ABLE(10,攻撃後誘発)
PRINTL ②: 1턴에 1번, 이 카드의 공격으로 상대 몬스터를 파괴했을 때에 발동할 수 있다.
PRINTL 이 카드는 한 번만 더 이어서 공격할 수 있다.
CALL CARD_ABLE(0,ターン開始時)
PRINTL ③: 자신 스탠바이 페이즈에 상대 필드의 앞면 표시 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 몬스터의 공격력을 절반으로 하고, 그 수치만큼 상대에게 데미지를 준다.


@CARDCAN_50592(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,50592)
SIF RESULT == 1
	RETURN 0

IF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃後誘発
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 直前戦闘破壊ID(듀얼리스트,1) == 0
		RETURN 0
	RETURN 1
ELSEIF 종류 == ターン開始時
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_50592(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"戦耐")
ELSEIF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50592,10)
	IF RESULT == 0
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"戦",1)
	ENDIF
ELSEIF 종류 == ターン開始時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50592)
	IF RESULT == 0
		CALL FULLRESET
		효과条件표시 = 表側표시
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"상대필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			;CALLFORM CARD_{GETVAR(@"%対面者%_필드:選択カード")}("攻撃力")
			攻撃力修正 = GETVAR(@"%対面者%_MZ_攻:選択カード") / 2
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",場所,"永攻DOWN",攻撃力修正)
			CALL LIFE_CHANGE(듀얼리스트,"減少","효과",攻撃力修正,1)
		ENDIF
	ENDIF
ENDIF
