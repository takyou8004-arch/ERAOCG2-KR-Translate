@CARDNAME_21192,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 갑룡－암드 드래곤－
	CASE "略称"
RESULTS = 갑룡 아머드
	CASE "カテゴリ"
RESULTS:0 = 암드 드래곤
ENDSELECT


@CARD_21192(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 바람속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1900
	CASE "守備力"
		RETURN 1400
	CASE "성별"
		RETURN 雄性
	CASE "추가日"
		RETURN 250406
ENDSELECT

RETURN 0

@CARD_EXPLANATION_21192(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ②③의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(0,攻撃後誘発)
PRINTL ①: 이 카드가 전투로 몬스터를 파괴했을 경우에 발동할 수 있다.
PRINTL 패/덱에서 「갑룡－암드 드래곤－」 이외의 레벨 5 이하의 드래곤족 바람 속성 몬스터 1장을 특수 소환한다.
PRINTL 이 효과로 특수 소환한 몬스터는 이 턴에 직접 공격할 수 없다.
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ②: 자신 묘지의 바람 속성 드래곤족 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 자신 필드의 몬스터 1장을 릴리스하고, 대상 몬스터를 특수 소환한다.
CALLFORM CARD_ABLE(3,필드→묘지 ,손패→묘지 ,덱→묘지 ,破壊→묘지)
PRINTL ③: 이 카드가 드래곤족 몬스터의 효과로 묘지로 보내졌을 경우에 발동할 수 있다.
PRINTL 자신 묘지에서 「갑룡－암드 드래곤－」 이외의 「암드 드래곤」 카드 1장을 패에 넣는다.

@CARDCAN_21192(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,21192)
SIF RESULT == 1
	RETURN 0

IF 종류 == 攻撃後誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 直前戦闘破壊ID(듀얼리스트,1) == 0
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲덱 = 1
	효과条件속성 = 바람속성
	효과条件종족 = 드래곤족
	CALLFORM 효과条件レベル_ADV(5,"以下")
	효과条件固名前 = 갑룡－암드 드래곤－
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,21192)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件속성 = 바람속성
	효과条件종족 = 드래곤족
	효과条件소환可 = 2
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드→묘지 || 종류 == 손패→묘지 || 종류 == 덱→묘지 || 종류 == 破壊→묘지
	SIF 발동済_3(듀얼리스트,21192)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	TRYCALLFORM CARD_{処理中_효과ID},"종족"
	SIF !GROUPMATCH(RESULT,드래곤족)
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 암드 드래곤
	효과条件固名前 = 갑룡－암드 드래곤－
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_21192(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

IF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21192)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲덱 = 1
		효과条件속성 = 바람속성
		효과条件종족 = 드래곤족
		CALLFORM 효과条件レベル_ADV(5,"以下")
		효과条件固名前 = 갑룡－암드 드래곤－
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン, 場所)
		SIF RESULT == 1
			CALL SET_FIELD_INFO(듀얼리스트,"필드",カード소환先,"直無",1,1)
	ENDIF

ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21192,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件속성 = 바람속성
		효과条件종족 = 드래곤족
		효과条件소환可 = 2
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"묘지",ゾーン,場所)
		IF RESULT != 9999
			選択カード = RESULT
			CALL FULLRESET
			효과範囲필드 = 1
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				CALL CARD_SP_SUMMON(듀얼리스트,"묘지",選択カード)
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == 필드→묘지 || 종류 == 손패→묘지 || 종류 == 덱→묘지 || 종류 == 破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21192,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 암드 드래곤
		효과条件固名前 = 갑룡－암드 드래곤－
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF

