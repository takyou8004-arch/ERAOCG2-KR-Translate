@CARDNAME_50584,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 슈팅 세이비어 스타 드래곤
	CASE "略称"
RESULTS = S S S D
	CASE "カテゴリ"
RESULTS:0 = 스타더스트
RESULTS:1 = 세이비어
ENDSELECT


@CARD_50584(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 싱크로
	CASE "속성"
		RETURN 바람속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 11
	CASE "攻撃力"
		RETURN 4000
	CASE "守備力"
		RETURN 3300
	CASE "성별"
		RETURN 牝性
	CASE "특수불가"
		RETURN 1
ENDSELECT

RETURN 0

@CARDSUMMON_50584(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要レベル数 = 11
IF EX参照 == 0 || EX参照 == 1
	素材条件튜너 = 1
	素材条件名前:0 = 구세룡 세이비어 드래곤
	素材条件名前:1 = 생각을 모은 룡
ELSEIF EX参照 >= 10
	素材条件カテゴリ = 스타더스트
	素材条件튜너 = -1
ENDIF

IF EX参照 == 1
	CALL SYNCAN(듀얼리스트,소환カード,50584) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_50584(종류)
#DIM DYNAMIC 종류

PRINTL 「구세룡 세이비어 드래곤」＋튜너 이외의 「스타더스트」 몬스터 1장 이상
PRINTL 이 카드는 S소환으로만 특수 소환할 수 없다.
PRINTL 이 카드명의 ①의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,필드起動,フリー誘発)
PRINTL ①: 자신/상대 턴에 발동할 수 있다. 상대 필드의 몬스터 1장을 고르고, 그 효과를 무효로 한다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ②: 이 카드는, 묘지의 「스타더스트」 S 몬스터의 수까지 1번의 배틀 페이즈 중에 추가 공격할 수 있다.
CALLFORM CARD_ABLE(10,발동誘発)
PRINTL ③: 1턴에 1번, 상대가 효과를 발동했을 때에 발동할 수 있다.
PRINTL 이 카드를 소실시키고, 그 발동을 무효로 하고 제외한다.


@CARDCAN_50584(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,50584)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動 || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,50584)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件除去種 = 無効
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所,50584)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 발동誘発
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_50584(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃횟수


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50584,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件표시 = 表側표시
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"無効")
	ENDIF
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종류 = 싱크로
	효과条件カテゴリ = 스타더스트
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	攻撃횟수 = RESULT
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"追戦",攻撃횟수)
ELSEIF 종류 == 발동誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50584,10)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,제외行き,1)
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"발동無効",場所,"제외")
	ENDIF
ENDIF