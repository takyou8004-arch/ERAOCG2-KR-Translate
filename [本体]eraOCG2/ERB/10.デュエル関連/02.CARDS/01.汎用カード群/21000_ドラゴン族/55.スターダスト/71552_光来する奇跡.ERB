@CARDNAME_71552,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 광래하는 기적
	CASE "略称"
RESULTS = 광래하는 기적
	CASE "カテゴリ"
RESULTS:0 = 스타더스트
ENDSELECT


@CARD_71552(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 지속마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_71552(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 카드는 1턴에 1장밖에 발동할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ①: 이 카드의 발동 시의 효과 처리로서,
PRINTL 덱에서 드래곤족 레벨 1 몬스터 1장을 패에 추가할 수 있다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ②: 자신 필드의 "스타더스트" S 몬스터는 완전 내성을 얻는다.
PRINTL ③: S 몬스터가 특수 소환되었을 경우에 발동할 수 있다. 이하의 효과 중 1개를 선택하고 적용한다.
PRINTL 이 턴에, 자신의 "광래하는 기적"의 효과로 같은 효과를 적용할 수 없다.
CALLFORM CARD_ABLE(2,효과後誘発)
PRINTL ● 자신은 덱에서 1장 드로우한다.
CALLFORM CARD_ABLE(3,효과後誘発)
PRINTL ● 패에서 튜너 1장을 특수 소환한다.


@CARDCAN_71552(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,71552)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 발동済_1(듀얼리스트,71552)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종족 = 드래곤족
	효과条件レベル = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF 발동済_2(듀얼리스트,71552) && 발동済_3(듀얼리스트,71552)
		RETURN 0
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 1
		RETURN 0
	CALL FULLRESET
	효과条件종류 = 싱크로
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"특수소환")
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件튜너 = 1
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1 && 발동済_2(듀얼리스트,71552)
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_71552(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM 選択カード


IF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동＆설치まで
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71552,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件종족 = 드래곤족
		효과条件レベル = 1
		CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
	ENDIF
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	효과条件カテゴリ = 스타더스트
	효과条件종류 = 싱크로
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"モ耐")
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"魔耐")
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"함정耐")
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71552)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件튜너 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1 && 발동済_2(듀얼리스트,71552) == 0 && 발동済_3(듀얼리스트,71552) == 0
			CALL YN_CHOICE(듀얼리스트,"●자신은 덱에서 １장드로우한다。","●패에서 튜너１장을 특수 소환한다。")
		ELSEIF 발동済_1(듀얼리스트,71552) == 1
			RESULT = 1
		ELSE
			RESULT = 0
		ENDIF
		IF RESULT == 0
			CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71552,2)
			IF RESULT == 0
				CALL CARDDRAW_BASIC(듀얼리스트,1)
			ENDIF
		ELSEIF RESULT == 1
			CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,71552,3)
			IF RESULT == 0
				CALL FULLRESET
				효과範囲손패 = 1
				효과条件튜너 = 1
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			ENDIF
		ENDIF
	ENDIF
ENDIF