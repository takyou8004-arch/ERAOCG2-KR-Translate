@CARDNAME_21841,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 라이트 엔드 서플리메이션 드래곤
	CASE "略称"
RESULTS = 빛과 승화의 룡
	CASE "カテゴリ"
RESULTS:0 = 없음
ENDSELECT


@CARD_21841(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 2600
	CASE "守備力"
		RETURN 2100
ENDSELECT

RETURN 0

@CARD_EXPLANATION_21841(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②③의 효과는 각각 １턴에 １번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①：EX덱에서 드래곤족 레벨８ 몬스터 １체를 제외하고 발동할 수 있다.
PRINTL 이 카드를 손패에서 특수 소환한다. 이 턴, 자신은 드래곤족 몬스터밖에 특수 소환할 수 없다.
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ②：자신 메인 페이즈에 발동할 수 있다.
PRINTL 자신의 손패 / 필드의 몬스터를 융합 소재로 하고, 융합 몬스터 １체를 융합 소환한다.
CALLFORM CARD_ABLE(3,攻撃時誘発)
PRINTL ③：상대 몬스터의 공격 선언시에 발동할 수 있다.
PRINTL 이 카드의 공격력 / 수비력을 ５００ 내리고, 그 상대 몬스터의 공격력을 １５００ 내린다.

@CARDCAN_21841(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,21841)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,21841)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,21841,"손패")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종족 = 드래곤족
	효과条件レベル = 8
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,21841)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0

	CALL RESETAREA_MAT
	素材範囲손패 = 1
	素材範囲필드 = 1
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 융합
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃時誘発
	SIF 발동済_3(듀얼리스트,21841)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_攻:場所") < 500
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_守:場所") < 500
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_21841(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21841,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件종족 = 드래곤족
		효과条件レベル = 8
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,"손패",場所)
			SS소환可能종족 = 드래곤족
			CALL CARD_SS_RESTRICTION(듀얼리스트)
		ENDIF
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21841,2)
	IF RESULT == 0
		CALL RESETAREA_MAT
		素材範囲손패 = 1
		素材範囲필드 = 1
		CALL FULLRESET
		효과条件종류 = 융합
		효과条件소환可 = 1
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
			CALL FUSSUMMON(듀얼리스트, 選択カード)
		ENDIF
	ENDIF
ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21841,3)
	IF RESULT == 0
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"永攻",-500)
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"永守",-500)
		CALL SET_FIELD_INFO(対面者,"필드",戦闘場所(対面者),"一時",-1500)
	ENDIF
ENDIF
