@CARDNAME_70142,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 원석의 명영
	CASE "略称"
RESULTS = 원석의 명영
	CASE "カテゴリ"
RESULTS:0 = 원석
ENDSELECT


@CARD_70142(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 速攻마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_70142(종류)
#DIM DYNAMIC 종류

CALLFORM CARD_ABLE(0,손패起動,마함起動,フリー誘発)
PRINTL ①: 2000 LP를 지불하고 발동할 수 있다.
PRINTL 턴 종료시까지, 자신의 일반 몬스터 및 자신의 「원석」 몬스터는 전투로는 파괴되지 않는다.
PRINTL 자신 필드에 몬스터가 존재하지 않을 경우, 또한 덱에서 일반 몬스터 1장을 수비 표시로 특수 소환할 수 있다.
CALLFORM CARD_ABLE(0,효과後誘発)
PRINTL ②: 상대가 몬스터를 소환했을 경우에 발동할 수 있다.
PRINTL 묘지의 이 카드를 제외하고, 자신 필드 / 묘지의 일반 몬스터 1장보다 공격력이 낮은 상대 필드의 몬스터 1장을 제외한다.


@CARDCAN_70142(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,70142)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	IF ゾーン == "손패"
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	;SIF 발동済_1(듀얼리스트,70142)
	;	RETURN 0

	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 2000
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲필드 = 1
	효과範囲묘지 = 1
	효과範囲덱 = 1
	효과範囲제외 = 1
	효과条件종류 = 通常モン
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	;SIF 발동済_2(듀얼리스트,70142)
	;	RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"通常소환",1)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲묘지 = 1
	효과条件종류 = 通常モン
	효과条件以攻撃力 = 0
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 短紋章起動
	RETURN 1
ENDIF

@CARDEFFECT_70142(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 攻撃力보존
#DIMS DYNAMIC 対面者
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,70142)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",2000)
		CALL CARD_RESERV(듀얼리스트,70142,短紋章予約)
		IF モンスター数(듀얼리스트) == 0
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件종류 = 通常モン
			효과条件任意 = 1
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"守備표시")
		ENDIF
	ENDIF
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,70142)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲묘지 = 1
		효과条件종류 = 通常モン
		효과条件以攻撃力 = 0
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		選択カード_ID = RESULT:99
		CALL CARD_SELECT(듀얼리스트,RESULT)
		IF 参照先（CS） == "필드"
			攻撃力보존 = GETVAR(@"%듀얼리스트%_MZ_攻:{RESULT}")
		ELSEIF 参照先（CS） == "묘지"
			TRYCALLFORM CARD_{選択カード_ID}, "攻撃力"
			攻撃力보존 = RESULT
		ENDIF
		CALL FULLRESET
		효과条件攻撃力 = 攻撃力보존
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"제외")

	ENDIF
ELSEIF 종류 == 短紋章起動
	CALL FULLRESET
	효과条件スーパーカテゴリ = 원석
	효과条件종류 = 通常モン
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"戦耐")
ENDIF

