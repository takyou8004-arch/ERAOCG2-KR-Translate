@CARDNAME_21202,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 초천신룡 오드아이즈 레볼루션 드래곤
	CASE "略称"
RESULTS = OE Rev 드래곤
	CASE "カテゴリ"
RESULTS:0 = 오드아이즈
ENDSELECT


@CARD_21202(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 12
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
	CASE "성별"
		RETURN 雄性
	CASE "分割"
		RETURN 펜듈럼
	CASE "소환불가"
		RETURN 1
	CASE "특수불가"
		RETURN 1
ENDSELECT
RETURN 0

@CARD_EXPLANATION_21202(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("P_MAGIC")
PRINTL 이 카드명의 ②의 P효과는, 1턴에 1번밖에 발동할 수 없다.
CALLFORM CARD_ABLE(0,손패起動)
PRINTL ①: 이 카드의 지속 마법으로서의 발동 시의 효과 처리로서, 500 LP를 지불한다.
PRINTL 그 후 덱에서 레벨 8 이하의 드래곤족 P 몬스터 1장을 패에 넣는다.
CALLFORM CARD_ABLE(1,마함起動)
PRINTL ②: 자신의 묘지의 드래곤족의 융합 / 싱크로 / 엑시즈 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 이 카드를 파괴하고, 그 몬스터를 특수 소환한다.
CALL TEXT_DECORATION("P_MAGIC_2")
PRINTL 이 카드는 일반 소환할 수 없으며, 이 카드의 효과로만 특수 소환할 수 있다.
CALLFORM CARD_ABLE(0,EX덱起動)
PRINTL ①: 필드의 드래곤족의 융합 / 싱크로 / 엑시즈 몬스터를 각각 1장씩 릴리스하고 발동할 수 있다.
PRINTL 이 카드를 EX덱에서 특수 소환한다.
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ②: 이 카드의 공격력 / 수비력은 상대 LP의 절반 수치만큼 상승한다.
CALLFORM CARD_ABLE(10,필드起動)
PRINTL ③: 1턴에 1번, LP를 절반 지불하고 발동할 수 있다.
PRINTL 이 카드 이외의 서로의 필드 / 묘지의 카드를 전부 주인의 덱으로 되돌린다.


@CARDSUMMON_AA_21202


@CARDCAN_21202(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,21202)
SIF RESULT == 1
	RETURN 0
IF 종류 == 마함起動
	SIF 발동済_1(듀얼리스트,21202)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0

	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종류:0 = 융합
	효과条件종류:1 = 싱크로
	효과条件종류:2 = 엑시즈
	효과条件종족 = 드래곤족
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == EX덱起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,21202,"ゾーン",1)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = 융합
	효과条件종족 = 드래곤족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = 싱크로
	효과条件종족 = 드래곤족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = 엑시즈
	효과条件종족 = 드래곤족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	RETURN 1
ELSEIF 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 500
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1

ELSEIF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲상대필드 = 1
	효과範囲마함 = 1
	효과範囲상대마함 = 1
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_21202(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 選択カード名前
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 마함起動
	;②：자신의 묘지의 드래곤족の융합 Ｓ Ｘ 몬스터 １장을 대상으로 하고 발동할 수 있다。
	;이 카드를 파괴해 、그 몬스터를 특수 소환한다。
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21202)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,破壊行き)
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件종류:0 = 융합
		효과条件종류:1 = 싱크로
		효과条件종류:2 = 엑시즈
		효과条件종족 = 드래곤족
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == EX덱起動
	;①：필드의 드래곤족の융합 S X몬스터를 １장ずつリリースして발동할 수 있다。
	;이 카드를 EX덱에서 특수 소환한다。
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21202)
	IF RESULT == 0
		CALL FULLRESET
		효과条件종족 = 드래곤족
		효과条件종류 = 융합
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"필드",ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
		CALL FULLRESET
		효과条件종족 = 드래곤족
		효과条件종류 = 싱크로
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"필드",ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
		CALL FULLRESET
		효과条件종족 = 드래곤족
		효과条件종류 = 엑시즈
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"필드",ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트, "EX덱", 場所)
	ENDIF

ELSEIF 종류 == 손패起動
	;②：이 카드의 지속마법として의 발동時의 효과処理として、５００LPを払う。
	;その後덱에서 レベル８이하의 드래곤족P 몬스터 １장을 패에 넣는다。
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21202)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",500)		
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
		IF RESULT == 1
			CALL FULLRESET
			효과条件종족 = 드래곤족
			효과条件分割 = 펜듈럼
			CALL 효과条件レベル_ADV(8, "以下")
			CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 攻守変動
	;③：이 카드의 攻撃力 守備力は상대의 LPの半分の数値分오른다。
	攻撃力修正 = GETVAR(@"ライフ_%対面者%")
	CALL STATCHANGE_BASIC(듀얼리스트,"自身",場所,攻撃力修正,攻撃力修正)
ELSEIF 종류 == 필드起動
	;④：１턴에 １번、LPを半分払って발동할 수 있다。
	;このカード 이외의 서로의 フィールド 묘지의 카드를 全て주인의 덱으로 되돌린다。
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21202,10)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",(GETVAR(@"ライフ_%듀얼리스트%")  / 2))
		CALL FULLRESET
		효과条件他所 = 1
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"両全場全体",場所,"덱")
	ENDIF
ENDIF
