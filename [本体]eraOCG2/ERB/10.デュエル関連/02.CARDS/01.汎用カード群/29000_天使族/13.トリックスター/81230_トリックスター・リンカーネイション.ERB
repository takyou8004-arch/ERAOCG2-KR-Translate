@CARDNAME_81230,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 트릭스터 리인카네이션
	CASE "略称"
RESULTS = 트스 리인카
	CASE "カテゴリ"
RESULTS:0 = 트릭스터
ENDSELECT


@CARD_81230(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
ENDSELECT

RETURN 0

@CARD_EXPLANATION_81230(종류)
#DIM DYNAMIC 종류

CALLFORM CARD_ABLE(0,마함起動,フリー誘発)
PRINTL ①: 상대의 손패를 모두 제외하고, 그 매수만큼 상대는 드로우한다.
CALLFORM CARD_ABLE(0,묘지起動)
PRINTL ②: 묘지의 이 카드를 제외하고 발동할 수 있다.
PRINTL 자신 묘지의 "트릭스터" 몬스터 1장을 특수 소환한다.



@CARDCAN_81230(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,81230)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == フリー誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	SIF 손패数(対面者) == 0
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF ゾーン != "묘지"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 트릭스터
	효과条件종류 = モン全般
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_81230(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC ドロー枚数
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 마함起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,81230)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL URARA_CHANCE(듀얼리스트,処理中_효과ID,"雑多")

		IF RESULT == 0
			VARSET ドロー枚数
			FOR カウンタ,0,紙束最大値
				SIF GETVAR(@"%対面者%_손패:カウンタ") == -1
					CONTINUE
				CALL CARD_DISPOSE(対面者,"손패",カウンタ,제외行き)
				ドロー枚数 ++
			NEXT
			CALL CARDDRAW_BASIC(対面者,ドロー枚数,2)
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,81230)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 트릭스터
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF
