@CARDNAME_12228,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 에퓨어리 뷰티
	CASE "略称"
		RESULTS = Ep 뷰티
	CASE "カテゴリ"
		RESULTS:0 = 퓨어리
ENDSELECT


@CARD_12228(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 2
	CASE "攻撃力"
		RETURN 1600
	CASE "守備力"
		RETURN 1100
	CASE "성별"
		RETURN 牝性
	CASE "連動ID"
		RETURN 2228
ENDSELECT

RETURN 0

@CARDSUMMON_12228(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 2
素材条件レベル = 2

IF EX参照 == 1
	CALL XYZCAN(듀얼리스트,소환カード,12228) 
	RETURN RESULT
ENDIF

@CARD_EXPLANATION_12228(종류)
#DIM DYNAMIC 종류

PRINTL 레벨 2 몬스터×2
PRINTL 이 카드명의  ①의 효과는 1턴에 1번밖에 사용할 수 없다.
CALL CARD_ABLE(1,필드起動,フリー誘発)
PRINTL ① : 상대 필드의 효과 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 몬스터의 효과를 턴 종료시까지 무효로 한다.
PRINTL (자신의 묘지에 "퓨어리 프리티 메모리」가 존재하고 있는 경우, 이 효과는 상대 턴에서도 발동할 수 있다.)

CALL CARD_ABLE(0,효과後誘発)
PRINTL ② : 1턴에 3번까지 자신이 「퓨어리」마법 카드를 발동했을 경우에 발동할 수 있다.
PRINTL 이 카드의 X소재를 1개 늘린다.
PRINTL 그 후, 상대 필드의 몬스터 1장을 선택해 표시 형식을 변경할 수 있다.

@CARDCAN_12228(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,12228)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_1(듀얼리스트,12228)
		RETURN 0

	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件名前 = 퓨어리 프리티메모리
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT != 1
		SIF 종류 == フリー誘発
			RETURN 0
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
		SIF メインフェイズ != 1
			RETURN 0
	ENDIF
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件除去種 = 無効
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_T1汎数:場所") >= 3
		RETURN 0
	CALL FULLRESET
	효과条件종류 = 마법全般
	효과条件カテゴリ = 퓨어리
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"효과발동")
	SIF RESULT != 1
		RETURN 0
	RETURN 1
;●퓨어리 행복메모리：묘지의 "퓨어리 행복메모리」の수＋１回まで１번のバトルフェイズ中に몬스터에 攻撃할 수 있다。
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件名前 = 퓨어리 행복메모리
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃後強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件名前 = 퓨어리 행복메모리
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
;●퓨어리 딜리셔스메모리：이 카드의 공격력 / 수비력은 、이 카드의 X소재의 수×３００ 올린다。
ELSEIF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件名前 = 퓨어리 딜리셔스메모리
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
;●퓨어리 슬리피메모리：상대 스탠바이 페이즈에 발동할 수 있다。は덱에서 １장 드로우한다。
ELSEIF 종류 == ターン開始時
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件名前 = 퓨어리 슬리피메모리
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1

;●퓨어리 프리티메모리：エンドフェイズ時に발동할 수 있다。상대 필드의 카드 １장을 選び패로 되돌린다。
ELSEIF 종류 == ターン終了時
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件名前 = 퓨어리 프리티메모리
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	CALL FULLRESET
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_12228(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードB
#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 選択カード名前
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 攻撃횟수
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,12228,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"無効")
	ENDIF
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,12228,30)
	IF RESULT == 0
		CALL FULLRESET
		CALL SET_FIELD_INFO(듀얼리스트,ゾーン,場所,"Ｘ",1)
		CALL FULLRESET
		효과추가종류 = 2
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"표시변경")
	ENDIF
;●퓨어리 행복메모리：묘지의 "퓨어리 행복메모리」の수＋１回まで１번のバトルフェイズ中に몬스터에 攻撃할 수 있다。
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件名前 = 퓨어리 행복메모리
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	攻撃횟수 = RESULT
	CALL STATCHANGE_EFFECT(듀얼리스트,"자신",場所,"追戦",攻撃횟수)
ELSEIF 종류 == 攻撃後強制誘発
	CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"戦",-99,1)
;●퓨어리 딜리셔스메모리：이 카드의 공격력 / 수비력은 、이 카드의 X소재의 수×３００ 올린다。
ELSEIF 종류 == 攻守変動
	攻撃力修正 = (GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") + 1) * 300
	CALL STATCHANGE_BASIC(듀얼리스트,"자신",場所,攻撃力修正,攻撃力修正)
;●퓨어리 슬리피메모리：상대 스탠바이 페이즈에 발동할 수 있다。は덱에서 １장 드로우한다。
ELSEIF 종류 == ターン開始時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,12228)
	IF RESULT == 0
		CALL CARDDRAW_BASIC(듀얼리스트,1)
	ENDIF
;●퓨어리 프리티메모리：エンドフェイズ時に발동할 수 있다。상대 필드의 카드 １장을 選び패로 되돌린다。
ELSEIF 종류 == ターン終了時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,12228)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"덱")
	ENDIF
ENDIF
