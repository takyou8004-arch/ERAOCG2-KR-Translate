@CARDNAME_29008,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 도이츠
	CASE "略称"
RESULTS = 도이츠
	CASE "カテゴリ"
RESULTS:0 = 없음
ENDSELECT


@CARD_29008(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
	
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 100
	CASE "守備力"
		RETURN 200
	CASE "分割"
		RETURN 유니온
ENDSELECT

RETURN 0

@CARD_EXPLANATION_29008(종류)
#DIM DYNAMIC 종류

PRINTL "도이츠"의 ③의 효과는 1턴에 1번만 사용할 수 있다.
CALLFORM CARD_ABLE(0,필드起動,손패起動 ,묘지起動)
PRINTL ①: 자신 메인 페이즈에 발동할 수 있다.
PRINTL 패, 필드, 묘지의 "도이츠"을 자신 필드의 "그 녀석"이나 "저 녀석"에게 장착한다.
PRINTL 장착 몬스터가 전투 / 효과로 파괴될 경우, 대신 이 카드를 파괴할 수 있다.
CALLFORM CARD_ABLE(0,攻守変動,지속효과)
PRINTL ②: "도이츠"을 장착하고 있는 몬스터의 공격력은 2500 올린다.
CALLFORM CARD_ABLE(1,攻撃後誘発)
PRINTL ③: "도이츠"을 장착한 몬스터가 전투로 상대 몬스터를 파괴했을 경우에 발동할 수 있다.
PRINTL 덱에서 천사족 몬스터 1장을 묘지로 보낸다.
PRINTL "도이츠"이 "저 녀석"에게 장착되어 있을 경우, 묘지로 보내지 않고 특수 소환할 수 있다.

@CARDCAN_29008(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,29008)
SIF RESULT == 1
	RETURN 0

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

IF 종류 == 필드起動 || 종류 == 손패起動 || 종류 == 묘지起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件名前:0 = 아이츠
	효과条件名前:1 = 코이츠
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == バトル開始時強制
	SIF ゾーン != "마함"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	TRYCALLFORM CARDNAME_{GETVAR(@"%装備者%_필드:装備先")}, "名前"
	SIF RESULTS != "아이츠" && RESULTS != "코이츠"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻撃後誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%装備者%_필드:装備先") == -1
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 装備先
		RETURN 0
	SIF 발동済_1(듀얼리스트,29008)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종족 = 천사족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻守変動
	SIF ゾーン != "마함"
		RETURN 0
	SIF 装備先 == 5
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF 装備先 == 5
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_29008(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

IF 종류 == 필드起動 || 종류 == 손패起動 || 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,29008)
	IF RESULT == 0
		CALL FULLRESET
		효과条件名前:0 = 아이츠
		효과条件名前:1 = 코이츠
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			IF 종류 == 필드起動
				CALL CARD_EQUIP_MONSTER(듀얼리스트,選択カード,"필드",場所,(4))
			ELSEIF 종류 == 손패起動
				CALL CARD_EQUIP_MONSTER(듀얼리스트,選択カード,"손패",場所,(4))
			ELSEIF 종류 == 묘지起動
				CALL CARD_EQUIP_MONSTER(듀얼리스트,選択カード,"묘지",場所,(4))
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == バトル開始時強制
	IF 발동済_2(듀얼리스트,29007)
		PRINTW これで最後！ポテトパワーで攻撃횟수＋１！
	ELSEIF 발동済_1(듀얼리스트,29007)
		PRINTW ジャ-マンパワーでさらに攻撃횟수＋１！
		CALL CARD_EFFECT_USE(듀얼리스트,29007,2)
	ELSE
		PRINTW ドイツパワーで攻撃횟수＋１！
		CALL CARD_EFFECT_USE(듀얼리스트,29007,1)
	ENDIF
	CALL SET_FIELD_INFO(듀얼리스트,"필드",装備先,"戦",1)

ELSEIF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,29008,1)
	IF RESULT == 0
		TRYCALLFORM CARDNAME_{GETVAR(@"%装備者%_필드:装備先")}, "名前"
		IF RESULTS == "코이츠" && モンスター数(듀얼리스트) < 5
			SIF 듀얼리스트 == "자신"
PRINTL 몬스터를 특수 소환하시겠습니까?
			CALL YN_CHOICE(듀얼리스트)
		ELSE
			RESULT = 1
		ENDIF
		IF RESULT == 0
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件종족 = 천사족
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ELSEIF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件종족 = 천사족
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF

ELSEIF 종류 == 攻守変動
	CALL FULLRESET
	CALL STATCHANGE_BASIC(듀얼리스트,"装備",場所,2500)
ENDIF

