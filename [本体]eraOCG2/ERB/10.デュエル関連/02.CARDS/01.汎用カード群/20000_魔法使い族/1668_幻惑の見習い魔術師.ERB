@CARDNAME_1668,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 나이트메어 어프렌티스
	CASE "略称"
RESULTS = 환혹의견습마술사
	CASE "カテゴリ"
RESULTS:0 = 마술사
ENDSELECT


@CARD_1668(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 미확인족
	CASE "レベル"
		RETURN 6
	CASE "攻撃力"
		RETURN 2000
	CASE "守備力"
		RETURN 1700
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_1668(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번만 사용할 수 없다
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 자신 메인 페이즈에 손패 1장을 버리고 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환한다.
CALLFORM CARD_ABLE(2,소환時,특수소환時)
PRINTL ②: 이 카드가 소환 / 특수 소환에 성공했을 경우에 발동할 수 있다.
PRINTL 「나이트메어 어프렌티스」 이외의 미확인족 몬스터 1장을 덱에서 패에 더한다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ③: 이 카드가 몬스터와 전투를 실행할 경우, 그 2체는 그 전투로는 파괴되지 않는다.

@CARDCAN_1668(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,1668)
SIF RESULT == 1
	RETURN 0

IF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,1668)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件固名前 = 나이트메어 어프렌티스
	효과条件종족 = 미확인족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,1668)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,1668,"손패")
	SIF RESULT == 1
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 손패数(듀얼리스트) == 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_1668(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 戦闘カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1668,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件他所 = 1
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)			
		ENDIF
	ENDIF
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1668,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件固名前 = 나이트메어 어프렌티스
		효과条件종족 = 미확인족
		CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
	ENDIF
ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"戦耐")
	CALL STATCHANGE_EFFECT(対面者,"自身",戦闘場所(듀얼리스트,1),"戦耐")
ENDIF
