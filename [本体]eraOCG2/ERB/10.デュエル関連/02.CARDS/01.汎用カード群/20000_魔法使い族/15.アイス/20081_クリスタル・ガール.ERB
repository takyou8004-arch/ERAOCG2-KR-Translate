@CARDNAME_20081,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 크리스탈 걸
	CASE "略称"
RESULTS = 크리스탈 걸
	CASE "カテゴリ"
RESULTS:0 = 블리자드
ENDSELECT


@CARD_20081(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 마법사족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 200
	CASE "守備力"
		RETURN 100
	CASE "성별"
		RETURN 牝性
	CASE "レア度"
		RETURN SPR
	CASE "추가日"
		RETURN 250615
ENDSELECT

RETURN 0

@CARD_EXPLANATION_20081(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,소환時,특수소환時)
PRINTL ①: 이 카드가 소환 / 특수 소환했을 경우에 발동할 수 있다.
PRINTL 이 턴의 엔드 페이즈에, 덱에서 레벨 5 이상의 물 속성 몬스터 1장을 패에 넣는다.
CALLFORM CARD_ABLE(10,필드起動)
PRINTL ②: 1턴에 1번, 자신 메인 페이즈에 발동할 수 있다.
PRINTL 자신의 덱에서 "블리자드" 마법・함정 카드 1장을 패에 넣는다. 그 후, 자신의 패를 1장 골라 묘지로 보낸다.
PRINTL 이 효과로 묘지로 보내진 카드의 효과는 유발하지 않는다.
CALLFORM CARD_ABLE(3,효과後誘発)
PRINTL ③: 자신 필드에 레벨 5 이상의 물 속성 몬스터가 소환 / 특수 소환되었을 경우에 발동할 수 있다.
PRINTL 묘지의 이 카드를 수비 표시로 특수 소환한다. 자신 필드에 레벨 5 이상의 마법사족・물 속성 몬스터가 존재하지 않을 경우,
PRINTL 이 효과로 특수 소환한 이 카드는, 필드에서 벗어났을 경우에 제외된다.

;「아이스 돌」と並んで【블리자드 프린세스】のメインエンジンになる下級モンスター。
;엔드 페이즈에 プリンセスを손패に加えられる효과は出しただけで得られるが、タイミングが若干遅いため상대ターンに構える使い道となる。
;③の自己蘇生が묘지의 起動효과から誘発효과に変わっているのがオシャレポイントで、상대ターンにも蘇生して①の손패アド確保ができるようになっている。
;마법사족・물속성の上級モンスターが필드에 居れば제외されない蘇生になるので、①、②で패를 整えることがより容易になっている。
;②はオリジナルで추가した효과で、結果的に손패は増えないものの名称ターン１ではないターン１であり、何度も出せば複数回使うことが할 수 있다。
;ただし、こ의 효과를 何度も利用する目的でこの몬스터를 묘지送りと蘇生を繰り返しても손패は増えないので結局そんなに旨味はなかったりする。
;とはいえ「블리자드 프린세스」関連の마법・함정카드를 サーチという、テーマの中心に触れるサーチは範囲も十分に優秀なので、
;常識の範囲内のサーチ횟수であれば、テーマ카드를 必要なぶん確保していけるので強いことは間違いない。


@CARDCAN_20081(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件達成

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,20081)
SIF RESULT == 1
	RETURN 0

IF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,20081)
		RETURN 0
	;덱に該当するモンスターが居なくても발동はできるらしい
	;CALL FULLRESET
	;효과範囲덱 = 1
	;효과条件속성 = 물속성
	;CALLFORM 효과条件レベル_ADV(5, "以上")
	;CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	;SIF RESULT != 1
	;	RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종류 = 마함全般
효과条件カテゴリ = 블리자드
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF 발동済_3(듀얼리스트,20081)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,20081,"묘지")
	;자신 필드에 レベル５以上の물속성モンスターが소환・특수소환されたかチェック
	CALL FULLRESET
	효과条件속성 = 물속성
	효과条件종류 = モン全般
	CALLFORM 효과条件レベル_ADV(5, "以上")
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"소환全般")
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == ターン終了時予約
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件속성 = 물속성
	효과条件종류 = モン全般
	CALLFORM 효과条件レベル_ADV(5, "以上")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_20081(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 戦闘カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件達成

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,20081,1)
	IF RESULT == 0
		CALL CARD_RESERV(듀얼리스트,20081)
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,20081,10)
	IF RESULT == 0
		CALL FULLRESET
		효과条件종류 = 마함全般
효과条件カテゴリ = 블리자드
		CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과条件行先플래그 = 完全묘지行き
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,20081,3)
	IF RESULT == 0
		;필드에 条件を満たすモンスターが居るか確認
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件종족 = 마법사족
		효과条件속성 = 물속성
		CALLFORM 효과条件レベル_ADV(5, "以上")
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			;居る場合、묘지의 이 카드를 自己蘇生
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所,"守備표시")
		ELSE
			;居ない場合、묘지의 이 카드를 제외される制約つきで自己蘇生
			CALL FULLRESET
			효과추가종류 = 効追除処
			CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所,"守備표시")
		ENDIF
	ENDIF
ELSEIF 종류 == ターン終了時予約
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,20081)
	IF RESULT == 0
		CALL FULLRESET
		효과条件속성 = 물속성
		효과条件종류 = モン全般
		CALLFORM 효과条件レベル_ADV(5, "以上")
		CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
	ENDIF
ENDIF
