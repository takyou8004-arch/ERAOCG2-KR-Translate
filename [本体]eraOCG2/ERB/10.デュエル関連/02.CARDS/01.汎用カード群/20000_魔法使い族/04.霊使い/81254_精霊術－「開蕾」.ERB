@CARDNAME_81254,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 정령술－「난만」
	CASE "略称"
RESULTS = 정령술－「난만」
	CASE "カテゴリ"
RESULTS:0 = 령사
		RESULTS:1 = 게임 오리지널
ENDSELECT


@CARD_81254(参照先)
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
ENDSELECT
RETURN 0


@CARD_EXPLANATION_81254(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①② 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,발동誘発)
PRINTL ①: 자신 필드의 「령사」 몬스터와 같은 속성의 상대 몬스터의 효과가
PRINTL 패 / 묘지 / 제외 상태에서 발동했을 경우에 발동할 수 있다.
PRINTL 그 발동을 무효로 하고, 발동한 몬스터를 자신 필드에 특수 소환한다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 자신 메인 페이즈에 발동할 수 있다.
PRINTL 묘지의 이 카드를 제외하고, 자신 묘지의 「령사」 카드 또는 공격력 1500 / 수비력 200의 몬스터를 합계 5장 덱으로 되돌린다.
PRINTL 그 후, 자신은 1장 드로우한다.


@CARDCAN_81254(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

#DIM DYNAMIC 대상속성, 8

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,81254)
SIF RESULT == 1
	RETURN 0

IF 종류 == 발동誘発
	SIF 발동済_1(듀얼리스트,81254)
		RETURN 0
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
		RETURN 0

	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲묘지 = 1
	효과範囲제외 = 1
	효과条件종류 = モン全般
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	CALL 複数속성検出(効後_実行者:(効後_カウンタ - 1),効後_ゾーン:(効後_カウンタ - 1),効後_設場所:(効後_カウンタ - 1),대상속성)

	CALL FULLRESET
	효과範囲필드 = 1
효과条件カテゴリ = 령사
	CALL 효과条件속성_複数(대상속성)
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,81254)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件スーパーカテゴリ = 령사
	효과条件固攻撃力 = 1500
	효과条件固守備力 = 200
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 5
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1
ENDIF


@CARDEFFECT_81254(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

IF 종류 == 발동誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,81254,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		SELECTCASE 効後_ゾーン:(処理中番号 - 1)
			CASE "손패"
				CALL CARD_SP_SUMMON(듀얼리스트,"상대손패",効後_設場所:(処理中番号 - 1))
			CASE "묘지"
				CALL CARD_SP_SUMMON(듀얼리스트,"상대묘지",効後_設場所:(処理中番号 - 1))
			CASE "제외"
				CALL CARD_SP_SUMMON(듀얼리스트,"상대제외",効後_設場所:(処理中番号 - 1))
		ENDSELECT

		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"발동無効",場所,"無効")
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,81254,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과条件スーパーカテゴリ = 령사
		효과条件固攻撃力 = 1500
		효과条件固守備力 = 200
		효과条件複数 = 5
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"묘지",ゾーン,場所)
		IF RESULT == 1
			CALL CARDDRAW_BASIC(듀얼리스트,1)
		ENDIF
	ENDIF
ENDIF
