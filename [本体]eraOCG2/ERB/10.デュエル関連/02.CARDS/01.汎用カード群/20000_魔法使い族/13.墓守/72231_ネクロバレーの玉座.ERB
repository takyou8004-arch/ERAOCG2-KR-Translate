@CARDNAME_72231,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 네크로밸리의 옥좌
	CASE "略称"
RESULTS = 네크로밸리 옥좌
	CASE "カテゴリ"
RESULTS:0 = 네크로밸리
ENDSELECT


@CARD_72231(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "추가日"
		RETURN 250409
ENDSELECT

RETURN 0

@CARD_EXPLANATION_72231(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ①: 이하의 효과에서 1개를 선택하여 발동할 수 있다.
PRINTL ● 덱에서 「묘지기」 몬스터 1장을 패에 추가한다.
PRINTL ● 패에서 「묘지기」 몬스터 1장을 특수 소환한다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 자신 메인 페이즈에 묘지의 이 카드를 제외하고 발동할 수 있다. 자신의 패 필드의
PRINTL 몬스터를 융합 소재로 하고, 「묘지기」 융합 몬스터 1장 또는 「아포피스」 융합 몬스터 1장을 융합 소환한다.
PRINTL 이 효과의 발동 후, 턴 종료시까지 자신은 융합 몬스터밖에 EX 덱에서 특수 소환할 수 없다.

;「묘지기」で最も便利な初動札。本家も同様に덱からのサーチと손패からのＳＳを自由に選べる。
;これで初動に足りないカードを自由に持ってこれるので、１枚初動がないながらも盤面は作りやすくなっている。
;カテゴリ지정で융합を行える묘지효과は、영술사を止める動きをされると先攻で盤面の妨害は네크로벨리のみとなりやすいので、
;もう少し융합できるようにオマケでつけた。便利ではあるが、네크로벨리先貼りしてると封印解除するために융합したいのに
;묘지起動が封印されてるから出来ない。という상황になることがままある。そのときは女審신者を一旦出して封印を解除するとよい。

@CARDCAN_72231(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 효과条件플래그 = 0


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,72231)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,72231)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 묘지기
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		효과条件플래그 |= 1
	
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件カテゴリ = 묘지기
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		효과条件플래그 |= 2
	
	SIF 효과条件플래그 & 1 && 효과条件플래그 & 2
		RETURN 0
	
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,72231)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	
	;융합条件
	CALL RESETAREA_MAT
	素材範囲필드 = 1
	素材範囲손패 = 1
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 융합
	효과条件カテゴリ:0 = 묘지기
	효과条件カテゴリ:1 = 아포피스
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_72231(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択結果
#DIM DYNAMIC 효과条件플래그 = 0

IF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,72231,1)
	IF RESULT == 0
		;通常마법汎用処理
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		
		;利用可能な효과を確認
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 묘지기
		효과条件종류 = モン全般
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			효과条件플래그 |= 1
		
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件カテゴリ = 묘지기
		효과条件종류 = モン全般
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			효과条件플래그 |= 2
		
		IF 효과条件플래그 & 1 && 효과条件플래그 & 2
			;両方の효과適用条件を満たしている場合は選択肢を出す(CARDCAN側と意味合いが逆になる)
			IF 듀얼리스트 == "상대"
				;CPUは、両方適用できるなら덱からのサーチを優先
				選択結果 = はい
			ELSE
				CUSTOMDRAWLINE -
PRINTL 네크로밸리의 옥좌의 어느 효과를 적용합니까?
				CUSTOMDRAWLINE -
				CALL YN_CHOICE(듀얼리스트,"●덱から「묘지기」モンスター１体を손패に加える。","●손패から「묘지기」モンスター１体を특수소환する。")
				選択結果 = RESULT
			ENDIF
		ELSEIF 효과条件플래그 & 2
			選択結果 = いいえ
		ELSE
			選択結果 = はい
		ENDIF
		
		IF 選択結果 == はい
			;はい＝묘지기モンスターをサーチ
			CALL FULLRESET
			효과条件カテゴリ = 묘지기
			효과条件종류 = モン全般
			CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
		ELSE
			;いいえ＝손패から묘지기モンスター１体を특수소환
			CALL FULLRESET
			효과範囲손패 = 1
			효과条件カテゴリ = 묘지기
			효과条件종류 = モン全般
			CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,72231,2)
	IF RESULT == 0
		;묘지から제외
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		
		;융합소환の処理
		CALL RESETAREA_MAT
		素材範囲필드 = 1
		素材範囲손패 = 1
		CALL FULLRESET
		효과条件종류 = 융합
		효과条件カテゴリ:0 = 묘지기
		효과条件カテゴリ:1 = 아포피스
		효과条件소환可 = 1	
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
			CALL FUSSUMMON(듀얼리스트, 選択カード)
		ENDIF
		;융합モンスターしかＥＸ덱から특수소환できない。縛り
		EX소환可能종류 = 융합
		CALL CARD_EX_RESTRICTION(듀얼리스트)
	ENDIF
ENDIF

