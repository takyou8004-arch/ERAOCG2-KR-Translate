@CARDNAME_82041,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 위치크래프트 마스터피스
	CASE "略称"
		RESULTS = WC 마스터피스
	CASE "カテゴリ"
		RESULTS:0 = 위치크래프트
ENDSELECT


@CARD_82041(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
ENDSELECT

RETURN 0

@CARD_EXPLANATION_82041(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 한 번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,フリー誘発)
PRINTL ①: 자신 필드에 "위치크래프트" 몬스터가 존재할 경우, 자신 또는 상대의 묘지의 마법 카드 1장을 대상으로 발동할 수 있다.
PRINTL 그 카드 1장을 복제하고, 패에 더한다.
CALLFORM CARD_ABLE(2,묘지起動,フリー誘発)
PRINTL ②: 자신의 묘지에서, 이 카드와 마법 카드를 임의의 수만큼 제외하고 발동할 수 있다.
PRINTL 제외한 마법 카드의 수와 같은 레벨의 "위치크래프트" 몬스터를 덱에서 특수 소환한다.
PRINTL 
;おそらく、①의 효과는 WC마법카드를 持ってきて、コストにしても次のターンも使えるねとか抜かしたい효과なんだと思う。
;もとから（ターン１な以上２回使えるわけでもないし）パッとしない효과なのだが、eraOCGでは使っても毎ターン戻ってくるようになったため、ますますパッとしない。
;しかも無駄に複雑だし。というわけでeraOCG版ミラの要領で마법카드를 복제するようになった。……一気に壊れたな。
;まあ②의 효과も芝や推理ゲ-トみたいな묘지型向き의 효과로 、eraOCGでは덱枚数や実装カード都合で묘지型あまり強くないからいいかな……

@CARDCAN_82041(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 最大レベル

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,82041)
SIF RESULT == 1
	RETURN 0

;起動효과
IF 종류 == 마함起動 || (종류 == フリー誘発 && ゾーン == "마함")
	SIF ゾーン != "마함"
		RETURN 0
	;既に使用してるなら
	SIF 발동済_1(듀얼리스트,82041)
		RETURN 0
	;伏せたターンなら
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
		RETURN 0
	;위치크래프트が場にいるかどうか
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 위치크래프트
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	;いずれかの묘지에 마법카드가 あるかどうかを판정する。
	CALL FULLRESET
	효과条件종류 = 마법全般
	효과範囲묘지 = 1
	효과範囲상대묘지 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 묘지起動 || (종류 == フリー誘発 && ゾーン == "묘지")
	SIF ゾーン != "묘지"
		RETURN 0
	;既に使用してるなら
	SIF 발동済_2(듀얼리스트,82041)
		RETURN 0
	;場が埋まっているならダメ
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	;묘지의 마법카드의 枚数を数える
	;あとでその枚数이하의 위치크래프트が덱にいるのか見るので、0枚でもこの段階では可否を판정しなくてよい
	;……ようで、효과条件レベルの引数が0だと何故かすべてのレベルに対して一致してしまうらしい。結局必要。
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종류 = 마법全般
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	最大レベル = RESULT
	SIF RESULT == 0
		RETURN 0
	;数えて最大レベルに入れた、묘지의 마법카드의 枚数이하의 위치크래프트がいなければFALSE
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 위치크래프트
	CALLFORM 효과条件レベル_ADV(最大レベル, "以下")
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
;	PRINTFORML 蘇生可能チェック結果：{RESULT}
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF




@CARDEFFECT_82041(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC コピー元プレイヤー
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 最大レベル
#DIM DYNAMIC 제외数
#DIMS DYNAMIC 対面者
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


;①의 효과
IF 종류 == 마함起動 || (종류 == フリー誘発 && ゾーン == "마함")
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,82041,1)
	IF RESULT == 0
		;使って묘지에 送られる処理
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과条件종류 = 마법全般
		효과範囲묘지 = 1
		효과範囲상대묘지 = 1
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			SIF INRANGE (選択カード , 200, 299)
				コピー元プレイヤー = %듀얼리스트%
			SIF INRANGE (選択カード , 2200, 2299)
				コピー元プレイヤー = %対面者%
			選択カード %= 100		
			選択カード_ID = GETVAR(@"%コピー元プレイヤー%_묘지:選択カード")
			CALL CARD_GENERATE_HAND(듀얼리스트,選択カード_ID)
		ENDIF
	ENDIF

;②의 효과
ELSEIF 종류 == 묘지起動 || (종류 == フリー誘発 && ゾーン == "묘지")
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,82041,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件종류 = 마법全般
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		最大レベル = RESULT

		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 위치크래프트
		CALLFORM 효과条件レベル_ADV(最大レベル, "以下")
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,場所)	

		CALLFORM CARD_{GETVAR(@"%듀얼리스트%_필드:カード소환先")}, "レベル"
		제외数 = RESULT

		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件종류 = 마법全般
		효과条件複数 = 제외数
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF



