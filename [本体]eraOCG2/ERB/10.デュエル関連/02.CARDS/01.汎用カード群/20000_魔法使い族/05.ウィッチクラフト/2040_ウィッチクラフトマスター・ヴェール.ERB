@CARDNAME_2040,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 위즈크래프트 마스터 베일
	CASE "略称"
RESULTS = WCM 베일
	CASE "カテゴリ"
RESULTS:0 = 위즈크래프트
ENDSELECT


@CARD_2040(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 마법사족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 1000
	CASE "守備力"
		RETURN 2800
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_2040(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
PRINTL ①: 이 카드가 소환/특수 소환에 성공했을 경우 발동할 수 있다.
PRINTL 덱에서 "위즈크래프트" 마법 카드 1장을 패에 넣는다.
CALLFORM CARD_ABLE(2,攻撃時誘発)
PRINTL ②: 자신의 마법사족 몬스터가 상대 몬스터와 전투를 실행할 경우 발동할 수 있다.
PRINTL 그 자신의 몬스터의 공격력 / 수비력은 턴 종료시까지 자신의 패의 마법 카드 수 × 1000 올린다.
CALLFORM CARD_ABLE(3,필드起動,フリー誘発)
PRINTL ③: 자신/상대 턴에, 패에서 마법 카드 1장을 버리고 발동할 수 있다.
PRINTL 상대 필드의 모든 앞면 표시 몬스터의 효과를 턴 종료시까지 무효로 한다.

@CARDCAN_2040(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,2040)
SIF RESULT == 1
	RETURN 0
IF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,2040)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종류 = 마법全般
	효과条件カテゴリ = 위치크래프트
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_3(듀얼리스트,2040)
		RETURN 0
	;위치크래프트モンスター側でWC지속마법の代替コスト효과を対応
	;OR条件なのでどっちか満たしているのが確認できればよい
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종류 = 마법全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT != 1
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件종류 = 지속마법
		효과条件표시 = 表側표시
		효과条件カテゴリ = 위치크래프트
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ENDIF
	CALL FULLRESET
	효과範囲상대필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃時誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,2040)
		RETURN 0
	SIF 戦闘場所(듀얼리스트) == 5
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	TRYCALLFORM CARD_{戦闘カードID(듀얼리스트) },"종족"
	SIF RESULT != 마법사족
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_2040(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%



IF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,2040,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件종류 = 마법全般
		효과条件カテゴリ = 위치크래프트
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,2040,3)
	IF RESULT == 0
		;先に지속마법を代替コストにするか聞く
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件종류 = 지속마법
		효과条件표시 = 表側표시
		효과条件カテゴリ = 위치크래프트
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			;손패に마법カードがあるならどちらをコストにするか尋ねる
			CALL FULLRESET
			효과範囲손패 = 1
			효과条件종류 = 마법全般
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 1
				;손패に마법カードもある
PRINTL 패의 마법 카드를 버리는 대신, 자신 필드의 "위즈크래프트" 지속 마법을 묘지로 보내겠습니까?
				CALL YN_CHOICE(듀얼리스트)
			ELSE
				;この分岐に入っている時点でWC지속마법はあるはずなので、손패の마법カードがなければ強制的に지속마법を捨てることを考える
				RESULT = はい
			ENDIF	
			IF RESULT == はい
				CALL FULLRESET
				효과範囲마함 = 1
				효과条件カテゴリ = 위치크래프트
				효과条件종류 = 지속마법
				효과条件표시 = 表側표시
				CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			ELSE
				;なぜか『はい』が0なので、捨てないなら戻り値がちゃんとFalseになるよう0にしておく。
				RESULT = 0
			ENDIF
		ENDIF
		IF RESULT !=1
			CALL FULLRESET
			효과条件종류 = 마법全般
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		ENDIF
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드全体",場所,"無効")
		ENDIF	
	ENDIF

ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,2040,2)
	IF RESULT == 0
		選択カード = 戦闘場所(듀얼리스트)
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件종류 = 마법全般
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"一攻",(RESULT * 1000))
		CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"一守",(RESULT * 1000))
	ENDIF
ENDIF
