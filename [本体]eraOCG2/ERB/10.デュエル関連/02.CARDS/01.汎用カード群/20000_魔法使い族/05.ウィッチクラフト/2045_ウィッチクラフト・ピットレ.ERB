@CARDNAME_2045,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 위치크래프트 피또레
	CASE "略称"
RESULTS = WC 피또레
	CASE "カテゴリ"
RESULTS:0 = 위치크래프트
ENDSELECT


@CARD_2045(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 마법사족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 1000
	CASE "守備力"
		RETURN 1500
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_2045(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,필드起動,フリー誘発)
PRINTL ①: 자신 / 상대의 메인 페이즈에, 이 카드를 릴리스하고, 패에서 마법 카드 1장을 버리고 발동할 수 있다.
PRINTL 덱에서 "위치크래프트 피또레" 이외의 "위치크래프트" 몬스터 1장을 특수 소환한다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 묘지의 이 카드를 제외하고 발동할 수 있다.
PRINTL 자신은 덱에서 1장 드로우하고, 그 후 패에서 카드 1장을 고르고 묘지로 보낸다.
PRINTL 

@CARDCAN_2045(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,2045)
SIF RESULT == 1
	RETURN 0


IF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_1(듀얼리스트,2045)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 위치크래프트
	효과条件固名前 = 위치크래프트 피또레
	효과条件소환可 = 2
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	;위치크래프트モンスター側でWC지속마법の代替コスト효과를 対応
	;OR条件なのでどっちか満たしているのが確認できればよい
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종류 = 마법全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT != 1
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件종류 = 지속마법
		효과条件표시 = 表側표시
		효과条件カテゴリ = 위치크래프트
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ENDIF
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,2045)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1
ENDIF


@CARDEFFECT_2045(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード


IF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,2045,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		;先に지속마법を代替コストにするか聞く
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件종류 = 지속마법
		효과条件표시 = 表側표시
		효과条件カテゴリ = 위치크래프트
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			;손패に마법카드가 あるならどちらをコストにするか尋ねる
			CALL FULLRESET
			효과範囲손패 = 1
			효과条件종류 = 마법全般
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 1
				;손패に마법 카드もある
PRINTL 패의 마법 카드를 버리는 대신에, 자신 필드의 "위치크래프트" 지속 마법을 묘지로 보냅니까?
				CALL YN_CHOICE(듀얼리스트)
			ELSE
				;この분기に入っている時点でWC지속마법はあるはずなので、손패の마법카드가 なければ強制的に지속마법を捨てることを考える
				RESULT = はい
			ENDIF	
			IF RESULT == はい
				CALL FULLRESET
				효과範囲마함 = 1
				효과条件カテゴリ = 위치크래프트
				효과条件종류 = 지속마법
				효과条件표시 = 表側표시
				CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			ELSE
				;なぜか『はい』が0なので、捨てないなら戻り値がちゃんとFalseになるよう0にしておく。
				RESULT = 0
			ENDIF
		ENDIF
		IF RESULT !=1
			CALL FULLRESET
			효과条件종류 = 마법全般
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		ENDIF
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 위치크래프트
			효과条件固名前 = 위치크래프트 피또레
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ENDIF	
	ENDIF

ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,2045,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL CARDDRAW_BASIC(듀얼리스트,1)
		CALL FULLRESET
		
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"손패",ゾーン,場所)	
		選択カード = RESULT
		IF 選択カード != 9999
			CALL CARD_DISPOSE(듀얼리스트,"손패",選択カード)
		ENDIF
	ENDIF
ENDIF
