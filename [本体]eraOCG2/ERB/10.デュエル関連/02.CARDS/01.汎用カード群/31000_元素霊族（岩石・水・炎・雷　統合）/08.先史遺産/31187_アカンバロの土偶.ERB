@CARDNAME_31187,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 오파츠 아캄바로의 토우
	CASE "略称"
RESULTS = 아캄바로의 토우
	CASE "カテゴリ"
RESULTS:0 = 오파츠
ENDSELECT


@CARD_31187(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 원소정령족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 1000
	CASE "守備力"
		RETURN 1800
	CASE "추가日"
		RETURN 250613
ENDSELECT

RETURN 0

@CARD_EXPLANATION_31187(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ②의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(0,身代효과)
PRINTL ①: 자신 필드의 "오파츠" 몬스터가 전투 / 효과로 파괴될 경우,
PRINTL 대신 패의 이 카드를 버릴 수 있다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 이 카드가 묘지에 존재할 경우, 1000 LP를 지불하고 발동할 수 있다.
PRINTL 이 카드를 특수 소환한다.
PRINTL 이 효과로 특수 소환한 이 카드는 엔드 페이즈에 주인의 패로 되돌아간다.
PRINTL 이 효과를 발동하는 턴, 자신은 "오파츠" 몬스터밖에 EX덱에서 특수 소환할 수 없다.

@CARDCAN_31187(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,31187)
SIF RESULT == 1
	RETURN 0

IF 종류 == 身代효과
	SIF ゾーン != "손패"
		RETURN 0
	SIF 破壊대상ゾーン != "필드"
		RETURN 0
	TRYCALLFORM CARDNAME_{破壊대상ID},"カテゴリ"
	SIF !MATCH(RESULTS,"오파츠")
		RETURN 0
	SIF !GROUPMATCH(破壊종류,"戦闘","효과")
		RETURN 0
	RETURN 1

ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,31187)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 1000
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,31187,ゾーン)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과条件固カテゴリ = 오파츠
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"EX이력")
	SIF RESULT != 0
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_31187(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 攻撃力修正

IF 종류 == 身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,31187)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		RETURN 1
	ENDIF
	RETURN 0

ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,31187,2)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",1000)
		CALL FULLRESET
		효과範囲묘지 = 1
효과条件名前 = 오파츠 아캄바로의 토우
		효과추가종류 = 効追終手
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		CALL CARD_EX_RESTRICTION(듀얼리스트,"오파츠")
	ENDIF
ENDIF


