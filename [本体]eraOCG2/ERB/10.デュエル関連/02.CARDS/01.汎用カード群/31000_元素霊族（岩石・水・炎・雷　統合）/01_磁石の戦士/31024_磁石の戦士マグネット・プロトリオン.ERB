@CARDNAME_31024,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 자석의 전사 마그넷 프로토리온
	CASE "略称"
RESULTS = M 프로토리온
	CASE "カテゴリ"
RESULTS:0 = 마그넷 워리어
ENDSELECT


@CARD_31024(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 원소정령족
	CASE "レベル"
		RETURN 7
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 2100
ENDSELECT

RETURN 0

@CARD_EXPLANATION_31024(종류)
#DIM DYNAMIC 종류

CALLFORM CARD_ABLE(0,지속효과)
PRINTL ①: 필드의 이 카드는 "자석의 전사 마그넷 발키리온"이기도 하다.
CALLFORM CARD_ABLE(0,손패起動)
PRINTL ②: 자신 필드의 땅 속성 몬스터 2장을 릴리스하고 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환한다.
CALLFORM CARD_ABLE(0,필드起動,フリー誘発)
PRINTL ③: 자신, 상대 턴에, 이 카드를 릴리스하고 발동할 수 있다.
PRINTL 자신의 묘지에서 땅 속성 일반 몬스터를 2장까지 선택하여 패에 넣는다.
PRINTL 그 후, 자신의 패 / 필드의 몬스터를 융합 소재로 하고, 땅 속성 융합 몬스터 1장을 융합 소환할 수 있다.
PRINTL 


@CARDCAN_31024(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,31024)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動 
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,31024,"손패",1)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件속성 = 땅속성
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件속성 = 땅속성
	효과条件종류 = 通常モン
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_31024(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,31024)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件속성 = 땅속성
		효과条件複数 = 2
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"필드",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)	
		ENDIF
	ENDIF

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,31024,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)

		CALL FULLRESET
		효과条件종류 = 通常モン
		효과条件속성 = 땅속성
		효과条件複数 = 98
		CALL CARD_ADD_BASIC(듀얼리스트,"묘지",ゾーン,場所)

		CALL RESETAREA_MAT
		素材範囲필드 = 1
		素材範囲손패 = 1
		CALL FULLRESET
		효과範囲EX덱 = 1
		효과条件종류 = 융합
		효과条件속성 = 땅속성
		효과条件소환可 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
PRINTL 몬스터를 융합 소환하시겠습니까?
			CALL YN_CHOICE(듀얼리스트)
		ELSE
			RESULT = いいえ
		ENDIF
		IF RESULT == はい
			CALL RESETAREA_MAT
			素材範囲필드 = 1
			素材範囲손패 = 1
			CALL FULLRESET
			효과条件종류 = 융합
			효과条件속성 = 땅속성
			효과条件소환可 = 1		
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
				CALL FUSSUMMON(듀얼리스트, 選択カード)
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == 지속효과
	CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"変化元ID",31016,上書きモード)
ENDIF
