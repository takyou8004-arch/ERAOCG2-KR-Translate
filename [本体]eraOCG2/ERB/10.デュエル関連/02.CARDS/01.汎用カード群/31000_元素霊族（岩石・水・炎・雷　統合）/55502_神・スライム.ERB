@CARDNAME_55502,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 갓 슬라임
	CASE "略称"
RESULTS = 신 슬라임
	CASE "カテゴリ"
RESULTS:0 = 슬라임
ENDSELECT


@CARD_55502(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 융합
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 원소정령족
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 3000
ENDSELECT
RETURN 0

@CARD_EXPLANATION_55502(종류)
#DIM DYNAMIC 종류

PRINTL 원소정령족 몬스터 + 물속성 레벨 10 몬스터
PRINTL 이 카드는 융합 소환 및 이하의 방법으로만 EX 덱에서 특수 소환할 수 있다.
PRINTL ● 자신 필드의 공격력 0의 원소정령족 레벨 10 몬스터 1장을 릴리스했을 경우에 EX 덱에서 특수 소환할 수 있다.
CALLFORM CARD_ABLE(0,필드起動)
PRINTL ①: 이 카드를 릴리스하고 발동할 수 있다.
PRINTL 패에서 환신야수족 몬스터 1장을 릴리스 없이 소환한다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ②: 이 카드는 전투 내성, 공격 유도, 대상 내성을 가진다.


@CARDSUMMON_AA_55502



@CARDSUMMON_55502(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要素材数 = 2

IF EX参照 == 0 || EX参照 == 1
	素材条件종족 = 원소정령족
ELSEIF EX参照 >= 10
	素材条件속성 = 물속성
	素材条件レベル = 10
ENDIF

IF EX参照 == 1
	;PRINTL サモンまでは言ってるよ
	CALL FUSCAN(듀얼리스트,소환カード,55502) 
	RETURN RESULT
ENDIF

@CARDCAN_55502(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,55502)
SIF RESULT == 1
	RETURN 0


IF 종류 == EX덱起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_EX_CHECK(듀얼리스트,55502)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件攻撃力 = 1
	효과条件종족 = 원소정령족
	효과条件レベル = 10
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종족 = 환신야수족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_55502(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 出処
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件合致数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == EX덱起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,55502)
	IF RESULT == 0
		CALL FULLRESET
		효과条件攻撃力 = 1
		효과条件종족 = 원소정령족
		효과条件レベル = 10
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		SIF 選択カード == 9999
			RETURN 0
		CALL CARD_DISPOSE(듀얼리스트,"필드",選択カード)
		CALL CARD_EX_SUMMON(듀얼리스트,"EX덱",場所)
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,55502)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과条件종족 = 환신야수족
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"손패",ゾーン,場所)
		選択カード = RESULT
		選択カード_ID = RESULT:99
		SIF 選択カード == 9999
			RETURN 0
		CALL CARD_SUMMON(듀얼리스트,選択カード)
		IF 選択カード_ID == 40002
			CALL SET_FIELD_INFO(듀얼리스트,"필드",カード소환先,"永攻",3000)
			CALL SET_FIELD_INFO(듀얼리스트,"필드",カード소환先,"永守",3000)
		ENDIF
	ENDIF
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"戦耐")
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"攻誘")
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"対耐")
ENDIF

