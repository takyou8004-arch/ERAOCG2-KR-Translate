@CARDNAME_35015,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 어비스 샤크
	CASE "略称"
RESULTS = 어비스 샤크
	CASE "カテゴリ"
RESULTS:0 = 샤크
ENDSELECT

@CARD_35015(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 바다생물족
	CASE "レベル"
		RETURN 5
	CASE "Xレベル"
		RESULT:1 = 4
		RETURN 3
	CASE "攻撃力"
		RETURN 1600
	CASE "守備力"
		RETURN 500
ENDSELECT

RETURN 0

@CARD_EXPLANATION_35015(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①② 효과는 1턴에 1번밖에 사용할 수 없다.
CALL CARD_ABLE(1,손패起動)
PRINTL ①: 자신 필드의 몬스터가 물 속성 몬스터뿐일 경우 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환하고, 덱에서 '어비스 샤크' 이외의 레벨 3~5의 해룡족 몬스터 1장을 패에 넣는다.
PRINTL 이 효과의 발동 후, 턴 종료시까지 자신은 엑스트라 덱에서 물 속성 몬스터밖에 특수 소환할 수 없다.
CALL CARD_ABLE(0,攻撃時誘発)
PRINTL ②: 자신의 'No.' 몬스터가 몬스터와 전투를 실행할 경우, 묘지의 이 카드를 제외하고 발동할 수 있다.
PRINTL 이 전투로 주는 전투 데미지는 배가 된다.
CALL CARD_ABLE(0,지속효과)
PRINTL ③: 이 카드를 엑시즈 소환에 사용할 경우, 이 카드의 레벨을 3 또는 4로 취급할 수 있다.



@CARDCAN_35015(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,35015)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,35015)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,35015,"손패")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件속성 = 물속성
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	CALLFORM 효과条件속성_ADV(물속성, "以外")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件レベル:0 = 3
	효과条件レベル:1 = 4
	효과条件レベル:2 = 5
	효과条件종족 = 바다생물족
	효과条件固名前 = 어비스 샤크
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	RETURN 1
ELSEIF 종류 == 攻撃時誘発
	SIF 발동済_2(듀얼리스트,35015)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	TRYCALLFORM CARD_{GETVAR(@"%듀얼리스트%_필드:戦闘場所(듀얼리스트)")}("カテゴリ")
	SIF !GROUPMATCH(RESULTS,"넘버즈")
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_35015(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード星
#DIM DYNAMIC 選択カード_ID
#DIMS DYNAMIC 対面者
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%



IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,35015,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)			
		CALL FULLRESET
		효과条件レベル:0 = 3
		효과条件レベル:1 = 4
		효과条件レベル:2 = 5
		효과条件종족 = 바다생물족
		효과条件固名前 = 어비스 샤크
		CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)


		EX소환可能속성 = 물속성
		CALL CARD_EX_RESTRICTION(듀얼리스트)
	ENDIF
ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,35015,2)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)

		CALL SET_PLAYER_INFO(듀얼리스트,"ダメージ倍化", 3 ,1)
	ENDIF
ENDIF

