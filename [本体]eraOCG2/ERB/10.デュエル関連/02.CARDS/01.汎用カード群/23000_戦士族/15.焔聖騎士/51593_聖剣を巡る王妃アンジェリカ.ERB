@CARDNAME_51593,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 성검을 두른 왕희 안젤리카
	CASE "略称"
RESULTS = 안젤리카
	CASE "カテゴリ"
RESULTS:0 = 성검
RESULTS:1 = 샤를
ENDSELECT


@CARD_51593(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 싱크로
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 전사족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 1200
	CASE "守備力"
		RETURN 2400
	CASE "튜너"
		RETURN 1
	CASE "성별"
		RETURN 牝性
ENDSELECT
RETURN 0

@CARDSUMMON_51593(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要レベル数 = 5
IF EX参照 == 0 || EX参照 == 1
	素材条件튜너 = 1
ELSEIF EX参照 >= 10
	素材条件튜너 = -1
ENDIF

IF EX参照 == 1
	CALL SYNCAN(듀얼리스트,소환カード,51593) 
	RETURN RESULT
ENDIF

@CARD_EXPLANATION_51593(종류)
#DIM DYNAMIC 종류

PRINTL 튜너＋튜너 이외의 몬스터 1장 이상
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,EX소환時 ,특수소환時)
PRINTL ①：이 카드가 특수 소환했을 경우에 발동할 수 있다. 덱에서 「샤를」 카드 1장을 패에 넣는다.
CALLFORM CARD_ABLE(2,효과後誘発 ,攻撃時誘発)
PRINTL ②：다른 카드의 효과가 발동했을 때, 또는 이 카드가 공격 대상으로 선택되었을 때에 발동할 수 있다.
PRINTL 이 카드를 소멸시키고, 덱에서 전사족 화염속성 몬스터 1장을 묘지로 보낸다.
PRINTL 그 후, 덱 / EX덱에서 「롤랑」 몬스터 1장을 특수 소환할 수 있다.


@CARDCAN_51593(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 효과制限달성
#DIM DYNAMIC カウンタ

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,51593)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX소환時 || 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,51593)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 샤를
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,51593)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과条件他所 = 1
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"효과발동")
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종족 = 전사족
	효과条件속성 = 화염속성
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃時誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,51593)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종족 = 전사족
	효과条件속성 = 화염속성
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_51593(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == EX소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51593,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 샤를
		CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
	ENDIF
ELSEIF 종류 == 효과後誘発 || 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51593,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,제외行き,1)
		CALL FULLRESET
		효과条件종족 = 전사족
		효과条件속성 = 화염속성
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"덱",ゾーン,場所)
		IF RESULT == 1
			효과範囲덱 = 1
			효과範囲EX덱 = 1
			효과条件カテゴリ = 롤랑
			효과条件任意 = 1
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ENDIF


