@CARDNAME_92590,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 올리판의 각적
	CASE "略称"
RESULTS = 올리판의 각적
	CASE "カテゴリ"
RESULTS:0 = 샤를
ENDSELECT


@CARD_92590(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"

		RETURN 通常함정
ENDSELECT

RETURN 0

@CARD_EXPLANATION_92590(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 카드는 1턴에 1장밖에 발동할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,フリー誘発)
PRINTL ①: 이하의 효과에서 1개를 선택하여 발동할 수 있다.
PRINTL ●자신 필드 / 묘지에서 장착 마법 카드 1장을 선택하여 제외한다.
PRINTL 그 후, 필드의 카드 1장을 선택하여 파괴할 수 있다.
PRINTL ●자신 필드의 '롤랑' 몬스터 1장을 선택하여 파괴한다. 그 후, 레벨의 합계가 9 이하가 되도록
PRINTL 덱에서 전사족 화염 속성 몬스터를 3장까지 선택하여, 효과를 무효로 하고 수비 표시로 특수 소환한다.
PRINTL 이 효과의 발동 후, 다음 자신 턴의 종료시까지 자신은 전사족 몬스터밖에 특수 소환할 수 없다.


@CARDCAN_92590(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 효과制限달성
#DIM DYNAMIC カウンタ

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,92590)
SIF RESULT == 1
	RETURN 0
IF 종류 == 마함起動 || 종류 == フリー誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
		RETURN 0
	SIF 발동済_1(듀얼리스트,92590)
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과範囲묘지 = 1
	효과条件종류 = 装備마법
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1 || GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		효과制限달성 ++
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 롤랑
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		효과制限달성 ++
	SIF 효과制限달성 == 2
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_92590(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 上限レベル
#DIM DYNAMIC 특수소환횟수

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,92590,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲마함 = 1
		효과範囲묘지 = 1
		효과条件종류 = 装備마법
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1 && GETVAR(@"%듀얼리스트%_제외NG置換") == 0
			CALL FULLRESET
			효과範囲필드 = 1
			효과条件カテゴリ = 롤랑
			효과条件종류 = モン全般
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 1
				CALL YN_CHOICE(듀얼리스트,"●装備마법を제외し、필드의 카드를 파괴한다","●「롤랑」몬스터를 破壊し、전사족 화염속성 몬스터를 ３장까지 특수 소환한다")
			ELSE
				RESULT = はい
			ENDIF
		ELSE
			RESULT = いいえ
		ENDIF
		IF RESULT == はい
			CALL FULLRESET
			효과範囲마함 = 1
			효과範囲묘지 = 1
			효과条件종류 = 装備마법
			효과条件行先플래그 = 제외行き
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				효과条件任意 = 1
				CALL CARD_REMOVAL_BASIC(듀얼리스트,"両全場単体",場所,"묘지")
			ENDIF
		ELSEIF RESULT == いいえ
			CALL FULLRESET
			효과条件カテゴリ = 롤랑
			효과条件종류 = モン全般
			효과条件行先플래그 = 破壊行き
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"필드",ゾーン,場所)
			IF RESULT == 1
				上限レベル = 9
				WHILE 上限レベル > 0
					CALL FULLRESET
					효과範囲덱 = 1
					효과条件종족 = 전사족
					효과条件속성 = 화염속성
					CALLFORM 효과条件レベル_ADV(上限レベル, "以下")
					효과추가종류 = 効追無効
					CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"守備표시")
					TRYCALLFORM CARD_{GETVAR(@"%듀얼리스트%_필드:カード소환先")}, "レベル"
					SIF RESULT < 9 && モンスター数(듀얼리스트) < 5
						上限レベル -= RESULT
					특수소환횟수 ++
					SIF 특수소환횟수 == 3
						上限レベル = 0
				WEND
				SS소환可能종족 = 전사족
				CALL CARD_SS_RESTRICTION(듀얼리스트)
			ENDIF
		ENDIF
	ENDIF
ENDIF
