@CARDNAME_51700,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 피안의 여행자 단테
	CASE "略称"
RESULTS = 단테
	CASE "カテゴリ"
RESULTS:0 = 피안
ENDSELECT


@CARD_51700(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 전사족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 1000
	CASE "守備力"
		RETURN 2500
ENDSELECT

RETURN 0

@CARDSUMMON_51700(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 2
素材条件レベル = 3
;추가효과用

IF EX参照 == 1
	CALL XYZCAN(듀얼리스트,소환カード,51700) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_51700(종류)
#DIM DYNAMIC 종류

PRINTL 레벨 3 몬스터 x 2
CALLFORM CARD_ABLE(10,필드起動)
PRINTL ①: 1턴에 1번, 이 카드의 엑시즈 소재를 1개 제거하고,
PRINTL 자신의 덱 맨 위에서 카드를 3장 묘지로 보내 발동할 수 있다.
PRINTL 이 카드의 공격력은 턴 종료 시까지 1500 올린다.
CALLFORM CARD_ABLE(0,攻撃後強制誘発)
PRINTL ②: 이 카드는 공격했을 경우, 데미지 스텝 종료 시에 수비 표시가 된다.
CALLFORM CARD_ABLE(0,戦破壊→묘지)
PRINTL ③: 이 카드가 묘지로 보내졌을 경우, 자신 묘지의 다른 "피안" 카드 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 카드를 패에 넣는다.

@CARDCAN_51700(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,51700)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") <= -1
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 3
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃後強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_戦:場所") == 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 戦破壊→묘지
	SIF !GROUPMATCH(묘지送り종류,"リリース","破壊","戦闘破壊")
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 피안
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_51700(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51700,10,X消費1)
	IF RESULT == 0
		CALL CARD_DECK_DISPOSE(듀얼리스트,"덱",3)
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"一攻",1500)
	ENDIF
ELSEIF 종류 == 攻撃後強制誘発
	CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"表",2,1)
ELSEIF 종류 == 戦破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,51700)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 피안
		효과条件他所 = 1
		CALL CARD_ADD_BASIC(듀얼리스트,"묘지",ゾーン,場所)
	ENDIF
ENDIF