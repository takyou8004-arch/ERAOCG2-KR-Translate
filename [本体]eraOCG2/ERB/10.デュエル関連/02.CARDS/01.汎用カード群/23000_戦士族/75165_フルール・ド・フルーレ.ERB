@CARDNAME_75165,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 플뢰르 드 플뢰레
	CASE "略称"
RESULTS = 플뢰르 드 플뢰레
	CASE "カテゴリ"
RESULTS:0 = 없음
ENDSELECT


@CARD_75165(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 装備마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_75165(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 카드는 1턴에 1장밖에 발동할 수 없으며,
PRINTL 이 카드명의 ③의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(0,마함起動 ,손패起動)
PRINTL ①: 자신의 묘지의 레벨 2 이하의 몬스터 1장을 대상으로 하여 이 카드를 발동할 수 있다.
PRINTL 그 몬스터를 효과를 무효로 하여 특수 소환하고, 이 카드를 장착한다.
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ②: 장착 몬스터의 공격력은 700 올린다.
CALLFORM CARD_ABLE(3,필드→묘지)
PRINTL ③: 이 카드가 마법 & 함정 존에서 묘지로 보내졌을 경우에 발동할 수 있다.
PRINTL 자신 필드의 S 몬스터 1장을 고르고, 이 카드를 장착한다.

@CARDCAN_75165(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,75165)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動

	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종류 = モン全般
	CALL 효과条件レベル_ADV(2, "以下")
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻守変動
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드→묘지
	SIF 발동済_3(듀얼리스트,75165)
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = 싱크로
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_75165(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 選択カード


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備先 = RESULT	
装備者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,75165)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		CALL 효과条件レベル_ADV(2, "以下")
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT
			CALL FULLRESET
			효과範囲필드 = 1
			효과条件固定 = カード소환先
			CALLFORM CARD_EQUIP_ADV(듀얼리스트,場所,ゾーン)
		ENDIF
	ENDIF
ELSEIF 종류 == 攻守変動
	CALL STATCHANGE_BASIC(듀얼리스트,"装備",場所,700)
ELSEIF 종류 == 필드→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,75165,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件종류 = 싱크로
		CALLFORM CARD_EQUIP_ADV(듀얼리스트,場所,"묘지")
	ENDIF
ENDIF