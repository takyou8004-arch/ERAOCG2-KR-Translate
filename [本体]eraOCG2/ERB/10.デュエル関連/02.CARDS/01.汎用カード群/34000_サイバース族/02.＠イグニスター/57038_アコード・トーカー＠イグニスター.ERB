@CARDNAME_57038,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 어코드 토커＠이그니스터
	CASE "略称"
		RESULTS = ＠코드 토커
	CASE "カテゴリ"
		RESULTS:0 = 코드 토커
		RESULTS:1 = ＠이그니스터
ENDSELECT


@CARD_57038(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 링크
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 사이버스족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 2300
	CASE "守備力"
		RETURN 2300
ENDSELECT
RETURN 0


@CARDSUMMON_57038(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照

CALL RESETCONDITION_MAT
必要링크数 = 5
素材条件종류 = 효과全般
IF EX参照 == 0 || EX参照 == 1 || EX参照 == 10
	素材条件링크:0 = 1
	素材条件링크:1 = 2
ENDIF

IF EX参照 == 1
	CALL LINCAN(듀얼리스트,소환カード,57038) 
	RETURN RESULT
ENDIF

@CARD_EXPLANATION_57038(종류)
#DIM DYNAMIC 종류

PRINTL 효과 몬스터 3장 이상
PRINTL 이 카드명의 ②의 효과는 1턴에 1번밖에 사용할 수 없다.
CALL CARD_ABLE(1,EX소환時)
PRINTL ①: 이 카드가 L 소환했을 경우에 발동할 수 있다.
PRINTL 자신의 묘지에서 공격력 2300의 사이버스족 몬스터를 3장까지 가능한 한 자신 필드에 특수 소환하고,
PRINTL 이 카드의 공격력을 그 수× 500 올린다.
PRINTL 이 효과의 발동 후, 턴 종료시까지 자신은 몬스터를 특수 소환할 수 없다.
CALL CARD_ABLE(2,발동誘発)
PRINTL ②: 상대가 카드의 효과를 발동했을 때에 발동할 수 있다.
PRINTL 이 카드 이외의 자신의 L 몬스터 1장을 릴리스하고,
PRINTL 그 발동을 무효로 하고 제외한다.




@CARDCAN_57038(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,57038)
SIF RESULT == 1
	RETURN 0


IF 종류 == EX소환時
	SIF 발동済_1(듀얼리스트,57038)
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件固攻撃力 = 2300
	효과条件종족 = 사이버스족
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 발동誘発
	SIF 발동済_2(듀얼리스트,57038)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = 링크
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_57038(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 攻撃力修正

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == EX소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,57038,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件固攻撃力 = 2300
		효과条件종족 = 사이버스족
		효과条件소환可 = 1
		효과条件複数 = 97
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"攻撃표시")
		IF RESULT != 0
			カウンタ = RESULT
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"永攻",カウンタ * 500)
		ENDIF
		CALL SET_PLAYER_INFO(듀얼리스트,"SS制限",2)
	ENDIF
ELSEIF 종류 == 발동誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,57038,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件종류 = 링크
		효과条件他所 = 1
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"발동無効",0,"제외")
		ENDIF
	ENDIF
ENDIF
