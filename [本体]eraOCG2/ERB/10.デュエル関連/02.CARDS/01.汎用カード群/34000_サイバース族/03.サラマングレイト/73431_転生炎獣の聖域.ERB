@CARDNAME_73431,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 샐러맨그레이트 성역
	CASE "略称"
		RESULTS = 전생염수의 성역
	CASE "カテゴリ"
		RESULTS:0 = 샐러맨그레이트
ENDSELECT


@CARD_73431(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN フィールド
ENDSELECT

RETURN 0

@CARD_EXPLANATION_73431(종류)
#DIM DYNAMIC 종류

PRINTL 자신 필드는 "샐러맨그레이트 성역"가 된다。
PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 1번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,마함起動)
PRINTL ①：이 카드가 필드 존에 존재하는 한、
PRINTL 자신 필드의 "샐러맨그레이트" 링크 몬스터 1장을 대상으로 발동할 수 있다.
PRINTL 그 링크 몬스터 1장으로, 같은 이름의 "샐러맨그레이트" 링크 몬스터를 링크 소환한다.
CALLFORM CARD_ABLE(2,攻撃時誘発)
PRINTL ②: 자신의 몬스터가 전투를 실행하는 데미지 계산 시에, 1000 LP를 지불하고,
PRINTL 자신 필드의 L몬스터 １장을 대상으로 하고 발동할 수 있다。
PRINTL 그 몬스터의 공격력을 0으로 하고, 그 몬스터의 원래 공격력만큼 자신의 LP를 회복한다.


@CARDCAN_73431(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
#DIMS DYNAMIC 효과条件보존,10

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,73431)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF 발동済_1(듀얼리스트,73431)
		RETURN 0
	CALL CARD_EX_CHECK(듀얼리스트,0)
	SIF RESULT == 1
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	FOR カウンタ,0,5
		CALL FULLRESET
		효과条件カテゴリ = 샐러맨그레이트
		효과条件종류 = 링크
		CALL CARD_FILTER_SET(듀얼리스트,"필드",カウンタ,듀얼리스트,1)
		SIF RESULT != 1
			CONTINUE
		カウンタB ++
		VARSET RESULTS
		TRYCALLFORM CARDNAME_{GETVAR(@"%듀얼리스트%_필드:カウンタ")}, "名前"
		효과条件보존:カウンタB = %RESULTS%
	NEXT

	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件カテゴリ = 샐러맨그레이트
	효과条件종류 = 링크
	효과条件名前:0 = ダミ-０
	효과条件名前:1 = %효과条件보존:1%
	효과条件名前:2 = %효과条件보존:2%
	효과条件名前:3 = %효과条件보존:3%
	효과条件名前:4 = %효과条件보존:4%
	효과条件名前:5 = %효과条件보존:5%
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 
	RETURN 1
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃時誘発
	SIF 발동済_2(듀얼리스트,73431)
		RETURN 0
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 1000
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = 링크
	효과条件カテゴリ = 샐러맨그레이트
	효과条件以攻撃力 = 1 
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_73431(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC ライフ回復
#DIMS DYNAMIC 選択カード名前

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,73431,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 샐러맨그레이트
		효과条件종류 = 링크
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			選択カード_ID = RESULT:99
			TRYCALLFORM CARDNAME_{選択カード_ID}, "名前"
			選択カード名前 = %RESULTS%

			CALL FULLRESET
			효과条件名前 = %選択カード名前%
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			選択カード = RESULT

			IF 選択カード != 9999
				選択カード_ID = RESULT:99
				TRYCALLFORM CARDSUMMON_{選択カード_ID}(듀얼리스트,選択カード)
				CALL RESETCONDITION_MAT(1)
				必要링크数 = 1
				素材条件L値無視 = 1
				素材条件名前 = %選択カード名前%
				CALL LINSUMMON(듀얼리스트, 選択カード)
			ENDIF
		ENDIF

	ENDIF
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동＆설치まで
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,73431)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF
ELSEIF 종류 == 지속효과
	CALL SET_FIELD_MAGIC_INFO(듀얼리스트,"샐러맨그레이트 성역")
ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,73431,2)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",1000)
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件종류 = 링크
		효과条件カテゴリ = 샐러맨그레이트
		효과条件以攻撃力 = 1 
		CALL CARD_GRANT_ADV(듀얼리스트,ゾーン,場所,"永攻０")
		選択カード_ID = RESULT:1
		CALLFORM CARD_{選択カード_ID}("攻撃力")
		ライフ回復 = RESULT
		CALL LIFE_CHANGE(듀얼리스트,"増加","효과",ライフ回復)
	ENDIF
ENDIF
