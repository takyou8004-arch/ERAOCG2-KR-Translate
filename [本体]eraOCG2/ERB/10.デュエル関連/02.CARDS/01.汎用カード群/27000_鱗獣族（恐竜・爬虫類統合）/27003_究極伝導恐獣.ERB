@CARDNAME_27003,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 얼티메이트 컨덕터- 티라노
	CASE "略称"
RESULTS = 궁극전도공수
	CASE "カテゴリ"
RESULTS:0 = 없음
ENDSELECT


@CARD_27003(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 비늘야수족
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 3500
	CASE "守備力"
		RETURN 3200
	CASE "소환불가"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_27003(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드는 일반 소환할 수 없으며, 이 카드명의 ②의 효과는 1턴에 1번밖에 사용할 수 없다.
CALL CARD_ABLE(0,손패起動)
PRINTL ①: 자신의 묘지에서 비늘야수족 몬스터 2장을 제외하고 발동할 수 있다.
PRINTL 이 카드를 손패에서 특수 소환한다.
CALL CARD_ABLE(2,필드起動 ,フリー誘発)
PRINTL ②: 자신/상대의 메인 페이즈에 발동할 수 있다.
PRINTL 자신의 손패 / 필드의 몬스터 1장을 선택하고 파괴하며, 상대 필드의 앞면 표시 몬스터를 전부 뒷면 수비 표시로 한다.
CALL CARD_ABLE(0,지속효과)
PRINTL ③: 이 카드는 상대 몬스터 전부에 1번씩 공격할 수 있다.
CALL CARD_ABLE(0,攻撃時誘発)
PRINTL ④: 이 카드가 수비 표시 몬스터를 공격할 경우에 발동할 수 있다.
PRINTL 상대에게 1000 데미지를 주고, 그 수비 표시 몬스터를 묘지로 보낸다.
PRINTL 

@CARDCAN_27003(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,27003)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	CALL CARD_SS_CHECK(듀얼리스트,27003,ゾーン)
	SIF RESULT == 1
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종족 = 비늘야수족
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,27003)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻撃時誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	SIF GETVAR(@"%対面者%_MZ_表:%対面者%_戦闘カード") == 1 
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_27003(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 破壊カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성


CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%
IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,27003)
	IF RESULT == 0
		CALL FULLRESET
		효과条件종족 = 비늘야수족
		CALL CARD_CHOOSE_BASIC(듀얼리스트, "묘지",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL CARD_DISPOSE(듀얼리스트,"묘지",選択カード)
			CALL FULLRESET
			효과条件종족 = 비늘야수족
			CALL CARD_CHOOSE_BASIC(듀얼리스트, "묘지",ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				CALL CARD_DISPOSE(듀얼리스트,"묘지",選択カード)
				CALL FULLRESET
				CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,27003,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲손패 = 1
		효과条件종류 = モン全般
		효과条件行先플래그 = 10
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)	
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드全体",場所,"표시변경")
		ENDIF
	ENDIF
ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,27003)
	IF RESULT == 0
		破壊カード = 戦闘場所(듀얼리스트,1)
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",破壊カード,"リリース")
		CALL LIFE_CHANGE(듀얼리스트,"減少","효과",1000,1)
	ENDIF
ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"連戦")
ENDIF



