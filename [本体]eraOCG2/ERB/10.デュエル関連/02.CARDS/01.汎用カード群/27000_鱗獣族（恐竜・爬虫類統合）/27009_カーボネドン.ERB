@CARDNAME_27009,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 카보네돈
	CASE "略称"
RESULTS = 카보네돈
	CASE "カテゴリ"
RESULTS:0 = 없음
ENDSELECT


@CARD_27009(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
	
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 비늘야수족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 800
	CASE "守備力"
		RETURN 600
ENDSELECT

RETURN 0

@CARD_EXPLANATION_27009(종류)
#DIM DYNAMIC 종류

;PRINTL "カ-ボネドン"の②의 효과는 １ターンに１번밖에 使用할 수 없다。
CALL CARD_ABLE(0,攻撃時強制誘発)
PRINTL ①: 이 카드가 화염속성 몬스터와 전투를 실행할 경우에 발동한다.
PRINTL 이 카드의 공격력은, 그 데미지 계산시에만 1000 올린다.
CALL CARD_ABLE(0,묘지起動)
PRINTL ②: 묘지의 이 카드를 제외하고 발동할 수 있다.
PRINTL 패/덱에서 레벨 7 이하의 드래곤족 일반 몬스터 1장을 앞면 수비 표시로 특수 소환한다.



@CARDCAN_27009(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,27009)
SIF RESULT == 1
	RETURN 0

IF 종류 == 攻撃時強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	CALLFORM CARD_{戦闘カードID(듀얼리스트,1)}("속성")
	SIF RESULT != 화염속성
		RETURN 0
	RETURN 1	
ELSEIF 종류 == 묘지起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET		
	효과範囲덱 = 1
	효과範囲손패 = 1
	효과条件종족 = 드래곤족
	효과条件종류 = 通常モン
	CALL 효과条件レベル_ADV(7, "以下")	
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_27009(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 戦闘カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 攻撃時強制誘発
	;一時的な攻撃力強化を발동
	戦闘カード = 戦闘場所(듀얼리스트)
	CALL SET_FIELD_INFO(듀얼리스트,"필드",戦闘カード,"一時",1000)

ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,27009)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET		
		효과範囲덱 = 1
		효과範囲손패 = 1
		효과条件종족 = 드래곤족
		효과条件종류 = 通常モン
		CALL 효과条件レベル_ADV(7, "以下")		
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"守備표시")
	ENDIF
ENDIF
