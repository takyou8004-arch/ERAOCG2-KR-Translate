@CARDNAME_72716,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 맹계의 뱀수련
	CASE "略称"
RESULTS = 맹계의 뱀수련
	CASE "カテゴリ"
RESULTS:0 = 맹계
ENDSELECT


@CARD_72716(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "レア度"
		RETURN SPR
ENDSELECT

RETURN 0

@CARD_EXPLANATION_72716(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드 명의 카드는 1턴에 1장밖에 발동할 수 없다.
CALL CARD_ABLE(1,마함起動 ,손패起動)
PRINTL ①: 덱에서 비늘야수족 몬스터 1장을 묘지로 보낸다.
PRINTL 그 후, 자신의 묘지에 비늘야수족 몬스터가 5종류 이상 존재할 경우,
PRINTL 자신의 묘지에서 비늘야수족 몬스터 1장을 특수 소환할 수 있다.

@CARDCAN_72716(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,72716)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,72716)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종족 = 비늘야수족
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_72716(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,72716,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件종족 = 비늘야수족
		효과条件종류 = モン全般
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件종족 = 비늘야수족
			효과条件종류 = モン全般
			효과条件固名前起動 = 1
			효과条件CNT = 1
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT < 5
				RETURN 0
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件종족 = 비늘야수족
			효과条件任意 = 1
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ENDIF
