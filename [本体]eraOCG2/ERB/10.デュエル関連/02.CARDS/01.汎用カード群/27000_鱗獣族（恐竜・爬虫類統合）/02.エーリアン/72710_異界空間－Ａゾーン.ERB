@CARDNAME_72710,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 이계공간 A존
	CASE "略称"
		RESULTS = 이계공간 A존
	CASE "カテゴリ"
		RESULTS:0 = 에일리언
ENDSELECT


@CARD_72710(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"

		RETURN フィールド
ENDSELECT

RETURN 0

@CARD_EXPLANATION_72710(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드는 룰 상 "에일리언" 카드로 취급한다.
PRINTL 자신의 필드는 "이계공간 A존"이 된다.
PRINTL 이 카드명의 ①의 효과는 1턴에 1번밖에 사용할 수 없다.
CALL CARD_ABLE(1,손패起動,마함起動)
;ヤバそうだったらモンに限定、それで弱過ぎたらカテゴリ全般おろまい
PRINTL ①: 이 카드의 발동시 처리로서, 덱에서 "에일리언" 카드를 2장까지 묘지로 보낼 수 있다.
CALL CARD_ABLE(10,마함起動)
PRINTL ②: 1턴에 1번, 필드의 악성 카운터를 3개까지 제거하고 발동할 수 있다.
PRINTL 제거한 수× 3 이하의 레벨의 비늘야수족 몬스터 1장을 패/덱에서 특수 소환한다.
PRINTL 이 효과를 발동한 턴에는, 자신은 비늘야수족 몬스터밖에 EX덱에서 특수 소환할 수 없다.
CALL CARD_ABLE(0,攻守変動)
PRINTL ③: 악성 카운터가 올려져 있는 상대 몬스터가 "에일리언" 몬스터와 전투를 실행할 경우,
PRINTL 그 몬스터의 공격력 / 수비력은 이 턴 중, 그 악성 카운터의 수× 300 다운한다.
CALL CARD_ABLE(0,지속효과)
PRINTL ④: 자신의 필드에 공격력 2600 이상의 비늘야수족 몬스터가 존재하는 한,
PRINTL 이 카드는 효과로는 파괴되지 않는다.

@CARDCAN_72710(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 最大レベル

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,72710)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT:10 < 1
		RETURN 0

	SIF RESULT > 3
		RESULT = 3
	最大レベル = RESULT * 3
	CALL CARD_DRAW_CHECK
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲덱 = 1
	효과条件종족 = 비늘야수족
	CALLFORM 효과条件レベル_ADV(最大レベル,"以下")
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	;とりあえずチェックはさせる
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 에일리언
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)

	RETURN 1

ELSEIF 종류 == 攻守変動
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	TRYCALLFORM CARDNAME_{GETVAR(@"%듀얼리스트%_필드:戦闘場所(듀얼리스트)")},"カテゴリ"
	SIF RESULTS != "에일리언"
		RETURN 0
	SIF GETVAR(@"%対面者%_MZ_悪Ｃ:戦闘場所(듀얼리스트,1)") == 0
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_72710(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 戦闘カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,72710,10)
	IF RESULT == 0
		REPEAT 3
			CALL FULLRESET
			효과範囲필드 = 1
			효과範囲마함 = 1
			효과範囲상대필드 = 1
			효과範囲상대마함 = 1
			효과条件悪Ｃ所持 = 999
			효과条件任意 = 1
			CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT != 9999
				CALL CARD_SELECT(듀얼리스트,RESULT)
				CALL SET_FIELD_INFO(대상者（CS）,参照先（CS）,選択カード（CS）,"悪Ｃ",-1)
				条件달성 ++
			ELSE
				BREAK
			ENDIF
		REND
		IF 条件달성 > 0
			CALL FULLRESET
			효과範囲손패 = 1
			효과範囲덱 = 1
			효과条件종족 = 비늘야수족
			CALL 효과条件レベル_ADV(条件달성 * 3,"以下")
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
		EX소환可能종족 = 비늘야수족
		CALL CARD_EX_RESTRICTION(듀얼리스트)
	ENDIF

ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동＆설치まで
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,72710)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
		SIF 발동済_1(듀얼리스트,72710)
			RETURN 0
		SIF 듀얼리스트 == "자신"
PRINTL 카드를 묘지로 보낼까요?
		CALL YN_CHOICE(듀얼리스트)
		IF RESULT == 0
			CALL CARD_EFFECT_USE(듀얼리스트,72710,1)
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 에일리언
			효과条件複数 = 98
			효과条件任意 = 1
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			SIF RESULT != 1
				BREAK
		ENDIF
	ENDIF

ELSEIF 종류 == 攻守変動
	戦闘カード = 戦闘場所(対面者)
	攻撃力修正 = GETVAR(@"%対面者%_MZ_悪Ｃ:戦闘場所(듀얼리스트,1)") * 300
	CALL SET_FIELD_INFO(対面者,"필드",戦闘カード,"一攻",-攻撃力修正)
	CALL SET_FIELD_INFO(対面者,"필드",戦闘カード,"一守",-攻撃力修正)

ELSEIF 종류 == 지속효과
	CALL SET_FIELD_MAGIC_INFO(듀얼리스트, "이계공간 A존")
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종족 = 비늘야수족
	효과条件以攻撃力 = 2600
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT == 1
		CALL STATCHANGE_EFFECT(듀얼리스트,"魔自身",場所,"破耐")
	ENDIF
ENDIF
