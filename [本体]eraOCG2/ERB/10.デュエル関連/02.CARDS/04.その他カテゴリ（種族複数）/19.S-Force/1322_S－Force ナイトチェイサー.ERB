@CARDNAME_1322,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = S－Force 나이트체이서
	CASE "略称"
		RESULTS = SF 나이트 C
	CASE "カテゴリ"
		RESULTS:0 = S－Force
ENDSELECT


@CARD_1322(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 링크
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 사이버스족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 800
	CASE "守備力"
		RETURN 800
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARDSUMMON_1322(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要링크数 = 1
素材条件종류 = 링크以外
素材条件カテゴリ = S－Force

IF EX参照 == 1
	CALL LINCAN(듀얼리스트,소환カード,1322) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_1322(종류)
#DIM DYNAMIC 종류

PRINTL 링크 몬스터 이외의 "S－Force" 몬스터 1장
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,효과後誘発,특수소환時,EX소환時)
PRINTL ①: 상대가 몬스터의 특수 소환에 성공했을 때, 또는 이 카드가 특수 소환에 성공했을 때,
PRINTL 상대 필드의 몬스터 1장을 대상으로 발동할 수 있다.
PRINTL 그 카드에 턴 종료시까지 "단체 공격"을 부여한다.
PRINTL (단체 공격을 가진 몬스터가 존재하는 한, 상대는 그 카드로밖에 공격할 수 없다)
CALLFORM CARD_ABLE(2,필드起動,フリー誘発)
PRINTL ②: 자신 / 상대의 메인 페이즈에, 자신 필드의 "S－Force" 몬스터 1장을 대상으로 발동할 수 있다.
PRINTL 그 몬스터를 주인의 덱으로 되돌린다.
PRINTL 그 후, 제외되어 있는 자신의 "S－Force" 몬스터 1장을 고르고 특수 소환할 수 있다.
PRINTL 

@CARDCAN_1322(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,1322)
SIF RESULT == 1
	RETURN 0


IF 종류 == 효과後誘発
	SIF 발동済_1(듀얼리스트,1322)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"특수소환",1)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,1322)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == EX소환時 || 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,1322)
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF




@CARDEFFECT_1322(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 효과後誘発 || 종류 == EX소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1322,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"상대필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"単攻",1,1)
		ENDIF
	ENDIF
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1322,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = S－Force
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1

			CALL FULLRESET
			효과範囲제외 = 1
			효과条件カテゴリ = S－Force
			효과条件任意 = 1
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ENDIF

