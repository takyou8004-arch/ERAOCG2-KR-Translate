@CARDNAME_23170,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 갤럭시아이즈 포톤 드래곤
	CASE "略称"
		RESULTS = 은하안의 광자룡
	CASE "カテゴリ"
		RESULTS:0 = 갤럭시아이즈
		RESULTS:1 = 갤럭시
		RESULTS:2 = 포톤
ENDSELECT


@CARD_23170(参照先)
;名前専用
#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 2500
	CASE "C불가"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_23170(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드의 효과의 발동에 대해, 상대는 카드의 효과를 발동할 수 없다.
CALLFORM CARD_ABLE(0,손패起動)
PRINTL ①：이 카드는 자신 필드의 공격력 2000 이상의 몬스터 2장을 릴리스하고 패에서 특수 소환할 수 있다.
CALLFORM CARD_ABLE(10,攻撃時誘発)
PRINTL ②：1턴에 1번, 이 카드가 상대 몬스터와 전투를 실행할 경우에 발동할 수 있다.
PRINTL 전투하는 상대 몬스터 1장을 엔드 페이즈까지 제외한다.
PRINTL 이 효과로 엑스 몬스터를 제외했을 경우,
PRINTL 이 카드의 공격력은 제외한 엑스 몬스터의 가지고 있던 엑스 소재의 수× 500 올린다.


@CARDCAN_23170(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,23170)
SIF RESULT == 1
	RETURN 0



IF 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,23170,"손패",1)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件以攻撃力 = 2000
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃時誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_23170(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,23170)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件以攻撃力 = 2000
		효과条件複数 = 2
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"필드",ゾーン,場所)
		IF RESULT == 1
			CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,23170,10)
	IF RESULT == 0
		CALL FULLRESET
		選択カード = 戦闘場所(듀얼리스트,1)
		CALLFORM CARD_{GETVAR(@"%対面者%_필드:選択カード")}, "종류"
		IF RESULT == 6
			攻撃力修正 = (GETVAR(@"%対面者%_MZ_Ｘ:選択カード") + 1) * 500
			CALL FULLRESET
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"永攻",攻撃力修正)
		ENDIF
		CALL CARD_DISPOSE(対面者,ゾーン,選択カード,제외行き,1)
	ENDIF
ENDIF

