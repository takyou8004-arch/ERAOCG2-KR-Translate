@CARDNAME_38200,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 스파이랄－댄디
	CASE "略称"
RESULTS = SPY 댄디
	CASE "カテゴリ"
RESULTS:0 = 스파이랄
RESULTS:1 = 스파이랄－댄디
ENDSELECT


@CARD_38200(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"	
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1900
	CASE "守備力"
		RETURN 1200
	CASE "성별"
		RETURN 雄性
	CASE "추가日"
		RETURN 250420
ENDSELECT

RETURN 0

@CARD_EXPLANATION_38200(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 이 카드가 패에 존재할 경우, 카드의 종류(몬스터 마법 / 함정)를 선언하고 발동할 수 있다.
PRINTL 상대 덱의 맨 위의 카드를 서로 확인하고, 선언한 종류의 카드였을 경우,
PRINTL 이 카드를 패에서 각성 상태로 특수 소환한다.
CALLFORM CARD_ABLE(2,특수소환時)
PRINTL ②: 이 카드가 특수 소환에 성공했을 경우, 상대 필드의 마법 / 함정 카드 1장을 대상으로 발동할 수 있다.
PRINTL 그 카드를 파괴한다. 이 카드가 각성 상태일 경우, 묘지의 「스파이랄」 몬스터 1장을 특수 소환할 수 있다.



@CARDCAN_38200(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,38200)
SIF RESULT == 1
	RETURN 0


IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,38200)
		RETURN 0
	SIF ゾーン != "손패"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0

	CALL CARD_SS_CHECK(듀얼리스트,38200,"손패")
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 특수소환時
	RETURN 1
ENDIF

@CARDEFFECT_38200(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択효과

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38200)
	IF RESULT == 0
		IF GETVAR(@"%対面者%_덱:0") != -1
			
			PRINTFORML >>——MESSAGE:——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————>>>
			PRINTL
			PRINTFORML 「スパイミッション開始だな、頼むぜ相棒」
			PRINTL 
			CUSTOMDRAWLINE ―

			IF 듀얼리스트 == "자신"
				PRINTFORML [1] - 몬스터 카드 
				PRINTFORML [2] - 마법 카드
				PRINTFORML [3] -  함정 카드 
				BINPUT
			ELSE
				RESULT = RAND:3 + 1
			ENDIF
			選択효과 = RESULT
			TRYCALLFORM CARD_UI(GETVAR(@"%対面者%_덱:0"))
			PRINTW
			CALLFORM CARD_{GETVAR(@"%対面者%_덱:0")}, "종류"
			IF RESULT < 10 && 選択효과 == 1
				CALL FULLRESET
				효과추가종류 = 効追条達
				CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)
			ELSEIF RESULT > 30 && 選択효과 == 3
				CALL FULLRESET
				효과추가종류 = 効追条達
				CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)
			ELSEIF INRANGE(RESULT,20,29) && 選択효과 == 2
				CALL FULLRESET
				효과추가종류 = 効追条達
				CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)
			ELSE
				PRINTFORML >>——MESSAGE:——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————>>>
				PRINTL
				PRINTFORML 「気にするな、相棒。必要な情報は手に入れた、だろ？」
				PRINTL 
				CUSTOMDRAWLINE ―
				PRINTW
				RETURN 0
			ENDIF
		ELSE
			PRINTFORMW 상대의 덱がありません。
			RETURN 0
		ENDIF

		PRINTFORML >>——MESSAGE:——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————>>>
		PRINTL
		PRINTFORML 「ジャックポット。流石だな、相棒！」
		PRINTL 
		CUSTOMDRAWLINE ―
		PRINTW
	ENDIF
ELSEIF 종류 == 특수소환時 
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38200)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"마함単体",場所,"묘지")
		IF GETVAR(@"%듀얼리스트%_MZ_条達:場所") >= 1
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件カテゴリ = 스파이랄
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)	
		ENDIF
	ENDIF
ENDIF

