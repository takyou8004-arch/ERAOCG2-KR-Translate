@CARDNAME_27012,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 노염파괴수 도고란
	CASE "略称"
		RESULTS = 도고란
	CASE "カテゴリ"
		RESULTS:0 = 파괴수
ENDSELECT

@CARD_27012(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 비늘야수족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 1200
	CASE "レア度"
		RETURN ULR
ENDSELECT

RETURN 0

@CARD_EXPLANATION_27012(종류)
#DIM DYNAMIC 종류

CALLFORM CARD_ABLE(0,손패起動)
PRINTL ①: 상대 필드에 "파괴수" 몬스터가 존재하지 않을 때, 상대 필드의 몬스터 1장을 릴리스하고 발동할 수 있다.
PRINTL 이 카드를 패에서 상대 필드에 공격 표시로 특수 소환할 수 있다.
CALLFORM CARD_ABLE(0,손패起動)
PRINTL ②: 자신 필드에 "파괴수" 몬스터가 존재하지 않고, 상대 필드에 "파괴수" 몬스터가 존재할 경우,
PRINTL 이 카드는 패에서 공격 표시로 특수 소환할 수 있다.
PRINTL 그 후、상대 필드의 마법 / 함정 카드를 1개 파괴할 수 있다。
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ③: 1턴에 1번, 자신 / 상대 필드의 파괴수 카운터를 2개 제거하고 발동할 수 있다.
PRINTL 상대 필드의 몬스터를 전부 파괴한다. 이 효과를 발동하는 턴, 이 카드는 상대 플레이어에게 직접 공격할 수 없다.


@CARDCAN_27012(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,27012)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,27012,"손패",1)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 =1
	효과条件カテゴリ = 파괴수
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT == 1
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = 파괴수
		효과条件종류 = モン全般
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			RETURN 0
		ENDIF
	ENDIF	
	RETURN 1
;ここまで共通효과

ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,27012)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	효과条件カテゴリ = 파괴수
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT:9 < 2
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_27012(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件달성

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,27012)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과条件カテゴリ = 파괴수
		효과条件종류 = モン全般
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲필드 = 1
			효과条件カテゴリ = 파괴수
			효과条件종류 = モン全般
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 0
				CALL FULLRESET
				효과範囲필드 = 1
				CALL CARD_SP_SUMMON(듀얼리스트, ゾーン, 場所, "攻撃표시")

				CALL FULLRESET
				효과範囲상대마함 = 1
				효과条件任意 = 1
				CALL CARD_REMOVAL_ADV(듀얼리스트,"単体除去",場所,"묘지")
			ENDIF
		ELSE
			CALL FULLRESET
			효과範囲필드 = 1
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"상대필드",ゾーン,場所)	
			IF RESULT == 1
				CALL FULLRESET
				CALL CARD_SP_SUMMON(듀얼리스트, ゾーン, 場所, "攻撃표시",1)
			ENDIF
		ENDIF
	ENDIF
;ここまで共通효과

ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,27012,2)
	IF RESULT == 0
		REPEAT 2
PRINTL 파괴수 카운터를 제거할 카드를 선택
			CALL FULLRESET
			효과範囲필드 = 1
			효과範囲마함 = 1
			효과範囲상대필드 = 1
			효과範囲상대마함 = 1
			효과条件カテゴリ = 파괴수
			효과条件個Ｃ所持 = 999
			CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT != 9999
				CALL CARD_SELECT(듀얼리스트,RESULT)
				CALL SET_FIELD_INFO(대상者（CS）,参照先（CS）,選択カード（CS）,"個Ｃ",-1)
				条件달성 ++
			ENDIF
		REND
		IF 条件달성 == 2
			PRINTFORMW ドゴランの화염が全てを焼き尽くす！
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드全体",場所,"묘지")
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"直無",1)
		ENDIF
	ENDIF
ENDIF
