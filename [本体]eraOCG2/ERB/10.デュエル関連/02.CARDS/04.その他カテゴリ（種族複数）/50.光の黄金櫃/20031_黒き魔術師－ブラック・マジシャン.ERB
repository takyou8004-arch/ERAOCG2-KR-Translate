@CARDNAME_20031,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 검은 마술사－블랙 매지션
	CASE "略称"
RESULTS = 검은 마술사
	CASE "カテゴリ"
RESULTS:0 = 블랙 매지션
RESULTS:1 = 빛의 황금궤
RESULTS:2 = 마술사
ENDSELECT



@CARD_20031(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 마법사족
	CASE "レベル"
		RETURN 7
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 2100
	CASE "성별"
		RETURN 雄性
ENDSELECT
RETURN 0


@CARD_EXPLANATION_20031(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①③ 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 필드에 「빛의 황금궤」가 존재할 경우 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환한다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ②: 이 카드는 몬스터 존에 존재하는 한, 카드명을 「블랙 매지션」으로 취급한다.
CALLFORM CARD_ABLE(3,破壊→묘지,제외時)
PRINTL ③: 이 카드가 효과로 파괴/제외되었을 경우, 필드에 레벨 5 이상의 몬스터가 존재한다면 발동할 수 있다.
PRINTL 자신 묘지/제외 상태에서 「검은 마술사－블랙 매지션」 1장을 특수 소환한다.
PRINTL 그 후, 덱에서 「블랙 매지션」 또는 「빛의 황금궤」 마법/함정 카드 1장을 자신 필드에 세트할 수 있다.



@CARDCAN_20031(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,20031)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,20031)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,20031,"손패")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과範囲상대마함 = 1
	효과条件名前 = 빛의 황금궤
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_表:場所") >= 3
		RETURN 0
	RETURN 1
ELSEIF 종류 == 破壊→묘지 || 종류 == 제외時
	SIF 발동済_3(듀얼리스트,20031)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,20031,ゾーン)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲상대필드 = 1
	CALL 효과条件レベル_ADV(5,"以上")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_20031(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,20031,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"変化元ID",20000)
ELSEIF 종류 == 破壊→묘지 || 종류 == 제외時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,20031,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과範囲제외 = 1
		효과条件名前 = 검은 마술사－블랙 매지션
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ:0 = 블랙 매지션
			효과条件カテゴリ:1 = 빛의 황금궤
			효과条件종류 = 마함全般
			효과条件任意 = 1
			CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"裏側표시")
		ENDIF
	ENDIF
ENDIF
