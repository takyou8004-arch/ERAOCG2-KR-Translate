@CARDNAME_78013,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 네오스 퓨전
	CASE "略称"
		RESULTS = 네오스 F
	CASE "カテゴリ"
		RESULTS:0 = 네오스
		RESULTS:1 = 융합
ENDSELECT


@CARD_78013(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
ENDSELECT
RETURN 0

@CARD_EXPLANATION_78013(종류)
#DIM DYNAMIC 종류

CALL CARD_ABLE(0,마함起動 ,손패起動)
PRINTL ①: 자신의 패 / 덱 / 필드에서, 융합 소재 몬스터를 묘지로 보내고,
PRINTL "네오스" 융합 몬스터 1장을 EX덱에서 융합 소환한다.
PRINTL 이 카드의 발동 후, 턴 종료시까지 자신은 몬스터를 특수 소환할 수 없다.
CALL CARD_ABLE(0,身代효과)
PRINTL ②: "네오스" 융합 몬스터가 전투 / 효과로 파괴될 경우,
PRINTL 또는 자신의 효과로 EX덱으로 되돌아갈 경우,
PRINTL 대신에 묘지의 이 카드를 제외할 수 있다.

@CARDCAN_78013(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,78013)
SIF RESULT == 1
	RETURN 0
	
IF 종류 == 마함起動 || 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL RESETAREA_MAT
	素材範囲손패 = 1
	素材範囲필드 = 1
	素材範囲덱 = 1
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 융합
	효과条件カテゴリ = 네오스
	효과条件소환可 = 2
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 身代효과
	SIF !MATCH(ゾーン,"묘지")
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	TRYCALLFORM CARDNAME_{破壊대상ID}("カテゴリ")
	SIF !MATCH(RESULTS,"네오스")
		RETURN 0
	SIF !GROUPMATCH(破壊종류,"효과","戦闘")
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_78013(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC EX参照
#DIM DYNAMIC 묘지送カード,10
#DIM DYNAMIC 손패묘지送カード,10
#DIM DYNAMIC 素材数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択済カード
;융합소환
VARSET 묘지送カード, 35
VARSET 손패묘지送カード, 35

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,78013)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL RESETAREA_MAT
		素材範囲손패 = 1
		素材範囲필드 = 1
		素材範囲덱 = 1
		CALL FULLRESET
		효과条件종류 = 융합
		효과条件カテゴリ = 네오스
		효과条件소환可 = 2
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
			CALL FUSSUMMON(듀얼리스트,選択カード)
			SIF RESULT == 1
				CALL SET_PLAYER_INFO(듀얼리스트,"SS制限",2)
		ENDIF
	ENDIF
ELSEIF 종류 == 身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,78013)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		RETURN 1
	ENDIF
	RETURN 0
ENDIF
