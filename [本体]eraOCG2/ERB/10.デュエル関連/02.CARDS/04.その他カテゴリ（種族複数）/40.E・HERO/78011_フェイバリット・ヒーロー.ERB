@CARDNAME_78011,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 페이버릿 히어로
	CASE "略称"
RESULTS = F 히어로
	CASE "カテゴリ"
RESULTS:0 = 페이버릿
ENDSELECT


@CARD_78011(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 装備마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_78011(종류)
#DIM DYNAMIC 종류

PRINTL "HERO" 몬스터에만 장착 가능.
PRINTL 이 카드명의 ②의 효과는 1턴에 1번밖에 사용할 수 없다.
CALL CARD_ABLE(0,攻守変動,지속효과)
PRINTL ①：자신 필드에 필드 마법이 존재할 경우, 장착 몬스터는 공격력이 원래의 수비력만큼 올라가고,
PRINTL 장착 몬스터를 상대는 효과의 대상으로 할 수 없다.

CALL CARD_ABLE(2,バトル開始時)
PRINTL ②：자신 또는 상대의 배틀 페이즈 개시시에 발동할 수 있다.
PRINTL 자신의 패 / 덱에서 필드 마법 카드 1장을 발동한다.

CALL CARD_ABLE(0,攻撃後誘発)
PRINTL ③：장착 몬스터의 공격으로 상대 몬스터를 파괴했을 때, 이 카드를 묘지로 보내 발동할 수 있다.
PRINTL 그 공격 몬스터는 한 번 더 공격할 수 있다.

@CARDCAN_78011(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 選択カード
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,78011)
SIF RESULT == 1
	RETURN 0

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT


IF 종류 == 마함起動 || 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF

	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = HERO
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻守変動 || 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件종류 = フィールド
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == バトル開始時
	SIF ゾーン != "마함"
		RETURN 0
	SIF 발동済_2(듀얼리스트,78011)
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲덱 = 1
	효과条件종류 = フィールド
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻撃後誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	SIF GETVAR(@"%装備者%_戦闘カード") != 装備先
		RETURN 0
	SIF 直前戦闘破壊ID(듀얼리스트,1) == 0
		RETURN 0

	RETURN 1
ENDIF

@CARDEFFECT_78011(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 選択カード
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT


IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,78011)
	IF RESULT == 0
		;패에서 발동後、마법トラップ 존에 カード이동
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = HERO
		CALLFORM CARD_EQUIP_ADV(듀얼리스트,場所,ゾーン)
	ENDIF
ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"装備",場所,"対耐")


ELSEIF 종류 == 攻守変動
	CALLFORM CARD_{GETVAR(@"%装備者%_필드:装備先")}("守備力")
	攻撃力修正 = RESULT
	CALL STATCHANGE_BASIC(듀얼리스트,"装備",場所,攻撃力修正,攻撃力修正)
ELSEIF 종류 == バトル開始時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,78011,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲덱 = 1
		효과条件종류 = フィールド
		CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")
	ENDIF
ELSEIF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,78011)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL SET_FIELD_INFO(듀얼리스트,"필드",装備先,"戦",1)
	ENDIF
ENDIF
