@CARDNAME_38022,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 엘리멘틀 히어로 스피릿 오브 네오스
	CASE "略称"
		RESULTS = 스피릿 네오스
	CASE "カテゴリ"
		RESULTS:0 = HERO
		RESULTS:1 = E•HERO
		RESULTS:2 = 네오스
ENDSELECT


@CARD_38022(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
	
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 전사족
	CASE "レベル"
		RETURN 7
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 2000
	CASE "성별"
		RETURN 雄性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_38022(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALL CARD_ABLE(1,攻撃時誘発)
PRINTL ①: 상대 몬스터의 공격 선언 시에 발동할 수 있다.
PRINTL 그 공격을 무효화하고, 이 카드를 패에서 수비 표시로 특수 소환한다.
PRINTL 이 효과로 특수 소환한 이 카드는 전투로는 파괴되지 않는다.
CALL CARD_ABLE(2,특수소환時)
PRINTL ②: 이 카드가 특수 소환에 성공했을 경우에 발동할 수 있다.
PRINTL 덱에서 "E•HERO"마법 / 함정 카드 또는 "융합" 1장을 패에 넣는다.
CALL CARD_ABLE(3,필드起動)
PRINTL ③: 자신 메인 페이즈에 발동할 수 있다.
PRINTL 이 카드를 주인의 덱으로 되돌리고,
PRINTL 덱에서 "E•HERO"일반 몬스터 1장을 특수 소환한다.

@CARDCAN_38022(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,38022)
SIF RESULT == 1
	RETURN 0

IF 종류 == 攻撃時誘発
	SIF ゾーン != "손패"
		RETURN 0
	SIF 발동済_1(듀얼리스트,38022)
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,38022,"손패")
	SIF RESULT == 1
		RETURN 0

	RETURN 1
ELSEIF 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,38022)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = E•HERO
	효과条件종류 = 마함全般
	효과条件スーパー名前 = 융합
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 발동済_3(듀얼리스트,38022)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = E•HERO
	효과条件종류 = 通常モン
	효과条件소환可 = 2
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_38022(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38022,1)
	IF RESULT == 0
		CALL FULLRESET
		효과추가종류 = 31
		CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所,"守備표시")
		CALL EVADE_ATTACK(듀얼리스트)
	ENDIF
ELSEIF 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38022,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = E•HERO
		효과条件종류 = 마함全般
		효과条件スーパー名前 = 융합
		CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38022,3)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,덱行き)
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件종류 = 通常モン
		효과条件カテゴリ = E•HERO
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF


