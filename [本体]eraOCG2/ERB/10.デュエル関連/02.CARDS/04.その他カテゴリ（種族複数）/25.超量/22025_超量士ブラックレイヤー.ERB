@CARDNAME_22025,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 초량사 블랙 레이어
	CASE "略称"
RESULTS = 초량 블랙
	CASE "カテゴリ"
RESULTS:0 = 초량
ENDSELECT


@CARD_22025(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 언데드족
	CASE "レベル"
		RETURN 7
	CASE "攻撃力"
		RETURN 2400
	CASE "守備力"
		RETURN 2400
	CASE "추가日"
		RETURN 250427
ENDSELECT

RETURN 0

@CARD_EXPLANATION_22025(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,발동誘発)
PRINTL ①: 상대가 몬스터의 효과를 발동했을 때에 발동할 수 있다.
PRINTL 자신의 손패에서 다른 카드 1장을 고르고 버려, 이 카드를 손패에서 특수 소환한다.
CALLFORM CARD_ABLE(2,특수소환時)
PRINTL ②: 이 카드가 특수 소환했을 경우에 발동할 수 있다.
PRINTL 자신의 묘지에서 「초량」 몬스터 1장을 고르고,
PRINTL 랭크 7 이하의 「초량」 엑시즈 몬스터 1장을 자신 필드의 이 카드 위에 겹쳐 엑시즈 소환 취급으로 엑스트라 덱에서 특수 소환한다.
PRINTL 이 효과로 엑시즈 소환한 몬스터는 엑시즈 소재를 2개 가지고, 고른 묘지의 몬스터의 카드명이 기재되어 있을 경우, 각성한다.
CALLFORM CARD_ABLE(3,필드→묘지 ,손패→묘지 ,덱→묘지 ,破壊→묘지 ,戦闘→묘지,X素材時효과)
PRINTL ③: 이 카드가 묘지로 보내졌을 경우에 발동할 수 있다.
PRINTL 덱에서 「초량」 마법 카드 1장을 손패에 넣는다.


@CARDCAN_22025(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,22025)
SIF RESULT == 1
	RETURN 0

IF 종류 == 발동誘発
	SIF 발동済_1(듀얼리스트,22025)
		RETURN 0
	SIF ゾーン != "손패"
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,22025,ゾーン)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과条件종류 = モン全般
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,22025)
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 초량
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件カテゴリ = 초량
	효과条件종류 = 엑시즈
	CALLFORM 효과条件レベル_ADV(7,"以下")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드→묘지 || 종류 == 손패→묘지 || 종류 == 덱→묘지 || 종류 == 破壊→묘지 || 종류 == 戦闘→묘지 || 종류 == X素材時효과
	SIF 발동済_3(듀얼리스트,22025)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 초량
	효과条件종류 = 마법全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_22025(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIM DYNAMIC 条件달성

IF 종류 == 발동誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,22025,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件他所 = 1
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,"손패",場所)
		ENDIF
	ENDIF
	RETURN 0

ELSEIF 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,22025,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 초량
		효과条件종류 = モン全般
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"묘지",ゾーン,場所)
		IF RESULT != 9999
			選択カード_ID = RESULT:99

			CALL FULLRESET
			효과条件カテゴリ = 초량
			효과条件종류 = 엑시즈
			CALLFORM 효과条件レベル_ADV(7,"以下")
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			IF RESULT != 9999
				選択カード = RESULT
				SELECTCASE RESULT:99
					;ブル-
					CASE 42262
						SIF 選択カード_ID == 2262
							条件달성 = 1
					;グリ-ン
					CASE 52142
						SIF 選択カード_ID == 20025
							条件달성 = 1
					;レッド
					CASE 52141
						SIF 選択カード_ID == 23025
							条件달성 = 1
					;ホ화이트
					CASE 52143
						SIF 選択カード_ID == 29025
							条件달성 = 1
				ENDSELECT

				CALL RESETCONDITION_MAT
				必要素材数 = 1
				重ねてＸ = 1
				素材条件固定 = 場所
				효과추가종류:0 = 効追Ｘ素 + 1
				SIF 条件달성 == 1
					효과추가종류:1 = 効追条達
				TRYCALLFORM XYZSUMMON(듀얼리스트,選択カード)
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == 필드→묘지 || 종류 == 손패→묘지 || 종류 == 덱→묘지 || 종류 == 破壊→묘지 || 종류 == 戦闘→묘지 || 종류 == X素材時효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,22025,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 초량
		효과条件종류 = 마법全般
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF
