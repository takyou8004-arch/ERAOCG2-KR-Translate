@CARDNAME_29012,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 천 제왕 아이테르
	CASE "略称"
		RESULTS = 아이테르
	CASE "カテゴリ"
		RESULTS:0 = 제
ENDSELECT


@CARD_29012(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 2800
	CASE "守備力"
		RETURN 1000
	CASE "성별"
		RETURN 牝性
	CASE "レア度"
		RETURN ULR
ENDSELECT
RETURN 0

@CARD_EXPLANATION_29012(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드는 일반 소환한 레벨 5 이상의 몬스터 1장을 릴리스하여 어드밴스 소환할 수 있다.
CALLFORM CARD_ABLE(0,소환時)
PRINTL ①: 이 카드가 소환했을 경우에 발동할 수 있다.
PRINTL 패/덱에서 "제왕" 마법 / 함정 카드 2종류를 묘지로 보내고,
PRINTL 덱에서 "제" 몬스터 1장을 특수 소환한다.
PRINTL 이 효과로 특수 소환한 몬스터는 엔드 페이즈에 주인의 패로 돌아간다.
CALLFORM CARD_ABLE(0,손패起動,フリー誘発)
PRINTL ②: 이 카드가 패에 있을 경우,
PRINTL 상대 메인 페이즈에 자신 묘지의 "제왕" 마법 / 함정 카드 1장을 제외하고 발동할 수 있다.
PRINTL 이 카드의 일반 소환을 실행한다.

@CARDSUMMON_AA_29012


@CARDCAN_29012(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 条件달성

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,29012)
SIF RESULT == 1
	RETURN 0

IF 종류 == 효과소환
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件召種 = 召種通常
	CALLFORM 효과条件レベル_ADV(5,"以上")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 소환時
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲덱 = 1
	효과条件カテゴリ = 제왕
	효과条件종류 = 마함全般
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 제
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 손패起動 || 종류 == フリー誘発
	SIF ゾーン != "손패"
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 제왕
	효과条件종류 = 마함全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 2
		RETURN 1
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件召種 = 召種通常
	CALLFORM 효과条件レベル_ADV(5,"以上")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 1
	RETURN 0
ENDIF

@CARDEFFECT_29012(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カード

IF 종류 == 효과소환
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件召種 = 召種通常
	CALLFORM 효과条件レベル_ADV(5,"以上")
	CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,29012)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲덱 = 1
		효과条件カテゴリ = 제왕
		효과条件종류 = 마함全般
		효과条件複数 = 2
		효과条件固名前選択 = 1
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 제
			효과추가종류 = 効追終手
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF

ELSEIF 종류 == 손패起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,29012)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 제왕
		효과条件종류 = 마함全般
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL MIKADO_ADVANCE_RELEASE(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				CALL CARD_SUMMON_SHORI(듀얼리스트,場所,"攻撃표시",1)
		ENDIF
	ENDIF
ENDIF


@MIKADO_ADVANCE_RELEASE(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件달성

CALL FULLRESET
CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
IF RESULT != 9999
	選択カード = RESULT
	SIF GETVAR(@"%듀얼리스트%_MZ_星:選択カード") >= 5
		条件달성 ++
	SIF GETVAR(@"%듀얼리스트%_MZ_召種:選択カード") == 召種通常
		条件달성 ++
	IF 条件달성 == 2
		CALL CARD_DISPOSE(듀얼리스트,"필드",選択カード)
		RETURN 1
	ENDIF
	CALL FULLRESET
	효과条件他所 = 1
	CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드","필드",選択カード)
	SIF RESULT == 9999
		RETURN 0
	CALL CARD_DISPOSE(듀얼리스트,"필드",RESULT)
	CALL CARD_DISPOSE(듀얼리스트,"필드",選択カード)
	RETURN 1
ELSE
	RETURN 0
ENDIF
