@CARDNAME_54566,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 마건빙선－아슈타르투
	CASE "略称"
		RESULTS = 아슈타르투
	CASE "カテゴリ"
		RESULTS:0 = 마건
ENDSELECT


@CARD_54566(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 3000
	CASE "성별"
		RETURN 雄性
ENDSELECT

RETURN 0

@CARDSUMMON_54566(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 2
素材条件レベル = 8

IF EX参照 == 1
	CALL XYZCAN(듀얼리스트,소환カード,54566) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_54566(종류)
#DIM DYNAMIC 종류
PRINTL 레벨 8 몬스터 × 2
PRINTL "마건빙선－아슈타르투"는 1턴에 1번, 자신 필드의 "마건" EX 몬스터 위에 겹쳐 엑시즈 소환할 수 있다.
PRINTL 이 카드명의 ②의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(10,攻撃後強制誘発)
PRINTL ①：1턴에 1번, 자신의 일반 몬스터 또는 "마건" 몬스터가 전투로 상대 몬스터를 파괴했을 때,
PRINTL 그 파괴된 몬스터의 원래 공격력만큼의 데미지를 상대에게 준다.
CALLFORM CARD_ABLE(2,필드起動,フリー誘発)
PRINTL ②：상대 메인 페이즈에,
PRINTL 자신 필드 / 묘지의 일반 몬스터 또는 "마건" 몬스터 중 어느 한쪽과 같은 속성을 가지는,
PRINTL 상대 필드의 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 이 카드의 엑시즈 소재를 1개 제거하고, 대상의 몬스터를 제외한다.


@CARDCAN_54566(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,54566)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX덱起動
	SIF 발동済_1(듀얼리스트,54566)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_EX_CHECK(듀얼리스트,54566)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류:0 = 의식
	효과条件종류:1 = 융합
	효과条件종류:2 = 싱크로
	효과条件종류:3 = 엑시즈
	효과条件カテゴリ = 마건
	효과条件重ねてＸ = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻撃後強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF 直前戦闘破壊ID(듀얼리스트,1) == 0
		RETURN 0
	TRYCALLFORM CARDNAME_{戦闘カードID(듀얼리스트)},"カテゴリ"
	SIF MATCH(RESULTS,"마건")
		RETURN 1
	TRYCALLFORM CARD_{戦闘カードID(듀얼리스트)},"종류"
	SIF MATCH(RESULT,通常モン)
		RETURN 1
	RETURN 0

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,54566)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") <= -1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	FOR カウンタ,1,8
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件スーパーカテゴリ = 마건
		효과条件スーパー속성 = カウンタ
		효과条件종류 = 通常モン
		효과条件속성 = カウンタ
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			CONTINUE
		
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과条件속성 = カウンタ
		효과条件표시 = 表側표시
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			CONTINUE
		条件달성 = 1
	NEXT
	SIF 条件달성 == 0
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_54566(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성,8

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == EX덱起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,54566,1)
	IF RESULT == 0
		CALL RESETCONDITION_MAT
		必要素材数 = 1
		素材条件종류:0 = 의식
		素材条件종류:1 = 융합
		素材条件종류:2 = 싱크로
		素材条件종류:3 = 엑시즈
		素材条件カテゴリ = 마건
		重ねてＸ = 1
		TRYCALLFORM XYZSUMMON(듀얼리스트,場所)
	ENDIF

ELSEIF 종류 == 攻撃後強制誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,54566,10)
	IF RESULT == 0
		TRYCALLFORM CARD_{直前戦闘破壊ID(듀얼리스트,1)}("攻撃力")
		CALL LIFE_CHANGE(듀얼리스트,"減少","효과",RESULT,1)
	ENDIF

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,54566,2,X消費1)
	IF RESULT == 0
		FOR カウンタ,1,8
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件スーパーカテゴリ = 마건
			효과条件スーパー속성 = カウンタ
			효과条件종류 = 通常モン
			효과条件속성 = カウンタ
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT != 1
				CONTINUE
			CALL FULLRESET
			효과範囲상대필드 = 1
			효과条件속성 = カウンタ
			효과条件표시 = 表側표시
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT != 1
				CONTINUE
			条件달성:カウンタ = 1
		NEXT
		CALL FULLRESET
		FOR カウンタ,1,8
			SIF 条件달성:カウンタ == 1
				효과条件속성:カウンタ = カウンタ
		NEXT
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"제외")
	ENDIF
ENDIF
