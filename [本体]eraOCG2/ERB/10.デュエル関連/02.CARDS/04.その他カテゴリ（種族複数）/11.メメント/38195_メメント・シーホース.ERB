@CARDNAME_38195,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 메멘토 씨호스
	CASE "略称"
RESULTS = 메멘토 해마
	CASE "カテゴリ"
RESULTS:0 = 메멘토
ENDSELECT


@CARD_38195(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 야수족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 1350
	CASE "守備力"
		RETURN 1600
ENDSELECT
RETURN 0

@CARD_EXPLANATION_38195(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 자신 필드에 "메멘토" 몬스터 이외의 몬스터가 존재하지 않는 경우에 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환한다.
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ②: 자신 메인 페이즈에 발동할 수 있다.
PRINTL 자신 필드의 "메멘토" 몬스터 1장을 파괴하고,
PRINTL 레벨의 합계가 파괴한 몬스터의 레벨 이하가 되도록,
PRINTL 덱에서 "메멘토" 몬스터를 묘지로 보낸다(같은 이름은 1장까지).

@CARDSUMMON_AA_38195


@CARDCAN_38195(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,38195)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,38195)
		RETURN 0
	SIF ゾーン != "손패"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,38195,ゾーン)
	SIF RESULT == 1
		RETURN 0
	;参考：歌姫ク-プレ
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件固カテゴリ = 메멘토
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,38195)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 메멘토
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_38195(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択レベル
#DIM DYNAMIC 選択カード_ID
#DIMS DYNAMIC 選択カード名前,5
#DIM DYNAMIC 選択횟수
#DIM DYNAMIC 묘지送りレベル

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38195,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,"손패",場所)
	ENDIF

ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38195,2)
	IF RESULT == 0
		;参考：ガイザ-
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = 메멘토
		효과条件行先플래그 = 破壊行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		;参考：スポ-ア
		IF RESULT == 1
			CALLFORM CARD_{RESULT:1}, "レベル"
			選択レベル = RESULT
			選択횟수 = 0
			;選択レベルが０じゃないなら繰り返す
			WHILE 選択レベル > 0
				CALL FULLRESET
				효과条件종류 = モン全般
				효과条件カテゴリ = 메멘토
				효과範囲덱 = 1
				;とりあえず５回まで
				SIF 選択횟수 > 0
					효과条件固名前:0 = %選択カード名前:0%
				SIF 選択횟수 > 1
					효과条件固名前:1 = %選択カード名前:1%
				SIF 選択횟수 > 2
					효과条件固名前:2 = %選択カード名前:2%
				SIF 選択횟수 > 3
					효과条件固名前:3 = %選択カード名前:3%
				SIF 選択횟수 > 4
					효과条件固名前:4 = %選択カード名前:4%
				효과条件任意 = 1
				CALL 효과条件レベル_ADV(選択レベル, "以下")
				CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
				IF RESULT == 1
					;ID취득
					選択カード_ID = RESULT:1
					;レベル취득
					CALLFORM CARD_{RESULT:1}, "レベル"
					묘지送りレベル = RESULT
					;名前취득
					TRYCALLFORM CARDNAME_{選択カード_ID}, "名前"
					;5
					SIF 選択횟수 == 4
						選択カード名前:4 = %RESULTS%
					;4
					SIF 選択횟수 == 3
						選択カード名前:3 = %RESULTS%
					;3
					SIF 選択횟수 == 2
						選択カード名前:2 = %RESULTS%
					;2
					SIF 選択횟수 == 1
						選択カード名前:1 = %RESULTS%
					;1
					SIF 選択횟수 == 0
						選択カード名前:0 = %RESULTS%

					選択횟수 += 1
					;残りレベル計算
					選択レベル -= 묘지送りレベル
					;PRINTFORML 残り{選択レベル}

				ELSE
					;選べなかったら終わり
					BREAK
				ENDIF
			WEND
		ENDIF
	ENDIF
ENDIF


