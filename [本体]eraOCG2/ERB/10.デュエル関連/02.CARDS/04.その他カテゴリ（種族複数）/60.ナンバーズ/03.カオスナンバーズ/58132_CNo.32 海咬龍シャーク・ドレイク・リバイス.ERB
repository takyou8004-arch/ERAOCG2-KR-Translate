@CARDNAME_58132,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = CNo.32 샤크 드레이크 리바이스
	CASE "略称"
		RESULTS = C#32 샤크바이스
	CASE "カテゴリ"
		RESULTS:0 = 넘버즈
		RESULTS:1 = 카오스
		RESULTS:2 = 샤크
		RESULTS:3 = 샤크 드레이크
ENDSELECT


@CARD_58132(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 바다생물족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 3100
	CASE "守備力"
		RETURN 2600
ENDSELECT

RETURN 0

@CARDSUMMON_58132(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 4
素材条件レベル = 5
지정素材名前 = バイス 샤크

IF EX参照 == 1
	CALL XYZCAN(듀얼리스트,소환カード,58132) 
	RETURN RESULT
ENDIF



@CARD_EXPLANATION_58132(종류)
#DIM DYNAMIC 종류

PRINTL 레벨 5 몬스터 × 4
CALLFORM CARD_ABLE(0,EX덱起動)
PRINTL 이 카드는 패의 마법 카드 1장을 버리고, 자신 필드의 랭크 4의 "샤크 드레이크" 엑시즈 몬스터 위에 겹쳐 엑시즈 소환할 수도 있다.
CALLFORM CARD_ABLE(10,필드起動,フリー誘発)
PRINTL ①: 자신 / 상대 턴에 1번, 이 카드의 엑시즈 소재 1개를 제거하고 발동할 수 있다.
PRINTL 상대 필드의 효과 몬스터 1체의 효과를 무효로 하고, 그 공격력 / 수비력은 0이 된다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ②: 이 카드는 1번의 배틀 페이즈 중에 2번 공격할 수 있으며,
PRINTL 수비 표시 몬스터를 공격했을 경우, 그 수비력을 공격력이 넘은 만큼 상대에게 전투 데미지를 준다.


@CARDCAN_58132(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,58132)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX덱起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,58132,"EX덱",1)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件レベル = 4
	효과条件カテゴリ = 샤크 드레이크
	효과条件종류 = 엑시즈
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종류 = 마법全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") <= -1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件종류 = 효과全般
	효과条件표시 = 10
	효과条件除去種 = 無効
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	RETURN 1
ELSEIF 종류 == 발동誘発
	SIF 발동済_2(듀얼리스트,58132)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_条達:場所") == 0
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") <= 0
		RETURN 0
	CALL FULLRESET
	효과条件종류 = 마함全般
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_58132(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先
#DIM DYNAMIC 条件달성
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == EX덱起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,58132)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件종류 = 마법全般
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과条件レベル = 4
			효과条件カテゴリ = 샤크 드레이크
			효과条件종류 = 엑시즈
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
			IF RESULT != 9999
				選択カード = RESULT
				SIF GETVAR(@"%듀얼리스트%_MZ_条達:選択カード") != 0
					条件달성 ++
				CALL RESETCONDITION_MAT(1)
				CALL FULLRESET
				必要素材数 = 1
				素材条件固定 = 選択カード
				重ねてＸ = 1
				SIF 条件달성 != 0
					효과추가종류 = 効追条達
				TRYCALLFORM XYZSUMMON(듀얼리스트,場所)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,58132,10,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件표시 = 表側표시
		효과추가종류:0 = 23
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"지속無効",99999)
	ENDIF
ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"追戦",1)
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"貫通")
ELSEIF 종류 == 발동誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,58132,2,2)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"발동無効",場所,"묘지")
	ENDIF
ENDIF
