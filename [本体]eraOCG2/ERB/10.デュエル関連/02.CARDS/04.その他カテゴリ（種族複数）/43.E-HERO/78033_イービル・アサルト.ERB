@CARDNAME_78033,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 이블 어썰트
	CASE "略称"
		RESULTS = 이블어썰트
	CASE "カテゴリ"
		RESULTS:0 = 다크 퓨전
ENDSELECT


@CARD_78033(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
ENDSELECT
RETURN 0

@CARD_EXPLANATION_78033(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①② 효과는 1턴에 1번, 어느 한 가지만 사용할 수 있다.
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ①: 자신의 손패를 1장 선택하여 버린다.
PRINTL 그 후, 덱에서 레벨 4 이하의 "E-HERO" 몬스터 1장을 특수 소환한다.
PRINTL 이 카드의 발동 후, 턴 종료시까지 자신은 "HERO" 몬스터밖에 EX덱에서 특수 소환할 수 없다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 이 카드를 묘지에서 제외하고 발동할 수 있다.
PRINTL 자신의 묘지에서 "다크 퓨전" 1장을 패에 추가한다.


@CARDCAN_78033(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성
#DIM DYNAMIC EX条件달성
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,78033)
SIF RESULT == 1
	RETURN 0


IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,78033)
		RETURN 0
	SIF 발동済_2(듀얼리스트,78033)
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	CALL 효과条件レベル_ADV(4,"以下")
	효과条件カテゴリ = E-HERO
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_1(듀얼리스트,78033)
		RETURN 0
	SIF 발동済_2(듀얼리스트,78033)
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件名前 = 다크 퓨전
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_78033(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC EX参照
#DIM DYNAMIC 묘지送カード,10
#DIM DYNAMIC 손패묘지送カード,10
#DIM DYNAMIC 素材数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択済カード
#DIM DYNAMIC EX条件달성
;융합소환
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,78033,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			CALL 효과条件レベル_ADV(4,"以下")
			효과条件カテゴリ = E-HERO
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,78033)
		ENDIF
		CALL CARD_EX_RESTRICTION(듀얼리스트,"HERO")
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,78033,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件名前 = 다크 퓨전
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF
