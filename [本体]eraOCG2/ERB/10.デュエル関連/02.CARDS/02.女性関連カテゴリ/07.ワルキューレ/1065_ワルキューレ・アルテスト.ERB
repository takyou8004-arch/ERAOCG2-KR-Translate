@CARDNAME_1065,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 왈큐레 아르테스트
	CASE "略称"
RESULTS = 아르테스트
	CASE "カテゴリ"
RESULTS:0 = 왈큐레
ENDSELECT


@CARD_1065(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 천사족 
	CASE "レベル"
		RETURN 6
	CASE "攻撃力"
		RETURN 1600
	CASE "守備力"
		RETURN 1800
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_1065(종류)
#DIM DYNAMIC 종류
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,특수소환時)
PRINTL ①: 이 카드가 특수 소환에 성공했을 경우에 발동할 수 있다.
PRINTL 자신의 덱 / 묘지에서 「시간의 여신 장난」 1장을 패에 넣는다.
CALLFORM CARD_ABLE(2,필드起動 ,フリー誘発)
PRINTL ②: 자신 / 상대 턴에 자신 필드에 「왈큐레 아르테스트」 이외의 「왈큐레」 몬스터가 존재할 경우에 발동할 수 있다.
PRINTL 상대의 묘지에서 몬스터 1장을 선택하고 제외한다.
PRINTL 그 후, 턴 종료시까지 이 카드의 공격력을, 제외한 몬스터의 원래 공격력만큼 올린다.


@CARDCAN_1065(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,1065)
SIF RESULT == 1
	RETURN 0

IF 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,1065)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲묘지 = 1
	효과条件名前 = 시간의 여신의 장난
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,1065)
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 왈큐레
	효과条件固名前 = 왈큐레 아르테스트
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대묘지 = 1
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_1065(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1065,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과範囲묘지 = 1
		효과条件名前 = 시간의 여신의 장난
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1065,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件종류 = モン全般
		CALL CARD_CHOOSE_BASIC(듀얼리스트, "상대묘지",ゾーン,場所)
		選択カード = RESULT
		IF RESULT != 9999
			TRYCALLFORM CARD_{GETVAR(@"%対面者%_묘지:選択カード")}, "攻撃力"
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"一攻",RESULT)
			CALL CARD_DISPOSE(対面者,"묘지",選択カード)
		ENDIF
	ENDIF
ENDIF

