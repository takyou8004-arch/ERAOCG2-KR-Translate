@CARDNAME_5500,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 낙원의 무녀 레이무
	CASE "略称"
		RESULTS = 하쿠레이 레이무
	CASE "カテゴリ"
		RESULTS:0 = 하쿠레이 신사
		RESULTS:4 = 동방 Project
ENDSELECT


@CARD_5500(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 바람속성
	CASE "종족"
		RETURN 사이킥족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 1500
	CASE "守備力"
		RETURN 1500
	CASE "성별"
		RETURN 牝性
	CASE "生成カード"
		RETURN 205500
	CASE "추가日"
		RETURN 250612
ENDSELECT

RETURN 0

@CARD_EXPLANATION_5500(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①② 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 이 카드가 패에 존재하고, 필드에 엑스트라 몬스터가 존재할 경우에 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환한다.
CALLFORM CARD_ABLE(2,소환時,특수소환時)
PRINTL ②: 이 카드가 소환 / 특수 소환에 성공했을 경우에 발동할 수 있다.
PRINTL 덱 밖에서 "영부 몽상봉인" 1장을 패에 넣는다.
CALLFORM CARD_ABLE(10,필드起動)
PRINTL ③: 1턴에 1번, 자신 패를 1장 버리고, 상대 필드의 엑스트라 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 몬스터를 파괴한다.


;霊符 夢想封印（速攻마법）
;①：자신 필드의 몬스터는 이 턴동안 １번만戦闘、효과로는 파괴되지 않는다。
;자신 필드의 "楽園の巫女 레이무"が파괴되어 ていた경우、이 카드는 "ラストワード"가 되며、以下を実行する。
;●자신의 묘지에서 "楽園の巫女 레이무"を특수 소환한다。
;●상대 필드의 EXモンスター全てを파괴한다。

@CARDCAN_5500(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 条件달성

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,5500)
SIF RESULT == 1
	RETURN 0
IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,5500)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,5500,ゾーン)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲상대필드 = 1
	효과条件종류 = EXモン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,5500)
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 0
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件종류 = EXモン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1


ENDIF

@CARDEFFECT_5500(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,5500,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트, ゾーン, 場所)
	ENDIF
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,5500,2)
	IF RESULT == 0
		CALL CARD_GENERATE_HAND(듀얼리스트,205500)
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,5500,10)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과条件종류 = EXモン全般
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"묘지")
		ENDIF
	ENDIF
ENDIF


;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――


@CARDNAME_205500,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 영부 몽상봉인
	CASE "略称"
RESULTS = 영부 몽상봉인
	CASE "カテゴリ"
RESULTS:0 = 스펠 카드
ENDSELECT


@CARD_205500(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "外部者"
		RETURN 1
	CASE "종류"
		RETURN 速攻마법
	CASE "トークン"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_205500(종류)
#DIM DYNAMIC 종류
PRINTL 이 카드명의 효과는 1턴에 1번밖에 사용할 수 없다.

CALLFORM CARD_ABLE(10,마함起動 ,손패起動)
PRINTL ①: 자신 필드의 몬스터는 이 턴 중 1번만 전투, 효과로는 파괴되지 않는다.
PRINTL 자신 필드의 "낙원의 무녀 레이무"가 파괴되었을 경우,
PRINTL 자신 묘지에서 "낙원의 무녀 레이무"를 특수 소환하고, 이 카드는 "라스트 워드"가 된다.
CALL TEXT_DECORATION("LASTWORD")
PRINTL ● 상대 필드의 엑스트라 몬스터를 전부 파괴한다.


@CARDCAN_205500(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,205500)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,205500)
		RETURN 0
	IF ゾーン == "손패"
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	RETURN 1
ENDIF



@CARDEFFECT_205500(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC ダメージ量

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,205500,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		
		CALL FULLRESET
효과条件名前 = 낙원의 무녀 레이무
		CALL CARD_CHECK_FIN_DATA(듀얼리스트,"破壊全般")
		IF RESULT == 1
			CALL FULLRESET
			효과範囲묘지 = 1
효과条件名前 = 낙원의 무녀 레이무
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)

			CALLFORM CHARA_CUTIN("Last Word","夢 想 天 生",,,,,1)

			CALL FULLRESET
			효과条件종류 = EXモン全般
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드全体",場所,"묘지")
		ELSE			
			CALLFORM CHARA_CUTIN("Spell Card","「霊符」夢想封印",,,,,1)
		ENDIF
		CALL FULLRESET
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"一破",1)

	ENDIF
ENDIF
