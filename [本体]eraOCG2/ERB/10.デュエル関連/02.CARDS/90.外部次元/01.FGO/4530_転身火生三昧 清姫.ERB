@CARDNAME_4530,参照先
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 전신 화생삼매 키요히메
	CASE "略称"
		RESULTS = 키요히메
	CASE "カテゴリ"
		RESULTS:0 = 영령
		RESULTS:1 = 버서커
		RESULTS:2 = FATE
ENDSELECT

@CARD_4530(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 1200
	CASE "守備力"
		RETURN 1400
	CASE "성별"
		RETURN 牝性
	CASE "生成カード"
		RETURN 104530
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4530(종류)
#DIM DYNAMIC 종류
#DIM DYNAMIC カード主

カード主 = NO_TO_CHARANUM(4530)

PRINTL 이 카드명의 ①③의 효과는 각각 1턴에 1번만 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①：자신 필드에 빈성이 존재하지 않는 경우에 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환한다.
CALLFORM CARD_ABLE(0,バトル開始時)
PRINTL ②：배틀 페이즈 개시 시에 발동할 수 있다.
PRINTL 이 카드는 "키요히메―뱀"으로 전신한다.
CALLFORM CARD_ABLE(3,攻撃時誘発)
PRINTL ③：상대의 빈성 몬스터의 직접 공격 선언 시에 발동할 수 있다.
PRINTL 묘지의 이 카드를 특수 소환하고, 공격 대상을 이 카드로 옮긴다.
IF TALENT:カード主:애처
PRINTL 그 후, 이 몬스터는 전신하고, 무적이 된다.
ENDIF

@CARDCAN_4530(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4530)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,4530)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,4530,"손패")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件성별 = 牝性
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == バトル開始時
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻撃時誘発
	SIF ゾーン != "묘지"
		RETURN 0
	SIF 발동済_3(듀얼리스트,4530)
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 5
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,4530,"묘지")
	SIF RESULT == 1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_性:戦闘場所(듀얼리스트,1)") == 牝性
		RETURN 1
	TRYCALLFORM CARD_{戦闘カードID(듀얼리스트,1)},"성별"
	SIF !GROUPMATCH(RESULT,牝性,ふたなり)
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_4530(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC カード主

カード主 = NO_TO_CHARANUM(4530)

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4530,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == バトル開始時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4530)
	IF RESULT == 0
		PRINTFORML >>——MESSAGE:——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————>>>
		PRINTL
		PRINTFORML "どうかご照覧あれ！"
		PRINTL
		CUSTOMDRAWLINE ―
		WAIT
		CALL CARD_RECONSTRUCT(듀얼리스트,ゾーン,場所,14530)
	ENDIF

ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4530,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件名前 = 전신화생삼매 키요히메
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL COVER_ATTACK(듀얼리스트,カード소환先)

			IF TALENT:カード主:애처
				CALL CARD_RECONSTRUCT(듀얼리스트,"필드",カード소환先,14530)
				CALL SET_FIELD_INFO(듀얼리스트,"필드",カード소환先,"条達",2,1)
				
				CALL SET_FIELD_INFO(듀얼리스트,"필드",カード소환先,"モ耐",1)
				CALL SET_FIELD_INFO(듀얼리스트,"필드",カード소환先,"魔耐",1)
				CALL SET_FIELD_INFO(듀얼리스트,"필드",カード소환先,"함정耐",1)
				CALL SET_FIELD_INFO(듀얼리스트,"필드",カード소환先,"戦耐",1)
				CALL SET_FIELD_INFO(듀얼리스트,"필드",カード소환先,"対耐",1)
				CALL SET_FIELD_INFO(듀얼리스트,"필드",カード소환先,"破耐",1)
			ENDIF
		ENDIF
	ENDIF
ENDIF


;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――


@CARDNAME_14530,参照先
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 키요히메―뱀
	CASE "略称"
		RESULTS = 키요히메―뱀
	CASE "カテゴリ"
		RESULTS:0 = 없음
		RESULTS:1 = 게임 오리지널
ENDSELECT


@CARD_14530(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 2300
	CASE "守備力"
		RETURN 1000
	CASE "성별"
		RETURN 牝性
	CASE "トークン"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_14530(종류)
#DIM DYNAMIC 종류
#DIM DYNAMIC カード主

カード主 = NO_TO_CHARANUM(4530)

CALLFORM CARD_ABLE(0,攻撃時誘発)
PRINTL ①：이 카드의 공격 선언 시에 발동할 수 있다.
IF TALENT:カード主:애처
PRINTL 상대 필드의 카드를 모두 파괴하고, 파괴한 수×500 데미지를 상대에게 준다.
ELSE
PRINTL 상대 필드의 카드 1장을 선택하여 파괴하고, 500 데미지를 상대에게 준다.
ENDIF
CALLFORM CARD_ABLE(0,バトル終了時強制,효과後強制誘発)
PRINTL ②：이 카드가 필드를 벗어난 경우, 또는 배틀 페이즈 종료 시에 발동한다.
PRINTL 이 카드는 "키요히메"로 전신한다.
SIF TALENT:カード主:애처
PRINTL 또한, 이 카드가 무적일 때, 이 카드는 배틀 페이즈 종료 시에 전신하지 않는다.
CALLFORM CARD_ABLE(0,攻守変動)
IF TALENT:カード主:애처
PRINTL ③：자신 필드에 다른 빈성이 존재하지 않는 경우, 이 카드의 공격력은 2000 올라간다.
ELSE
PRINTL ③：자신 필드에 다른 빈성이 존재하지 않는 경우, 이 카드의 공격력은 1000 올라간다.
ENDIF

@CARDCAN_14530(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,14530)
SIF RESULT == 1
	RETURN 0

IF 종류 == 攻撃時誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == バトル終了時強制
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_条達:場所") != 0
		RETURN 0
	RETURN 1

ELSEIF 종류 == 효과後強制誘発
	;SIF !GROUPMATCH(ゾーン,"손패","묘지","제외")
	;	RETURN 0
	SIF ゾーン == "필드"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件성별 = 牝性
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_14530(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC カード主

カード主 = NO_TO_CHARANUM(4530)

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,14530)
	IF RESULT == 0
		IF TALENT:カード主:애처
			PRINTFORML >>——MESSAGE:——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————>>>
			PRINTL
			PRINTFORML "これより逃げた大噓つきを退治します"
			PRINTL
			SETCOLOR C_RED
			PRINTFORML 　　　　　『転身火生三昧！』
			PRINTL
			CUSTOMDRAWLINE ―
			RESETCOLOR
			WAIT
			CALL FULLRESET
			효과추가종류 = 3
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場全体",場所,"묘지",500)
		ELSE
			CALL FULLRESET
			효과추가종류 = 3
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"묘지",500)
		ENDIF
	ENDIF

ELSEIF 종류 == バトル終了時強制 || 종류 == 효과後強制誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,14530)
	IF RESULT == 0
		CALL CARD_RECONSTRUCT(듀얼리스트,ゾーン,場所,4530)
	ENDIF

ELSEIF 종류 == 攻守変動
	IF TALENT:カード主:애처
		攻撃力修正 = 2000
	ELSE
		攻撃力修正 = 1000
	ENDIF
	CALL STATCHANGE_BASIC(듀얼리스트,"自身",場所,攻撃力修正)
ENDIF
