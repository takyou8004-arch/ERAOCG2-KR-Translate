@CARDNAME_31081,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 츄잉 모아이
	CASE "略称"
		RESULTS = 츄잉 모아이
	CASE "カテゴリ"
		RESULTS:0 = 모아이
		RESULTS:1 = 게임 오리지널
ENDSELECT


@CARD_31081(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 원소정령족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 500
	CASE "守備力"
		RETURN 1000
	CASE "튜너"
		RETURN 1
	CASE "生成カード"
		RETURN 131081
ENDSELECT

RETURN 0

@CARD_EXPLANATION_31081(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動,묘지起動)
PRINTL ①：자신 필드에 "츄잉 모아이" 이외의 "모아이" 몬스터가 존재할 경우에 발동할 수 있다.
PRINTL 이 카드를 패 / 묘지에서 특수 소환한다.
CALLFORM CARD_ABLE(2,필드起動 ,フリー誘発)
PRINTL ②：자신 / 상대의 메인 페이즈에,
PRINTL "츄잉 모아이" 이외의 자신 필드의 레벨 5의 "모아이" 몬스터 1장을 대상으로 하고 발동할 수 있다.
PRINTL 이 카드와 그 몬스터를 묘지로 보내고, 덱 외부에서 "크리스탈 모아이" 1장을 생성한다.

@CARDCAN_31081(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,31081)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動 || 종류 == 묘지起動
	SIF 발동済_1(듀얼리스트,31081)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,31081,ゾーン)
	SIF RESULT == 1
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 모아이
	효과条件固名前 = 츄잉 모아이
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,31081)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,131081,"",1)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 모아이
	효과条件종류 = レベル持ち
	효과条件レベル = 5
	효과条件固名前 = 츄잉 모아이
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_31081(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 戦闘カード
#DIM DYNAMIC 対戦カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動 || 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,31081,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,31081,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = 모아이
		효과条件종류 = レベル持ち
		효과条件レベル = 5
		효과条件固名前 = 츄잉 모아이
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
			CALL FULLRESET
			CALL CARD_GENERATE(듀얼리스트,131081)
		ENDIF
	ENDIF
ENDIF


@CARDNAME_131081,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 크리스탈 모아이
	CASE "略称"
		RESULTS = 크리스탈 모아이
	CASE "カテゴリ"
		RESULTS:0 = 모아이
ENDSELECT


@CARD_131081(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 싱크로
	CASE "トークン"
		RETURN 1
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 원소정령족
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 2000
	CASE "守備力"
		RETURN 3000
ENDSELECT

RETURN 0


@CARDSUMMON_131081(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要レベル数 = 10
IF EX参照 == 0 || EX参照 == 1
	素材条件튜너 = 1
ELSEIF EX参照 >= 10
	素材条件튜너 = -1
ENDIF
IF EX参照 == 1
	CALL SYNCAN(듀얼리스트,소환カード,131081) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_131081(종류)
#DIM DYNAMIC 종류

PRINTL 튜너 + 튜너 이외의 몬스터 1장 이상
PRINTL 이 카드명의 ①②③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,EX소환時 ,특수소환時)
PRINTL ①：이 카드가 특수 소환했을 경우, 자신 필드의 "모아이" 몬스터 1장을 대상으로 하고 발동할 수 있다.
PRINTL 상대 필드의 모든 몬스터의 공격력은 대상으로 한 몬스터의 수비력만큼 내린다.
CALLFORM CARD_ABLE(2,필드起動,フリー誘発)
PRINTL ②：자신 / 상대 턴에 발동할 수 있다.
PRINTL 코어의 색이 무작위로 변화하고, 그 색에 따라 이하의 효과를 적용한다.
PRINTL ●황：상대 필드의 카드 1장을 패로 되돌린다.
PRINTL ●청：상대 필드의 카드 1장을 파괴한다.
PRINTL ●녹：이 카드는 이 턴 직접 공격할 수 있다.
CALLFORM CARD_ABLE(3,묘지起動)
PRINTL ③：자신 필드의 "모아이" 몬스터를 포함하는 몬스터 2장을 릴리스하고 발동할 수 있다.
PRINTL 이 카드를 묘지에서 특수 소환한다.


@CARDCAN_131081(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,131081)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX소환時 || 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,131081)
		RETURN 0
	SIF モンスター数(対面者) < 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,131081)
		RETURN 0
	IF 듀얼리스트 != "자신"
		SIF モンスター数(対面者) < 1 && 마함数(対面者) < 1
			RETURN 0
	ENDIF
	RETURN 1

ELSEIF 종류 == 묘지起動
	SIF 발동済_3(듀얼리스트,131081)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,131081,"묘지",1)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 모아이
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_131081(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC コアカラー


IF 종류 == EX소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,131081,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 모아이
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			攻撃力修正 = GETVAR(@"%듀얼리스트%_MZ_守:選択カード") + 1
			CALL FULLRESET
			CALL STATCHANGE_EFFECT(듀얼리스트,"상대필드",場所,"永攻",-攻撃力修正)
		ENDIF
	ENDIF

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,131081,2)
	IF RESULT == 0
		PRINTW 컬러 체인지！
		コアカラー = RAND:3
		IF コアカラー == 0
			SETCOLOR C_YELLOW
		ELSEIF コアカラー == 1
			SETCOLOR C_L_BLUE
		ELSEIF コアカラー == 2
			SETCOLOR C_L_GREEN
		ENDIF
		PRINTL
		PRINTW 　　　●
		PRINTL
		IF コアカラー == 0
			PRINTW 이온링 발사！
			RESETCOLOR
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"손패")
		ELSEIF コアカラー == 1
			PRINTW 레이저 발사！
			RESETCOLOR
			CALL FULLRESET
			효과範囲상대필드 = 1
			효과範囲상대마함 = 1
			CALL CARD_REMOVAL_ADV(듀얼리스트,"単体除去",場所,"묘지")
		ELSEIF コアカラー == 2
			PRINTW 몸통박치기 공격！
			RESETCOLOR
			CALL FULLRESET
			CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"直攻",1)
		ENDIF
	ENDIF

ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,131081,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = 모아이
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲필드 = 1
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				CALL CARD_SP_SUMMON(듀얼리스트,"묘지",場所)
			ENDIF
		ENDIF
	ENDIF
ENDIF
