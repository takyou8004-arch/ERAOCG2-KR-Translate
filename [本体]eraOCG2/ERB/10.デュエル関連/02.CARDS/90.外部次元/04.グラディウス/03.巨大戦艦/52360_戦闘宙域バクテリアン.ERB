@CARDNAME_52360,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 전투주역 박테리안
	CASE "略称"
		RESULTS = 전투주역박테리안
	CASE "カテゴリ"
		RESULTS:0 = 박테리안
		RESULTS:1 = 게임 오리지널
ENDSELECT


@CARD_52360(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 초차원
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 미확인족
	CASE "レベル"
		RETURN 12
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
	CASE "특수불가"
		RETURN 1
	CASE "単一存在"
		RETURN 1
	CASE "生成カード"
		RETURN 152360
ENDSELECT
RETURN 0

@CARD_EXPLANATION_52360(종류)
#DIM DYNAMIC 종류

PRINTL 자신 필드는 "전투주역 박테리안"이 된다.
PRINTL "전투주역 박테리안"은 자신 필드에 1개밖에 존재할 수 없으며,
PRINTL 그 ②③의 효과는 각각 1턴에 1번만 발동할 수 있다.
CALLFORM CARD_ABLE(0,EX덱起動)
PRINTL ①: 자신의 덱 / EX덱에 "박테리안" 몬스터만 존재할 경우,
PRINTL 이 카드는 EX덱에서 필드에 전개할 수 있다.
PRINTL 이 카드의 전개 시, 자신의 덱에 모든 "박테리안" 몬스터를 각각 2장씩 추가할 수 있다.
PRINTL
CALLFORM CARD_ABLE(2,ターン終了時)
PRINTL ②: 엔드 페이즈에 발동할 수 있다.
PRINTL 덱에서 "박테리안" 몬스터를 3장까지 선택하여 특수 소환한다.
PRINTL 그 후, 자신의 덱이 5장 이하일 경우, 무작위 "박테리안" 몬스터 4장을 덱에 추가한다.
PRINTL
CALLFORM CARD_ABLE(1,마함起動)
PRINTL ③: 자신 메인 페이즈에 발동할 수 있다.
PRINTL 자신 묘지의 "박테리안" 몬스터 수 이하의 레벨을 가진 "거대전함" 몬스터 1장을 자신의 EX덱에서 출현시킨다.
PRINTL
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ④: 이 카드가 전개되어 있는 한, 이 카드는 상대의 효과의 대상이 되지 않으며, 효과로는 파괴되지 않는다.
PRINTL 또한, 자신은 EX덱에서 "거대전함" 몬스터밖에 특수 소환할 수 없으며,
PRINTL 자신 필드의 실드 카운터가 없는 "거대전함" 몬스터의 공격력은 1000 상승한다.
PRINTL
CALLFORM CARD_ABLE(0,ターン開始時)
PRINTL ⑤: 10번째 턴 이후의 턴 개시 시에, 자신 필드에 "거대전함" 몬스터가 존재하지 않을 경우에 발동할 수 있다.
PRINTL 자신 필드의 몬스터를 전부 묘지로 보내고, 자신의 EX덱에서 "거대전함" 몬스터 1장을 출현시킨다.
PRINTL
PRINTL ⑥: 자신의 필드/EX덱에 "거대전함" 몬스터가 존재하지 않을 경우, 이 카드는 제외된다.


@CARDCAN_52360(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC ゴミ数

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,52360)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX덱起動
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件名前 = 전투 주역　박테리안
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲EX덱 = 1
	효과条件固カテゴリ = 박테리안
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 마함起動
	SIF 발동済_1(듀얼리스트,52360)
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 박테리안
	효과条件종류 = モン全般
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	ゴミ数 = RESULT
	SIF ゴミ数 < 1
		RETURN 0

	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件カテゴリ = 거대전함
	CALL 효과条件レベル_ADV(ゴミ数,"以下")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == ターン終了時
	SIF ゾーン != "마함"
		RETURN 0
	SIF 발동済_2(듀얼리스트,52360)
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	RETURN 1

ELSEIF 종류 == ターン開始時
	SIF ゾーン != "마함"
		RETURN 0
	SIF ターン数 < 10
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 거대전함
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 효과後誘発 || 종류 == 攻撃後強制誘発
	SIF ゾーン != "마함"
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲EX덱 = 1
	효과条件カテゴリ = 거대전함
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	;確認待たずに제외
PRINTL 우주 공간이 소실되어 간다
	SETCOLOR 255,255,255
	CALL CARD_RECONSTRUCT(듀얼리스트,"마함",場所,152360)
	CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,完全제외行き)
	RESETCOLOR
	RETURN 0

ELSEIF 종류 == 攻守変動
	SIF ゾーン != "마함"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_52360(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カード_ID
#DIMS DYNAMIC カード_名前
#DIM DYNAMIC ゴミ数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 依頼進行度修正

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == EX덱起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52360)
	IF RESULT == 0
		;CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
		CALL FULLRESET
		CALL CARD_ADD(듀얼리스트,"EX덱","마함",場所, "表側표시")
		IF 듀얼리스트 != "자신" && 依頼_完了횟수:515 == 0
			依頼進行度修正 = 24854
		ELSE
			依頼進行度修正 = 24855
		ENDIF
		FOR カード_ID,24850,依頼進行度修正
			TRYCALLFORM CARDNAME_{カード_ID},"名前"
			カード_名前 = %RESULTS%
			SIF 듀얼리스트 == "자신"
				PRINTFORML 《%カード_名前%》を추가しますか？
			CALL YN_CHOICE(듀얼리스트)
			SIF RESULT == 1
				CONTINUE

			REPEAT 2
				CALL FULLRESET
				CALL CARD_GENERATE_DECK(듀얼리스트,カード_ID)
			REND
			PRINTL
		NEXT
		SIF 듀얼리스트 != "자신" && 依頼_完了횟수:515 == 0
			RETURN 0
		FOR カード_ID,39680,39683
			TRYCALLFORM CARDNAME_{カード_ID},"名前"
			カード_名前 = %RESULTS%
			SIF 듀얼리스트 == "자신"
				PRINTFORML 《%カード_名前%》を추가しますか？
			CALL YN_CHOICE(듀얼리스트)
			SIF RESULT == 1
				CONTINUE

			REPEAT 2
				CALL FULLRESET
				CALL CARD_GENERATE_DECK(듀얼리스트,カード_ID)
			REND
			PRINTL
		NEXT
	ENDIF

ELSEIF 종류 == 마함起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52360,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 박테리안
		효과条件종류 = モン全般
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		ゴミ数 = RESULT

		CALL FULLRESET
		효과条件カテゴリ = 거대전함
		CALL 효과条件レベル_ADV(ゴミ数,"以下")
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL FULLRESET
			CALL CARD_EX_SUMMON(듀얼리스트,"EX덱",選択カード)
		ENDIF
	ENDIF

ELSEIF 종류 == ターン終了時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52360,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 박테리안
		효과条件任意 = 1
		효과条件複数 = 97
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		IF 現山札数(듀얼리스트) <= 5
			IF 듀얼리스트 != "자신" && 依頼_完了횟수:515 == 0
				依頼進行度修正 = 5
			ELSE
				依頼進行度修正 = 8
			ENDIF
			REPEAT 4
				CALL FULLRESET
				RESULT = RAND:依頼進行度修正
				;ノ-マル共
				IF RESULT <= 4
					カード_ID = 24850 + RESULT
				;era共
				ELSEIF RESULT >= 5
					カード_ID = 39680 + RESULT - 5
				ENDIF
				CALL FULLRESET
				CALL CARD_GENERATE_DECK(듀얼리스트,カード_ID)
			REND
		ENDIF
	ENDIF

ELSEIF 종류 == ターン開始時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52360)
	IF RESULT == 0
		FOR カウンタ,0,5
			SIF GETVAR(@"%듀얼리스트%_필드:カウンタ") == -1
				CONTINUE
			CALL CARD_DISPOSE(듀얼리스트,"필드",カウンタ,10)
		NEXT
		CALL FULLRESET
		효과条件カテゴリ = 거대전함
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			CALL FULLRESET
			CALL CARD_EX_SUMMON(듀얼리스트,"EX덱",選択カード)
		ENDIF
	ENDIF

ELSEIF 종류 == 효과後誘発 || 종류 == 攻撃後強制誘発

ELSEIF 종류 == 攻守変動
	FOR カウンタ,0,5
		;거대전함じゃなきゃ終了
		TRYCALLFORM CARDNAME_{GETVAR(@"%듀얼리스트%_필드:カウンタ")},"カテゴリ"
		SIF !MATCH(RESULTS,"거대전함")
			CONTINUE
		SIF GETVAR(@"%듀얼리스트%_MZ_個Ｃ:カウンタ") > 0
			CONTINUE
		CALL FULLRESET
		CALL STATCHANGE_BASIC(듀얼리스트,"自身",カウンタ,1000)
	NEXT

ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"魔自身",場所,"破耐")
    CALL STATCHANGE_EFFECT(듀얼리스트,"魔自身",場所,"対耐")
	CALL CARD_EX_RESTRICTION(듀얼리스트,"거대전함")
	CALL SET_FIELD_MAGIC_INFO(듀얼리스트, "전투주역　박테리안")
ENDIF

;----------------------------------------------------------トークン

@CARDNAME_152360,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 스페이스 데브리
	CASE "略称"
		RESULTS = 우주의 쓰레기
	CASE "カテゴリ"
		RESULTS:0 = 없음
ENDSELECT

@CARD_152360(参照先)
#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 通常モン
	CASE "トークン"
		RETURN 1
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 미확인족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
ENDSELECT

@CARD_EXPLANATION_152360(종류)
#DIM DYNAMIC 종류

PRINTL 스페이스 데브리. 우주의 쓰레기.
PRINTL 아무런 쓸모도 없다.
PRINTL
PRINTL
PRINTL

@CARDCAN_152360(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
RETURN 0

