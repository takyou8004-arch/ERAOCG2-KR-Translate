@CARDNAME_39680,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 옵션 체인저
	CASE "略称"
		RESULTS = OP 체인저
	CASE "カテゴリ"
		RESULTS:0 = 박테리안
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_39680(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
	CASE "生成カード"
		RETURN 139680
ENDSELECT
RETURN 0

@CARD_EXPLANATION_39680(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 발동할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 자신 필드에 "박테리안" 이외의 몬스터가 존재하지 않을 경우에 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환한다.
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ②: 서로의 턴에, 필드의 이 카드를 묘지로 보내고,
PRINTL 상대 필드의 공격력 500 이하의 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 몬스터를 "C 옵션"으로 개조한다.
CALL TEXT_DECORATION("ROGUE")


@CARDCAN_39680(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,39680)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,39680)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,39680,"손패")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件固カテゴリ = 박테리안
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF 발동済_2(듀얼리스트,39680)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件攻撃力 = 500
	효과条件固名前 = C 옵션
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_39680(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 選択カード名前

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,39680,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,"손패",場所)
	ENDIF

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,39680,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과条件攻撃力 = 500
		효과条件固名前 = C 옵션
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"상대필드",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			TRYCALLFORM CARDNAME_{RESULT:99},"名前"
			選択カード名前 = %RESULTS%
			PRINTFORMW %選択カード名前%는《C 옵션》に改造された！
			CALL CARD_RECONSTRUCT(対面者,"필드",選択カード,139680)
		ENDIF
	ENDIF
ENDIF


@CARDNAME_139680,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = C 옵션
	CASE "略称"
RESULTS = Change Option
	CASE "カテゴリ"
RESULTS:0 = 없음
ENDSELECT


@CARD_139680(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "トークン"
		RETURN 1
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
	CASE "소환불가"
		RETURN 1
	CASE "특수불가"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_139680(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드는 소환할 수 없으며, "옵션 체인저"의 효과로만 특수 소환할 수 있다.
PRINTL ①: 자신 필드에 기계족 암컷 몬스터가 존재하지 않을 경우, 이 카드는 묘지로 보내진다.
PRINTL ②: 배틀 페이즈 개시시에 자신 필드의 기계족 암컷 몬스터 1장을 대상으로 하여 발동한다.
PRINTL 그 몬스터에 악성 카운터를 주입하고, 이 카드의 공격력은 그 몬스터의 공격력과 같아진다.
;PRINTL 그 몬스터의 悪性카운터の数が그 몬스터의 レベル以上の경우、그 몬스터は이 턴 攻撃할 수 없으며、효과는 무효화된다。


@CARDCAN_139680(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,139680)
SIF RESULT == 1
	RETURN 0

IF 종류 == バトル開始時強制
	SIF ゾーン != "필드"
		RETURN 0
	SIF バトルフェイズ != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 효과後誘発 || 종류 == 攻撃後強制誘発 || 종류 == 발동強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件성별 = 牝性
	효과条件종족 = 기계족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	
	CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
ENDIF


@CARDEFFECT_139680(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIMS DYNAMIC 選択カード名前
#DIM DYNAMIC 選択カードレベル
#DIM DYNAMIC 攻撃力修正

IF 종류 == バトル開始時強制
	SETCOLOR C_RED
	PRINTL
PRINTL OPTION AWAKEN！
	RESETCOLOR
	CALL FULLRESET
	효과条件성별 = 牝性
	효과条件종족 = 기계족
	CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
	IF RESULT != 9999
		選択カード = RESULT
		選択カード_ID = RESULT:99
		TRYCALLFORM CARD_{選択カード_ID},"レベル"
		選択カードレベル = RESULT
		TRYCALLFORM CARDNAME_{選択カード_ID},"名前"
		選択カード名前 = %RESULTS%
		CALL FULLRESET
		攻撃力修正 = GETVAR(@"%듀얼리스트%_MZ_攻:選択カード") + 1
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"攻固",攻撃力修正)
		CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"悪Ｃ",1) 
		PRINTFORMW %選択カード名前%에게 悪性ウイルスが流し込まれた！
		IF GETVAR(@"%듀얼리스트%_MZ_悪Ｃ:選択カード") >= 選択カードレベル
			PRINTL
			PRINTFORMW 流し込まれた悪性ウイルスを処理しきれなかった%選択カード名前%는動作を停止した！
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"戦無",1)
			CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"無効",1)
		ENDIF
	ENDIF

ELSEIF 종류 == 효과後誘発 || 종류 == 攻撃後強制誘発 || 종류 == 발동強制誘発
	CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
ENDIF