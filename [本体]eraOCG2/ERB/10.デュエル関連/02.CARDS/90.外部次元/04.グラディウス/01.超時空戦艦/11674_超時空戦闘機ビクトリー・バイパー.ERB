@CARDNAME_18001,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 초시공전투기 빅토리 바이퍼
	CASE "略称"
RESULTS = VICTORY VIPER
	CASE "カテゴリ"
RESULTS:0 = 시공전투기
RESULTS:1 = 게임 오리지널
ENDSELECT


@CARD_18001(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 6
	CASE "攻撃力"
		RETURN 2400
	CASE "守備力"
		RETURN 800
	CASE "성별"
		RETURN 牝性
	CASE "トークン"
		RETURN 1
	CASE "소환불가"
		RETURN 1
	CASE "특수불가"
		RETURN 1
ENDSELECT
RETURN 0

@CARD_EXPLANATION_18001(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드는 일반 소환할 수 없으며, "파워 캡슐 TYPE-C"의 효과로만 특수 소환할 수 있다.
PRINTL 이 카드는 필드에 존재하는 한, "초시공전투기 빅 바이퍼"로도 취급한다.
PRINTL "초시공전투기 빅토리 바이퍼"의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,필드起動)
PRINTL ①: 자신 메인 페이즈에 발동할 수 있다.
PRINTL 자신의 묘지의 "시공전투기" 카드 1장을 패에 추가한다.
CALLFORM CARD_ABLE(2,攻撃後誘発)
PRINTL ②: 이 카드가 전투로 상대 몬스터를 파괴했을 경우에 발동할 수 있다.
PRINTL 덱에서 "시공전투기" 카드 1장을 패에 추가한다.
CALLFORM CARD_ABLE(0,身代효과)
PRINTL ③: 이 카드가 파괴될 경우, 대신에 자신 필드의 "시공전투기" 카드를 파괴할 수 있다.
CALLFORM CARD_ABLE(0,戦破壊→묘지)
PRINTL ④: 이 카드가 필드에서 벗어났을 경우에 LP 100을 지불하고 발동할 수 있다.
PRINTL 패 / 덱 / 묘지 / 제외 상태인 "초시공전투기 빅 바이퍼" 1장을 특수 소환한다.

@CARDSUMMON_AA_18001

PRINTW LIMITTER   　 ……　OFF
PRINTW MACHINEPOWER　……　FULL
PRINTW CHANGE　      ……　COMPLETE！
PRINTL
PRINTW Are you ready？
PRINTL
PRINTW 《Gradius VICTORY VIPER》！


@CARDCAN_18001(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,18001)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動
	SIF 발동済_1(듀얼리스트,18001)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 시공전투기
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

;드라고뉴트から
ELSEIF 종류 == 攻撃後誘発
	SIF 발동済_2(듀얼리스트,18001)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_戦闘カード") != 場所
		RETURN 0
	SIF 直前戦闘破壊ID(듀얼리스트,1) == 0
		RETURN 0
	RETURN 1

ELSEIF 종류 == 身代효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF !GROUPMATCH(破壊종류,"효과","戦闘")
		RETURN 0
	SIF 破壊대상ゾーン != "필드"
		RETURN 0
	SIF 破壊대상場所 != 場所
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 시공전투기
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 戦破壊→묘지

	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲손패 = 1
	효과範囲묘지 = 1
	효과範囲제외 = 1
효과条件名前 = 초시공전투기 빅 바이퍼
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_18001(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,18001,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 시공전투기
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,18001,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 시공전투기
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == 身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,18001)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件カテゴリ = 시공전투기
		효과条件他所 = 1
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			RETURN 1
		ENDIF
	ENDIF
	RETURN 0

ELSEIF 종류 == 戦破壊→묘지
	IF 듀얼리스트 == "자신"
		PRINTW CONTINUE？
	ENDIF
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,18001)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",100)
		CALL FULLRESET
		효과範囲덱 = 1
		효과範囲손패 = 1
		효과範囲묘지 = 1
		효과範囲제외 = 1
효과条件名前 = 초시공전투기 빅 바이퍼
		효과추가종류 = 1 + 51
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF
