@CARDNAME_4824,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS
;？？？出身
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 고어마가라 헤비 헌터
	CASE "略称"
		RESULTS = 흑식의 헌터공주
	CASE "カテゴリ"
		RESULTS:0 = 헌터
		RESULTS:2 = 몬스터 헌터
ENDSELECT


@CARD_4824(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

	
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1700
	CASE "守備力"
		RETURN 1300
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4824(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.

;なんか探してるポイ？
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①：필드에 드래곤족 몬스터가 존재할 경우에 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환한다. 이렇게 특수 소환한 이 카드의 공격력은 1000 올린다.
;服椀による攻撃
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ②：자신 묘지의 '렵수'인 EX 몬스터를 대상으로 하여 발동할 수 있다.
PRINTL ('렵수'란, 드래곤족, 야수족, 비행야수족, 파충류족, 악마족, 곤충족, 해룡족, 미확인족, 원소정령족 몬스터를 지칭한다)
PRINTL 그 카드를 어둠 속성 드래곤족 몬스터로서 자신 필드에 특수 소환한다.
PRINTL 그 카드는 턴 종료 시에 파괴된다.
PRINTL 이 효과 발동 후, 턴 종료 시까지 자신은 EX덱에서 몬스터를 특수 소환할 수 없다.
;ただただ不穏
CALLFORM CARD_ABLE(0,지속효과,攻守変動)
PRINTL ③：이 몬스터가 필드에 존재하는 한, 자신 필드의 몬스터는 드래곤족이 되며,
PRINTL 매 턴 가능하면 공격한다.


@CARDCAN_4824(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4824)
SIF RESULT == 1
	RETURN 0
IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,4824)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,4824,"손패")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲상대필드 = 1
	효과条件종족 = 드래곤족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,4824)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	CALL SET_MONSTER_HUNTER
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과 || 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_4824(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4824,1)
	IF RESULT == 0
		CALL FULLRESET
		효과추가종류 = 1000 + 効追一攻
		CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)
		IF RESULT == 1
			CALL COVER_ATTACK(듀얼리스트,カード소환先)
		ENDIF
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4824,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		CALL SET_MONSTER_HUNTER
		효과추가종류:0 = 100 + 드래곤족
		효과추가종류:1 = 120 + 어둠속성
		효과추가종류:2 = 10 + 破壊行き
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		CALL SET_PLAYER_INFO(듀얼리스트,"EX制限",2)
	ENDIF
ELSEIF 종류 == 攻守変動
	CALL FULLRESET
	CALL STATCHANGE_OTHER(듀얼리스트,"필드",場所,"種",드래곤족)
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"戦狂")
ENDIF
