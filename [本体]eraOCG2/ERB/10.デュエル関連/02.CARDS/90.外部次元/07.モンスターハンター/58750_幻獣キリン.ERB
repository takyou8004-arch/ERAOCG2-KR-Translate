@CARDNAME_58750,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 환수 키린
	CASE "略称"
		RESULTS = 환수 키린
	CASE "カテゴリ"
		RESULTS:0 = 수렵조수
		RESULTS:1 = 환상수
		RESULTS:2 = 몬스터 헌터
ENDSELECT


@CARD_58750(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 싱크로
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 미확인족
	CASE "レベル"
		RETURN 6
	CASE "攻撃力"
		RETURN 1500
	CASE "守備力"
		RETURN 3000
	CASE "성별"
		RETURN 雄性
ENDSELECT

RETURN 0


@CARDSUMMON_58750(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要レベル数 = 6

IF EX参照 == 0 || EX参照 == 1
	素材条件튜너 = 1
ELSEIF EX参照 >= 10
	素材条件튜너 = -1
ENDIF

素材条件종족:0 = 드래곤족
素材条件종족:1 = 야수족
素材条件종족:2 = 비행야수족
素材条件종족:3 = 비늘야수족
素材条件종족:4 = 악마족
素材条件종족:5 = 바다생물족
素材条件종족:6 = 미확인족
素材条件종족:7 = 원소정령족
IF EX参照 == 1
	;PRINTL サモンまでは言ってるよ
	CALL SYNCAN(듀얼리스트,소환カード,58750) 
	RETURN RESULT
ENDIF



@CARD_EXPLANATION_58750(종류)
#DIM DYNAMIC 종류

PRINTL 『렵수』인 튜너 ＋ 『렵수』인 튜너 이외의 몬스터 1체 이상
PRINTL (렵수란, 드래곤족, 야수족, 비행야수족, 비늘야수족, 악마족, 곤충족, 바다생물족, 미확인족, 원소정령족 몬스터를 지칭한다)
PRINTL 이 카드명의 ③의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ①: 필드의 이 카드는 효과로는 파괴되지 않으며, 매 턴 가능하면 공격한다.
CALLFORM CARD_ABLE(0,ターン開始時強制,バトル開始時強制,ターン終了時強制,バトル終了時強制)
PRINTL ②: 이 카드명 이외의 카드의 효과가 발동했을 경우에 발동한다.
PRINTL 이 카드 이외의 랜덤한 카드 1장을 파괴한다.
CALLFORM CARD_ABLE(3,묘지起動)
PRINTL ③: 묘지의 이 카드를 제외하고 발동할 수 있다.
PRINTL 덱에서 "헌터" 마법 / 함정 카드 1장을 패에 넣는다.

@CARDCAN_58750(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,58750)
SIF RESULT == 1
	RETURN 0


IF 종류 == 효과後強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"효과발동",2)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_3(듀얼리스트,58750)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0

	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 헌터
	효과条件종류 = 마함全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_58750(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC ヒット위치 

IF 종류 == 효과後強制誘発

	ヒット위치 = RAND:20
	IF INRANGE(ヒット위치,0,4)
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",ヒット위치,"묘지")
	ELSEIF INRANGE(ヒット위치,5,9)
		ヒット위치 -= 5
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"魔自動処理",ヒット위치,"묘지")
	ELSEIF INRANGE(ヒット위치,10,14)
		ヒット위치 -= 10
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",ヒット위치,"묘지",,1)
	ELSEIF INRANGE(ヒット위치,15,19)
		ヒット위치 -= 15
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"魔自動処理",ヒット위치,"묘지",,1)
	ENDIF
ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"破耐")
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"戦狂")
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,58750,3)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과条件カテゴリ = 헌터
		효과条件종류 = 마함全般
		CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
	ENDIF
ENDIF
