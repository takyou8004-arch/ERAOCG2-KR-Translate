@CARDNAME_4820,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS
;ポッケ村出身
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 키린 소드 헌터
	CASE "略称"
		RESULTS = 환수의한손검헌터
	CASE "カテゴリ"
		RESULTS:0 = 헌터
		RESULTS:2 = 몬스터 헌터
ENDSELECT


@CARD_4820(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

	
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 전사족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1500
	CASE "守備力"
		RETURN 1500
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4820(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 『렵수』인 몬스터가 필드에 존재할 경우에 발동할 수 있다.
PRINTL (렵수란, 드래곤족, 야수족, 비행야수족, 비늘야수족, 악마족, 곤충족, 바다생물족, 미확인족, 원소정령족 몬스터를 의미한다)
PRINTL 이 카드를 패에서 특수 소환한다.
;ベテラン헌터の바람格
CALLFORM CARD_ABLE(2,소환時,특수소환時)
PRINTL ②: 이 카드가 소환 / 특수 소환했을 경우에 발동할 수 있다.
PRINTL EX덱에서 레벨 6 이상의 『렵수』인 몬스터 1장을 묘지로 보낸다.
PRINTL 이 효과의 발동 후, 턴 종료 시까지 자신은 EX덱에서 몬스터를 특수 소환할 수 없다.
;サポ-トは任せて～。속성開放で麻痺효과
CALLFORM CARD_ABLE(10,필드起動,フリー誘発)
PRINTL ③: 서로의 턴에 1번, 상대 필드의 『렵수』인 몬스터 1장을 대상으로 발동할 수 있다.
PRINTL 그 몬스터를 수비 표시로 한다. 그 몬스터는 다음 페이즈까지 효과를 무효로 한다.

@CARDCAN_4820(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4820)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,4820)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,4820,"손패")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲상대필드 = 1
	CALL SET_MONSTER_HUNTER
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,4820)
		RETURN 0
	CALL FULLRESET
	효과範囲EX덱 = 1
	CALL SET_MONSTER_HUNTER
	CALL 효과条件レベル_ADV(6, "以上")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	CALL SET_MONSTER_HUNTER
	효과条件除去種 = 無効
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_4820(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4820,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)
	ENDIF
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4820,2)
	IF RESULT == 0
		CALL FULLRESET
		CALL SET_MONSTER_HUNTER

		CALL 효과条件レベル_ADV(6, "以上")
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		CALL SET_PLAYER_INFO(듀얼리스트,"EX制限",2)
	ENDIF
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4820,10)
	IF RESULT == 0
		CALL FULLRESET
		CALL SET_MONSTER_HUNTER
		효과추가종류 = 9
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"표시변경")
	ENDIF
ENDIF

@SET_MONSTER_HUNTER

효과条件종족:0 = 드래곤족
효과条件종족:1 = 야수족
효과条件종족:2 = 비행야수족
효과条件종족:3 = 비늘야수족
효과条件종족:4 = 악마족
효과条件종족:5 = 바다생물족
효과条件종족:6 = 미확인족
효과条件종족:7 = 원소정령족
효과条件종족:8 = 곤충족