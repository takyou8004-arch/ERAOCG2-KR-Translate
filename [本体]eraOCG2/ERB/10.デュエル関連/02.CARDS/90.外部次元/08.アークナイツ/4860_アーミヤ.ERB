@CARDNAME_4860,参照先
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 아미야
	CASE "略称"
		RESULTS = 아미야
	CASE "カテゴリ"
		RESULTS:0 = 로도스 아일랜드
		RESULTS:1 = Caster
		RESULTS:2 = 오퍼레이터
		RESULTS:4 = 명일방주
ENDSELECT


@CARD_4860(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 미확인족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 2300
	CASE "守備力"
		RETURN 1300
	CASE "성별"
		RETURN 牝性
	CASE "単一存在"
		RETURN 1
	CASE "生成カード"
		RETURN 14860
	CASE "추가日"
		RETURN 250406
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4860(종류)
#DIM DYNAMIC 종류

PRINTL "아미야"는 자신 필드에 1장밖에 존재할 수 없으며,
PRINTL 그 ①③의 효과는 각각 1턴에 1번밖에 사용할 수 없으며, ③의 효과는 마법 / 함정 존에서도 발동할 수 있다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 2000LP를 지불하고 발동할 수 있다.
PRINTL 이 카드를 자신의 마법 & 함정 존에 앞면 표시로 놓는다.
PRINTL 자신 필드에 "오퍼레이터"가 3체 이상 존재할 경우, "아미야 영소"로서 자신 필드에 특수 소환할 수도 있다.
PRINTL 그 후, 덱에서 "PRTS" 1장을 패에 넣을 수 있다.
CALLFORM CARD_ABLE(0,발동強制誘発)
PRINTL ②: 카드의 효과가 발동할 때마다, 이 카드의 SP 카운터를 1개 늘린다 (최대 5).
CALLFORM CARD_ABLE(3,마함起動,필드起動,フリー誘発)
PRINTL ③: 서로의 턴에, 이 카드의 SP 카운터 5개를 제거하고 발동할 수 있다.
PRINTL 상대 필드의 카드를 2장까지 파괴하고, 이 카드를 덱으로 되돌린다.
PRINTL 이 효과는 대상 내성 몬스터 내성의 영향을 받지 않는다.


@CARDCAN_4860(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 발동불가,2
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4860)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	발동불가:0 = 0
	발동불가:1 = 0

	SIF 발동済_1(듀얼리스트,4860)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 2000
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		발동불가:0 = 1
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件名前 = 아미야
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		발동불가:0 = 1

	CALL CARD_SS_CHECK(듀얼리스트,4860,"손패")
	SIF RESULT == 1
		발동불가:1 = 1
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 오퍼레이터
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 3
		발동불가:1 = 1
	
	SIF 발동불가:0 == 1 && 발동불가:1 == 1
		RETURN 0

	CALL CARD_DRAW_CHECK
	RETURN 1

ELSEIF 종류 == 발동強制誘発
	SIF !GROUPMATCH(ゾーン,"필드","마함")
		RETURN 0
	IF ゾーン == "필드"
		SIF GETVAR(@"%듀얼리스트%_MZ_個Ｃ:場所") >= 5
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_個Ｃ:場所") >= 5
			RETURN 0
	ENDIF
	CALL FULLRESET
	CALL CARD_CHECK_START_DATA_2(듀얼리스트,2)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 마함起動 || 종류 == 필드起動 || 종류 == フリー誘発
	SIF !GROUPMATCH(ゾーン,"필드","마함")
		RETURN 0
	SIF 발동済_3(듀얼리스트,4860)
		RETURN 0
	IF ゾーン == "필드"
		SIF GETVAR(@"%듀얼리스트%_MZ_個Ｃ:場所") < 5
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_個Ｃ:場所") < 5
			RETURN 0
	ENDIF
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_4860(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 발동불가,2
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4860,1)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",2000)

		발동불가:0 = 0
		발동불가:1 = 0

		SIF 마함数(듀얼리스트) >= 5
			발동불가:0 = 1
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件名前 = 아미야
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			발동불가:0 = 1

		CALL CARD_SS_CHECK(듀얼리스트,4860,"손패")
		SIF RESULT == 1
			발동불가:1 = 1
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件カテゴリ = 오퍼레이터
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT < 3
			발동불가:1 = 1
		
		IF 발동불가:0 != 1 && 발동불가:1 != 1
			IF 듀얼리스트 == "자신"
				CALL YN_CHOICE(듀얼리스트,"●마법＆함정 존에 둔다","●影霄を握る")
			ELSE
				RESULT = 1
			ENDIF
		ELSEIF 발동불가:0 != 1
			RESULT = 0
		ELSEIF 발동불가:1 != 1
			RESULT = 1
		ENDIF

		IF RESULT == 0
			CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
		ELSEIF RESULT == 1
			CALL CARD_RECONSTRUCT(듀얼리스트,ゾーン,場所,14860)
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
		ENDIF

		CALL FULLRESET
		효과範囲덱 = 1
		효과条件名前 = PRTS
		효과条件任意 = 1
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == 발동強制誘発
	CALL SET_FIELD_INFO(듀얼리스트,ゾーン,場所,"個Ｃ",1)

ELSEIF 종류 == 마함起動 || 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4860,3)
	IF RESULT == 0
		CALL SET_FIELD_INFO(듀얼리스트,ゾーン,場所,"個Ｃ",-5)
		REPEAT 2
			CALL FULLRESET
			효과範囲상대필드 = 1
			효과範囲상대마함 = 1
			효과条件任意 = 1
			CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT != 9999
				CALL CARD_SELECT(듀얼리스트,RESULT)
				選択カード = RESULT
				CALL CARD_DISPOSE(対面者,参照先（CS）,選択カード,破壊行き)
			ELSE
				BREAK
			ENDIF
		REND
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,덱行き)
	ENDIF
ENDIF

;------------------------------------------------------------------------------------------

@CARDNAME_14860,参照先
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 아미야 영소
	CASE "略称"
		RESULTS = 아미야, 블레이드
	CASE "カテゴリ"
		RESULTS:0 = 로도스 아일랜드
		RESULTS:1 = Guard
		RESULTS:2 = 오퍼레이터
ENDSELECT


@CARD_14860(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 미확인족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 1300
	CASE "성별"
		RETURN 牝性
	CASE "単一存在"
		RETURN 1
	CASE "別途ID"
		RETURN 4860
	CASE "トークン"
		RETURN 1
	CASE "生成カード"
		RETURN 4860
ENDSELECT

RETURN 0

@CARD_EXPLANATION_14860(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드는 룰 상 "아미야"로도 취급한다.
PRINTL "아미야"는 자신 필드에 1장밖에 존재할 수 없으며,
PRINTL "아미야 영소"의 ①의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,발동誘発)
PRINTL ①: 상대의 효과가 발동했을 때 발동할 수 있다.
PRINTL 그 발동을 무효로 하고, 상대는 자신의 필드의 공격력이 가장 높은 몬스터 1장을 묘지로 보낸다.
PRINTL 그 후, 이 카드의 공격력은 [묘지로 보내진 몬스터의 원래 코스트] × 200 올린다.
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ②: 자신 필드의 "오퍼레이터"의 공격력 / 수비력은 500 올린다.
CALLFORM CARD_ABLE(0,戦破壊→묘지)
PRINTL ③: 이 카드가 상대에 의해 필드를 벗어났을 경우 발동할 수 있다.
PRINTL 덱 / 묘지에서 "아미야" 이외의 "오퍼레이터" 1장을 특수 소환하고, 이 카드는 "아미야"가 된다.


@CARDCAN_14860(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,14860)
SIF RESULT == 1
	RETURN 0

IF 종류 == 발동誘発
	SIF 발동済_1(듀얼리스트,14860)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 戦破壊→묘지
	SIF 処理中_듀얼리스트 == 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 오퍼레이터
	효과条件固名前 = 아미야
	효과条件소환可 = 2
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_14860(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 발동誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,14860,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"발동無効",場所,"無効")

		FOR カウンタ,0,5
			SIF GETVAR(@"%対面者%_필드:カウンタ") < 1
				CONTINUE
			SIF GETVAR(@"%対面者%_MZ_表:カウンタ") == 3
				CONTINUE
			SIF 攻撃力修正 >= GETVAR(@"%対面者%_MZ_攻:カウンタ")
				CONTINUE
			攻撃力修正 = GETVAR(@"%対面者%_MZ_攻:カウンタ")
		NEXT
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件以攻撃力 = 攻撃力修正
		CALL CARD_ADD_GY_ADV(対面者,ゾーン,場所)
		IF RESULT == 1
			TRYCALLFORM CARD_{RESULT:1},"レベル"
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"永攻",RESULT*200)
		ENDIF
	ENDIF
ELSEIF 종류 == 攻守変動
	CALL FULLRESET
	효과条件カテゴリ = 오퍼레이터
	CALL STATCHANGE_BASIC(듀얼리스트,"필드",場所,500,500)

ELSEIF 종류 == 戦破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,14860)
	IF RESULT == 0
		CALL CARD_RECONSTRUCT(듀얼리스트,ゾーン,場所,4860)
		CALL FULLRESET
		효과範囲덱 = 1
		효과範囲묘지 = 1
		효과条件カテゴリ = 오퍼레이터
		효과条件固名前 = 아미야
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF

