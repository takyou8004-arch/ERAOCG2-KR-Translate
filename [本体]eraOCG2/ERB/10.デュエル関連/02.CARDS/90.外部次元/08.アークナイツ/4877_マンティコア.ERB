@CARDNAME_4877,参照先
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 맨티코어
	CASE "略称"
		RESULTS = Manticore
	CASE "カテゴリ"
		RESULTS:0 = Specialist
		RESULTS:1 = 오퍼레이터
		RESULTS:4 = 명일방주
ENDSELECT


@CARD_4877(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 미확인족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 1600
	CASE "守備力"
		RETURN 1100
	CASE "성별"
		RETURN 牝性
	CASE "単一存在"
		RETURN 1
	CASE "추가日"
		RETURN 250406
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4877(종류)
#DIM DYNAMIC 종류

PRINTL "맨티코어"는 자신의 필드에 한 장밖에 존재할 수 없으며,
PRINTL 그 ①의 효과는 한 턴에 한 번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動,묘지起動)
PRINTL ①: 자신의 필드에 "오퍼레이터"가 두 장 이상 존재할 경우,
PRINTL 상대 턴에 1800 LP를 지불하고 발동할 수 있다.
PRINTL 이 카드를 패 / 묘지에서 특수 소환한다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ②: 이 카드는 상대의 공격/효과의 대상이 되지 않는다.
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ③: 상대 필드의 몬스터의 공격력은 1000 다운되고,
PRINTL 상대 배틀 페이즈 동안, 추가로 500 다운된다.


@CARDCAN_4877(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4877)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動 || 종류 == 묘지起動
	SIF 발동済_1(듀얼리스트,4877)
		RETURN 0
	SIF ターンプレイヤー == 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,4877,ゾーン)
	SIF RESULT == 1
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 1800
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 오퍼레이터
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과 || 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_4877(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

IF 종류 == 손패起動 || 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4877,1)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",1800)
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,ゾーン, 場所)
	ENDIF

ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"攻避")
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"対耐")

ELSEIF 종류 == 攻守変動
	CALL FULLRESET
	CALL STATCHANGE_BASIC(듀얼리스트,"상대필드",場所,-1000,0,4877)
	SIF ターンプレイヤー != 듀얼리스트 && バトルフェイズ == 1
		CALL STATCHANGE_BASIC(듀얼리스트,"상대필드",場所,-500,0,4877)
ENDIF
