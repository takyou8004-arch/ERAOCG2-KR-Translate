@CARDNAME_64862,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 안전 크레인 / 간이 보급 물자
	CASE "略称"
		RESULTS = 안전C / 보급물자
	CASE "カテゴリ"
		RESULTS:0 = 전술 장비
		RESULTS:4 = 명일방주
ENDSELECT


@CARD_64862(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 速攻마법
	CASE "추가日"
		RETURN 250406
ENDSELECT

RETURN 0


@CARD_EXPLANATION_64862(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1장밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動,마함起動,フリー誘発)
PRINTL ①: 자신 필드의 "오퍼레이터" 1장을 릴리스하고 발동할 수 있다.
PRINTL 그 "오퍼레이터"와는 레벨이 다르고, 레벨의 차이가 2 이하인 "오퍼레이터" 1장을 덱에서 특수 소환한다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 자신 메인 페이즈에 묘지의 이 카드를 제외하고 발동할 수 있다.
PRINTL 자신 필드의 "오퍼레이터" 1체에 SP 카운터를 2개 놓는다.


@CARDCAN_64862(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,64862)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動  || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,64862)
		RETURN 0
	IF ゾーン == "손패"
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0		
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	CALL FULLRESET
	효과範囲필드 = 1
	SIF モンスター数(듀얼리스트) < 5
		효과範囲마함 = 1
	효과条件カテゴリ = 오퍼레이터
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 오퍼레이터
	효과条件소환可 = 2
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,64862)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 오퍼레이터
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_64862(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カードレベル

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動  || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,64862,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲필드 = 1
		SIF モンスター数(듀얼리스트) < 5
			효과範囲마함 = 1
		효과条件カテゴリ = 오퍼레이터
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			TRYCALLFORM CARD_{RESULT:1},"レベル"
			選択カードレベル = RESULT
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 오퍼레이터
			효과条件レベル:0 = 選択カードレベル + 1
			효과条件レベル:1 = 選択カードレベル + 2
			효과条件レベル:2 = 選択カードレベル - 1
			효과条件レベル:3 = 選択カードレベル - 2
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF

ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,64862,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件カテゴリ = 오퍼레이터
		효과条件표시 = 表側표시
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT != 9999
			CALL CARD_SELECT(듀얼리스트,RESULT)
			CALL SET_FIELD_INFO(듀얼리스트,参照先（CS）,RESULT,"個Ｃ",2)
		ENDIF
	ENDIF
ENDIF