@CARDNAME_4880,参照先
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 위셔델
	CASE "略称"
		RESULTS = Wiš'adel
	CASE "カテゴリ"
		RESULTS:0 = Sniper
		RESULTS:1 = 오퍼레이터
		RESULTS:4 = 명일방주
ENDSELECT


@CARD_4880(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 7
	CASE "攻撃力"
		RETURN 2200
	CASE "守備力"
		RETURN 1600
	CASE "성별"
		RETURN 牝性
	CASE "単一存在"
		RETURN 1
	CASE "生成カード"
		RETURN 104880
	CASE "추가日"
		RETURN 250406
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4880(종류)
#DIM DYNAMIC 종류

PRINTL "위셔델"은 자신 필드에 1장밖에 존재할 수 없으며,
PRINTL 그 ①③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 2500 LP를 지불하고 발동할 수 있다.
PRINTL 이 카드를 자신 마법 & 함정 존에 앞면 표시로 놓는다.
PRINTL 그 후, 자신 필드에 "레버넌트의 그림자" 1장을 생성할 수 있다.
CALLFORM CARD_ABLE(0,ターン開始時強制,バトル開始時強制,ターン終了時強制,バトル終了時強制)
PRINTL ②: 페이즈가 바뀔 때마다,
PRINTL 이 카드의 SP 카운터를 1개 늘린다 (최대 10).
PRINTL 그 후, SP 카운터가 3의 배수일 경우, 자신 필드에 "레버넌트의 그림자" 1장을 특수 소환한다.
CALLFORM CARD_ABLE(0,ターン終了時)
PRINTL ③: 엔드 페이즈에, 이 카드의 SP 카운터를 10개 제거하고 발동할 수 있다.
PRINTL 상대 필드의 앞면 표시의 카드를 전부 파괴한다.


@CARDCAN_4880(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 발동불가,2
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4880)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,4880)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 2500
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件名前 = 위셔델
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == ターン開始時強制 || 종류 == ターン終了時強制 || 종류 == バトル開始時強制 || 종류 == バトル終了時強制
	SIF !GROUPMATCH(ゾーン,"필드","마함")
		RETURN 0
	IF ゾーン == "필드"
		SIF GETVAR(@"%듀얼리스트%_MZ_個Ｃ:場所") >= 10
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_個Ｃ:場所") >= 10
			RETURN 0
	ENDIF
	RETURN 1
ELSEIF 종류 == ターン終了時
	SIF !GROUPMATCH(ゾーン,"필드","마함")
		RETURN 0
	IF ゾーン == "필드"
		SIF GETVAR(@"%듀얼리스트%_MZ_個Ｃ:場所") < 10
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_個Ｃ:場所") < 10
			RETURN 0
	ENDIF
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_4880(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 발동불가,2
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4880,1)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",2500)
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)

		SIF 듀얼리스트 == "자신"
			PRINTL "레버넌트의 그림자"를 생성하시겠습니까?
		CALL YN_CHOICE(듀얼리스트)

		CALL CARD_SS_CHECK(듀얼리스트,204880,"")
		IF RESULT != 1 && 마함数(듀얼리스트) < 5
			SIF 듀얼리스트 == "자신"
				PRINTL "레버넌트의 그림자"를 몬스터 존에 생성하시겠습니까?
			CALL YN_CHOICE(듀얼리스트)
		ELSEIF RESULT == 1
			RESULT = 0
		ELSEIF 마함数(듀얼리스트) < 5
			RESULT = 1
		ELSE
			SIF 듀얼리스트 == "자신"
				PRINTL 필드가 가득 찼기 때문에 처리를 종료합니다
		ENDIF

		IF RESULT == 0
			CALL FULLRESET
			CALL CARD_GENERATE(듀얼리스트,204880)
		ELSEIF RESULT == 1
			CALL CARD_GENERATE_MAGIC(듀얼리스트,204880,1)
		ENDIF
	ENDIF

ELSEIF 종류 == ターン開始時強制 || 종류 == ターン終了時強制 || 종류 == バトル開始時強制 || 종류 == バトル終了時強制
	CALL SET_FIELD_INFO(듀얼리스트,ゾーン,場所,"個Ｃ",1)

	IF ゾーン == "필드"
		IF GROUPMATCH(GETVAR(@"%듀얼리스트%_MZ_個Ｃ:場所"),3,6,9)
			CALL FULLRESET
			CALL CARD_GENERATE(듀얼리스트,204880)
		ENDIF
	ELSEIF ゾーン == "마함"
		IF GROUPMATCH(GETVAR(@"%듀얼리스트%_魔_個Ｃ:場所"),3,6,9)
			CALL FULLRESET
			CALL CARD_GENERATE(듀얼리스트,204880)
		ENDIF
	ENDIF
ELSEIF 종류 == ターン終了時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4880)
	IF RESULT == 0
		CALL SET_FIELD_INFO(듀얼리스트,ゾーン,場所,"個Ｃ",-10)
		CALL FULLRESET
		효과条件표시 = 表側표시
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場全体",場所,"묘지")
	ENDIF
ENDIF

;────────────────────────────────────────────────────────────


@CARDNAME_204880,参照先
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 레버넌트의 그림자
	CASE "略称"
		RESULTS = 레버넌트의 그림자
	CASE "カテゴリ"
		RESULTS:0 = 소환물
ENDSELECT


@CARD_204880(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 0
	CASE "守備力"
		RETURN 0
	CASE "トークン"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_204880(종류)
#DIM DYNAMIC 종류

CALLFORM CARD_ABLE(0,지속효과)
PRINTL ①: "레버넌트의 그림자"는 소재 불가를 지닌다.
CALLFORM CARD_ABLE(10,필드起動,마함起動,フリー誘発)
PRINTL ②: 서로의 턴에 1번, 상대 필드의 몬스터 1장을 대상으로 발동할 수 있다.
PRINTL 그 몬스터의 공격력 수비력을 700 내리고, 자신 필드의 "위셔델"의 SP 카운터를 1개 늘린다.
PRINTL 이 효과로 공격력 또는 수비력이 0이 된 몬스터는 파괴된다.
PRINTL ③: 자신 필드에 "위셔델"이 존재하지 않을 경우, 또는 이 카드가 필드를 벗어났을 경우, 이 카드는 소멸한다.

@CARDCAN_204880(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 발동불가,2
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,204880)
SIF RESULT == 1
	RETURN 0

IF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 마함起動 || 종류 == 필드起動 || 종류 == フリー誘発
	SIF !GROUPMATCH(ゾーン,"필드","마함")
		RETURN 0
	SIF 효과발동済(듀얼리스트,場所,ゾーン)
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 효과後強制誘発
	SIF !GROUPMATCH(ゾーン,"필드","마함")
		RETURN 1
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件名前 = 위셔델
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 1
	RETURN 0
ENDIF

@CARDEFFECT_204880(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"不材")
	;CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"攻避")

ELSEIF 종류 == 마함起動 || 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,204880,10)
	IF RESULT == 0
		CALL FULLRESET
		효과추가종류 = 11
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"永攻DOWN",700)

		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件名前 = 위셔델
		CALL CARD_GRANT_ADV(듀얼리스트,ゾーン,場所,"個Ｃ",1)
	ENDIF

ELSEIF 종류 == 효과後強制誘発
	CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,소멸行き)
ENDIF
