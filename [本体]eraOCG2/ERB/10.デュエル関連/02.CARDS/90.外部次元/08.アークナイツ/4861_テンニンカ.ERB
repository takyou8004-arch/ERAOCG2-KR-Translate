@CARDNAME_4861,参照先
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS
SELECTCASE 参照先
	CASE "名前"
RESULTS = 텐닌카
	CASE "略称"
RESULTS = Myrtle
	CASE "カテゴリ"
RESULTS:0 = 로도스 아일랜드
RESULTS:1 = Vanguard
RESULTS:2 = 오퍼레이터
RESULTS:4 = 아크나이츠
ENDSELECT


@CARD_4861(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 2
	CASE "攻撃力"
		RETURN 750
	CASE "守備力"
		RETURN 600
	CASE "성별"
		RETURN 牝性
	CASE "単一存在"
		RETURN 1
	CASE "추가日"
		RETURN 250406
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4861(종류)
#DIM DYNAMIC 종류

PRINTL "텐닌카"는 자신 필드에 1장밖에 존재할 수 없으며,
PRINTL 그 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 자신 필드에 "오퍼레이터"가 존재할 경우, 800 LP를 지불하고 발동할 수 있다.
PRINTL 이 카드를 손패에서 특수 소환한다.
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ②: 자신 메인 페이즈에 발동할 수 있다.
PRINTL 자신은 1400 LP 회복한다.
PRINTL 그 후, 덱에서 "로도스 아일랜드" 몬스터 1장을 손패에 추가할 수 있다.
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ③: 자신 필드에 다른 몬스터가 존재할 경우, 이 카드는 공격 대상이 되지 않는다.


@CARDCAN_4861(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4861)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,4861)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,4861,"손패")
	SIF RESULT == 1
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 800
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 오퍼레이터
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1

ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,4861)
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_4861(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4861,1)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",800)
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)
	ENDIF

ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4861,2)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"増加","효과",1400)

		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 로도스 아일랜드
		효과条件종류 = モン全般
		효과条件任意 = 1
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"攻避")
ENDIF
