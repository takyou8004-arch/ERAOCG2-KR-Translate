@CARDNAME_4875,参照先
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 폰사이러스
	CASE "略称"
		RESULTS = Poncirus
	CASE "カテゴリ"
		RESULTS:0 = Vanguard
		RESULTS:1 = 오퍼레이터
		RESULTS:4 = 명일방주
ENDSELECT


@CARD_4875(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 바람속성
	CASE "종족"
		RETURN 비행야수족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 1600
	CASE "守備力"
		RETURN 1700
	CASE "성별"
		RETURN 牝性
	CASE "単一存在"
		RETURN 1
	CASE "추가日"
		RETURN 250406
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4875(종류)
#DIM DYNAMIC 종류

PRINTL "폰사이러스"는 자신 필드에 1장밖에 존재할 수 없으며,
PRINTL 그 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 자신 필드에 "오퍼레이터"가 존재할 경우, 1100 LP를 지불하고 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환한다.
CALLFORM CARD_ABLE(2,필드起動,フリー誘発)
PRINTL ②: 이 카드가 "각성"하지 않은 경우, 자신 메인 페이즈 또는 상대 메인 페이즈 2에 발동할 수 있다.
PRINTL 자신은 1100 LP 회복한다.
PRINTL 그 후, 덱에서 공격력 1000 이하의 "오퍼레이터" 1장을 패에 넣을 수 있다.
PRINTL 이 효과가 필드에서의 합계로 2번째 발동인 경우, 추가로 이 카드는 "각성"한다.
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ③: 이 카드가 "각성"되어 있는 경우, 공격력 수비력이 700 증가하고,
PRINTL 페이즈가 바뀔 때마다 자신은 300 LP 회복한다.
CALLFORM CARD_ABLE(0,ターン開始時)
PRINTL ④: 이 카드가 "각성"되어 있는 경우, 자신 스탠바이 페이즈에 발동할 수 있다.
PRINTL 덱에서 "오퍼레이터" 1장을 패에 넣는다.


@CARDCAN_4875(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4875)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,4875)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,4875,"손패")
	SIF RESULT == 1
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 1100
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 오퍼레이터
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,4875)
		RETURN 0
	IF ターンプレイヤー == 듀얼리스트
		SIF メインフェイズ != 1
			RETURN 0
	ELSE
		SIF メインフェイズ２ != 1
			RETURN 0
	ENDIF
	SIF GETVAR(@"%듀얼리스트%_MZ_条達:場所") >= 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻守変動 || 종류 == ターン開始時強制 || 종류 == ターン終了時強制 || 종류 == バトル開始時強制 || 종류 == バトル終了時強制
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_条達:場所") < 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == ターン開始時
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_条達:場所") < 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 오퍼레이터
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_4875(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4875,1)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",1100)
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)
	ENDIF

ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4875,2)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"増加","효과",1100)
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"汎数",1)
		SIF GETVAR(@"%듀얼리스트%_MZ_汎数:場所") >= 2
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"条達",2)

		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 오퍼레이터
		효과条件攻撃力 = 1000
		효과条件任意 = 1
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == 攻守変動
	CALL STATCHANGE_BASIC(듀얼리스트,"自身",場所,700,700)

ELSEIF 종류 == ターン開始時強制 || 종류 == ターン終了時強制 || 종류 == バトル開始時強制 || 종류 == バトル終了時強制
	CALL LIFE_CHANGE(듀얼리스트,"増加","효과",300)

ELSEIF 종류 == ターン開始時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4875)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 오퍼레이터
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF
