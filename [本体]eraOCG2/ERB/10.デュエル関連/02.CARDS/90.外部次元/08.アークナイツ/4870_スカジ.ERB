@CARDNAME_4870,参照先
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 스카디
	CASE "略称"
		RESULTS = 스카디
	CASE "カテゴリ"
		RESULTS:0 = 어비설 헌터스
		RESULTS:1 = Guard
		RESULTS:2 = 오퍼레이터
		RESULTS:4 = 명일방주
ENDSELECT


@CARD_4870(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 바다생물족
	CASE "レベル"
		RETURN 7
	CASE "攻撃力"
		RETURN 2900
	CASE "守備力"
		RETURN 2500
	CASE "성별"
		RETURN 牝性
	CASE "単一存在"
		RETURN 1
	CASE "추가日"
		RETURN 250406
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4870(종류)
#DIM DYNAMIC 종류

PRINTL "스카디"는 자신 필드에 1장밖에 존재할 수 없으며,
PRINTL 그 ①③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 자신 필드에 "오퍼레이터"가 3체 이상, 또는 "어비설 헌터스"가 1체 이상 존재할 경우,
PRINTL 자신 또는 상대 배틀 페이즈에 1700 LP를 지불하고 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환한다.
PRINTL 이 효과를 상대의 공격 선언시에 발동했을 경우, 또한 공격 대상을 이 카드로 옮긴다.
CALLFORM CARD_ABLE(0,소환時強制,특수소환時強制)
PRINTL ②: 이 카드가 소환 / 특수 소환했을 때에 발동한다.
PRINTL 상대 필드의 몬스터 1장을 선택하고,
PRINTL 이 카드의 공격력은 턴 종료시까지 그 몬스터의 공격력의 절반만큼 상승한다.
PRINTL 상대 필드에 대상으로 할 수 있는 몬스터가 존재하지 않을 경우, 대신에 턴 종료시까지 500 상승한다.
CALLFORM CARD_ABLE(3,攻撃後誘発)
PRINTL ③: 이 카드가 전투로 몬스터를 파괴했을 경우에 발동할 수 있다.
PRINTL 패 / 덱 / 묘지에서 "어비설 헌터스" 1장을 자신 필드에 특수 소환한다.


@CARDCAN_4870(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4870)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動 || 종류 == フリー誘発
	SIF ゾーン != "손패"
		RETURN 0
	SIF 발동済_1(듀얼리스트,4870)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,4870,"손패")
	SIF RESULT == 1
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 1700
		RETURN 0
	SIF バトルフェイズ != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 오퍼레이터
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT >= 3
		RETURN 1
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件カテゴリ = 어비설 헌터스
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 1
	RETURN 0

ELSEIF 종류 == 소환時強制 || 종류 == 특수소환時強制
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻撃後誘発
	SIF 발동済_3(듀얼리스트,4870)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 直前戦闘破壊ID(듀얼리스트,1) == 0
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲덱 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 어비설 헌터스
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_4870(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4870,1)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",1700)
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)

		IF 戦闘場所(듀얼리스트,1) > -1 && 戦闘場所(듀얼리스트,1) < 5 && ターンプレイヤー != 듀얼리스트
			CALL COVER_ATTACK(듀얼리스트,カード소환先)
		ENDIF
	ENDIF

ELSEIF 종류 == 소환時強制 || 종류 == 특수소환時強制
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4870)
	IF RESULT == 0
		CALL FULLRESET
		효과条件표시 = 表側표시
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"選択")
		IF RESULT == 1
			攻撃力修正 = GETVAR(@"%対面者%_MZ_攻:(RESULT:1)") / 2
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"一攻",攻撃力修正)
		ELSE
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"一攻",500)
		ENDIF
	ENDIF

ELSEIF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4870,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲덱 = 1
		효과範囲묘지 = 1
		효과条件カテゴリ = 어비설 헌터스
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン, 場所)
	ENDIF
ENDIF
