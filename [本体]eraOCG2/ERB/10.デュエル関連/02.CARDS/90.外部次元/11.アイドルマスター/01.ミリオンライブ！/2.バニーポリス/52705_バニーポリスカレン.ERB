@CARDNAME_52705,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 바니 폴리스 대장 카렌
	CASE "略称"
		RESULTS = 바니 P 카렌
	CASE "カテゴリ"
		RESULTS:0 = 래빗시티
		RESULTS:1 = 바니 폴리스
		RESULTS:2 = 밀리언 라이브!
		; RESULTS:2 = THE IDOLM@STER
		; RESULTS:4 = MILLION LIVE!
		RESULTS:4 = 시노미야 카렌
ENDSELECT

@CARD_52705(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 야수족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 2800
	CASE "守備力"
		RETURN 2800
	CASE "성별"
		RETURN 牝性
	CASE "連動ID"
		RETURN 4650	
	CASE "추가日"
		RETURN 250601
ENDSELECT
RETURN 0

@CARDSUMMON_52705(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 2
素材条件レベル = 8
素材条件カテゴリ = 래빗 걸

IF EX参照 == 1
	CALL XYZCAN(듀얼리스트,소환カード,52705) 
	RETURN RESULT
ENDIF

@CARD_EXPLANATION_52705(종류)
#DIM DYNAMIC 종류

PRINTL 레벨 8 "래빗 걸" 몬스터 2장
CALL CARD_ABLE(1,EX덱起動)
PRINTL "바니 폴리스 대장 카렌"은 1턴에 1번, 자신 필드의 뒷면 표시로 발동 불가 또는 표시 고정 상태의 카드 1장을 제외하고,
PRINTL 필드의 레벨 8의 "래빗 걸" 몬스터 1장 위에 겹쳐서 엑시즈 소환할 수 있다.
PRINTL 이 방법으로 엑시즈 소환한 이 카드는 각성 상태가 된다.
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALL CARD_ABLE(2,EX소환時)
PRINTL ①: 이 카드가 엑시즈 소환했을 경우에, 이 카드가 각성 상태라면 발동할 수 있다.
PRINTL 상대 필드의 몬스터 전부에 효과 무효를 부여한다.
CALL CARD_ABLE(3,필드起動)
PRINTL ②: 자신 메인 페이즈에 발동할 수 있다.
PRINTL 이 카드의 엑시즈 소재를 1개 제거하거나, 자신 필드의 뒷면 표시의 카드 1장을 제외하고,
PRINTL 덱 / 묘지에서 "바니 폴리스" 마법 / 함정 카드 1장을 패에 넣는다.
CALL CARD_ABLE(0,ターン終了時強制)
PRINTL ③: 자신 / 상대의 엔드 페이즈 시에 발동한다.
PRINTL 자신 필드의 이 카드를 엑스트라 덱으로 되돌린다.
PRINTL 그 후, 묘지 / 엑스트라 덱에서 레벨 8의 "래빗 걸" 펜듈럼 몬스터 카드 1장을 상대의 마법 & 함정 존에 놓을 수 있다.


@CARDCAN_52705(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,52705)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX덱起動
	SIF 발동済_1(듀얼리스트,52705)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件レベル = 8
효과条件カテゴリ = 래빗 걸
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	CALL CARDCHECKADV_BUNNYPOLICE_EVIDENCE(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == EX소환時
	SIF 발동済_2(듀얼리스트,52705)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_条達:場所") == 0
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 발동済_3(듀얼리스트,52705)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") > -1
		条件달성++
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과条件표시 = 裏側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1 && GETVAR(@"%듀얼리스트%_제외NG置換") < 1
		条件달성++
	CALL FULLRESET
	효과範囲덱 = 1
	효과範囲묘지 = 1
	효과条件종류 = 마함全般
효과条件カテゴリ = 바니 폴리스
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	SIF 条件달성 < 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == ターン終了時強制
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_52705(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

#DIM DYNAMIC 条件Ａ
#DIM DYNAMIC 条件Ｂ
#DIM DYNAMIC 選択효과
#DIM DYNAMIC コスト消費

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == EX덱起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52705,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件行先플래그 = 제외行き
		CALL CARDADDGYADV_BUNNYPOLICE_EVIDENCE(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL RESETCONDITION_MAT
			必要素材数 = 1
			素材条件カテゴリ = 래빗 걸
			素材条件レベル = 8
			효과추가종류 = 効追条達
			重ねてＸ = 1
			TRYCALLFORM XYZSUMMON(듀얼리스트,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == EX소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52705,2)
	IF RESULT == 0
		CALL FULLRESET
		CALL STATCHANGE_EFFECT(듀얼리스트,"상대필드",場所,"無効",1)
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52705,3)
	IF RESULT == 0
		SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") > -1
			条件Ａ = 1
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件표시 = 裏側표시
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1 && GETVAR(@"%듀얼리스트%_제외NG置換") < 1
			条件Ｂ = 1
		IF 条件Ａ == 1 && 条件Ｂ == 1
PRINTL 코스트를 선택해 주세요
			CALL YN_CHOICE(듀얼리스트,"이 카드의 Ｘ소재를 １개 제거한다","자신 필드의 뒷면 표시인 카드１장을 제외한다")
			選択효과 = RESULT
		ELSEIF 条件Ａ == 1 && 条件Ｂ == 0
			選択효과 = はい
		ELSEIF 条件Ａ == 0 && 条件Ｂ == 1
			選択효과 = いいえ
		ENDIF
		IF 選択효과 == はい
			SETVAR @"%듀얼리스트%_MZ_Ｘ:場所", GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") - 1
			コスト消費 = 1
		ELSEIF 選択효과 == いいえ
			CALL FULLRESET
			효과範囲필드 = 1
			효과範囲마함 = 1
			효과条件표시 = 裏側표시
			효과条件行先플래그 = 제외行き
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			SIF RESULT == 1
				コスト消費 = 1
		ELSE
			RETURN
		ENDIF
		IF コスト消費 == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과範囲묘지 = 1
효과条件カテゴリ = 바니 폴리스
			효과条件종류 = 마함全般
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == ターン終了時強制
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52705)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,EX덱行き)
		IF RESULT == 1 && 마함数(対面者) < 5
			CALL FULLRESET
			효과範囲묘지 = 1
			효과範囲EX덱 = 1
			효과条件종류 = モン全般
			효과条件レベル = 8
효과条件カテゴリ = 래빗 걸
			효과条件分割 = 펜듈럼
			효과条件任意 = 1
			CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시",2)
		ENDIF
	ENDIF
ENDIF

;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
; 자신 필드의 裏側표시で표시固定 또는 발동불가(효과無効)の카드를 探す
@CARDCHECKADV_BUNNYPOLICE_EVIDENCE(듀얼리스트,종류,ゾーン,場所,カードID)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 종류
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 場所
#DIM DYNAMIC カードID

#DIM DYNAMIC 条件合致数

SELECTCASE ゾーン
	CASE "","효과処理"
		PRINT
	CASEELSE
		カードID = GETVAR(@"%듀얼리스트%_%ゾーン%:場所")
ENDSELECT

CALL CARDFILTER_BUNNYPOLICE_EVIDENCE(듀얼리스트,"上級",ゾーン,場所,0,1,カードID)
条件合致数 = RESULT:1
VARSET RESULT
;条件달성！
IF 효과条件CNT == 1
	IF 효과条件限界値 >= 1
		IF 条件合致数 >= 효과条件限界値
			条件合致数 = 효과条件限界値
		ENDIF
	ENDIF
	RETURN 条件合致数
ELSE
	IF 条件合致数 >= 1
		RETURN 1
	ENDIF
ENDIF
RETURN 0

;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
; 자신 필드의 裏側표시で표시固定 또는 발동불가(효과無効)の카드를 色々な所に落とす
@CARDADDGYADV_BUNNYPOLICE_EVIDENCE(듀얼리스트,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 대상者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カードID
#DIM DYNAMIC 選択カード,99
#DIM DYNAMIC 選択カード_ID,99
#DIM DYNAMIC 行先보존
#DIM DYNAMIC 上級値
#DIMS DYNAMIC 参照先
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 複数カウンタ
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
#DIM DYNAMIC 덱サーチ플래그
#DIM DYNAMIC 묘지回収플래그
#DIM DYNAMIC 最大値
#DIM DYNAMIC 消去カード
#DIM DYNAMIC 一時보존,99
#DIM DYNAMIC 選択数
#DIM DYNAMIC 추가플래그

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%
VARSET 選択カード,-1
SIF 효과条件複数 == 100
	RETURN 0

カードID = 処理中_효과ID

IF (효과範囲묘지 == 1 || 효과範囲상대묘지 == 1) && 효과条件行先플래그 == 0 && GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
	PRINTFORML 카드를 제외할 수 없습니다。
	RETURN 0
ENDIF
IF GROUPMATCH(효과条件行先플래그,제외行き) && GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
	PRINTFORML 카드를 제외할 수 없습니다。
	RETURN 0
ENDIF

IF 효과条件行先플래그 >= 1
	行先보존 = 효과条件行先플래그
ELSE
	行先보존 = 효과묘지行き
ENDIF

IF 종류 == 손패起動
	효과条件他所 = 場所
ENDIF
추가플래그 = 효과条件추가플래그

$CHOICE_LOOP
IF 듀얼리스트 == "자신"
	CUSTOMDRAWLINE —
PRINTL 【카드를 선택해 주세요】
	CUSTOMDRAWLINE —
	PRINTL 
ELSEIF 듀얼리스트 == "상대"
	CALL ENEMY_RUTIN_CARD_ADD(参照先,場所,カードID)
ENDIF

$INPUT_LOOP
IF 효과条件複数
	FOR カウンタ,0,複数カウンタ
		효과条件選択済:カウンタ = 選択カード:カウンタ 
	NEXT
ENDIF

CALL CARDFILTER_BUNNYPOLICE_EVIDENCE(듀얼리스트,"上級",ゾーン,場所)
条件달성 = RESULT:1
IF 듀얼리스트 == "자신"
	PRINTL
	IF  (INRANGE(효과条件複数,90,99) || 효과条件複数 == 任意の数) && 複数カウンタ >= 1
PRINTL [8888] - 결정
	ENDIF
	IF 条件달성 != 0 && 효과条件任意 != 1
		SETCOLOR C_GRAY
PRINTL (9999) - 되돌아가기 (선택할 수 있는 카드가 있습니다)
		RESETCOLOR
	ELSE
PRINTL [9999] - 되돌아가기
	ENDIF
	PRINTL
	PRINTFORML　%"[7777] - 필드 / 패 정보 확인",95,RIGHT%
	PRINTL
	BINPUT
	IF RESULT == 7777
		CALL DIPLAY_BOARD(2)			
		GOTO CHOICE_LOOP
	ENDIF
	選択カード:複数カウンタ = RESULT
ELSEIF 듀얼리스트 == "상대"
	IF RESULT == 9999 &&  (INRANGE(효과条件複数,90,99) || 효과条件複数 == 任意の数) && 複数カウンタ >= 1 
		選択カード:複数カウンタ = 8888
	ELSE
		選択カード:複数カウンタ = RESULT
	ENDIF
	IF 選択カード:複数カウンタ == 8888
		SETCOLOR C_W_RED
PRINTL 더 이상 선택하지 않는 것 같습니다.
		RESETCOLOR
	ENDIF
ENDIF
IF 選択カード:複数カウンタ == 9999
	PRINTFORMW 돌아갑니다。
	RETURN 0

ELSEIF 選択カード:複数カウンタ == 8888
	選択カード:複数カウンタ = -1
	;複数カウンタ -= 1
	GOTO INPUT_SKIP
ELSEIF 選択カード:複数カウンタ != 9999
	CALL CARD_SELECT(듀얼리스트,選択カード:複数カウンタ)
	選択カード_ID = GETVAR(@"%대상者（CS）%_%参照先（CS）%:{選択カード（CS）}")
	CALL SELECT_UI_SET(듀얼리스트,選択カード_ID,2)
	IF RESULT == 1
		GOTO CHOICE_LOOP
	ENDIF
	IF 参照先（CS） == "덱"
		덱サーチ플래그 = 1
	ELSEIF 参照先（CS） == "묘지"
		묘지回収플래그 = 1
	ENDIF
ENDIF



IF 효과条件複数 != 0
	IF 효과条件複数 > (複数カウンタ + 1)
		複数カウンタ ++
		選択数 ++
		SIF INRANGE(효과条件複数,90,99) && 複数カウンタ >= (100 - 효과条件複数)
			GOTO INPUT_SKIP
		GOTO INPUT_LOOP
	ELSE
		$INPUT_SKIP
		FOR カウンタ,0, (複数カウンタ + 1)
			最大値 = -1
			FOR カウンタB,0,(複数カウンタ + 1)
				IF 選択カード:カウンタB >= 最大値
					最大値 = 選択カード:カウンタB
					一時보존:カウンタ = 選択カード:カウンタB
					消去カード = カウンタB
				ENDIF
			NEXT
			ARRAYREMOVE 選択カード, 消去カード, 1
		NEXT
		FOR カウンタ,0,(複数カウンタ + 1)
			選択カード:カウンタ = 一時보존:カウンタ
		NEXT
	ENDIF
ENDIF

;PRINTFORML 複数カウンタ= {複数カウンタ}
IF 효과条件複数 != 0
	FOR カウンタ,0,(複数カウンタ + 1)
		;묘지送り処理
		SIF 選択カード:カウンタ == -1
			CONTINUE
		CALL CARD_SELECT(듀얼리스트,(選択カード:カウンタ))
		選択カード_ID = GETVAR(@"%대상者（CS）%_%参照先（CS）%:選択カード（CS）")
		VARSET RESULT
		CALL CARD_DISPOSE(대상者（CS）,参照先（CS）,選択カード（CS）,行先보존,추가플래그)
	NEXT
ELSE
	;묘지送り処理
	CALL CARD_SELECT(듀얼리스트,選択カード)
	選択カード = RESULT
	選択カード_ID = GETVAR(@"%대상者（CS）%_%参照先（CS）%:選択カード（CS）")

	CALL CARD_DISPOSE(대상者（CS）,参照先（CS）,選択カード,行先보존,추가플래그)
ENDIF

;PRINTFORML リピート数A:{複数カウンタ}
VARSET RESULT

RESULT:1 = 選択カード_ID
RESULT:2 = 選択数

RETURN 1

;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
; 자신 필드의 裏側표시で표시固定 또는 발동불가(효과無効)の카드를 探す필터
; (スーパーは省略)
@CARDFILTER_BUNNYPOLICE_EVIDENCE(듀얼리스트,元参照先,ゾーン,場所,めくり数,チェック플래그,カードID)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 実行者
#DIM DYNAMIC めくり数

#DIMS DYNAMIC 元参照先
#DIMS DYNAMIC 参照先
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 場所
#DIM DYNAMIC 上級플래그
#DIM DYNAMIC 上級値
#DIM DYNAMIC 参照先セットアップ, 30

#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
#DIM DYNAMIC チェック플래그
#DIM DYNAMIC カードID
#DIM DYNAMIC サーチ範囲
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件달성

#DIM DYNAMIC 条件合致数

#DIM DYNAMIC 現在最大攻撃力
#DIM DYNAMIC 現在最小攻撃力
#DIMS DYNAMIC MZ魔판정
#DIM DYNAMIC 대상カードID

;PRINTFORML 듀얼리스트 = %듀얼리스트%
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

VARSET 選択カード, -1
VARSET 現在最大攻撃力, -1
VARSET 現在最小攻撃力, 999999


IF 元参照先 == "上級"
	SIF 효과範囲필드 == 1
		参照先セットアップ:0 = 1
	SIF 효과範囲마함 == 1
		参照先セットアップ:1 = 1
	上級플래그 = 1
ENDIF


$INPUTLOOP
IF 上級플래그
	IF 参照先セットアップ:カウンタB == 0
		カウンタB ++
		IF カウンタB <= 19
			GOTO INPUTLOOP
		ELSE
			GOTO INPUTSKIP
		ENDIF
	ENDIF
	IF カウンタB == 0
		元参照先 = 필드
	ELSEIF カウンタB == 1
		元参照先 = 마함
	ENDIF
ENDIF

IF 元参照先 == "필드"
	サーチ範囲 = 5
	実行者 = %듀얼리스트%
	参照先 = 필드
ELSEIF 元参照先 == "마함"
	サーチ範囲 = 5
	実行者 = %듀얼리스트%
	参照先 = 마함
	上級値 = 10
ENDIF

SIF 上級플래그 == 0
	上級値 = 0

FOR カウンタ,0,サーチ範囲
	SIF GETVAR(@"%実行者%_%参照先%:カウンタ") <= -1
		CONTINUE
	IF 효과条件固定 > -1
		SIF 효과条件固定 != カウンタ
			CONTINUE
	ENDIF
	IF 효과条件複数 > 0
		SIF MATCH(효과条件選択済, (カウンタ + 上級値))
			CONTINUE
	ENDIF
	IF 효과条件他所 > 0
		IF 元参照先 == ゾーン
			SIF カウンタ == 場所
				CONTINUE
		ENDIF
	ENDIF
	VARSET 条件달성
	CALL CARDFILTERSET_BUNNYPOLICE_EVIDENCE(実行者,参照先,カウンタ,듀얼리스트,チェック플래그,カードID)
	SIF RESULT == 1
		条件달성 = 1

	SIF 条件달성 != 1
		CONTINUE

	IF 듀얼리스트 == "자신"
		SIF チェック플래그 == 1
			SKIPDISP チェック플래그
		대상カードID = GETVAR(@"%実行者%_%参照先%:カウンタ")
		CALL CARD_SETCOLOR(実行者,参照先,カウンタ)
		CALLFORM CARD_KANPRINT(参照先,(カウンタ + 上級値),대상カードID)
		RESETCOLOR

	ELSEIF 듀얼리스트 == "상대"
		;ゾーン특수：上級소환のリリース、モンスター묘지送りのためのやつ
		IF ゾーン == "특수"
			IF 参照先 == "필드"
				IF GETVAR(@"%実行者%_MZ_攻:カウンタ") < 現在最小攻撃力
					現在最小攻撃力 = RESULT
					選択カード = カウンタ + 上級値
				ENDIF
			ELSE
				CALLFORM CARD_{GETVAR(@"%実行者%_%参照先%:カウンタ")}, "攻撃力"
				IF RESULT < 現在最小攻撃力
					現在最小攻撃力 = RESULT
					選択カード = カウンタ + 上級値
				ENDIF
			ENDIF
		ELSE
			IF 参照先 == "필드"
				IF GETVAR(@"%実行者%_MZ_攻:カウンタ") > 現在最大攻撃力
					現在最大攻撃力 = RESULT
					選択カード = カウンタ + 上級値
				ENDIF
			ELSE
				CALLFORM CARD_{GETVAR(@"%実行者%_%参照先%:カウンタ")}, "攻撃力"
				IF  RESULT > 現在最大攻撃力
					現在最大攻撃力 = RESULT
					選択カード = カウンタ + 上級値
				ENDIF
			ENDIF
		ENDIF
	ENDIF

	;合計数確認系
	条件合致数 ++
NEXT


IF 上級플래그 && カウンタB <= 19
	カウンタB ++
	GOTO INPUTLOOP
ENDIF
$INPUTSKIP
VARSET RESULT
RESULT:1 = 条件合致数
SKIPDISP 0

IF 듀얼리스트 == "자신"
	SIF チェック플래그 != 1
		CUSTOMDRAWLINE —
	RETURN 0
ELSEIF 듀얼리스트 == "상대"
	IF 選択カード != -1
		RETURN 選択カード
	ELSE
		RETURN 9999
	ENDIF
ENDIF

;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――
; 자신 필드의 裏側표시で표시固定 또는 발동불가(효과無効)の카드를 探す필터
@CARDFILTERSET_BUNNYPOLICE_EVIDENCE(実行者,参照先,カウンタ,듀얼리스트,チェック플래그,カードID)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC 実行者
#DIMS DYNAMIC 参照先
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カードID

#DIMS DYNAMIC MZ魔판정
#DIM DYNAMIC チェック플래그


SIF GETVAR(@"%実行者%_%参照先%:カウンタ") == -1
	RETURN 0

;SIF GETVAR(@"%実行者%_%参照先%:カウンタ") == 使用불가ゾーン
;	RETURN 0

IF 参照先 == "필드" || 参照先 == "마함"
	IF 参照先 == "필드"
		MZ魔판정 = MZ
	ELSEIF 参照先 == "마함"
		MZ魔판정 = 魔
	ENDIF
	;裏側표시
	IF 参照先 == "필드"
		SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") != 3
			RETURN 0
			
	ELSEIF 参照先 == "마함"		
		SIF GETVAR(@"%実行者%_%MZ魔판정%_表:カウンタ") == 1
			RETURN 0
	ENDIF

	SIF (MZ魔판정 == "魔" && GETVAR(@"%実行者%_%MZ魔판정%_無効:カウンタ") < 1)
		RETURN 0
	
	SIF (MZ魔판정 == "MZ" && GETVAR(@"%実行者%_%MZ魔판정%_表固:カウンタ") < 1)
		RETURN 0

	IF 효과条件除去種 != "" && チェック플래그 != 3
		IF 参照先 == "필드"
			CALL CARD_REMOVAL_ABLE(実行者,듀얼리스트,"単体",カウンタ,효과条件除去種,カードID)
			SIF RESULT != 1
				RETURN 0
		ELSEIF 参照先 == "마함"
			CALL CARD_REMOVAL_ABLE(実行者,듀얼리스트,"単体",カウンタ,효과条件除去種,カードID,1)
			SIF RESULT != 1
				RETURN 0
		ENDIF
	ENDIF
ENDIF

RETURN 1