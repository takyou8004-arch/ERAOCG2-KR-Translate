@CARDNAME_84861,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 함께 달콤한 꿈을
	CASE "略称"
		RESULTS = 함께달콤한 꿈을
	CASE "カテゴリ"
		RESULTS:0 = 벽람항로
		RESULTS:1 = 사쿠라 엠파이어(AL)
ENDSELECT

@CARD_84861(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
		RETURN 通常함정
ENDSELECT
RETURN 0

@CARD_EXPLANATION_84861(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드 이름의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,효과後誘発)
PRINTL ①: 자신 필드의 "벽람항로" 몬스터가 파괴되었을 경우에 발동할 수 있다.
PRINTL 패 / 묘지에서 파괴된 몬스터의 코스트 이하의 레벨을 가지는 "사쿠라 엠파이어(AL)" 몬스터 1장을 특수 소환한다.
PRINTL 그 후, 자신의 패 / 필드의 몬스터를 융합 소재로서 덱 / EX덱으로 되돌리고,
PRINTL "사쿠라 엠파이어(AL)" 융합 몬스터 1장을 EX덱에서 융합 소환할 수 있다.
CALLFORM CARD_ABLE(2,戦破壊→묘지)
PRINTL ②: 필드의 이 카드가 상대에 의해 파괴되었을 경우에 발동할 수 있다.
PRINTL EX덱에서 "사쿠라 엠파이어(AL)" 융합 몬스터 1장을 융합 소환 취급으로 특수 소환한다.

@CARDCAN_84861(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 破壊レベル

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,84861)
SIF RESULT == 1
	RETURN 0

IF 종류 == 효과後誘発
	SIF 발동済_1(듀얼리스트,84861)
		RETURN 0
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
		RETURN 0
	FOR カウンタ,2,13
		CALL FULLRESET
		효과条件カテゴリ = 벽람항로
		효과条件종류 = モン全般
		효과条件レベル = カウンタ
		CALL CARD_CHECK_FIN_DATA(듀얼리스트,"破壊全般")
		SIF RESULT == 1
			破壊レベル = カウンタ - 1
	NEXT
	SIF 破壊レベル < 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 사쿠라 엠파이어(AL)
	CALL 효과条件レベル_ADV(破壊レベル,"以下")
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
	
ELSEIF 종류 == 戦破壊→묘지
	SIF 발동済_2(듀얼리스트,84861)
		RETURN 0
	SIF 処理中_듀얼리스트 == 듀얼리스트
		RETURN 0
	SIF !GROUPMATCH(묘지送り종류,"破壊","戦闘破壊")
		RETURN 0
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件カテゴリ = 사쿠라 엠파이어(AL)
	효과条件종류 = 융합
	효과条件特不無視 = 1
	효과条件소환可 = 2
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_84861(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 破壊レベル
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 효과後誘発
	FOR カウンタ,2,13
		CALL FULLRESET
		효과条件カテゴリ = 벽람항로
		효과条件종류 = モン全般
		효과条件レベル = カウンタ
		CALL CARD_CHECK_FIN_DATA(듀얼리스트,"破壊全般")
		SIF RESULT == 1
			破壊レベル = カウンタ - 1
	NEXT
	
	IF 듀얼리스트 == "자신"
		SETCOLOR C_YELLOW
		PRINTFORML 특수소환できるのは{破壊レベル}以下です
		RESETCOLOR
	ENDIF
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,84861,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲묘지 = 1
		효과条件カテゴリ = 사쿠라 엠파이어(AL)
		CALL 효과条件レベル_ADV(破壊レベル,"以下")
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL RESETAREA_MAT
			素材範囲필드 = 1
			素材範囲손패 = 1
			CALL FULLRESET
			효과条件종류 = 융합
			효과条件カテゴリ = 사쿠라 엠파이어(AL)
			효과条件소환可 = 1
			효과条件任意 = 1
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				TRYCALLFORM CARDSUMMON_{RESULT:99}(듀얼리스트,選択カード)
				CALL FUSSUMMON(듀얼리스트, 選択カード,덱行き)
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == 戦破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,84861,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲EX덱 = 1
		효과条件カテゴリ = 사쿠라 엠파이어(AL)
		효과条件종류 = 융합
		CALL CARD_CHOOSE_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT != 9999
			選択カード = RESULT
			CALL FULLRESET
			CALL CARD_EX_SUMMON(듀얼리스트,"EX덱",選択カード)
		ENDIF
	ENDIF
ENDIF
