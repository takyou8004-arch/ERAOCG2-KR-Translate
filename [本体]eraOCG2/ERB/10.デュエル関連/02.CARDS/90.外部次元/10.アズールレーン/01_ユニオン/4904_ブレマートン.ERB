@CARDNAME_4904,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 중순양함 브레머튼
	CASE "略称"
		RESULTS = 브레머튼
	CASE "カテゴリ"
		RESULTS:0 = 벽람항로
		RESULTS:1 = 유니온(AL)
ENDSELECT

@CARD_4904(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 2000
	CASE "守備力"
		RETURN 2400
	CASE "성별"
		RETURN 牝性
	CASE "추가日"
		RETURN 250503
ENDSELECT
RETURN 0

@CARD_EXPLANATION_4904(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALL CARD_ABLE(1,효과後誘発)
PRINTL ①: 자신의 "벽람항로" L 몬스터 또는 상대 몬스터의 효과가 발동했을 경우에 발동할 수 있다.
PRINTL 패의 이 카드를 특수 소환한다.
CALL CARD_ABLE(2,소환時,특수소환時)
PRINTL ②: 이 카드가 소환 / 특수 소환되었을 경우에, 패의 "벽람항로" 카드 1장을 상대에게 보여주고 발동할 수 있다.
PRINTL 이하의 효과에서 1개를 선택하여 적용한다.
PRINTL ● 이 카드를 덱으로 되돌리고, 상대 필드의 몬스터 1장을 고르고 패로 되돌린다.
PRINTL ● 자신 필드의 몬스터를 L 소재로서 "유니온(AL)" L 몬스터 1장을 L 소환한다.

@CARDCAN_4904(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 条件Ａ
#DIM DYNAMIC 条件Ｂ

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4904)
SIF RESULT == 1
	RETURN 0

IF 종류 == 효과後誘発
	SIF 발동済_1(듀얼리스트,4904)
		RETURN 0
	SIF ゾーン != "손패"
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과条件カテゴリ = 벽람항로
	효과条件종류 = 링크
	CALL CARD_CHECK_START_DATA(듀얼리스트)
	IF RESULT != 1
		CALL FULLRESET
		효과条件종류 = モン全般
		CALL CARD_CHECK_START_DATA(듀얼리스트,1)
		SIF RESULT != 1
			RETURN 0
	ENDIF
	CALL CARD_SS_CHECK(듀얼리스트,4904,ゾーン)
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,4904)
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件カテゴリ = 벽람항로
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		条件Ａ = 1
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件カテゴリ = 유니온(AL)
	효과条件종류 = 링크
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		条件Ｂ = 1
	SIF 条件Ａ == 0 && 条件Ｂ == 0
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_4904(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードID

#DIM DYNAMIC 条件Ａ
#DIM DYNAMIC 条件Ｂ
#DIM DYNAMIC 選択효과

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4904,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4904,2)
	IF RESULT == 0
		CUSTOMDRAWLINE —
		PRINTFORML 상대に見せる"벽람항로"카드를 選択してください。
		CALL FULLRESET
		효과条件カテゴリ = 벽람항로
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"손패",ゾーン,場所)
		IF RESULT != 9999
			IF ゾーン == "필드"
				CALL FULLRESET
				효과範囲상대필드 = 1
				효과条件종류 = モン全般
				CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
				SIF RESULT == 1
					条件Ａ = 1
			ENDIF
			CALL FULLRESET
			효과範囲EX덱 = 1
			효과条件カテゴリ = 유니온(AL)
			효과条件종류 = 링크
			효과条件소환可 = 1
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT == 1
				条件Ｂ = 1
			IF 条件Ａ != 0 && 条件Ｂ != 0
				CALL YN_CHOICE(듀얼리스트,"●이 카드를 덱に戻し、상대 필드의 몬스터 １장을 골라 패로 되돌린다。","●자신필드의 몬스터를 Ｌ素材として「유니온(AL)」Ｌ 몬스터 １장을 Ｌ소환する。")
				選択효과 = RESULT
			ELSEIF 条件Ｂ != 0
				選択효과 = 1
			ENDIF
			IF 選択효과 == はい
				CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,덱行き)
				IF RESULT == 1
					CALL FULLRESET
					CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"손패")
				ENDIF
			ELSEIF 選択효과 == いいえ
				CALL FULLRESET
				효과条件종류 = 링크
				효과条件소환可 = 1
				효과条件カテゴリ = 유니온(AL)
				CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
				選択カード= RESULT
				IF 選択カード!= 9999
					CALLFORM RESETCONDITION
					選択カードID = GETVAR(@"%듀얼리스트%_EX덱:選択カード")
					TRYCALLFORM CARDSUMMON_{選択カードID}(듀얼리스트,選択カード)
					TRYCALLFORM LINSUMMON(듀얼리스트,選択カード)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF