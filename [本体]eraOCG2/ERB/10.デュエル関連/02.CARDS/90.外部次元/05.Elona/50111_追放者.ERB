@CARDNAME_50111,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 추방자
	CASE "略称"
		RESULTS = 추방자
	CASE "カテゴリ"
		RESULTS:0 = 신의 화신
		RESULTS:2 = Elona
ENDSELECT


@CARD_50111(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
		RETURN 융합
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 마법사족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 1700
	CASE "守備力"
		RETURN 1100
	CASE "성별"
		RETURN 雄性
	CASE "生成カード"
		RESULT:2 = 350111
		RESULT:1 = 250111
		RETURN 150111
ENDSELECT
RETURN 0

@CARDSUMMON_50111(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
;素材条件

CALL RESETCONDITION_MAT
必要素材数 = 2

IF EX参照 == 0 || EX参照 == 1
	素材条件종족 = 마법사족
ELSEIF EX参照 == 10
	素材条件종족 = 원소정령족
ENDIF

IF EX参照 == 1
	CALL FUSCAN(듀얼리스트,소환カード,50111) 
	RETURN RESULT
ENDIF

@CARD_EXPLANATION_50111(종류)
#DIM DYNAMIC 종류

PRINTL 마법사족 ＋ 원소정령족 몬스터
PRINTL 이 카드명의 ①②③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALL CARD_ABLE(1,EX소환時)
PRINTL ①: 이 카드가 융합 소환했을 경우에 3000 LP를 지불하고 발동할 수 있다.
PRINTL 자신 필드에 "불꽃의 구현", "얼음의 구현", "번개의 구현"을 1장씩 특수 소환한다.
PRINTL 이 턴, 자신은 융합 몬스터밖에 EX덱에서 특수 소환할 수 없다.
CALL CARD_ABLE(2,필드起動)
PRINTL ②: 자신 메인 페이즈에 발동할 수 있다.
PRINTL 자신의 패 / 필드의 몬스터를 융합 소재로 하고,
PRINTL EX덱에서 융합 몬스터 1장을 융합 소환한다.
CALL CARD_ABLE(3,F素材時효과)
PRINTL ③: 이 카드가 융합 소재가 되었을 경우, 자신 필드의 융합 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 몬스터의 레벨 5당 1장까지, 자신의 묘지에서 일반 마법 카드를 패에 넣는다 (최대 3장).


@CARDCAN_50111(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성,3

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,50111)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX소환時
	SIF 발동済_1(듀얼리스트,50111)
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 3000
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 3
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,50111)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL RESETAREA_MAT
	素材範囲필드 = 1
	素材範囲손패 = 1
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 융합
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == F素材時효과
	SIF 발동済_3(듀얼리스트,50111)
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = 융합
	CALLFORM 효과条件レベル_ADV(5,"以上")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종류 = 通常마법
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_50111(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択可能数
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == EX소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50111,1)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",3000)
		CALL FULLRESET
		CALL CARD_GENERATE(듀얼리스트,150111)
		CALL CARD_GENERATE(듀얼리스트,250111)
		CALL CARD_GENERATE(듀얼리스트,350111)
		EX소환可能종류 = 융합
		CALL CARD_EX_RESTRICTION(듀얼리스트)
	ENDIF

ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50111,2)
	IF RESULT == 0
		CALL RESETAREA_MAT
		素材範囲필드 = 1
		素材範囲손패 = 1
		CALL FULLRESET
		효과条件종류 = 융합
		효과条件소환可 = 1
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			TRYCALLFORM CARDSUMMON_{RESULT:99}(듀얼리스트,選択カード)
			CALL FUSSUMMON(듀얼리스트, 選択カード)
		ENDIF
	ENDIF

ELSEIF 종류 == F素材時효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50111,3)
	IF RESULT == 0
		CALL FULLRESET
		효과条件종류 = 융합
		효과条件표시 = 表側표시
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		IF RESULT != 9999
			SIF GETVAR(@"%듀얼리스트%_MZ_星:RESULT") >= 5
				選択可能数 ++
			SIF GETVAR(@"%듀얼리스트%_MZ_星:RESULT") >= 10
				選択可能数 ++
			SIF GETVAR(@"%듀얼리스트%_MZ_星:RESULT") >= 15
				選択可能数 ++
			選択可能数 = 100 - 選択可能数
			SIF 選択可能数 == 100
				RETURN 0
			
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件종류 = 通常마법
			효과条件複数 = 選択可能数
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ENDIF

;------------------------------------------------------------------------------------------------------------

@CARDNAME_150111,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 불꽃의 구현
	CASE "略称"
		RESULTS = 불꽃의 구현
	CASE "カテゴリ"
		RESULTS:0 = 없음
		RESULTS:2 = Elona
ENDSELECT


@CARD_150111(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 원소정령족
	CASE "トークン"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_150111(종류)
#DIM DYNAMIC 종류

CALLFORM CARD_ABLE(0,마함起動,손패起動)
PRINTL ①: 상대 필드의 몬스터를 2체까지 대상으로 하여 발동할 수 있다.
PRINTL 그 카드의 효과를 무효로 하고, 상대에게 500 데미지를 준다.
PRINTL
PRINTL

@CARDCAN_150111(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,150111)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件除去種 = 無効
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_150111(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,150111)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과条件複数 = 98
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"無効")
		IF RESULT == 1
			CALL LIFE_CHANGE(듀얼리스트,"減少","효과",500,1)
		ENDIF
	ENDIF
ENDIF

;------------------------------------------------------------------------------------------------------------

@CARDNAME_250111,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 얼음의 구현
	CASE "略称"
		RESULTS = 얼음의 구현
	CASE "カテゴリ"
		RESULTS:0 = 없음
		RESULTS:2 = Elona
ENDSELECT


@CARD_250111(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "속성"
		RETURN 물속성
	CASE "종족"
		RETURN 원소정령족
	CASE "トークン"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_250111(종류)
#DIM DYNAMIC 종류

CALLFORM CARD_ABLE(0,마함起動,손패起動)
PRINTL ①: 상대 필드의 마법 & 함정 존의 카드를 2장까지 대상으로 하여 발동할 수 있다.
PRINTL 이하의 효과를 전부 적용한다.
PRINTL ● 그 카드가 앞면 표시라면 뒷면 표시로 한다.
PRINTL ● 그 카드는 발동할 수 없다.
PRINTL

@CARDCAN_250111(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,250111)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件除去種 = 選択
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_250111(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード,2
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,250111)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과条件複数 = 2
		효과条件任意 = 1
		효과추가종류 = 4
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"마함単体",場所,"選択")
	ENDIF
ENDIF

;------------------------------------------------------------------------------------------------------------

@CARDNAME_350111,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 번개의 구현
	CASE "略称"
		RESULTS = 번개의 구현
	CASE "カテゴリ"
		RESULTS:0 = 없음
		RESULTS:2 = Elona
ENDSELECT


@CARD_350111(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 원소정령족
	CASE "トークン"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_350111(종류)
#DIM DYNAMIC 종류

CALLFORM CARD_ABLE(0,마함起動,손패起動)
PRINTL ①: 상대 덱 위에서 카드를 3장까지 묘지로 보낸다.
PRINTL 다음 자신 스탠바이 페이즈까지, 상대는 묘지의 카드의 효과를 발동할 수 없다.
PRINTL 
PRINTL

@CARDCAN_350111(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,350111)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 現山札数(対面者) == 0
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_350111(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 落とし枚数
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,350111)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		IF 듀얼리스트 == "자신"
			PRINTL [묘지로 보낼 매수를 선택]
			PRINTL
			PRINT [1]　
			PRINT [2]　
			PRINTL [3]
			BINPUT
			落とし枚数 = RESULT
		ELSE
			落とし枚数 = 3
			SIF 現山札数(対面者) < 3
				落とし枚数 = 現山札数(対面者)
		ENDIF
		CALL CARD_DECK_DISPOSE(듀얼리스트,"덱",落とし枚数,,1)
		CALL SET_PLAYER_INFO(듀얼리스트,"묘지_無効",4,1)
	ENDIF
ENDIF
