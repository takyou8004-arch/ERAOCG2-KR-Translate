@CARDNAME_39893,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 농락괴한수 저로파
	CASE "略称"
		RESULTS = 저로파
	CASE "カテゴリ"
		RESULTS:0 = 괴한수
		RESULTS:1 = 괴한에 물든 경계의 날개
		RESULTS:2 = E.R.A
ENDSELECT

@CARD_39893(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 바람속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 7
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 2000
	CASE "성별"
		RETURN 雄性
	CASE "추가日"
		RETURN 250420
ENDSELECT
RETURN 0

@CARD_EXPLANATION_39893(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드명의  ①②③의 효과는 각각 １턴에 1번밖에 사용할 수 없다。
CALL CARD_ABLE(1,손패起動,フリー誘発)
PRINTL ①：자신 / 상대 턴에 상대 필드에 암컷 몬스터가 존재하는 경우에、패에서 이 카드 이외의 "괴한수" 카드를 １장 버리고 발동할 수 있다。
PRINTL 이 카드를 패에서 특수 소환한다。
PRINTL 이 효과의 발동 후、턴 종료시까지 자신은 패에서 몬스터를 특수 소환할 수 없다。
PRINTL 상대 필드에 "계한전희" 몬스터가 존재하는 경우、이 효과는 패를 버리지 않고 발동할 수 있다。
CALL CARD_ABLE(2,필드起動)
PRINTL ②：자신 메인 페이즈에 상대 필드의 암컷 몬스터 １장을 대상으로 하고 발동할 수 있다。
PRINTL 그 몬스터를 수비 표시로 하고、수비력을 ０으로 한다。
PRINTL 그 후、이 몬스터는 턴 종료시까지 각성상태가 되며、이하의 효과를 얻는다。
PRINTL ●이 몬스터가 암컷 몬스터를 전희로 파괴한 경우、그 몬스터를 "괴한 플랜트"로서 상대의 마법＆함정존에 둔다。
CALL CARD_ABLE(3,효과後誘発)
PRINTL ③：몬스터가 특수 소환 되었을 경우에、자신 / 상대 필드의 괴한 카우넡가 ２개 이상 있다면、
PRINTL 상대 필드의 "계한전희" 이외의 카드１장을 대상으로 하고 발동할 수 있다。
PRINTL 자신 / 상대 필드의 악성 카운터를 ２개 제거하고、그 카드를 원래 주인의 패로 되돌린다。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_39893(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,39893)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動 || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,39893)
		RETURN 0
	SIF ゾーン != "손패"
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,39893,"손패")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件성별 = 牝性 
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件カテゴリ = 계한전희
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT != 1
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件他所 = 1
		효과条件カテゴリ = 괴한수
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
	ENDIF
	RETURN 1
;ここまで上級덱戒悍수共通
ELSEIF 종류 == 攻撃時強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	壊悍戦闘상대성별 = GETVAR(@"%対面者%_MZ_性:{戦闘場所(듀얼리스트,1)}")
	SIF 壊悍戦闘상대성별 != 牝性	
	; CALLFORM CARD_{戦闘カードID(듀얼리스트,1)}("성별")
	; SIF RESULT != 牝性
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃後強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_条達:場所") < 1
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	; SIF 壊悍戦闘상대성별 != 牝性	
	SIF 直前戦闘破壊ID(듀얼리스트,1) == 0
		RETURN 0	
	CALLFORM CARD_{直前戦闘破壊ID(듀얼리스트,1)}("성별")
	SIF RESULT != 牝性
		RETURN 0
	SIF 마함数(対面者) == 5
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,39893)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件성별 = 牝性
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF 발동済_3(듀얼리스트,39893)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"특수소환",2)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	효과条件除去種 = 손패
	효과条件固カテゴリ = 계한전희
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT:10 < 2
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_39893(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 戦闘カード
#DIM DYNAMIC 효과先

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動 || 종류 == フリー誘発
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件カテゴリ = 계한전희
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT != 1
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,39893,1)
		IF RESULT == 0
			CALL FULLRESET
			효과条件他所 = 1
			효과条件カテゴリ = 괴한수
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)		
				CALL SET_PLAYER_INFO(듀얼리스트,"SS소환禁止손패",1)
			ENDIF
		ENDIF
	ELSE
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,39893,1)
		IF RESULT == 0
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)
			CALL SET_PLAYER_INFO(듀얼리스트,"SS소환禁止손패",1)
		ENDIF
	ENDIF
ELSEIF 종류 == 攻撃時強制誘発
	; 戦闘상대의 情報기록
	CALLFORM CARDNAME_{戦闘カードID(듀얼리스트,1)}("カテゴリ")
	IF MATCH(RESULTS, "계한전희")
		戦姫戦闘場所 = 戦闘場所(듀얼리스트,1)
	ENDIF
	괴수플랜트元オーナー = GETVAR(@"%対面者%_MZ_所有:{戦闘場所(듀얼리스트,1)}")
ELSEIF 종류 == 攻撃後強制誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,39893)
	IF RESULT == 0
		CALL SEEDBED_PLEASUREBEAST(듀얼리스트,종류,ゾーン,場所,39893)
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,39893,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과条件성별 = 牝性
		효과条件표시 = 表側표시
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"選択")
		IF RESULT == 1
			選択カードID = 他参照選択カードID
			CALL CARD_SELECT(対面者,RESULT:1)
			選択カード = 選択カード（CS）
			CALL KOJOINFO_PLEASUREBEAST(対面者,参照先（CS）,選択カード（CS）,選択カードID)
			SIF RESULT:3 == 牝性
				CALL KOJO_PLEASUREBEAST_MANAGER(RESULT:1,"能力１",39893,RESULTS:1)
			CALL SET_FIELD_INFO(対面者,"필드",選択カード,"表",2,1)
			CALL SET_FIELD_INFO(対面者,"필드",選択カード,"永守",-999999)
			CALL FULLRESET
			CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"条達",1,1)
		ENDIF
	ENDIF
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,39893,3)
	IF RESULT == 0
		CALL USE_COUNTER_PLESUREBEAST(듀얼리스트,종류,ゾーン,場所,2)
		IF RESULT == 1
			CALL FULLRESET
			효과条件固カテゴリ = 계한전희
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"손패")
		ENDIF
	ENDIF
ENDIF