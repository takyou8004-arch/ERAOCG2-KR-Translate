@CARDNAME_59771,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 괴한수비 플레저리아
	CASE "略称"
		RESULTS = 플레저리아
	CASE "カテゴリ"
		RESULTS:0 = 계한전희
		RESULTS:1 = 괴한에 물든 경계의 날개
		RESULTS:2 = E.R.A
ENDSELECT

@CARD_59771(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 9
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 2500
	CASE "성별"
		RETURN 牝性
	CASE "특수不可"
		RETURN 1
	CASE "추가日"
		RETURN 250420
ENDSELECT
RETURN 0

@CARDSUMMON_59771(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
RETURN 0

@CARD_EXPLANATION_59771(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
CALL CARD_ABLE(1,EX덱起動)
PRINTL 이 카드는 상대의 마법＆함정 존의 "괴한 플랜트"이며 악성 카운터가 ３개 이상 놓여진 "계한전희" 암컷 몬스터 카드 １장을 묘지로 보낸 경우에만、
PRINTL ＥＸ덱에서 Ｘ소환 취급으로 특수 소환할 수 있다。
PRINTL 이 효과로 특수 소환한 이 카드는、릴리스 한 암컷 몬스터를 참조한 명칭이 된다。
PRINTL 이 방법에 의한 Ｘ소환은 １턴에 １번밖에 사용할 수 없다。
PRINTL 이 카드명의  ③④의 효과는 각각 １턴에 1번밖에 사용할 수 없다。
CALL CARD_ABLE(0,攻守変動)
PRINTL ①：이 몬스터의 공격력 / 수비력은 이 카드의 악성 카운터의 수×２５０ 올린다。
PRINTL 또한、이 카드는 필드에 앞면 표시로 존재하는 한、변화원에 상관 없이 암컷 으로 취급한다。
CALL CARD_ABLE(0,지속효과)
PRINTL ②：자신 필드에 "괴한수" 수컷 몬스터가 존재하는 경우、이 몬스터는 파괴내성 / 대상내성을 얻는다。
CALL CARD_ABLE(2,필드起動)
PRINTL ③：자신 메인 페이즈에 자신 / 상대 필드의 악성 카운터가 ２개 이상일 경우에 발동할 수 있다。
PRINTL 자신 / 상대 필드의 악성 카운터를 ２개 제거하고、
PRINTL 자신 묘지에서 레벨 ７이상의 "괴한수" 수컷 몬스터 １장을 턴 종료시까지 효과를 무효로 하고 특수 소환한다。
PRINTL 그 후、이 몬스터에 악성 카운터를 ２개 놓는다。
PRINTL 이 효과를 발동하는 턴、이 카드는 공격할 수 없다。
CALL CARD_ABLE(3,발동誘発)
PRINTL ④：상대가 몬스터의 효과를 발동한 경우에、자신 / 상대 필드의 악성 카운터가 ５개 이상이라면 발동할 수 있다。
PRINTL 자신 / 상대 필드의 악성 카운터를 ５개 제거하고、그 효과를 무효로 한다。
PRINTL 그 후、상대 필드의 몬스터 １장을 고르고 파괴하며、
PRINTL 그 때、파괴한 몬스터가 성별을 가진 경우、이 카드에 악성 카운터를 ２개 놓고、
PRINTL 거기에 파괴한 몬스터가 "계한전희" 암컷 몬스터라면 그 몬스터를 "괴한 플랜트"로 하고 상대의 마법＆함정존에 놓는다。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_59771(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ数

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,59771)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX덱起動
	SIF 발동済_1(듀얼리스트,59771)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_EX_CHECK(듀얼리스트,59771)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대마함 = 1
	효과条件カテゴリ = 계한전희
	효과条件성별 = 牝性
	효과条件悪Ｃ所持 = 3
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	효과範囲필드 = 1
	효과条件カテゴリ = 괴한수
	효과条件성별 = 雄性
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF ゾーン != "필드"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 발동済_2(듀얼리스트,59771)
		RETURN 0
	SIF 발동済_2(듀얼리스트,59771)
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 괴한수
	효과条件성별 = 雄性
	CALLFORM 효과条件レベル_ADV(7, "以上")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	효과条件カテゴリ:0 = 괴한수
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT:10 < 2
		RETURN 0
	RETURN 1
ELSEIF 종류 == 발동誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_3(듀얼리스트,59771)
		RETURN 0
	CALL FULLRESET
	효과条件종류 = モン全般
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	효과条件カテゴリ:0 = 괴한수
	효과条件カテゴリ:1 = 괴한플랜트
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT:10 < 5
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_59771(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 대상者
#DIMS DYNAMIC 대상ゾーン
#DIM DYNAMIC 戦闘カード
#DIM DYNAMIC 変身カードID
#DIM DYNAMIC 修正値
#DIM DYNAMIC 元괴한플랜트위치
#DIMS DYNAMIC 괴한플랜트名前
#DIMS DYNAMIC ヒロインネーム
#DIMS DYNAMIC 元ヒロインネーム
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードID
#DIM DYNAMIC 変化元カードID
#DIMS DYNAMIC 選択カードゾーン
#DIMS DYNAMIC 破壊대상ヒロインネーム
#DIM DYNAMIC 성별플래그
#DIM DYNAMIC 戦姫플래그

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == EX덱起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,59771,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲상대마함 = 1
		효과条件カテゴリ = 계한전희
		효과条件성별 = 牝性
		효과条件個Ｃ所持 = 3
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			元괴한플랜트위치 = 選択カード（CS）
			変身カードID = GETVAR(@"%対面者%_魔_戒悍変身元ID:選択カード（CS）")
			CALL FULLRESET
			효과추가종류:1 = 1000000 + 変身カードID
			CALL CARD_EX_SUMMON(듀얼리스트,"EX덱",場所,"攻撃표시")
			ヒロインネーム = 플레저리아 
			CALL CARD_EROTICS(変身カードID)
			IF RESULT != -1
				ヒロインネーム = %ヒロインネーム%%CALLNAME:RESULT%
			ELSE
				TRYCALLFORM CARDNAME_{変身カードID}("略称")
				ヒロインネーム = %ヒロインネーム%%RESULTS%
			ENDIF
			CALL CARD_SET_RENAME(듀얼리스트,"필드",カード소환先,ヒロインネーム)
			元ヒロインネーム = %GETVARS(@"%対面者%_魔_戒悍ネーム:{元괴한플랜트위치}")%
			SETVAR @"%対面者%_魔_戒悍ネーム:{元괴한플랜트위치}", ""
			SETVAR @"%対面者%_魔_戒悍変身元ID:{元괴한플랜트위치}", 0
			SETVAR @"%듀얼리스트%_MZ_戒悍ネーム:{カード소환先}", ヒロインネーム
			SETVAR @"%듀얼리스트%_MZ_戒悍変身元ID:{カード소환先}", 変身カードID
			CALL CARD_SET_CATEGORY(듀얼리스트,"필드",カード소환先,"괴한수")
			CALL CARD_SET_CATEGORY(듀얼리스트,"필드",カード소환先,"괴한플랜트",1)
			CALL KOJO_PLEASUREBEAST_MANAGER(変身カードID,"悪堕ち",,ヒロインネーム, 元ヒロインネーム)
		ENDIF
	ENDIF
ELSEIF 종류 == 攻守変動
	修正値 = GETVAR(@"%듀얼리스트%_MZ_悪Ｃ:場所") * 250
	CALL STATCHANGE_BASIC(듀얼리스트,"자신",場所,修正値,修正値)
	CALL STATCHANGE_OTHER(듀얼리스트,"자신",場所,"性",1)
ELSEIF 종류 == 지속효과
	CALL STATCHANGE_EFFECT(듀얼리스트,"자신",場所,"破耐")
	CALL STATCHANGE_EFFECT(듀얼리스트,"자신",場所,"対耐")
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,59771,2)
	IF RESULT == 0
		CALL USE_COUNTER_PLESUREBEAST(듀얼리스트,종류,ゾーン,場所,2)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件カテゴリ = 괴한수
			효과条件성별 = 雄性
			CALLFORM 효과条件レベル_ADV(7, "以上")
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				選択カードID = RESULT:1
				CALL SET_FIELD_INFO(듀얼리스트,"필드",カード소환先,"無効",1,1)
				ヒロインネーム = %GETVARS(@"%듀얼리스트%_MZ_改名:場所")%
				変身カードID = GETVAR(@"%듀얼리스트%_MZ_変化元ID:場所")
				CALL KOJO_PLEASUREBEAST_EFFECT_BEASTQUEEN(,"특수소환時",変身カードID,,ヒロインネーム)
				CALL KOJO_PLEASUREBEAST_MANAGER(変身カードID,"プラント使用",選択カードID,ヒロインネーム,,3)
				PRINTFORMW 괴한수비 플레저리아『{場所}』에게 카운터가 ２개 놓였습니다。
				CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"悪Ｃ",2)
				CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"戦無",1,1)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 발동誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,59771,3)
	IF RESULT == 0
		CALL USE_COUNTER_PLESUREBEAST(듀얼리스트,종류,ゾーン,場所,5)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"발동無効",場所,"無効")
			ヒロインネーム = %GETVARS(@"%듀얼리스트%_MZ_改名:場所")%
			変身カードID = GETVAR(@"%듀얼리스트%_MZ_変化元ID:場所")
			CALL KOJO_PLEASUREBEAST_EFFECT_BEASTQUEEN(,"無効時",,,ヒロインネーム)
			CALL FULLRESET
			효과範囲상대필드 = 1
			효과条件任意 = 1
			효과条件除去種 = 묘지
			CALL CARD_REMOVAL_ADV(듀얼리스트,"全場単体",場所,"選択")
			IF RESULT == 1
				選択カード = RESULT:1
				選択カードID = 他参照選択カードID
				CALL CARD_SELECT(듀얼리스트,選択カード)
				CALL KOJOINFO_PLEASUREBEAST(対面者,参照先（CS）,選択カード,選択カードID)
				変化元カードID = RESULT:1
				戦姫플래그 = RESULT:2
				성별플래그 = RESULT:3
				破壊대상ヒロインネーム = %RESULTS:1%
				IF 戦姫플래그
					괴한플랜트元オーナー = GETVAR(@"%対面者%_MZ_所有:{選択カード（CS）}")
					元괴한플랜트위치 = 現마함(対面者)
					CALLFORM CARDNAME_{選択カードID}("名前")
					괴한플랜트名前 = %RESULTS%
				ENDIF
				CALL CARD_SELECT(対面者,選択カード)
				CALL CARD_DISPOSE(대상者（CS）,参照先（CS）,選択カード,破壊行き)
				IF RESULT == 1
					IF 戦姫플래그
						SETVAR @"%対面者%_MZ_戒悍ネーム:選択カード",""
						SETVAR @"%対面者%_MZ_戒悍変身元ID:選択カード",0
						CALL FULLRESET
						효과条件名前 = %괴한플랜트名前%
						IF 괴한플랜트元オーナー == 1
							효과範囲묘지 = 1
							CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시",2)
						ELSE
							효과範囲상대묘지 = 1
							CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시",1)
						ENDIF
						CALL CARD_SET_RENAME(対面者,"마함",元괴한플랜트위치,破壊대상ヒロインネーム)
						CALL CARD_SET_CATEGORY(対面者,"마함",元괴한플랜트위치,"계한전희")
						CALL CARD_SET_CATEGORY(対面者,"마함",元괴한플랜트위치,"괴한플랜트",1)
						SETVAR @"%対面者%_魔_戒悍ネーム:{元괴한플랜트위치}",破壊대상ヒロインネーム
						SETVAR @"%対面者%_魔_戒悍変身元ID:{元괴한플랜트위치}",選択カードID
					ENDIF
					CALL KOJO_PLEASUREBEAST_EFFECT_BEASTQUEEN(変化元カードID,"破壊時",変身カードID,破壊대상ヒロインネーム,ヒロインネーム,성별플래그,戦姫플래그)
					IF 성별플래그 != 無性
						PRINTFORMW 괴한수비 플레저리아『{場所}』에게 카운터가 ２개 놓였습니다。
						CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"悪Ｃ",2)
					ENDIF
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF