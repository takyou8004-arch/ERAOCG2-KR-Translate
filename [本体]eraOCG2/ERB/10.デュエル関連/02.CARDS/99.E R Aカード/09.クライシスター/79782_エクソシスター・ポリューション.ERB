@CARDNAME_79782,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 엑소시스터 폴루션
	CASE "略称"
		RESULTS = ES폴루션
	CASE "カテゴリ"
		RESULTS:0 = 엑소시스터
		RESULTS:1 = 크라이시스터
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_79782(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 速攻마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_79782(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL  이 카드 명의 ①②의 효과는 1턴에 1번、어느쪽이든 1개만 발동할 수 있다、
PRINTL 이 효과를 발동한 턴、자신은 빛속성 몬스터만 EX덱에서 특수 소환할 수 있다。
CALL CARD_ABLE(1,마함起動,손패起動,フリー誘発)
PRINTL ①：서로의 메인 페이즈에 ８００LP를 지불하고 발동할 수 있다。
PRINTL 자신 묘지의 "크라이시스터" 몬스터 ２장을 소재로 덱으로 되돌리고、
PRINTL 「엑소시스터」Ⅹ몬스터 １장을 Ⅹ소환한다。
PRINTL 이 효과로 소환된 「엑소시스터」Ⅹ몬스터는 각성상태가 되고、「카타르시스터」로도 사용할 수 있다。
CALL CARD_ABLE(2,묘지起動)
PRINTL ②：자신 메인 페이즈에 ８００LP지불하고、묘지의 이카드를 제외하고 발동할 수 있다。
PRINTL 묘지에 존재하는 서로 다른 이름의 랭크4「엑소시스터」Ⅹ몬스터 2장과、
PRINTL 몬스터로 특수 소환할 수 있는 「악령」 마법 / 함정 카드 １장을 제외하고、
PRINTL EX덱에서「카타르시스터" 몬스터 １장을 Ｓ소환 취급으로 특수 소환한다。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_79782(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,79782)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	IF ゾーン == "손패"
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF

	SIF 발동済_1(듀얼리스트,79782)
		RETURN 0
	SIF 발동済_2(듀얼리스트,79782)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 800
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 크라이시스터
	효과条件종류 = モン全般
	효과条件レベル = 4
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0
	
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 엑시즈
	효과条件カテゴリ = 엑소시스터
	효과条件レベル = 4
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_1(듀얼리스트,79782)
		RETURN 0
	SIF 발동済_2(듀얼리스트,79782)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 엑소시스터
	효과条件종류 = 엑시즈
	효과条件レベル = 4
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 악령
	효과条件종류 = 마함全般
	효과条件分割 = 효과なし
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件カテゴリ = 카타르시스터
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_79782(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 조교대상

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%



IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79782,1)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",800)
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 크라이시스터
		효과条件종류 = モン全般
		효과条件レベル = 4
		효과条件複数 = 2
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과条件カテゴリ = 엑소시스터
			효과条件종류 = 엑시즈
			효과条件レベル = 4
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
			選択カード = RESULT
			;돌아간다
			IF 選択カード != 9999
				PRINTFORML 
				PRINTFORML 필드에 묘지와 연결되는 구멍이 열리며,
				PRINTFORML 거기로부터 의식을 잃고 있는 크라이시스터들이
				PRINTFORMW 안보이는 무언가에 끌려가도록 모습을 드러냈다!
				PRINTFORML 
				
				CALL FULLRESET
				효과추가종류:0 = 効追条達
				효과추가종류:1 = 40000 + 2 
				효과추가カテ = 카타르시스터
				CALL CARD_EX_SUMMON(듀얼리스트,"EX덱",選択カード)
				PRINTFORML 
				PRINTFORML 그대로 오버레이 네트워크가 구축되며,
				PRINTFORML 늠름하고 아름다운 아가씨가 모습을 드러냈다!
				PRINTFORMW ...눈동자는 초점이 맞지 않고, 쇠사슬이 붙은 어울리지 않는 목걸이를 하고 있지만.
				PRINTFORML 
				CALL FULLRESET
				EX소환可能속성 = 빛속성
				CAll CARD_EX_RESTRICTION(듀얼리스트)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79782,2)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",800)
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,덱行き)
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 엑소시스터
		효과条件종류 = 엑시즈
		효과条件レベル = 4
		효과条件複数 = 2
		효과条件固名前選択 = 1
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"묘지",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件カテゴリ = 악령
			효과条件종류 = 마함全般
			효과条件分割 = 효과なし
			효과条件行先플래그 = 제외行き
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"묘지",ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				효과条件종류 = 싱크로
				효과条件カテゴリ = 카타르시스터
				CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
				選択カード = RESULT
				IF 選択カード != 9999
					PRINTFORML 
					PRINTFORML 필드에 묘지와 연결되는 구멍이 열리며,
					PRINTFORML 거기로부터 의식을 잃고 있는 엑소시스터들이
					PRINTFORMW 안보이는 무언가에 끌려가도록 모습을 드러냈다!
					PRINTFORML 악령이 빛의 고리에 변화하며
					PRINTFORML 전혀 도망칠 수 없게 둘러싸는 것 같은 형태로 전개하고
					PRINTFORMW 싱크로 소재가 된 몬스터를 광점에 변화시킨다...
					PRINTFORML 
					CALL CARD_EX_SUMMON(듀얼리스트,"EX덱",選択カード)
					PRINTFORML 
					PRINTFORML 눈부실 정도인 빛중에서 아가씨가 모습을 드러냈다!
					PRINTFORMW ...눈동자는 어둡게 탁해져 있고, 쇠사슬이 붙은 어울리지 않는 목걸이를 하고 있지만.
					PRINTFORML 
					CALL FULLRESET
					EX소환可能속성 = 빛속성
					CAll CARD_EX_RESTRICTION(듀얼리스트)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ENDIF



