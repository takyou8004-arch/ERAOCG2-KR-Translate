@CARDNAME_79905,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

;マジック・エキスパンド
SELECTCASE 参照先
	CASE "名前"
		RESULTS = 서큐버스 매직 익스팬드
	CASE "略称"
		RESULTS = 음마도강정
	CASE "カテゴリ"
	    RESULTS:0 = 매지션 걸
		RESULTS:1 = 서큐버스 걸
		RESULTS:2 = E.R.A
ENDSELECT

@CARD_79905(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 速攻마법
	CASE "追加日"
		RETURN 250802
ENDSELECT
RETURN 0

@CARDSUMMON_79905(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
RETURN 0

@CARD_EXPLANATION_79905(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드명의 ①②의 효과는 １턴에 어느쪽인가 １개밖에 사용할 수 없다。
CALL CARD_ABLE(1,마함起動,손패起動,フリー誘発)
PRINTL ①：자신 필드의 "서큐버스 걸"몬스터의 수에 따라 이하의 효과를 적용한다。
PRINTL ●１장 이상：묘지의 "서큐버스 걸"마법 카드의 수×１００ＬＰ를 회복한다。
PRINTL ●２장 이상：자신 필드의 "서큐버스 걸"몬스터의 수×３００ＬＰ를 회복한다。
PRINTL ●３장 이상：턴 종료시 까지、자신은 카드의 효과에 의해 ＬＰ를 잃게되지 않으며、지불할 필요가 없어진다。
CALL CARD_ABLE(2,효과後誘発)
PRINTL ②：자신의 "서큐버스 걸" 몬스터가 효과를 발동한 경우에 발동할 수 있다。
PRINTL 묘지의 이 카드를 자신 필드에 세트한다。
PRINTL 이 효과로 세트된 이 카드는、필드에서 벗어난 경우에 제외된다。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_79905(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

#DIM DYNAMIC 条件달성

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,79905)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	SIF 발동済_1(듀얼리스트,79905)
		RETURN 0
	SIF 발동済_2(듀얼리스트,79905)
		RETURN 0
	IF ゾーン == "손패"
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 서큐버스 걸
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ = 서큐버스 걸
	효과条件종류 = 마법全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF 발동済_1(듀얼리스트,79905)
		RETURN 0
	SIF 발동済_2(듀얼리스트,79905)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	CALL FULLRESET
	효과条件カテゴリ = 서큐버스 걸
	효과条件종류 = モン全般
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"효과발동")
	SIF RESULT != 1
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
    RETURN 1
ENDIF

@CARDEFFECT_79905(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 보정値

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79905,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = 서큐버스 걸
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		カウンタ = RESULT
		IF カウンタ
			CALL SUCCUGIRL_SPELL_EXPAND(듀얼리스트)
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件カテゴリ = 서큐버스 걸
			효과条件CNT = 1
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			보정値 = RESULT * 100
			CALL LIFE_CHANGE(듀얼리스트,"増加","효과",보정値)
			CALL RECOVER_PERMIL(MASTER,보정値,"체력",1)
			CALL RECOVER_PERMIL(MASTER,보정値,"기력",1)
			IF カウンタ > 1
				CALL LIFE_CHANGE(듀얼리스트,"増加","효과",カウンタ * 300)
				CALL RECOVER_PERMIL(MASTER,カウンタ * 300,"체력",1)
				CALL RECOVER_PERMIL(MASTER,カウンタ * 300,"기력",1)
				IF カウンタ > 2
					PRINTFORML %듀얼리스트%는 이 턴 ＬＰ를 잃지 않고、지불할 필요도 없어졌습니다。
					CALL SET_PLAYER_INFO(듀얼리스트,"コスト無効",1)
				ENDIF
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79905,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件名前 = 서큐버스 매직 익스팬드
		CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"裏側表示")
	ENDIF
ENDIF