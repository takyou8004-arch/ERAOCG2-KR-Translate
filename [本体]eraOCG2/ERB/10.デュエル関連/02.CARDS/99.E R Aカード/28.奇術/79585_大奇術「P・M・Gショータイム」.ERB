@CARDNAME_79585,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 대기술"P.M.G쇼 타임♪"
	CASE "略称"
		RESULTS = P.M.G 쇼♪
	CASE "カテゴリ"
		RESULTS:0 = 기술
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_79585(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN フィールド
	CASE "추가日"
		RETURN 250415
ENDSELECT

RETURN 0

@CARD_EXPLANATION_79585(종류)
#DIM DYNAMIC 종류


CALL TEXT_DECORATION("ROGUE")
PRINTL 자신의 몬스터 존에 "판도라 매지션 걸" 몬스터가 앞면 표시로 존재하는 한、
PRINTL 자신 필드는 "P.M.G쇼 타임♪"가 된다。
PRINTL 이 카드명의 카드는 １턴에 １번밖에 발동할 수 없으며、②③의 효과는 각각 １턴에 1번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ①：이 카드의 발동시의 효과 처리로서、패 / 덱에서 
PRINTL "영겁의 재액" １장을 고르고 자신 필드에 앞면 표시로 둘 수 있다。
CALLFORM CARD_ABLE(2,마함起動)
PRINTL ②：자신 메인 페이즈에 ３００ＬＰ를 지불하고 발동할 수 있다。
PRINTL 자신의 묘지 / 제외 상태인 마법 / 함정 카드 ３장을 골라 덱으로 되돌리고『숏 건 셔플』한다。
PRINTL 그 후、자신은 １장 드로우한다。자신 필드에 "기술"Ｘ몬스터가 존재하는 경우、
PRINTL 드로우 대신 "기술" 카드 １장을 패에 넣을 수 있다。
CALLFORM CARD_ABLE(3,효과後誘発)
PRINTL ③："기술"속공마법 / 일반함정 카드의 효과를 자신이 발동한 경우에 발동할 수 있다。
PRINTL 상대 필드의 카드 １장을 제외한다。
CALL TEXT_DECORATION("ROGUE")

;フィールドを"쇼 타임♪"に変化させる、テ-マの中心的存在となるフィールド마법。
;これを先攻で貼れば、패에서 直接"기술"마법함정を발동할 수 있다ようになる。要するに"루닉の泉"の亜種。
;ル-ンとの違いは、場にPMGの維持が必須で、特に마법함정にデメリットがなく、勝ち筋がPMGによるビ-トダウンである点と、
;ドロー等の손패補充효과를 各種마함の묘지효과에 も分散し、共通で"いずれかターン１"に설정している点。
;마법함정 카드의 발동효과と묘지효과의 、どちらを使用するかをターン内で考えながら運用する必要がある想定。
;盤面維持していれば強いのだが、このフィールドを除去されると妨害が減るため다른  카드의 효과で守る必要がある。
;ショットガンシャッフルは原作と元々の 카드를 意識してつけている。PMGのX体が居るとイカサマもしてくれる。

@CARDCAN_79585(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,79585)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF 발동済_2(듀얼리스트,79585)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	;ライフポイント300以下だと使用불가
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 300
		RETURN 0
	;묘지 / 제외の마법함정카드가 ３장 이상 必要
	CALL FULLRESET
	효과範囲묘지 = 1
	효과範囲제외 = 1
	효과条件종류 = 마함全般
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 3
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,79585)
		RETURN 0
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_DRAW_CHECK(1)
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	;판도라 매지션 걸 또는 기술X 몬스터가 자신 필드에 必要
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 기술
	효과条件종류 = 엑시즈
	효과条件スーパー名前 = 마술쇼의 조수：판도라 매지션 걸
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF 발동済_3(듀얼리스트,79585)
		RETURN 0
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과条件종류:0 = 速攻마법
	효과条件종류:1 = 通常함정
	효과条件カテゴリ = 기술
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"효과발동",0)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_79585(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択結果


IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79585,2)
	IF RESULT == 0
		;ライフコスト300
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",300)
		CALL FULLRESET
		효과範囲묘지 = 1
		효과範囲제외 = 1
		효과条件종류 = 마함全般
		효과条件複数 = 3
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			;PMGが필드에 居るなら、サーチも할 수 있다。
			CALL FULLRESET
			효과範囲필드 = 1
			효과条件カテゴリ = 기술
			효과条件종류 = 엑시즈
			효과条件スーパー名前 = 마술쇼의 조수：판도라 매지션 걸
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				효과範囲덱 = 1
				효과条件カテゴリ = 기술
				CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
				IF RESULT != 1
					選択結果 = いいえ
				ELSEIF 듀얼리스트 == "상대"
					;CPUは条件を満たしていると必ずサーチを行う。
					選択結果 = はい
				ELSE
					;条件を満たしている場合はサーチするかドローするか選択할 수 있다。
					PRINTL 덱에서 "기술"카드 １장을 패에 넣으시겠습니까？
					CALL YN_CHOICE(듀얼리스트,"●덱에서 「기술」카드 １장을 패에 넣는다。","●덱에서 １장 드로우한다。")
					選択結果 = RESULT
				ENDIF
				
				IF 選択結果 == はい
					;ショット ガン シャッフル！　をP.M.Gが色気を振りまきながらやるよ。
					;そっちに注意向けさせてる間に덱トップに기술카드仕込むイカサマしてるよ。
					;CALL FULLRESET
					;효과条件カテゴリ = 기술
					;CALL CARD_CHOOSE_BASIC(듀얼리스트, "덱", ゾーン, 場所)
					;選択カード = RESULT
					;SIF 選択カード != 9999
					;	CALL CARD_ADD_DECK_SPC(듀얼리스트, "덱", 選択カード)
					;CALL CARDDRAW_BASIC(듀얼리스트,1)
					
					CALL FULLRESET
					효과条件カテゴリ = 기술
					CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
				ELSE
					CALL CARDDRAW_BASIC(듀얼리스트,1)
				ENDIF
			ELSE
				CALL CARDDRAW_BASIC(듀얼리스트,1)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동＆설치まで
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79585,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲덱 = 1
		효과条件名前 = "영겁의 재액"
		효과条件任意 = 1
		CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")
	ENDIF
ELSEIF 종류 == 지속효과
	CALL SET_FIELD_MAGIC_INFO(듀얼리스트, "P.M.G쇼 타임♪")
ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79585,3)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"제외")
	ENDIF
ENDIF
