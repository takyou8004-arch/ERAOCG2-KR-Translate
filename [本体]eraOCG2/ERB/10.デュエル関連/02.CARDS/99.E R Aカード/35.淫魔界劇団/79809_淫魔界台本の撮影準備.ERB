@CARDNAME_79809,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 음마계대본의 촬영 준비
	CASE "略称"
		RESULTS = "촬영 준비"
	CASE "カテゴリ"
		RESULTS:0 = 마계대본
		RESULTS:1 = 음마계대본
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_79809(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常마법
	CASE "추가日"
		RETURN 250605
ENDSELECT

RETURN 0

@CARD_EXPLANATION_79809(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,마함起動,손패起動)
PRINTL ①: 덱에서 "음마계극단" 몬스터 1장을 마법 & 함정 존에 앞면 표시로 놓는다.
PRINTL 상대 필드에 카드가 존재할 경우, 이 효과로 상대 덱의 암컷 몬스터 1장을 고를 수도 있다.
PRINTL 이 효과로 놓인 카드는 "음마계극단" / "마계극단" 카드로도 취급한다.
CALLFORM CARD_ABLE(2,破壊→묘지)
PRINTL ②: 필드에 "음마계극단" 카드가 존재하고,
PRINTL 이 카드가 상대 효과로 파괴되었을 경우에 발동할 수 있다.
PRINTL 덱 / 묘지에서 이 카드명 이외의 "음마계대본" 카드를 원하는 수만큼 마법 & 함정 존에 세트한다.
PRINTL 이 효과로 세트한 카드는 세트한 턴에도 발동할 수 있다.
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_79809(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,79809)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,79809)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件성별 = 牝性
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL CARD_DRAW_CHECK(1)
	RETURN 1
ELSEIF 종류 == 破壊→묘지
	SIF 발동済_2(듀얼리스트,79809)
		RETURN 0
	SIF 処理中_듀얼리스트 == 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲상대필드 = 1
	효과範囲마함 = 1
	효과範囲상대마함 = 1
	효과条件カテゴリ = 음마계극단
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 음마계대본
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_79809(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 조교대상
#DIM DYNAMIC 条件달성
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79809,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과範囲상대마함 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT == 1
			条件달성 = 1
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件종류 = モン全般
		효과条件성별 = 牝性
		IF 条件달성 != 1
		효과条件カテゴリ = 음마계극단
		ELSE
			효과範囲상대덱 = 1
		ENDIF
		CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"表側표시")
		IF RESULT == 1
			選択カード = RESULT:1
			CALL CARD_EROTICS(選択カード)
			IF RESULT != -1
				조교대상 = RESULT
				SETCOLOR C_YELLOW
				PRINTFORMW 촬영준비가 정리되어、%CALLNAME:조교대상%에게 인터뷰가 시작되었다
				PRINTL
				SELECTCASE RAND:3
					CASE 0
					PRINTFORMW %CALLNAME:조교대상%는 연령을 질문받고있다…
						SELECTCASE TALENT:(조교대상):연령
							;子供
							CASE -2
								PRINTFORMW %CALLNAME:조교대상%의 외견은 아무리 봐도 어린이지만、마계에선 합법인 연령인듯 하다
							;学生
							CASE -1
								SELECTCASE TALENT:(조교대상):소속
									CASE 1
										PRINTFORMW 모 아카데미아의 학생이라는듯 하다
									CASEELSE
										PRINTFORMW 평소엔 학생인듯 하다
								ENDSELECT
							;若者
							CASE 0
								PRINTFORML
							;大人
							CASE 1 TO 2
								PRINTFORML
						ENDSELECT
					CASE 1
						PRINTFORM 가슴 사이즈를 질문받은 %CALLNAME:조교대상%는、
						IF TALENT:(조교대상):수치심 == 1 || TALENT:(조교대상):정조 == 1 || TALENT:(조교대상):자기과시욕 == -1
							PRINT 부끄러운듯이
						ENDIF
						PRINTFORMW %DECIMAL_STRING(CFLAG:(조교대상):가슴초기치 + CFLAG:(조교대상):가슴성장,2)%cm의 %TOSTR_CUPSIZE(조교대상)%컵이라고 대답했다
					CASE 2
					PRINTFORM 자위에 대해서 질문받은 %CALLNAME:조교대상%는、
					IF TALENT:(조교대상):무지 == 1 
						PRINTFORMW 잘 모른다고 대답했다
					ELSE
						IF TALENT:(조교대상):수치심 == 1 || TALENT:(조교대상):정조 == 1 || TALENT:(조교대상):자기과시욕 == -1
							PRINT 부끄러운듯이
						ENDIF
						IF ABL:(조교대상):자위중독 > 0 || TALENT:(조교대상):자위하기쉬움 == 1
							PRINTW 매일 하고있다고 대답했다
						ELSEIF EXP:(조교대상):자위경험 == 0
							PRINTW 한적 없다고 대답했다
						ELSE
							PRINTW 가끔 한다고 대답했다
						ENDIF
					ENDIF
				ENDSELECT
				RESETCOLOR
				CALL EROTIC_MOVE("晒しもの",조교대상,-1,150)
			ENDIF
			TRYCALLFORM CARDNAME_{選択カード}, "カテゴリ"	
			IF !MATCH(RESULTS, "음마계극단")
				CALL CARD_SET_CATEGORY(듀얼리스트,"마함",カード설치先,"마계극단")
				CALL CARD_SET_CATEGORY(듀얼리스트,"마함",カード설치先,"음마계극단",1)
			ENDIF
			;インタビューだけで売るのはちょっと
			;SIF GETVARS(@"%듀얼리스트%_フィールド名") == "淫魔界スタジオ"
				;CALL INMAKAIFILM_GENERATE(듀얼리스트,79809,1,選択カード)
		ENDIF
	ENDIF
ELSEIF 종류 == 破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,79809,2)
	IF RESULT == 0
		REPEAT 5
			SIF 마함数(듀얼리스트) >= 5
				BREAK
			CALL FULLRESET
			효과範囲덱 = 1
			효과範囲묘지 = 1
			효과条件カテゴリ = 음마계대본
			효과条件固名前 = 음마계대본의 촬영 준비
			효과条件종류 = 마함全般
			효과条件任意 = 1
			CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"使化裏側표시")
			SIF RESULT == 9999
				BREAK
		REND
	ENDIF
ENDIF