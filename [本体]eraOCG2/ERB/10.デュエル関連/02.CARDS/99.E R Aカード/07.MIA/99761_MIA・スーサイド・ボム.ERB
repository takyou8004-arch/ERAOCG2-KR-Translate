@CARDNAME_99761,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = MIA 수어사이드 봄
	CASE "略称"
		RESULTS = MIA자폭
	CASE "カテゴリ"
		RESULTS:0 = MIA
		RESULTS:1 = E.R.A
ENDSELECT


@CARD_99761(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 通常함정
	CASE "추가日"
		RETURN 250521
ENDSELECT

RETURN 0

@CARD_EXPLANATION_99761(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드명의  ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1,마함起動)
PRINTL ①：자신 필드의 기계족・어둠속성 몬스터 １장을 파괴하고、상대 필드의 카드２장을 제외한다。
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②：묘지의 이 카드를 제외하고 발동할 수 있다。
PRINTL 상대의 제외 상태인"생체파츠"의 수 만큼、
PRINTL 자신의 제외 상태인 "MIA"카드를 덱의 가장 아래로 되돌린다。
CALL TEXT_DECORATION("ROGUE")



@CARDCAN_99761(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 효과制限달성
#DIM DYNAMIC カウンタ

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,99761)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || (종류 == フリー誘発 && ゾーン == "마함")
	SIF ゾーン != "마함"
		RETURN 0
	SIF 발동済_1(듀얼리스트,99761)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 2
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종족 = 기계족
	효과条件속성 = 어둠속성
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	효과条件除去種 = 제외
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動 || (종류 == フリー誘発 && ゾーン == "묘지")
	SIF ゾーン != "묘지"
		RETURN 0
	SIF 발동済_2(듀얼리스트,99761)
		RETURN 0
	CALL FULLRESET
	효과範囲상대제외 = 1
	효과条件カテゴリ = 생체파츠
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_99761(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 変換カード
#DIM DYNAMIC カウンタ
#DIM DYNAMIC ダメージ量
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 조교대상
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 마함起動 || (종류 == フリー誘発 && ゾーン == "마함")
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99761,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件종족 = 기계족
		효과条件속성 = 어둠속성
		효과条件行先플래그 = 破壊行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과条件複数 = 2
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"제외")
		ENDIF
	ENDIF
ELSEIF 종류 == 묘지起動 || (종류 == フリー誘発 && ゾーン == "묘지")
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,99761,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲상대제외 = 1
		효과条件カテゴリ = 생체파츠
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		カウンタ = RESULT
		IF カウンタ
			FOR カウンタ,0,紙束最大値
				VARSET RESULT
				SIF GETVAR(@"%対面者%_제외:カウンタ") != 39600
					CONTINUE
				CALL FULLRESET
				조교대상 = GETVAR(@"%対面者%_除_変化元ID:カウンタ")
				CALL CARD_EROTICS(조교대상)
				IF RESULT != -1
					조교대상 = RESULT
					PRINTFORML
					PRINTFORML 에너지 추출 장치에 삼켜진 %NAME:조교대상%는 고통과 음락이 섞인 목소리를 흘리고있다！
					CALL EROTIC_MOVE("생체파츠",조교대상,-1,200)
				ENDIF
				PRINTFORML
			NEXT
			PRINTFORML MIA의 잔해는 생체파츠에서 흡수한 에너지를 사용해 수복되어간다…
			PRINTFORML 
		ENDIF

		CALL FULLRESET
		효과範囲제외 = 1
		효과条件カテゴリ = MIA
		효과条件複数 = 100 - カウンタ
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ENDIF

