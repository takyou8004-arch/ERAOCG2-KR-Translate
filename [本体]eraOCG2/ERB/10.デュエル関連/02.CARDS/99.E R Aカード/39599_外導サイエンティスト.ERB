@CARDNAME_39599,参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 외도 사이언티스트
	CASE "略称"
		RESULTS = 외도과학자
	CASE "カテゴリ"
		;RESULTS:0 = 패러사이트
		RESULTS:2 = E.R.A
ENDSELECT


@CARD_39599(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 마법사족
	CASE "レベル"
		RETURN 1
	CASE "攻撃力"
		RETURN 300
	CASE "守備力"
		RETURN 300
	CASE "성별"
		RETURN 雄性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_39599(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드명의  ①②의 효과는 각각 1턴에 1번만 사용할 수 있다.
CALLFORM CARD_ABLE(1,손패起動,묘지起動)
PRINTL ①：패 / 묘지의 이 카드를 제외하고 1000LP를 지불하고 발동할 수 있다.
PRINTL 자신의 덱에서 "패러사이트" 몬스터 1장 또는
PRINTL 천사족 / 마법사족 / 조류족 / 야수족 / 야수족 암컷 몬스터 1장을 선택하고 패에 넣는다.
;PRINTL 이 효과의 발동 후, 턴 종료시까지 자신은 암컷 몬스터밖에 EX덱에서 특수 소환할 수 없다.
CALLFORM CARD_ABLE(2,제외時)
PRINTL ②：이 카드가 제외되었을 경우에 발동할 수 있다. 엔드 페이즈에 자신 필드 / 묘지에
PRINTL "패러사이트" 몬스터 카드가 존재할 경우, 이하의 효과에서 1개를 선택하고 적용한다.
PRINTL "패러사이트" 몬스터 카드가 존재하지 않을 경우, 자신은 1000 LP를 잃는다.
PRINTL ●제외 상태의 '외도 사이언티스트' 1장을 덱으로 되돌리고, 덱 / 묘지에서 '패러사이트' 함정 카드 1장을 고르고 세트한다.
PRINTL ●제외 상태의 '외도 사이언티스트' 1장을 패에 넣는다.
CALL TEXT_DECORATION("ROGUE")

;遊戯王ARC-Vで"パラサイト フュ-ジョナ-"を用いてメインヒロインである柚子たち４人を洗脳した事で
;有名な캐릭터"ドクトル"のイメ-ジのモンスター。ステータスは"魔導サイエンティスト"がベ-ス。
;能力は水遣いに似ていて、손패コスト等で切ってから、묘지제외하고 サーチをすると無駄なくアドを稼げる。サーチ範囲は
;"パラサイト フュ-ジョナ-"関連の"パラサイト" 몬스터의 ほか、ARC-Vのヒロインたちが使う 몬스터의 종족
;（천사족：幻奏 마법사족：ＷＷ 비행야수족：ＬＬ 야수족：月光）になっていて、"歌氷麗月"と補い合うサーチ範囲をしている。

@CARDCAN_39599(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 효과使用불가,3

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,39599)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動 || 종류 == 묘지起動
	SIF 발동済_1(듀얼리스트,39599)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	SIF GETVAR(@"ライフ_%듀얼리스트%") <= 1000
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	;효과範囲묘지 = 1
	효과条件종족:0 = 천사족
	효과条件종족:1 = 마법사족
	효과条件종족:2 = 비행야수족
	효과条件종족:3 = 야수족
	효과条件성별 = 牝性
	효과条件종류 = モン全般
	효과条件スーパーカテゴリ = 패러사이트
	효과条件スーパー종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 제외時
	SIF 발동済_2(듀얼리스트,39599)
		RETURN 0
	;CALL CARD_RESERV(듀얼리스트,39599)
	;CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,39599,2)
	RETURN 1
ELSEIF 종류 == ターン終了時予約
	CALL FULLRESET
	효과範囲제외 = 1
	효과条件名前 = 외도 사이언티스트
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲마함 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 패러사이트
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT != 1
		CALL LIFE_CHANGE(듀얼리스트,"減少","효과",1000)
		RETURN 0
	ENDIF
	RETURN 1
ENDIF


@CARDEFFECT_39599(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択結果

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動 || 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,39599,1)
	IF RESULT == 0
		CALL LIFE_CHANGE(듀얼리스트,"減少","コスト",1000)
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,제외行き)
		CALL FULLRESET
		효과範囲덱 = 1
		;효과範囲묘지 = 1
		효과条件종족:0 = 천사족
		효과条件종족:1 = 마법사족
		효과条件종족:2 = 비행야수족
		효과条件종족:3 = 야수족
		효과条件성별 = 牝性
		효과条件종류 = モン全般
		효과条件スーパーカテゴリ = 패러사이트
		효과条件スーパー종류 = モン全般
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 제외時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,39599,2)
	IF RESULT == 0
		CALL CARD_RESERV(듀얼리스트,39599)
	ENDIF
ELSEIF 종류 == ターン終了時予約
	;CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,39599,2)
	;IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과範囲묘지 = 1
		효과条件カテゴリ = 패러사이트
		효과条件종류 = 함정全般
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			;パラサイト함정が덱 / 묘지에 있는 경우、CPUはセットする효과를 選択。
			IF 듀얼리스트 == "상대"
				選択結果 = はい
			ELSE
				;プレイヤーが使用して両方の条件を満たしている場合は選択肢を出す
				CUSTOMDRAWLINE -
				PRINTL 외도 사이언티스트의 어떤 효과를 사용합니까?
				CUSTOMDRAWLINE -
				CALL YN_CHOICE(듀얼리스트,"●제외 상태의 '외도 사이언티스트' 1장을 덱으로 되돌리고, 덱 / 묘지에서 '패러사이트' 함정 카드 1장을 고르고 세트한다.","●제외 상태의 '외도 사이언티스트' 1장을 패에 넣는다.")
				選択結果 = RESULT
			ENDIF
			
			IF 選択結果 == はい
				;はい＝덱 / 묘지에서 "パラサイト"함정카드 １장을 고르고 セット
				CALL FULLRESET
				효과範囲제외 = 1
				효과条件名前 = 외도 사이언티스트
				효과条件行先플래그 = 덱行き
				CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
				IF RESULT == 1
					CALL FULLRESET
					효과範囲덱 = 1
					효과範囲묘지 = 1
					효과条件カテゴリ = 패러사이트
					효과条件종류 = 함정全般
					CALL CARD_SET_ADV(듀얼리스트,ゾーン,場所,"裏側표시")
				ENDIF
			ELSE
				;いいえ＝제외状態의 이 카드를 패로 되돌린다。
				CALL FULLRESET
				효과範囲제외 = 1
				효과条件名前 = 외도 사이언티스트
				효과条件行先플래그 = 손패行き
				CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			ENDIF
		ELSE
			;条件を満たしてない場合もいいえと同じ＝제외状態의 이 카드를 패로 되돌린다。
			CALL FULLRESET
			효과範囲제외 = 1
			효과条件名前 = 외도 사이언티스트
			효과条件行先플래그 = 손패行き
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	;ENDIF
ENDIF
