@CARDNAME_79531, 参照先
;ココで지정 카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = E.R.A.ShadDOLL.Fusion
		;RESULTS = 사악한 그림자 저주
	CASE "略称"
		RESULTS = ERA섀도르 퓨전
	CASE "カテゴリ"
		RESULTS:0 = 섀도르
		RESULTS:1 = ShadDOLL
		RESULTS:2 = 융합
		RESULTS:3 = E.R.A
ENDSELECT


@CARD_79531(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"

		RETURN 지속마법
ENDSELECT
RETURN 0

@CARD_EXPLANATION_79531(종류)
#DIM DYNAMIC 종류

CALL TEXT_DECORATION("ROGUE")
PRINTL 이 카드명의  ①의 효과는 1턴에 1번밖에 사용할 수 없다。
CALLFORM CARD_ABLE(1, 마함起動)
PRINTL ①：자신 메인 페이즈에 발동할 수 있다。자신의 패 / 필드의 "섀도르" 몬스터를 융합 소재로 해
PRINTL "섀도르"융합 몬스터 1장을 융합 소환한다. 필드 / 묘지에 여성 몬스터가 존재할 경우,
PRINTL 자신의 엑스트라 덱에 "섀도르" 몬스터도 융합 소재로 할 수 있다。
CALLFORM CARD_ABLE(0, 지속효과)
PRINTL ②：자신 필드에 "섀도르"융합 몬스터가 존재하는 한
PRINTL 공격 가능한 상대 몬스터는 "섀도르" 융합 몬스터를 공격해야 한다。
CALLFORM CARD_ABLE(0, 지속효과)
PRINTL ③：자신의 "섀도르" 융합 몬스터의 전투로 발생하는 자신에게로의 전투 데미지는 대신에 상대가 받는다。
CALL TEXT_DECORATION("ROGUE")

@CARDCAN_79531(듀얼리스트, 종류, ゾーン, 場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC EX条件달성

CALL CARD_NEGATE(듀얼리스트, 종류, ゾーン, 場所, 79531)
SIF RESULT == 1
	RETURN 0
IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF 발동済_1(듀얼리스트, 79531)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	;필드 / 묘지에 牝性 몬스터가 いる場合の条件달성。
	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲묘지 = 1
	효과範囲상대필드 = 1
	효과範囲상대묘지 = 1
	효과条件성별 = 牝性
	CALL CARD_CHECK_ADV(듀얼리스트, 종류, ゾーン, 場所)
	SIF RESULT == 1
		EX条件달성 = 1
	CALL RESETAREA_MAT
	SIF EX条件달성 == 1
		素材範囲EX덱 = 1
	素材範囲필드 = 1
	素材範囲손패 = 1
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 융합
	효과条件カテゴリ = 섀도르
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트, 종류, ゾーン, 場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃時強制誘発 || 종류 == 지속효과
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") != 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 섀도르
	효과条件종류 = 융합
	CALL CARD_CHECK_ADV(듀얼리스트, 종류, ゾーン, 場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_79531(듀얼리스트, 종류, ゾーン, 場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC EX参照
#DIM DYNAMIC 묘지送カード,10
#DIM DYNAMIC 손패묘지送カード,10
#DIM DYNAMIC 素材数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択済カード
#DIM DYNAMIC EX条件달성
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタB
;융합소환
#DIMS DYNAMIC 対面者

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트, 종류, ゾーン, 場所, 79531, 1)
	IF RESULT == 0
		;CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲묘지 = 1
		효과範囲상대필드 = 1
		효과範囲상대묘지 = 1
		효과条件성별 = 牝性
		CALL CARD_CHECK_ADV(듀얼리스트, 종류, ゾーン, 場所)
		SIF RESULT == 1
			EX条件달성 = 1
		;EX덱確認
		CALL RESETAREA_MAT
		SIF EX条件달성 == 1
			素材範囲EX덱 = 1
		素材範囲필드 = 1
		素材範囲손패 = 1
		素材条件カテゴリ = 섀도르
		CALL FULLRESET
		효과条件종류 = 융합
		효과条件カテゴリ = 섀도르
		효과条件소환可 = 1	
		
		CALL CARD_CHOOSE_BASIC(듀얼리스트, "EX덱", ゾーン, 場所)
		選択カード = RESULT
		;돌아간다
		IF 選択カード != 9999
			;마법 카드를 묘지送り
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트, 選択カード)
			;素材処理を読み込む
			CALL FUSSUMMON(듀얼리스트, 選択カード)
		ENDIF
	ENDIF
ELSEIF 종류 == 攻撃時強制誘発
	CALL SET_PLAYER_INFO(듀얼리스트, "ダメージ反射", 1)
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	CALL STATCHANGE_EFFECT(듀얼리스트, "상대필드", 場所, "戦狂")
	CALL STATCHANGE_EFFECT(듀얼리스트, "상대필드", 場所, "直無")
	
	CALL FULLRESET
	효과条件固カテゴリ = 섀도르
	효과条件종류 = 융합以外
	CALL STATCHANGE_EFFECT(듀얼리스트, "필드", 場所, "攻避")
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동＆설치まで
	CALL CARD_CHOICE(듀얼리스트, 종류, ゾーン, 場所, 79531)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트, ゾーン, 場所, 마함行き)
	ENDIF
ENDIF
