@CARDNAME_38024,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = N 플레어 스캐럽
	CASE "略称"
		RESULTS = N 스캐럽
	CASE "カテゴリ"
		RESULTS:0 = 네오 스페이시언
		RESULTS:1 = 네오스
ENDSELECT


@CARD_38024(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 전사족 ;Nは全部戦士に
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 500
	CASE "守備力"
		RETURN 500
	CASE "성별"
		RETURN 雄性
ENDSELECT
RETURN 0

@CARD_EXPLANATION_38024(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 １턴에 １번밖에 사용할 수 없다。
CALL CARD_ABLE(1,손패起動)
PRINTL ①：자신의 메인 페이즈에 패에서 발동할 수 있다。
PRINTL 자신 필드의 앞면 표시 몬스터 １장을 고르고、이 카드는 패에서 특수 소환한다。
PRINTL 고른 몬스터의 공격력은、이 카드의 원래 공격력 만큼 오른다。
CALL CARD_ABLE(2,소환時,특수소환時)
PRINTL ②：이 카드가 소환 / 특수 소환됐을 때에 발동할 수 있다。
PRINTL 덱에서 "네오스" 마법 / 함정 카드 １장을 패에 넣는다。
;オリジナルアッパー
CALL CARD_ABLE(0,攻守変動)
PRINTL ③：이 카드의 공격력은、자신의 마법＆함정 존의 카드의 수×４００ 포인트 오른다。



@CARDSUMMON_AA_38024


@CARDCAN_38024(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,38024)
SIF RESULT == 1
	RETURN 0


IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,38024)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,38024,ゾーン)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件표시 = 10
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,38024)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 네오스
	효과条件종류 = 마함全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_38024(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38024,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과条件표시 = 10
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"필드",ゾーン,場所)
		IF RESULT != 9999
			選択カード = RESULT
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				TRYCALLFORM CARD_38024("攻撃力")
				攻撃力修正 = RESULT
				CALL SET_FIELD_INFO(듀얼리스트,"필드",選択カード,"永攻",攻撃力修正)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,38024,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 네오스
		효과条件종류 = 마함全般
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 攻守変動
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	攻撃力修正 = RESULT * 400
	CALL STATCHANGE_BASIC(듀얼리스트,"自身",場所,攻撃力修正)	
ENDIF

