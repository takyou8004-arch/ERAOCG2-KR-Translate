@CARDNAME_73340,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 사이킥 장비 융합
	CASE "略称"
RESULTS = 초장비융합
	CASE "カテゴリ"
RESULTS:0 = 융합
ENDSELECT


@CARD_73340(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 0
	CASE "종류"

		RETURN 通常마법
ENDSELECT
RETURN 0

@CARD_EXPLANATION_73340(종류)
#DIM DYNAMIC 종류


CALLFORM CARD_ABLE(0,마함起動 ,손패起動)
PRINTL ①: 자신의 패 / 필드의 몬스터(사이킥족)를 융합 소재로 하여, 융합 몬스터 1장을 융합 소환한다.
PRINTL 그 후, 자신의 묘지에 몬스터(바람 속성)가 있는 경우,
PRINTL 자신의 묘지의 장비 마법 카드 1장을 선택하여 패에 추가할 수 있다.

@CARDCAN_73340(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 条件달성


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,73340)
SIF RESULT == 1
	RETURN 0
IF 종류 == 마함起動 || 종류 == 손패起動
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0

	CALL RESETAREA_MAT
	素材範囲손패 = 1
	素材範囲필드 = 1
	효과条件종족 = 사이킥족
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 융합
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_73340(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC EX参照
#DIM DYNAMIC 묘지送カード,10
#DIM DYNAMIC 손패묘지送カード,10
#DIM DYNAMIC 素材数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択済カード
#DIMS DYNAMIC 選択,10
;융합소환
VARSET 묘지送カード, 35
VARSET 손패묘지送カード, 35

IF 종류 == 마함起動 || 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,73340)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL RESETAREA_MAT
		素材範囲손패 = 1
		素材範囲필드 = 1
		효과条件종족 = 사이킥족
		CALL FULLRESET
		효과条件종류 = 융합
		효과条件소환可 = 1
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			;카드의 条件を読み込む：
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
			;素材処理を読み込む
			CALL FUSSUMMON(듀얼리스트, 選択カード)
			
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件속성 = 바람속성
			효과条件종류 = モン全般
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			SIF RESULT != 1
				RETURN 0
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件종류 = 装備마법
			효과条件任意 = 1
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ENDIF
