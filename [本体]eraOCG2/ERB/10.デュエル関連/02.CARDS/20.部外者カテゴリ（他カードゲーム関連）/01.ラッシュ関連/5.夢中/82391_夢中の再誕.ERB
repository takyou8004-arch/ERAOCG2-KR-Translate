@CARDNAME_82391,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 몽중의 재탄생
	CASE "略称"
RESULTS = 몽중의 재탄생
	CASE "カテゴリ"
RESULTS:0 = 몽중
ENDSELECT


@CARD_82391(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 0
	CASE "종류"

		RETURN 通常함정
ENDSELECT

RETURN 0

@CARD_EXPLANATION_82391(종류)
#DIM DYNAMIC 종류
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번만 사용할 수 있다.
CALLFORM CARD_ABLE(1,효과後誘発)
PRINTL ①: 자신 묘지에 곤충족 몬스터가 존재하고,
PRINTL 상대가 드로우했을 경우, 또는 몬스터를 소환 / 특수 소환했을 경우에 발동할 수 있다.
PRINTL 자신 / 상대 필드의 몬스터를 융합 소재로 하고, 곤충족 빛 속성 융합 몬스터 1장을 융합 소환한다.
CALLFORM CARD_ABLE(2,묘지起動)
PRINTL ②: 묘지의 이 카드를 제외하고 발동할 수 있다.
PRINTL 자신의 묘지에서 융합 몬스터를 포함하는 곤충족 몬스터 3장을 덱 / EX덱으로 되돌리고,
PRINTL 자신은 1장 드로우한다.


@CARDCAN_82391(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,82391)
SIF RESULT == 1
	RETURN 0

IF 종류 == 효과後誘発
	SIF ゾーン != "마함"
		RETURN 0
	SIF 발동済_1(듀얼리스트,82391)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
		RETURN 0

	CALL RESETAREA_MAT
	素材範囲필드 = 1
	素材範囲상대필드 = 1
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 융합
	효과条件소환可 = 1
	효과条件종족 = 곤충족
	효과条件속성 = 빛속성
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"소환全般",1)
	SIF RESULT == 1
		RETURN 1
	CALL FULLRESET
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"Ｃドロー",1)
	SIF RESULT == 1
		RETURN 1
	RETURN 0

ELSEIF 종류 == 묘지起動
	SIF 발동済_2(듀얼리스트,82391)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종류 = 융합
	효과条件종족 = 곤충족
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종족 = 곤충족
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 3
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1
ENDIF


@CARDEFFECT_82391(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,82391,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL RESETAREA_MAT
		素材範囲필드 = 1
		素材範囲상대필드 = 1
		CALL FULLRESET
		효과条件종류 = 융합
		효과条件소환可 = 1
		효과条件종족 = 곤충족
		효과条件속성 = 빛속성
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		;戻る
		IF 選択カード != 9999
			;카드의 条件を読み込む：
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
			;素材処理を読み込む
			CALL FUSSUMMON(듀얼리스트, 選択カード)
		ENDIF
	ENDIF

ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,82391,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件종류 = 융합
		효과条件종족 = 곤충족
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件종족 = 곤충족
			효과条件行先플래그 = 덱行き
			효과条件複数 = 2
			CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			SIF RESULT == 1
				CALL CARDDRAW_BASIC(듀얼리스트,1)
		ENDIF
	ENDIF
ENDIF
