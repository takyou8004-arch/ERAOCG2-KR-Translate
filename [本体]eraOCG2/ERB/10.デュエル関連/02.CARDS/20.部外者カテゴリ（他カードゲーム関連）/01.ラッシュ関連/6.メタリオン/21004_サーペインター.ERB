@CARDNAME_21004,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 사-페인타-
	CASE "略称"
RESULTS = 사-페인타-
	CASE "カテゴリ"
RESULTS:0 = 이매지너리
ENDSELECT


@CARD_21004(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 0
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1400
	CASE "守備力"
		RETURN 500
	CASE "성별"
		RETURN 雄性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_21004(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,묘지起動)
PRINTL ①: EX덱의 "메타리온" 융합 몬스터 1장을 상대에게 보여주고 발동할 수 있다.
PRINTL 묘지에서 이 카드를 특수 소환한다.
PRINTL 그 후, 보여준 몬스터에 카드명이 기재되어 있는 융합 소재 몬스터 1장을 자신의 패 / 묘지에서 특수 소환할 수 있다.
PRINTL 이 턴, 자신은 융합 몬스터밖에 EX덱에서 특수 소환할 수 없다.
CALLFORM CARD_ABLE(2,F素材時효과)
PRINTL ②: 이 카드가 융합 소환의 소재가 되었을 경우에 발동할 수 있다.
PRINTL 이 턴, 자신의 융합 몬스터는 전투 내성을 얻는다.

@CARDCAN_21004(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,21004)
SIF RESULT == 1
	RETURN 0

IF 종류 == 묘지起動
	SIF 발동済_1(듀얼리스트,21004)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,21004,"묘지")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件カテゴリ = 메타리온
	효과条件종류 = 융합
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == F素材時효과
	SIF 발동済_2(듀얼리스트,21004)
		RETURN 0
	RETURN 1

ELSEIF 종류 == 短紋章起動
	RETURN 1
ENDIF


@CARDEFFECT_21004(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択종족
#DIM DYNAMIC 選択カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 選択カード_ID
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21004,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 메타리온
		효과条件종류 = 융합
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		IF RESULT != 9999
			選択カード = RESULT
			選択カード_ID = RESULT:99
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,"묘지",場所)

			CALL FULLRESET
			효과範囲손패 = 1
			효과範囲묘지 = 1
			효과条件任意 = 1

			효과条件名前:0 = ダミ- 
			TRYCALLFORM CARDSUMMON_{選択カード_ID}(듀얼리스트,選択カード,0)
			효과条件名前:1 = %素材条件名前:0%
			효과条件名前:2 = %素材条件名前:1%
			효과条件名前:3 = %素材条件名前:2%
			TRYCALLFORM CARDSUMMON_{選択カード_ID}(듀얼리스트,選択カード,10)
			효과条件名前:4 = %素材条件名前:0%
			효과条件名前:5 = %素材条件名前:1%
			효과条件名前:6 = %素材条件名前:2%
			TRYCALLFORM CARDSUMMON_{選択カード_ID}(듀얼리스트,選択カード,11)
			효과条件名前:7 = %素材条件名前:0%
			효과条件名前:8 = %素材条件名前:1%
			효과条件名前:9 = %素材条件名前:2%
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)

			EX소환可能종류 = 융합
			CALL CARD_EX_RESTRICTION(듀얼리스트)
		ENDIF
	ENDIF

ELSEIF 종류 == F素材時효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21004,2)
	IF RESULT == 0
		CALL CARD_RESERV(듀얼리스트,21004,短紋章予約)
	ENDIF

ELSEIF 종류 == 短紋章起動
	CALL FULLRESET
	효과条件종류 = 융합
	CALL STATCHANGE_EFFECT(듀얼리스트,"필드",場所,"戦阻")
ENDIF
