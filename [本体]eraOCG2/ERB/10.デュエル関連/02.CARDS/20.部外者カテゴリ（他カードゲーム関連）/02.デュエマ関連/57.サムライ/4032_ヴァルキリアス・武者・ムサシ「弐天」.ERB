@CARDNAME_4032,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 발키리어스 무사 무사시"이천"
	CASE "略称"
RESULTS = 무사시 이천
	CASE "カテゴリ"
RESULTS:0 = 사무라이
RESULTS:4 = 듀얼 마스터즈
ENDSELECT


@CARD_4032(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 2000
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4032(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,소환時 ,특수소환時)
PRINTL ①: 이 카드가 소환 / 특수 소환에 성공했을 경우에 발동할 수 있다.
PRINTL 자신 필드에 존재하는 "사무라이" 몬스터의 수만큼 상대 필드의 앞면 표시의 카드를 파괴하고,
PRINTL 자신 필드에 존재하는 "크로스 기어"의 수만큼, 카드를 뽑는다(최대 3장).
CALLFORM CARD_ABLE(2,攻撃時誘発)
PRINTL ②: 이 카드의 공격 선언 시에 발동할 수 있다.
PRINTL 패에서 "사무라이" 카드를 1장 고른다.
PRINTL 이렇게 하여 몬스터를 고른 경우, 그 카드를 몬스터 존에 특수 소환하고,
PRINTL 지속 마법 카드를 고른 경우, 그 카드를 앞면 표시로 세트하고 그 패에서 발동했을 때의 능력을 발동할 수 있다.


@CARDCAN_4032(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4032)
SIF RESULT == 1
	RETURN 0

IF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,4032)
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1
ELSEIF 종류 == 攻撃時誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,4032)
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF モンスター数(듀얼리스트) >= 5 && 마함数(듀얼리스트) >= 5
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_4032(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 사무라이数
#DIM DYNAMIC 크로스기어数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード_ID

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4032,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲필드 = 1
		효과範囲마함 = 1
		효과条件カテゴリ = 크로스기어
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		크로스기어数 = RESULT
		SIF 크로스기어数 >= 3
			크로스기어数 = 3

		CALL FULLRESET
		효과範囲필드 = 1
		효과条件カテゴリ = 사무라이
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		사무라이数 = RESULT

		CALL FULLRESET
		효과条件표시 = 表側표시
		효과条件複数 = 100 - 사무라이数
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"묘지")
		CALL CARDDRAW_BASIC(듀얼리스트,크로스기어数)
	ENDIF
ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4032,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 사무라이
		효과条件종류:0 = 효과モン
		효과条件종류:1 = 通常モン
		효과条件종류:2 = 지속마법
		CALL CARD_CHOOSE_BASIC(듀얼리스트, "손패",ゾーン,場所)	
		IF RESULT != 9999
			選択カード = RESULT
			選択カード_ID = RESULT:99
			CALLFORM CARD_{選択カード_ID}("종류")
			IF RESULT == 지속마법
				CALL CARD_DISPOSE(듀얼리스트,"손패",選択カード,마함行き,1)
				CALLFORM CARDEFFECT_{選択カード_ID}(듀얼리스트,손패起動,"효과処理")
			ELSE
				CALL FULLRESET
				CALL CARD_SP_SUMMON(듀얼리스트,"손패", 選択カード)
			ENDIF
		ENDIF
	ENDIF
ENDIF


