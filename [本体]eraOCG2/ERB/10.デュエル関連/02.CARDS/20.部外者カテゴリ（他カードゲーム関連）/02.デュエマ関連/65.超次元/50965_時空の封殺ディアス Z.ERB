@CARDNAME_50965,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 시공의 봉쇄 디아스Z
	CASE "略称"
		RESULTS = 시공 디아스
	CASE "カテゴリ"
		RESULTS:0 = 사이킥
		RESULTS:1 = 커맨드
		RESULTS:4 = DUEL MASTERS
ENDSELECT


@CARD_50965(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	;효과モン/通常モン/의식/융합/싱크로/엑시즈/링크/초차원
	CASE "종류"
		RETURN 초차원
	;어둠속성/빛속성/땅속성/물속성/화염속성/바람속성
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 3000
	CASE "守備力"
		RETURN 2500
	CASE "生成カード"
		RETURN 150965
ENDSELECT
RETURN 0

@CARD_EXPLANATION_50965(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 1턴에 각각 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(2,攻撃時誘発)
PRINTL ①：이 카드의 공격 선언 시에 자신의 묘지의 카드 4장을 덱으로 되돌리고 발동할 수 있다.
PRINTL 상대 필드의 카드 1장을 선택하고 파괴한다.
PRINTL 그 후, 상대의 묘지의 카드 4장을 덱으로 되돌리는 것으로, 상대 필드의 카드 1장을 선택하고 파괴할 수 있다.
CALLFORM CARD_ABLE(1,ターン終了時)
PRINTL ②：자신의 엔드 페이즈에, 상대 필드에 몬스터가 존재하지 않는 경우에 발동할 수 있다.
PRINTL 이 카드는 각성하고 "섬멸의 각성자 디아블로스Z"가 된다.
PRINTL 각성은 1턴에 1번밖에 할 수 없다.

@CARDCAN_50965(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,50965)
SIF RESULT == 1
	RETURN 0

IF 종류 == 攻撃時誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,50965)
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과範囲상대마함 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 4
		RETURN 0
	RETURN 1
ELSEIF 종류 == ターン終了時
	SIF ゾーン != "필드"
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 발동済_1(듀얼리스트,覚醒발동)
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 0
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_50965(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタ数

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50965,2)

	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件複数 = 4
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"묘지")
		ENDIF

		CALL FULLRESET
		효과範囲상대묘지 = 1
		효과条件複数 = 4
		효과条件任意 = 1
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"묘지")
		ENDIF
	ENDIF

ELSEIF 종류 == ターン終了時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,覚醒발동,1)
	IF RESULT == 0
	CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"条達",2,1)
	CALL CARD_RECONSTRUCT(듀얼리스트,ゾーン,場所,150965)
	ENDIF
ENDIF

;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――

@CARDNAME_150965,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 섬멸의 각성자 디아블로스Z
	CASE "略称"
		RESULTS = 섬멸 디아블로스
	CASE "カテゴリ"
		RESULTS:0 = 사이킥
		RESULTS:1 = 각성자
		RESULTS:2 = 커맨드
		RESULTS:4 = DUEL MASTERS
ENDSELECT


@CARD_150965(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 초차원
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 3800
	CASE "守備力"
		RETURN 3300
	CASE "トークン"
		RETURN 1
	CASE "生成カード"
		RETURN 50965
ENDSELECT
RETURN 0

@CARD_EXPLANATION_150965(종류)
#DIM DYNAMIC 종류

CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ①：자신 필드의 몬스터의 공격력은 1000 상승한다.
CALLFORM CARD_ABLE(0,소환誘発)
PRINTL ②：상대 필드에 몬스터가 소환 / 특수 소환될 때마다 발동할 수 있다.
PRINTL 그 몬스터의 공격력이 1000 이하라면 파괴하고, 1000보다 높다면 공격력을 1000 내린다.
CALLFORM CARD_ABLE(0,身代효과)
PRINTL ③：이 카드가 파괴될 경우, 대신 이 카드는 "시공의 봉쇄 디아스Z"가 되고, 각성 상태를 잃는다.

@CARDCAN_150965(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 攻撃修正
#DIM DYNAMIC 守備修正
#DIM DYNAMIC 효과先

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,150965)
SIF RESULT == 1
	RETURN 0

IF 종류 == 攻守変動
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 소환誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 最終소환決者 == 듀얼리스트
		RETURN 0
	SIF 最終소환타입 == ""
		RETURN 0
	RETURN 1
ELSEIF 종류 == 身代효과
	SIF ゾーン != "필드"
		RETURN 0
	SIF !GROUPMATCH(破壊종류,"효과","戦闘")
		RETURN 0
	SIF 破壊대상ゾーン != "필드"
		RETURN 0
	SIF 破壊대상場所 != 場所
		RETURN 0
	RETURN 1
ENDIF


@CARDEFFECT_150965(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC カウンタ数
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃修正
#DIM DYNAMIC 守備修正
#DIM DYNAMIC 효과先

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 ==  攻守変動
	CALL FULLRESET
	효과条件종류 = モン全般
	CALL STATCHANGE_BASIC(듀얼리스트,"필드",場所,1000)
ELSEIF 종류 == 소환誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,150965)
	IF RESULT == 0
		;召雷弾！
		효과先 = 最終소환場所
		IF GETVAR(@"%対面者%_MZ_攻:효과先") < 1001 || GETVAR(@"%対面者%_MZ_守:효과先") < 1001
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",最終소환場所,"묘지")
		ELSEIF GETVAR(@"%対面者%_MZ_モ耐:효과先") == 0
			CALL FULLRESET
			효과추가종류 = 10
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",最終소환場所,"永攻DOWN",1000)
		ENDIF
	ENDIF
ELSEIF 종류 == 身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,150965)
	IF RESULT == 0
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"条達",0,1)
		CALL CARD_RECONSTRUCT(듀얼리스트,ゾーン,場所,50965)
		RETURN 1
	ENDIF
	RETURN 0
ENDIF