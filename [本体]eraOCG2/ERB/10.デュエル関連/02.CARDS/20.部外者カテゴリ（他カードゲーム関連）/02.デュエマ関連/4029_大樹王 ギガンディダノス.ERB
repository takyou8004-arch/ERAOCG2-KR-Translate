@CARDNAME_4029,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 대수왕 기간디다노스
	CASE "略称"
		RESULTS = 기간디다노스
	CASE "カテゴリ"
		RESULTS:0 = 후시기 왕국
		RESULTS:1 = 자이언트
		RESULTS:4 = DUEL MASTERS
ENDSELECT


@CARD_4029(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RESULT:1 = 어둠속성
		RETURN 땅속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 12
	CASE "攻撃力"
		RETURN 5000
	CASE "守備力"
		RETURN 5000
	CASE "성별"
		RETURN 牝性
	CASE "소환불가"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4029(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,묘지起動)
PRINTL ①: 코스트의 합계가 14 이상이 되도록 자신 묘지의 어둠 땅 속성 몬스터를 제외하고 발동할 수 있다.
PRINTL 묘지에서 이 카드를 특수 소환한다.
CALLFORM CARD_ABLE(2,특수소환時)
PRINTL ②: 이 카드가 특수 소환했을 경우에 발동할 수 있다. (이 효과는 선공 1턴째에는 사용할 수 없다.)
PRINTL 상대는 자신의 손패를 전부 버린다.
CALLFORM CARD_ABLE(0,지속효과)
PRINTL ③: 이 카드가 몬스터 존에 존재하는 한,
PRINTL 상대는 공격력 5000보다 적은 몬스터로 공격 선언할 수 없다.


@CARDCAN_4029(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4029)
SIF RESULT == 1
	RETURN 0

IF 종류 == 묘지起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,4029,"묘지")
	SIF RESULT == 1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件속성:0 = 어둠속성
	효과条件속성:1 = 땅속성
	효과条件他所 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT:2 < 14
		RETURN 0
	RETURN 1

ELSEIF 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,4029)
		RETURN 0
	SIF ターン数 <= 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_4029(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 묘지送りレベル

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4029,1)
	IF RESULT == 0
		$INPUT_LOOP
		CALL FULLRESET
		효과条件속성:0 = 어둠속성
		효과条件속성:1 = 땅속성
		효과条件他所 = 1
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"묘지",ゾーン,場所)
		IF RESULT == 1
			CALLFORM CARD_{RESULT:1}, "レベル"
			IF RESULT + 묘지送りレベル >= 14
				CALL FULLRESET
				CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
			ELSE
				묘지送りレベル += RESULT
				GOTO INPUT_LOOP
			ENDIF
		ENDIF
	ENDIF

ELSEIF 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4029,2)
	IF RESULT == 0
		FOR カウンタ,0,99
			SIF GETVAR(@"%対面者%_손패:カウンタ") == -1
				CONTINUE
			CALL CARD_DISPOSE(対面者,"손패",カウンタ)
		NEXT
	ENDIF

ELSEIF 종류 == 지속효과
	CALL FULLRESET
	효과条件攻撃力 = 4999
	CALL STATCHANGE_EFFECT(対面者,"필드",場所,"戦無")
ENDIF

