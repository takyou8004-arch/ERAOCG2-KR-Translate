@CARDNAME_24163,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 각인수 메탈 파티 드래곤
	CASE "略称"
		RESULTS = 메탈 파티
	CASE "カテゴリ"
		RESULTS:0 = 기어 크로니클
		RESULTS:1 = 십이지 각인수
		RESULTS:4 = 뱅가드
ENDSELECT


@CARD_24163(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RESULT:1 = 드래곤족
		RETURN 기계족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 2000
	CASE "守備力"
		RETURN 1200
ENDSELECT

RETURN 0

@CARD_EXPLANATION_24163(종류)
#DIM DYNAMIC 종류


PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 덱에서 "각인수 메탈 파티 드래곤" 이외의 "십이지 각인수" 카드 1장을 제외하고 발동할 수 있다.
PRINTL 이 카드를 패에서 특수 소환한다.
CALLFORM CARD_ABLE(2,필드起動,フリー誘発)
PRINTL ②: 자신의 메인 페이즈에 필드의 이 카드를 제외하고 발동할 수 있다.
PRINTL 레벨의 합계가 12 이하가 되도록,
PRINTL 자신의 제외 상태의 "각인수 메탈 파티 드래곤" 이외의 "십이지 각인수" 몬스터를 2장까지 선택하여 특수 소환한다.
PRINTL 이 효과로 특수 소환한 몬스터는, 이 턴 효과를 발동할 수 없다.
PRINTL (이 턴이 3턴째 이후라면, 이 효과는 상대 턴에도 발동할 수 있다.)


@CARDCAN_24163(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,24163)
SIF RESULT == 1
	RETURN 0


IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,24163)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,24163,"손패")
	SIF RESULT == 1
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0

	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 십이지각수
	효과条件固名前 = 각인수 메탈 파티 드래곤
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 발동済_2(듀얼리스트,24163)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲제외 = 1
	효과条件固名前 = 각인수 메탈 파티 드래곤
	효과条件カテゴリ = 십이지각수
	효과条件종류 = レベル持ち
	CALL 효과条件レベル_ADV(12,"以下")
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	IF ターン数 < 3
		SIF 종류 == フリー誘発
			RETURN 0
		SIF メインフェイズ != 1
			RETURN 0
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0
	ENDIF
	RETURN 1
ENDIF


@CARDEFFECT_24163(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 条件달성
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カードB
#DIM DYNAMIC 選択カードID
#DIMS DYNAMIC 選択カード名前
#DIM DYNAMIC 소환済レベル

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24163,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 십이지각수
		효과条件固名前 = 각인수 메탈 파티 드래곤
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"덱",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
			SS소환可能속성 = 어둠속성
			CALL CARD_SS_RESTRICTION(듀얼리스트)
		ENDIF
	ENDIF
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24163,2)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,제외行き)
		소환済レベル = 12
		REPEAT 2
			SIF モンスター数(듀얼리스트) >= 5
				BREAK
			CALL FULLRESET
			효과範囲제외 = 1
			효과条件カテゴリ = 십이지각수
			효과条件固名前 = 각인수 메탈 파티 드래곤
			효과条件종류 = レベル持ち
			CALL 효과条件レベル_ADV(소환済レベル, "以下")
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			SIF RESULT == 1
				CALL SET_FIELD_INFO(듀얼리스트,"필드",カード소환先,"発無",1)
			IF RESULT == 0
				BREAK
			ELSE
				CALLFORM CARD_{RESULT:1}, "レベル"
				소환済レベル -= RESULT
			ENDIF
		REND
	ENDIF
ENDIF



