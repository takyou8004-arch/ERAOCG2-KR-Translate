@CARDNAME_20073,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 상념의 신기 헤니르
	CASE "略称"
		RESULTS = 헤니르
	CASE "カテゴリ"
		RESULTS:0 = 제네시스
		RESULTS:1 = 신기
		RESULTS:4 = 뱅가드
ENDSELECT


@CARD_20073(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
	
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 천사족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1400
	CASE "守備力"
		RETURN 1400
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_20073(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드의 ①② 효과는 1턴에 각각 1번만 사용할 수 있다.
CALL CARD_ABLE(1,필드起動)
PRINTL ①: 이 카드가 소환 / 특수 소환했을 경우에 발동할 수 있다.
PRINTL 자신의 손패를 1장 골라 제외하고,
PRINTL 덱에서 "신기" 마법 / 함정 카드 1장을 자신 필드에 세트한다.
CALL CARD_ABLE(2,제외時)
PRINTL ②: 이 카드가 제외되었을 경우에 발동할 수 있다.
PRINTL 다음 턴의 스탠바이 페이즈에, 제외 상태의 이 카드를 특수 소환한다.

@CARDCAN_20073(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,20073)
SIF RESULT == 1
	RETURN 0

IF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,20073)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	SIF 손패数(듀얼리스트) < 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 신기
	효과条件종류 = 마함全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 제외時
	SIF 발동済_2(듀얼리스트,20073)
		RETURN 0
	RETURN 1	
ELSEIF 종류 == ターン開始時予約
	CALL CARD_SS_CHECK(듀얼리스트,20073,"제외")
	SIF RESULT == 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_20073(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カード

IF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,20073,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 신기
			효과条件종류 = 마함全般
			CALL CARD_SET_BASIC(듀얼리스트,"덱",ゾーン,場所,"使化裏側표시")
		ENDIF
	ENDIF
ELSEIF 종류 == 제외時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,20073,2)
	IF RESULT == 0
		CALL CARD_RESERV(듀얼리스트,20073,スタン予約)
	ENDIF
ELSEIF 종류 == ターン開始時予約
	CALL FULLRESET
	효과範囲제외 = 1
	효과条件名前 = 상념의 신기 헤니르
	CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
ENDIF

