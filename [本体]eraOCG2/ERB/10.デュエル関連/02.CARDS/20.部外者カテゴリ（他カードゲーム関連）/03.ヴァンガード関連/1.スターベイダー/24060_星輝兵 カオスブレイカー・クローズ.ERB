@CARDNAME_24060,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 성휘병 카오스 브레이커 클로즈
	CASE "略称"
		RESULTS = 카오클로
	CASE "カテゴリ"
		RESULTS:0 = 카오스 브레이커
		RESULTS:1 = 성휘병
		RESULTS:2 = 링크 조커
		RESULTS:4 = 뱅가드
ENDSELECT


@CARD_24060(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 기계족 
	CASE "レベル"
		RETURN 6
	CASE "攻撃力"
		RETURN 1800
	CASE "守備力"
		RETURN 2400
	CASE "レア度"
		RETURN SPR

ENDSELECT

RETURN 0

@CARD_EXPLANATION_24060(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①：자신 메인 페이즈에 발동할 수 있다.
PRINTL 패의 이 카드를 묘지로 보내고, 덱에서 "성휘병" 마법 / 함정 카드 1장을 패에 더한다.
PRINTL 이 효과를 발동한 후, 턴 종료 시까지 자신은 "링크 조커" 몬스터밖에 특수 소환할 수 없다.
CALLFORM CARD_ABLE(2,제외時)
PRINTL ②：자신 필드에 "성휘병" 초차원 몬스터가 존재하고, 이 카드가 제외된 경우에 발동할 수 있다.
PRINTL 상대 필드의 주박 상태의 몬스터 1장을 골라 파괴하고, 덱에서 1장 드로우한다.
CALLFORM CARD_ABLE(3,묘지起動)
PRINTL ③：이 카드가 묘지에 존재할 경우에 발동할 수 있다.
PRINTL 자신의 묘지에서 다른 빛 속성 "성휘병" 몬스터 1장을 제외하고,
PRINTL 이 카드를 특수 소환한다.


@CARDCAN_24060(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 条件달성


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,24060)
SIF RESULT == 1
	RETURN 0


IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,24060)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종류 = 마함全般
	효과条件カテゴリ = 성휘병
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 지속효과
	SIF ゾーン != "필드"
		RETURN 0
	RETURN 1
ELSEIF 종류 == 攻撃時強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件표시 = 裏側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT == 1
		RETURN 0

	TRYCALLFORM CARDNAME_{戦闘カードID(듀얼리스트)}("カテゴリ")
	IF !MATCH(RESULTS,"링크 조커")
		RETURN 0
	ENDIF
	RETURN 1
ELSEIF 종류 == 제외時
	SIF 발동済_2(듀얼리스트,24060)
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = 초차원
	효과条件カテゴリ = 성휘병
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件표시 = 裏側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動
	SIF 발동済_3(듀얼리스트,24060)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,24060,"묘지")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件속성 = 빛속성
	효과条件カテゴリ = 성휘병
	효과条件종류 = モン全般
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_24060(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 条件カード数

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24060,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件종류 = 마함全般
		효과条件カテゴリ = 성휘병
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		CALL CARD_SS_RESTRICTION(듀얼리스트,"링크 조커")
	ENDIF
ELSEIF 종류 == 제외時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24060,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件표시 = 裏側표시
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"묘지",50760)
		IF RESULT == 1
			CALL CARDDRAW_BASIC(듀얼리스트,1)
		ENDIF
	ENDIF
ELSEIF 종류 == 지속효과
	CALL FULLRESET
	효과条件표시 = 裏側표시
	CALL STATCHANGE_EFFECT(듀얼리스트,"상대필드",場所,"攻避",3)

	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件표시 = 表側표시
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	IF RESULT == 0
		CALL STATCHANGE_EFFECT(듀얼리스트,"自身",場所,"直攻")
	ENDIF
ELSEIF 종류 == 攻撃時強制誘発
	CALL SET_PLAYER_INFO(듀얼리스트,"ダメージ半化",3,1)
ELSEIF 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24060,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件속성 = 빛속성
		효과条件カテゴリ = 성휘병
		효과条件종류 = モン全般
		효과条件他所 = 1
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF
ENDIF

