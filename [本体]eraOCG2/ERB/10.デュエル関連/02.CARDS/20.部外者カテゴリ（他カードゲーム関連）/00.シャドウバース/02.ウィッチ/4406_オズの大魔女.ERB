@CARDNAME_4406,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 오즈의 대마녀
	CASE "略称"
		RESULTS = 오즈의 대마녀
	CASE "カテゴリ"
		RESULTS:0 = 위치 클래스
		RESULTS:4 = Shadowverse
ENDSELECT


@CARD_4406(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 땅속성
	CASE "종족"
		RETURN 미확인족
	CASE "レベル"
		RETURN 5
	CASE "攻撃力"
		RETURN 1700
	CASE "守備力"
		RETURN 2100
	CASE "성별"
		RETURN 牝性
	CASE "추가日"
		RETURN 250428
ENDSELECT

RETURN 0

@CARD_EXPLANATION_4406(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①② 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 이 카드는 패의 "위치 클래스" 카드 1장을 버리고, 패에서 특수 소환할 수 있다.
CALLFORM CARD_ABLE(2,소환時,특수소환時)
PRINTL ②: 이 카드가 소환 / 특수소환했을 경우에 발동할 수 있다.
PRINTL "오즈의 대마녀"를 제외한 "위치 클래스" 카드 2장을 덱에서 패에 넣는다. (같은 이름의 카드는 1장까지)
PRINTL 이 효과를 발동한 턴의 종료 시, 자신은 패의 마법 카드 1장을 버린다.

@CARDCAN_4406(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,4406)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,4406)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,4406,"손패")
	SIF RESULT == 1
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件他所 = 1
	효과条件カテゴリ = 위치 클래스
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,4406)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件固ID = 4406
	효과条件カテゴリ = 위치 클래스
	효과条件固名前起動 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT < 2
		RETURN 0
	RETURN 1
ELSEIF 종류 == ターン終了時強制
	SIF 발동済_2(듀얼리스트,4406) != 1
		RETURN 0
	SIF 발동済_3(듀얼리스트,4406)
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종류 = 마법全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_4406(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 戦闘カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4406,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件他所 = 1
		효과条件カテゴリ = 위치 클래스
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,"손패",場所)			
		ENDIF
	ENDIF
ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,4406,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件固ID = 4406
		효과条件カテゴリ = 위치 클래스
		효과条件固名前選択 = 1
		효과条件複数 = 2
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		;SIF RESULT == 1
		;	CALL CARD_RESERV(듀얼리스트,4406,エンド予約)
	ENDIF
ELSEIF 종류 == ターン終了時強制
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종류 = 마법全般
	CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
	SIF RESULT == 1
		CALL CARD_EFFECT_USE(듀얼리스트,4406,3)
ENDIF
