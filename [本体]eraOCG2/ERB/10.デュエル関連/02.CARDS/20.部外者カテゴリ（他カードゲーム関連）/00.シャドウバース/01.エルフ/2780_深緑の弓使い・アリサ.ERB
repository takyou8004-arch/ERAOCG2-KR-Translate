@CARDNAME_2780,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 심록의 궁수 아리사
	CASE "略称"
RESULTS = 아리사
	CASE "カテゴリ"
RESULTS:0 = 엘프 클래스
RESULTS:4 = Shadowverse
ENDSELECT


@CARD_2780(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 효과モン
	CASE "속성"
		RETURN 바람속성
	CASE "종족"
		RETURN 사이킥족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 1200
	CASE "守備力"
		RETURN 1600
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_2780(종류)
#DIM DYNAMIC 종류
PRINTL 이 카드명의 ①의 효과는 1턴에 1번밖에 발동할 수 없다.
CALLFORM CARD_ABLE(1,소환時,특수소환時)
PRINTL ①: 이 카드가 소환 / 특수 소환했을 경우, 이 턴 중에 자신이 2회 이상 카드의 효과를 발동하고 있었다면 발동할 수 있다.
PRINTL 덱에서 "엘프 클래스" 카드 1장을 패에 추가한다.
PRINTL 그 후, 수비력이 자신의 패 × 500 이하인 상대 몬스터 1장을 파괴한다.
CALLFORM CARD_ABLE(1,필드起動)
PRINTL ②: 이 카드는, 자신이 4회 이상 카드의 효과를 발동한 자신의 메인 페이즈에 1번만 "진화"할 수 있다.
PRINTL 그렇게 했을 경우, 이 카드의 공격력 / 수비력은 1000 상승하고, 패에 "스톰 애로우"를 추가한다.
PRINTL "진화(각성)"은 1턴에 1번밖에 할 수 없다.
PRINTL 이 듀얼 중에 "자신의 몬스터가 진화(각성)한 횟수"가 3회 이상이라면,
PRINTL 이 카드의 "진화(각성)"은 1턴에 1번밖에 할 수 없는 제한을 받지 않는다.
IF ターンプレイヤー == "자신"
	PRINTFORML （현재의 ターンプレイヤーが발동した효과の数は{자신_발동数}回）
ELSE
	PRINTFORML （현재의 ターンプレイヤーが발동した효과の数は{상대_발동数}回）
ENDIF
IF ターンプレイヤー == "자신"
	PRINTFORML （현재의 ターンプレイヤーが진화(覚醒)した数は{자신_覚醒횟수}回）
ELSE
	PRINTFORML （현재의 ターンプレイヤーが진화(覚醒)した数は{상대_覚醒횟수}回）
ENDIF



@CARDCAN_2780(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,2780)
SIF RESULT == 1
	RETURN 0

IF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,2780)
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_발동数") < 2
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 엘프 클래스
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF GETVAR(@"%듀얼리스트%_MZ_条達:場所") != 0
		RETURN 0
	IF 覚醒횟수(듀얼리스트) < 3
		SIF 발동済_1(듀얼리스트,覚醒발동)
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_발동数") < 4
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_2780(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 守備力修正
IF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,2780,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 엘프 클래스
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲상대필드 = 1
		효과条件표시 = 表側표시
		효과条件除去種 = 묘지
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲손패 = 1
			CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
			守備力修正 = RESULT * 500
			CALL FULLRESET
			효과条件표시 = 表側표시
			효과条件守備力 = 守備力修正
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"묘지")
		ENDIF
	ENDIF
ELSEIF 종류 == 필드起動
	IF 覚醒횟수(듀얼리스트) >= 3
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,覚醒발동)
	ELSE
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,覚醒발동,1)
	ENDIF
	IF RESULT == 0
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"条達",2,1)
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"永攻",1000)
		CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"永守",1000)
		CALL CARD_GENERATE_HAND(듀얼리스트,202780)
	ENDIF
ENDIF

;――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――――


@CARDNAME_202780,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 스톰 애로우
	CASE "略称"
RESULTS = 스톰 애로우
	CASE "カテゴリ"
RESULTS:0 = 엘프 클래스
ENDSELECT


@CARD_202780(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "외부자"
		RETURN 1
	CASE "종류"

		RETURN 通常마법
	CASE "トークン"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_202780(종류)
#DIM DYNAMIC 종류
PRINTL 이 카드명의 카드는 듀얼 중 1장밖에 발동할 수 없다.
CALLFORM CARD_ABLE(20,마함起動 ,손패起動)
PRINTL ①: 자신 필드의 카드 전부를 패에 추가한다.
PRINTL 그 후, 상대에게 [자신의 패의 매수 + 1] × 400 포인트의 데미지를 준다.

@CARDCAN_202780(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,202780)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 || 종류 == 손패起動
	IF ゾーン == "손패"
		SIF ターンプレイヤー != 듀얼리스트
			RETURN 0		
	ELSEIF ゾーン == "마함"
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 3
			RETURN 0
	ELSE
		RETURN 0
	ENDIF
	SIF 발동済_D1(듀얼리스트,202780)
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_202780(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC ダメージ量

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,202780,20)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		FOR カウンタ,0,5
			SIF GETVAR(@"%듀얼리스트%_필드:カウンタ") == -1
				CONTINUE
			CALL CARD_DISPOSE(듀얼리스트,"필드",カウンタ,손패行き)
		NEXT
		FOR カウンタ,0,5
			SIF GETVAR(@"%듀얼리스트%_마함:カウンタ") == -1
				CONTINUE
			CALL CARD_DISPOSE(듀얼리스트,"마함",カウンタ,손패行き)
		NEXT
		SETCOLOR C_WIND_T
		PRINTFORML >>——MESSAGE:——————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————————>>>
		PRINTL
		PRINTFORML 「この바람に──
		PRINTFORML 　　　　　　　　　願いを乗せて！」
		PRINTL
		CUSTOMDRAWLINE ―
		RESETCOLOR
		WAIT
		ダメージ量 = 400 * (손패数(듀얼리스트) + 1)
		CALL LIFE_CHANGE(対面者,"減少","효과",ダメージ量)
	ENDIF
ENDIF
