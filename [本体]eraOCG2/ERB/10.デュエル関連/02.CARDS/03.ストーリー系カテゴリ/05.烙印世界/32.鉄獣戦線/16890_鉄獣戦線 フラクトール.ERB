@CARDNAME_26520,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 트라이브리게이드 프랙탈
	CASE "略称"
		RESULTS = 철수 프랙탈
	CASE "カテゴリ"
		RESULTS:0 = 트라이브리게이드
ENDSELECT


@CARD_26520(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
	
		RETURN 효과モン
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 야수족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1900
	CASE "守備力"
		RETURN 1600
	CASE "성별"
		RETURN 雄性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_26520(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 패의 이 카드를 묘지로 보내고 발동할 수 있다.
PRINTL 덱에서 레벨 3 이하의 야수족/비행야수족 몬스터 1장을 묘지로 보낸다.
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ②: 자신 묘지에서 야수족/비행야수족 몬스터를 임의의 수만큼 제외하고 발동할 수 있다.
PRINTL 제외한 수와 같은 수의 링크 마커를 가지는 야수족/비행야수족 링크 몬스터 1장을 엑스트라 덱에서 특수 소환한다.
PRINTL 이 턴, 자신은 야수족/비행야수족 몬스터밖에 엑스트라 덱에서 특수 소환할 수 없다.
PRINTL 

@CARDCAN_26520(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 最大링크
#DIM DYNAMIC 条件달성
CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,26520)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,26520)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件종류 = モン全般
	효과条件종족:0 = 야수족
	효과条件종족:1 = 비행야수족
	CALL 효과条件レベル_ADV(3, "以下")
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,26520)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0


	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종족:0 = 야수족
	효과条件종족:1 = 비행야수족
	효과条件종류 = モン全般
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	最大링크 = RESULT

	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종족:0 = 야수족
	효과条件종족:1 = 비행야수족
	CALL 효과条件レベル_ADV(最大링크, "以下")
	효과条件소환可 = 1
	효과条件特不無視 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_26520(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 最大링크
#DIM DYNAMIC 選択링크値
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 選択カード2

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,26520,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과条件종류 = モン全般
		효과条件종족:0 = 야수족
		효과条件종족:1 = 비행야수족
		CALL 효과条件レベル_ADV(3, "以下")
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"덱",ゾーン,場所)
	ENDIF
ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,26520,2)
	IF RESULT == 0
		;묘지に存在する야수족/鳥獣の合計数が最大링크数
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件종족:0 = 야수족
		효과条件종족:1 = 비행야수족
		효과条件종류 = モン全般
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		最大링크 = RESULT
			
		CALL FULLRESET
		효과条件종족:0 = 야수족
		효과条件종족:1 = 비행야수족
		효과条件종류 = 링크
		CALL 효과条件レベル_ADV(最大링크, "以下")
		CALL CARD_CHOOSE_BASIC(듀얼리스트, "EX덱",ゾーン,場所)	
		選択カード = RESULT
		IF 選択カード != 9999
			CALLFORM CARD_{GETVAR(@"%듀얼리스트%_EX덱:選択カード")}, "レベル"
			選択링크値 = RESULT
			CALL FULLRESET
			효과条件종류 = モン全般
			효과条件종족:0 = 야수족
			효과条件종족:1 = 비행야수족
			효과条件複数 = 選択링크値
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"묘지",ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				효과範囲EX덱 = 1
				효과条件固定 = 選択カード
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			ENDIF	
		ENDIF
		EX소환可能종족:0 = 야수족
		EX소환可能종족:1 = 비행야수족
		CALL CARD_EX_RESTRICTION(듀얼리스트)
	ENDIF
ENDIF

