@CARDNAME_52401,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 스프리건즈 십 익스브로울러
	CASE "略称"
		RESULTS = 익스브로울러
	CASE "カテゴリ"
		RESULTS:0 = 스프리건즈
ENDSELECT


@CARD_52401(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 화염속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 8
	CASE "攻撃力"
		RETURN 1600
	CASE "守備力"
		RETURN 2500
ENDSELECT

RETURN 0

@CARDSUMMON_52401(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 5
素材条件レベル = 8
素材条件数自由 = 1

IF EX参照 == 1
	CALL XYZCAN(듀얼리스트,소환カード,52401) 
	RETURN RESULT
ENDIF



@CARD_EXPLANATION_52401(종류)
#DIM DYNAMIC 종류

PRINTL 레벨 8 몬스터 x 2장 이상
CALLFORM CARD_ABLE(0,필드起動)
PRINTL ①: 이 카드에 엑시즈 소재를 1개 제거하고 발동할 수 있다.
PRINTL 덱에서 "스프리건즈" 몬스터 1장을 고르고 묘지로 보내고,
PRINTL 상대 필드의 카드 1장을 고르고 파괴한다.
CALLFORM CARD_ABLE(0,발동誘発)
PRINTL ②: 상대가 마법 / 함정 / 몬스터의 효과를 발동했을 때에 발동할 수 있다.
PRINTL 이 카드는 소실한다.
PRINTL (소실한 몬스터는 제외되며, 필드에 빈 공간이 있을 경우 엔드 페이즈 시에 특수 소환된다)



@CARDCAN_52401(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,52401)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") <= -1
		RETURN 0
	SIF モンスター数(対面者) < 1 && 마함数(対面者) < 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 발동誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	CALL CARD_CHECK_START_DATA(듀얼리스트,1)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_52401(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC ターゲット
#DIM DYNAMIC 弾数
#DIM DYNAMIC 残弾

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 필드起動
	IF 듀얼리스트 == "자신"
		PRINTFORMW 発射オプション！
		PRINTFORMW ※フルバ-ストを押すと가능한 한 효과를 연속使用します！※
		CALL YN_CHOICE(듀얼리스트,"単発사격","フルバ-スト！タリホ－！")
	ELSE
		RESULT = はい
	ENDIF
	IF RESULT == はい
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52401,0,X消費1)
		IF RESULT == 0
			CALL FULLRESET
			효과条件カテゴリ = 스프리건즈
			효과条件종류 = モン全般
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"덱",ゾーン,場所)
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"묘지")
		ENDIF
	ELSEIF RESULT == いいえ
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52401)
		IF RESULT == 0
			ターゲット = モンスター数(対面者) + 마함数(対面者)
			弾数 = 0
			残弾 = 0
			WHILE GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") > -1
				CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"Ｘ",-1)
				弾数 = 弾数 + 1
			WEND

			IF ターゲット > 弾数
				ターゲット = 弾数
			ELSEIF 弾数 > ターゲット
				残弾 = 弾数 - ターゲット
			ENDIF

			CALL FULLRESET
			효과条件複数 = 100 - ターゲット
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"全場単体",場所,"묘지")


			CALL FULLRESET
			효과条件カテゴリ = 스프리건즈
			효과条件종류 = モン全般
			효과条件複数 = ターゲット
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"덱",ゾーン,場所)


			REPEAT 残弾
				CALL SET_FIELD_INFO(듀얼리스트,"필드",場所,"Ｘ",+1)
			REND
			PRINTFORMW タリホ－！！！
		ENDIF
	ENDIF
ELSEIF 종류 == 발동誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,52401)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,제외行き,1)
		SIF 듀얼리스트 == "자신"
			PRINTFORMW エクスブロウラ-は危機を感じ取り砂中へ潜航した。
	ENDIF
ENDIF