@CARDNAME_1882,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 혁의 성녀 카르테시아
	CASE "略称"
RESULTS = 카르테시아
	CASE "カテゴリ"
RESULTS:0 = 에클레시아
RESULTS:1 = 알버스
ENDSELECT


@CARD_1882(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 마법사족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1500
	CASE "守備力"
		RETURN 1500
	CASE "튜너"
		RETURN 1
	CASE "성별"
		RETURN 牝性
ENDSELECT

RETURN 0

@CARD_EXPLANATION_1882(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②③ 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 자신 필드 또는 묘지에 "알버스의 낙윤"이 존재할 경우에 발동할 수 있다.
PRINTL 이 카드를 손패에서 특수 소환한다.
CALLFORM CARD_ABLE(2,필드起動,フリー誘発)
PRINTL ②: 자신 상대 메인 페이즈에 발동할 수 있다.
PRINTL 자신의 손패 / 필드의 몬스터를 융합 소재로 하고,
PRINTL 레벨 8 이상의 융합 몬스터 1체를 EX 덱에서 융합 소환한다.
CALLFORM CARD_ABLE(3,ターン終了時)
PRINTL ③: 엔드 페이즈에 발동할 수 있다.
PRINTL 묘지의 융합 몬스터 1체를 덱으로 되돌리고, 이 카드를 손패에 넣는다.



@CARDCAN_1882(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성
#DIM DYNAMIC カウンタ

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,1882)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,1882)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,1882,"손패")
	SIF RESULT == 1
		RETURN 0

	CALL FULLRESET
	효과範囲필드 = 1
	효과範囲묘지 = 1
	효과条件名前 = 알버스의 낙윤
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	SIF 발동済_2(듀얼리스트,1882)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0

	CALL RESETAREA_MAT
	素材範囲필드 = 1
	素材範囲손패 = 1
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件종류 = 융합
	CALL 효과条件レベル_ADV(8, "以上")
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == ターン終了時
	SIF 발동済_3(듀얼리스트,1882)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종류 = 융합
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_1882(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード


IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1882,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)
	ENDIF
ELSEIF 종류 == 필드起動 || 종류 == フリー誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1882,2)
	IF RESULT == 0
		CALL RESETAREA_MAT
		素材範囲필드 = 1
		素材範囲손패 = 1
		CALL FULLRESET
		효과条件종류 = 융합
		CALL 효과条件レベル_ADV(8, "以上")
		효과条件소환可 = 1	
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"EX덱",ゾーン,場所)
		選択カード = RESULT
		IF 選択カード != 9999
			TRYCALLFORM CARDSUMMON_{GETVAR(@"%듀얼리스트%_EX덱:{選択カード}")}(듀얼리스트,選択カード)
			CALL FUSSUMMON(듀얼리스트, 選択カード)
		ENDIF
	ENDIF	
ELSEIF 종류 == ターン終了時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1882,3)
	IF RESULT == 0
		CALL FULLRESET
		효과条件종류 = 융합
		효과条件行先플래그 = 덱行き
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"묘지",ゾーン,場所)
		IF RESULT == 1
			CALL CARD_DISPOSE(듀얼리스트,"묘지",場所,3)
		ENDIF	
	ENDIF	
ENDIF
