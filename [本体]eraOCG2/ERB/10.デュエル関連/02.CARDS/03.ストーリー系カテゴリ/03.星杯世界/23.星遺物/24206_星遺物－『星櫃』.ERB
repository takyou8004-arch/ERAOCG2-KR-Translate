@CARDNAME_24206,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 성유물－『성궤』
	CASE "略称"
RESULTS = 『성궤』
	CASE "カテゴリ"
RESULTS:0 = 성유물
RESULTS:1 = 수호룡
ENDSELECT


@CARD_24206(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 7
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 500
ENDSELECT

RETURN 0

@CARD_EXPLANATION_24206(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,효과後誘発)
PRINTL ①: 자신 필드의 링크 몬스터가 전투 / 효과로 파괴되었을 경우,
PRINTL 이 카드를 패에서 묘지로 보내고, 묘지의 링크 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 몬스터를 특수 소환한다.
CALLFORM CARD_ABLE(3,손패起動 ,필드起動)
PRINTL ②: 패/필드의 이 카드를 릴리스하고 발동할 수 있다.
PRINTL 패에서 "성유물－『성궤』" 이외의 "성유물" 몬스터 1장 또는 "수호룡" 몬스터 1장을 특수 소환한다.
CALLFORM CARD_ABLE(2,효과後誘発)
PRINTL ③: 상대가 몬스터를 특수 소환했을 경우에 발동할 수 있다.
PRINTL 덱에서 몬스터 1장을 묘지로 보낸다.

@CARDCAN_24206(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,24206)
SIF RESULT == 1
	RETURN 0

IF 종류 == 효과後誘発
	IF ゾーン == "손패"
		SIF 발동済_1(듀얼리스트,24206)
			RETURN 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件종류 = 링크
		효과条件소환可 = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
		CALL FULLRESET
		효과条件종류 = 링크
		CALL CARD_CHECK_FIN_DATA(듀얼리스트,"破壊全般")
		SIF RESULT != 1
			RETURN 0
		RETURN 1
	ELSEIF ゾーン == "필드"
		SIF 발동済_2(듀얼리스트,24206)
			RETURN 0
		CALL FULLRESET
		CALL CARD_CHECK_FIN_DATA(듀얼리스트,"특수소환",1)
		SIF RESULT != 1
			RETURN 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件종류 = モン全般
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		SIF RESULT != 1
			RETURN 0
		RETURN 1
	ELSE
		RETURN 0
	ENDIF

ELSEIF 종류 == 손패起動 || 종류 == 필드起動 
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF 발동済_3(듀얼리스트,24206)
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件종류 = モン全般
	효과条件カテゴリ:0 = 성유물
	효과条件カテゴリ:1 = 수호룡
	효과条件固名前 = 성유물－『성궤』
	효과条件소환可 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_24206(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

IF 종류 == 효과後誘発
	IF ゾーン == "손패"
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24206,1)
		IF RESULT == 0
			CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
			CALL FULLRESET
			효과範囲묘지 = 1
			효과条件종류 = 링크
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ELSEIF ゾーン == "필드"
		CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24206,2)
		IF RESULT == 0
			CALL FULLRESET
			효과条件종류 = モン全般
			CALL CARD_ADD_GY_BASIC(듀얼리스트,"덱",ゾーン,場所)
		ENDIF
	ENDIF
ELSEIF 종류 == 손패起動 || 종류 == 필드起動 
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24206,3)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲손패 = 1
		효과条件종류 = モン全般
		효과条件カテゴリ:0 = 성유물
		효과条件カテゴリ:1 = 수호룡
		효과条件固名前 = 성유물－『성궤』
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所,"守備표시")
	ENDIF
ENDIF
