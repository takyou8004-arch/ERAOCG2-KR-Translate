@CARDNAME_28172,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 인벨즈를 부르는 자
	CASE "略称"
RESULTS = 침략자를부르는자
	CASE "カテゴリ"
RESULTS:0 = 인벨즈
RESULTS:1 = 벨즈
ENDSELECT


@CARD_28172(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 악마족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1700
	CASE "守備力"
		RETURN 0
ENDSELECT

RETURN 0

@CARD_EXPLANATION_28172(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 이 카드를 패에서 버리고 발동할 수 있다.
PRINTL 덱에서 "침략의" 마법 / 함정 카드 1장을 패에 넣는다.
CALLFORM CARD_ABLE(2,戦破壊→묘지)
PRINTL ②: 이 카드가 필드에서 묘지로 보내졌을 경우에 발동할 수 있다.
PRINTL 덱에서 "벨즈" 몬스터 1장을 패에 넣는다.
PRINTL 이 효과로 "인벨즈" 몬스터를 패에 넣었을 경우,
PRINTL 패에서 레벨 4 이하의 "벨즈" 몬스터 1장을 특수 소환할 수 있다.

@CARDCAN_28172(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,28172)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,28172)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 침략의
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 戦破壊→묘지
	SIF !GROUPMATCH(묘지送り종류,"リリース","破壊","戦闘破壊")
		RETURN 0
	SIF 발동済_2(듀얼리스트,28172)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 벨즈
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_28172(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,28172,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 침략의
		CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
	ENDIF
ELSEIF 종류 == 戦破壊→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,28172,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 벨즈
		CALL CARD_ADD_BASIC(듀얼리스트,"덱",ゾーン,場所)
		IF RESULT == 1
			TRYCALLFORM CARDNAME_{RESULT:1},"カテゴリ"
			IF MATCH(RESULTS,"인벨즈")
				CALL FULLRESET
				효과範囲손패 = 1
				효과条件カテゴリ = 벨즈
				효과条件任意 = 1
				CALL 효과条件レベル_ADV(4, "以下")
				CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
			ENDIF
		ENDIF
	ENDIF
ENDIF

