@CARDNAME_61821,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 령수의 계성
	CASE "略称"
RESULTS = 령수의 계성
	CASE "カテゴリ"
RESULTS:0 = 령수
ENDSELECT


@CARD_61821(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"

		RETURN 지속마법
ENDSELECT

RETURN 0

@CARD_EXPLANATION_61821(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드 명의 ②③ 효과는 각각 1턴에 1번만 사용할 수 있다.
CALLFORM CARD_ABLE(0,攻守変動)
PRINTL ①: 상대 필드의 몬스터의 공격력은, 자신 필드의 「령수」 몬스터의 종류 × 200 다운한다.
CALLFORM CARD_ABLE(2,마함起動)
PRINTL ②: 손패의 「령수」 몬스터 1장을 상대에게 보여주고 발동할 수 있다.
PRINTL 그 몬스터와는 이름이 다른 「령수」 몬스터 1장을 덱에서 손패에 넣는다.
PRINTL 그 후, 자신의 손패를 1장 고르고 버린다.
CALLFORM CARD_ABLE(3,효과後誘発)
PRINTL ③: 자신 필드에 「령수」 몬스터가 특수 소환되었을 경우에,
PRINTL 필드의 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 몬스터의 표시 형식을 변경한다.


@CARDCAN_61821(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,61821)
SIF RESULT == 1
	RETURN 0

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	SIF 발동済_2(듀얼리스트,61821)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件カテゴリ = 령수
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 령수
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0

	RETURN 1
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	IF 종류 == 마함起動
		SIF GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
			RETURN 0
	ENDIF
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	RETURN 1
ELSEIF 종류 == 효과後誘発
	SIF 발동済_3(듀얼리스트,61821)
		RETURN 0
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2
		RETURN 0

	CALL FULLRESET
	효과条件カテゴリ = 령수
	CALL CARD_CHECK_FIN_DATA(듀얼리스트,"특수소환")
	SIF RESULT != 1
		RETURN 0

	RETURN 1
ELSEIF 종류 == 攻守変動
	SIF ゾーン != "마함"
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_魔_表:場所") >= 2	
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_61821(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 속성数
#DIMS DYNAMIC 選択カード名前

IF 종류 == 마함起動 && GETVAR(@"%듀얼리스트%_魔_表:場所") == 1
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,61821,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 령수
		효과条件종류 = モン全般
		CALL CARD_CHOOSE_BASIC(듀얼리스트,"손패",ゾーン,場所)
		IF RESULT != 9999
			TRYCALLFORM CARDNAME_{RESULT:99},"名前"
			選択カード名前 = %RESULTS%
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 령수
			효과条件종류 = モン全般
			효과条件固名前 = %選択カード名前%
			CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
			IF RESULT == 1
				CALL FULLRESET
				효과範囲손패 = 1
				CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
			ENDIF
		ENDIF
	ENDIF
ELSEIF 종류 == 마함起動 || 종류 == 손패起動
	;カード발동＆설치まで
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,61821)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所,마함行き)
	ENDIF

ELSEIF 종류 == 효과後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,61821,3)
	IF RESULT == 0
		CALL FULLRESET
		효과추가종류 = 3
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"両필드単体",場所,"표시변경")
	ENDIF
ELSEIF 종류 == 攻守変動
	;攻撃UP
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = 령수
	효과条件종류 = モン全般
	효과条件固名前起動 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	攻撃力修正 = 200 * RESULT

	CALL FULLRESET
	CALL STATCHANGE_BASIC(듀얼리스트,"상대필드",場所,-攻撃力修正,0,61821)
ENDIF
