@CARDNAME_50170,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 테라나이트 세이크리드 카두케우스
	CASE "略称"
		RESULTS = TKC카두케우스
	CASE "カテゴリ"
		RESULTS:0 = 세이크리드
		RESULTS:1 = 테라나이트
ENDSELECT


@CARD_50170(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 엑시즈
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 마법사족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 2500
	CASE "守備力"
		RETURN 1650
ENDSELECT

RETURN 0

@CARDSUMMON_50170(듀얼리스트,소환カード,EX参照)
#DIMS DYNAMIC 듀얼리스트
#DIM DYNAMIC 소환カード
#DIM DYNAMIC EX参照
#DIM DYNAMIC カウンタ

CALL RESETCONDITION_MAT
必要素材数 = 99
素材条件レベル = 4
素材条件数自由 = 1
지정素材名前 = 세이크리드 카두케우스

IF EX参照 == 1
	CALL XYZCAN(듀얼리스트,소환カード,50170) 
	RETURN RESULT
ENDIF


@CARD_EXPLANATION_50170(종류)
#DIM DYNAMIC 종류

PRINTL 레벨 4 몬스터 × 2장 이상
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,EX소환時)
PRINTL ①: 이 카드가 엑시즈 소환에 성공했을 경우,
PRINTL 자신 묘지의 "테라나이트", "세이크리드" 카드를 각각 1장까지 대상으로 하여 발동할 수 있다.
PRINTL 그 카드를 패에 넣는다.
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ②: 이 카드의 엑시즈 소재를 1개 제거하고 발동할 수 있다.
PRINTL 패 / 덱에서 "테라나이트", "세이크리드" 몬스터 1장을 제외하고,
PRINTL 이 효과는, 제외한 그 몬스터의 소환 성공 시에 발동하는 효과와 같아진다.


@CARDCAN_50170(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 必要カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,50170)
SIF RESULT == 1
	RETURN 0

IF 종류 == EX소환時
	SIF 발동済_1(듀얼리스트,50170)
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件カテゴリ:0 = 세이크리드
	효과条件カテゴリ:1 = 테라나이트
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,50170)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_Ｘ:場所") <= -1
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲덱 = 1
	효과条件カテゴリ:0 = 세이크리드
	효과条件カテゴリ:1 = 테라나이트
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻撃時誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_MZ_条達:場所") == 0
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	SIF GETVAR(@"%対面者%_MZ_属:戦闘場所(듀얼리스트,1)") != 어둠속성 && GETVAR(@"%対面者%_MZ_属:戦闘場所(듀얼리스트,1)") != 빛속성
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_50170(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == EX소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50170,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 세이크리드
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)

		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件カテゴリ = 테라나이트
		CALL CARD_ADD_ADV(듀얼리스트,ゾーン,場所)
	ENDIF

ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50170,2,X消費1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲덱 = 1
		효과条件カテゴリ:0 = 세이크리드
		효과条件カテゴリ:1 = 테라나이트
		효과条件종류 = モン全般
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALLFORM CARDEFFECT_{RESULT:1}(듀얼리스트,소환時,"효과処理")
		ENDIF
	ENDIF

ELSEIF 종류 == 攻撃時誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,50170)
	IF RESULT == 0
		選択カード = 戦闘場所(듀얼리스트,1)
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",選択カード,"제외")
	ENDIF
ENDIF