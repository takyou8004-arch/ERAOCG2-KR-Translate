@CARDNAME_24252,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = Ａ Ｏ Ｊ 코어 디스트로이
	CASE "略称"
RESULTS = 코어 디스트로이
	CASE "カテゴリ"
RESULTS:0 = Ａ Ｏ Ｊ
ENDSELECT


@CARD_24252(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 기계족
	CASE "レベル"
		RETURN 3
	CASE "攻撃力"
		RETURN 1200
	CASE "守備力"
		RETURN 200
	CASE "튜너"
		RETURN 1
ENDSELECT

RETURN 0

@CARD_EXPLANATION_24252(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①② 효과는 각각 1턴에 1번만 사용할 수 있다.
CALLFORM CARD_ABLE(1,손패起動 ,필드起動)
PRINTL ①: 자신 필드에 "Ａ Ｏ Ｊ" 몬스터가 존재할 경우, 패/필드의 이 카드를 릴리스하고 발동할 수 있다.
PRINTL 자신의 패/필드의 카드 1장을 묘지로 보내고, 엑스트라 덱에서 "Ａ Ｏ Ｊ 카타스톨" 1장을 특수 소환한다.
CALLFORM CARD_ABLE(2,소환時 ,특수소환時)
PRINTL ②: 자신 필드에 "Ａ Ｏ Ｊ" 싱크로 몬스터가 존재하고, 이 카드의 소환/특수 소환에 성공했을 경우에 발동할 수 있다.
PRINTL 자신은 덱에서 1장 드로우한다.
CALLFORM CARD_ABLE(0,攻撃時強制誘発)
PRINTL ③: 이 카드가 빛 속성 몬스터와 전투를 실행할 경우에 발동한다.
PRINTL 그 몬스터를 파괴한다.

@CARDCAN_24252(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,24252)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動 || 종류 == 필드起動
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0	
	SIF メインフェイズ != 1
		RETURN 0
	SIF 발동済_1(듀얼리스트,24252)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,52021,"EX덱",1)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件カテゴリ = Ａ Ｏ Ｊ
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲마함 = 1
	효과範囲필드 = 1
	효과条件他所 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과範囲EX덱 = 1
	효과条件名前 = Ａ Ｏ Ｊ 카타스톨
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_2(듀얼리스트,24252)
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = 싱크로
	효과条件カテゴリ = Ａ Ｏ Ｊ
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL CARD_DRAW_CHECK
	RETURN 1

ELSEIF 종류 == 攻撃時強制誘発
	SIF ゾーン != "필드"
		RETURN 0
	SIF 戦闘場所(듀얼리스트) != 場所
		RETURN 0
	SIF 戦闘場所(듀얼리스트,1) == 5
		RETURN 0
	TRYCALLFORM CARD_{戦闘カードID(듀얼리스트,1)}("속성")
	SIF !MATCH(RESULT,빛속성)
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_24252(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 破壊カード
#DIMS DYNAMIC 対面者
#DIM DYNAMIC 条件달성

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

IF 종류 == 손패起動 || 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24252,1)
	IF RESULT == 0
		CALL CARD_DISPOSE(듀얼리스트,ゾーン,場所)
		CALL FULLRESET
		IF モンスター数(듀얼리스트) < 5
			효과範囲손패 = 1
			효과範囲마함 = 1
		ENDIF
		효과範囲필드 = 1
		효과条件他所 = 1
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲EX덱 = 1
			효과条件名前 = Ａ Ｏ Ｊ 카타스톨
			CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF

ELSEIF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24252,2)
	IF RESULT == 0
		CALL CARDDRAW_BASIC(듀얼리스트,1)
	ENDIF

ELSEIF 종류 == 攻撃時強制誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,24252)
	IF RESULT == 0
		破壊カード = 戦闘場所(듀얼리스트,1)
		CALL FULLRESET
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",破壊カード,"묘지")
	ENDIF
ENDIF
