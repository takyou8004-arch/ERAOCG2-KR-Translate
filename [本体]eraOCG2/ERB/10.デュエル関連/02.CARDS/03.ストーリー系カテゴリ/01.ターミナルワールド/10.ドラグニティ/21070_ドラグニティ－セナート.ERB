@CARDNAME_21070,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 드래그니티-세나트
	CASE "略称"
		RESULTS = 세나트
	CASE "カテゴリ"
		RESULTS:0 = 드래그니티
ENDSELECT


@CARD_21070(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 바람속성
	CASE "종족"
		RETURN 비행야수족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1800
	CASE "守備力"
		RETURN 600
ENDSELECT

RETURN 0

@CARD_EXPLANATION_21070(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②의 효과는 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,필드起動)
PRINTL ①: 패에서 "드래그니티" 카드 1장을 버리고 발동할 수 있다.
PRINTL 덱에서 "드래그니티" 몬스터 1장을 장착 카드로서 이 카드에 장착한다.
CALLFORM CARD_ABLE(2,身代효과)
PRINTL ②: 자신 필드의 "드래그니티" 몬스터가 전투 / 효과로 파괴될 경우,
PRINTL 대신에 자신의 마법 & 함정 존의 "드래그니티" 카드 1장을 파괴할 수 있다.

@CARDCAN_21070(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,21070)
SIF RESULT == 1
	RETURN 0

IF 종류 == 필드起動
	SIF 발동済_1(듀얼리스트,21070)
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	CALL FULLRESET
	효과条件カテゴリ = 드래그니티
	효과範囲손패 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL FULLRESET
	효과条件カテゴリ = 드래그니티
	효과条件종류 = モン全般
	효과範囲덱 = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	CALL CARD_DRAW_CHECK(1)
	RETURN 1

ELSEIF 종류 == 身代효과
	SIF 발동済_2(듀얼리스트,21070)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF 破壊대상ゾーン != "필드"
		RETURN 0	
	TRYCALLFORM CARDNAME_{破壊대상ID}("カテゴリ")
	SIF !MATCH(RESULTS,"드래그니티")
		RETURN 0
	SIF !GROUPMATCH(破壊종류,"효과","戦闘")
		RETURN 0
	CALL FULLRESET
	효과範囲마함 = 1
	효과条件カテゴリ = 드래그니티
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_21070(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 효과先
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

IF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21070,1)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 드래그니티
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"손패",ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과範囲덱 = 1
			효과条件カテゴリ = 드래그니티
			CALL CARD_EQUIP_MONSTER_ADV(듀얼리스트,ゾーン,場所,,21070)
		ENDIF
	ENDIF

ELSEIF 종류 == 身代효과
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21070,2)
	IF RESULT == 0
		CALL FULLRESET
		효과条件カテゴリ = 드래그니티
		효과条件行先플래그 = 破壊行き
		CALL CARD_ADD_GY_BASIC(듀얼리스트,"마함",ゾーン,場所)
		IF RESULT == 1
			RETURN 1
		ENDIF
	ENDIF
	RETURN 0
ENDIF
