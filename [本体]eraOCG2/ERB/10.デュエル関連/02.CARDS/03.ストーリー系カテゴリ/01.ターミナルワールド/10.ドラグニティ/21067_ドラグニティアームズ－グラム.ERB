@CARDNAME_21067,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
		RESULTS = 드래그니티 암즈-그램
	CASE "略称"
		RESULTS = 그램
	CASE "カテゴリ"
		RESULTS:0 = 드래그니티
ENDSELECT


@CARD_21067(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 바람속성
	CASE "종족"
		RETURN 드래곤족
	CASE "レベル"
		RETURN 10
	CASE "攻撃力"
		RETURN 2900
	CASE "守備力"
		RETURN 2200
ENDSELECT

RETURN 0

@CARD_EXPLANATION_21067(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動,묘지起動)
PRINTL ①: 자신 묘지에서 드래곤족 비행야수족 몬스터 2장을 제외하고 발동할 수 있다.
PRINTL 이 카드를 패 / 묘지에서 특수 소환한다.
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ②: 상대 필드의 앞면 표시 몬스터 1장을 대상으로 하여 발동할 수 있다.
PRINTL 그 몬스터의 효과는 무효화되고, 그 공격력은 자신 마법 & 함정 존의 앞면 표시 카드의 수× 1000 내린다.
CALLFORM CARD_ABLE(3,攻撃後誘発)
PRINTL ③: 상대 필드의 몬스터가 전투로 파괴되었을 때 발동할 수 있다.
PRINTL 상대 묘지의 그 카드와 원래 카드명이 같은 카드를 장착 카드로 이 카드에 장착한다.

@CARDCAN_21067(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC 条件달성
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,21067)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動 || 종류 == 묘지起動
	SIF 발동済_1(듀얼리스트,21067)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	SIF GETVAR(@"%듀얼리스트%_제외NG置換") >= 1
		RETURN 0
	CALL FULLRESET
	효과範囲묘지 = 1
	효과条件종족:0 = 비행야수족
	효과条件종족:1 = 드래곤족
	효과条件他所 = 1
	효과条件CNT = 1
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT <= 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,21067)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲상대필드 = 1
	효과条件표시 = 表側표시
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1

ELSEIF 종류 == 攻撃後誘発
	SIF 발동済_3(듀얼리스트,21067)
		RETURN 0
	SIF ゾーン != "필드"
		RETURN 0
	SIF 마함数(듀얼리스트) >= 5
		RETURN 0
	SIF 直前戦闘破壊ID(듀얼리스트,1) == 0
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_21067(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 選択カード
#DIM DYNAMIC 攻撃力修正
#DIMS DYNAMIC 装備者
#DIM DYNAMIC 装備先
#DIMS DYNAMIC 対面者

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%

CALL 装備판정(듀얼리스트,場所,ゾーン)
装備者 = %RESULTS%
装備先 = RESULT

IF 종류 == 손패起動 || 종류 == 묘지起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21067,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件종족:0 = 비행야수족
		효과条件종족:1 = 드래곤족
		효과条件他所 = 1
		효과条件複数 = 2
		효과条件行先플래그 = 제외行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)	
		IF RESULT == 1
			CALL FULLRESET
			CALL CARD_SP_SUMMON(듀얼리스트,ゾーン,場所)
		ENDIF
	ENDIF

ELSEIF 종류 == 필드起動
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21067,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲마함 = 1
		효과条件표시 = 表側표시
		효과条件CNT = 1
		CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
		攻撃力修正 = RESULT * 1000

		CALL FULLRESET
		효과条件표시 = 表側표시
		CALL CARD_REMOVAL_BASIC(듀얼리스트,"필드単体",場所,"選択")
		IF RESULT == 1
			選択カード = RESULT:1
			CALL CARD_SELECT(듀얼리스트,選択カード)
			選択カード = RESULT
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",選択カード,"無効")
			CALL FULLRESET
			CALL CARD_REMOVAL_BASIC(듀얼리스트,"自動処理",選択カード,"永攻DOWN",攻撃力修正)
		ENDIF
	ENDIF

ELSEIF 종류 == 攻撃後誘発
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,21067,3)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲상대묘지 = 1
		CALLFORM CARDNAME_{直前戦闘破壊ID(듀얼리스트,1)}("名前")
		효과条件名前 = %RESULTS%
		CALL CARD_EQUIP_MONSTER_ADV(듀얼리스트,ゾーン,場所,,21067)
	ENDIF
ENDIF
