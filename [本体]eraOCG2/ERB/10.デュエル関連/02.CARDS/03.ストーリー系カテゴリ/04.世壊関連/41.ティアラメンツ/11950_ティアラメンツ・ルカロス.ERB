@CARDNAME_11950,参照先
;ココで지정카드의 名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 티아라멘츠 루카로스
	CASE "略称"
RESULTS = 루카로스
	CASE "カテゴリ"
RESULTS:0 = 티아라멘츠
RESULTS:1 = 게임 오리지널
ENDSELECT


@CARD_11950(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 어둠속성
	CASE "종족"
		RETURN 원소정령족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 1500
	CASE "守備力"
		RETURN 2100
	CASE "성별"
		RETURN 牝性
	CASE "制限"
		RETURN 制限
	CASE "別途ID"
		RETURN 31031
ENDSELECT
RETURN 0

@CARD_EXPLANATION_11950(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명은 룰 상 "티아라멘츠 레이노하트"로 취급한다.
PRINTL 이 카드명의 ①②의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,소환時,특수소환時)
PRINTL ①: 이 카드가 소환 / 특수 소환했을 경우에 발동할 수 있다.
PRINTL 덱에서 "티아라멘츠 루카로스" 이외의 "티아라멘츠" 몬스터 1장을 묘지로 보낸다.
CALLFORM CARD_ABLE(2,덱→묘지)
PRINTL ②: 이 카드가 덱에서 묘지로 보내졌을 경우에 발동할 수 있다.
PRINTL 묘지에서 "티아라멘츠 루카로스" 1장을 특수 소환하고,
PRINTL 패에서 "티아라멘츠" 카드 1장을 덱의 가장 위에 놓는다.
PRINTL 이 효과로 특수 소환한 카드는, 필드에서 벗어났을 경우에 제외된다.

@CARDSUMMON_AA_11950

@CARDCAN_11950(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,11950)
SIF RESULT == 1
	RETURN 0

IF 종류 == 소환時 || 종류 == 특수소환時
	SIF 발동済_1(듀얼리스트,31031)
		RETURN 0
	CALL FULLRESET
	효과範囲덱 = 1
	효과条件カテゴリ = 티아라멘츠
	효과条件종류 = モン全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 덱→묘지
	SIF 발동済_2(듀얼리스트,31031)
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,11950,"묘지")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과条件カテゴリ = 티아라멘츠
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF

@CARDEFFECT_11950(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正
#DIM DYNAMIC 選択カード
CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%



IF 종류 == 소환時 || 종류 == 특수소환時
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,11950,1)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲덱 = 1
		효과条件カテゴリ = 티아라멘츠
		효과条件종류 = モン全般
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
	ENDIF
ELSEIF 종류 == 덱→묘지
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,11950,2)
	IF RESULT == 0
		CALL FULLRESET
		효과範囲묘지 = 1
		효과条件名前 = 티아라멘츠 레이노하트
		효과추가종류 = 効追除処
		CALL CARD_SP_SUMMON_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과条件カテゴリ = 티아라멘츠
			CALL CARD_CHOOSE_BASIC(듀얼리스트,"손패",ゾーン,場所)
			選択カード = RESULT
			IF 選択カード != 9999
				CALL CARD_ADD_DECK_SPC(듀얼리스트,"손패",選択カード)
			ENDIF
		ENDIF
	ENDIF
ENDIF

