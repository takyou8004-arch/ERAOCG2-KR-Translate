@CARDNAME_1932,参照先
;ココで지정カードの名前、略称を返す予定
#DIMS DYNAMIC 参照先

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "名前"
RESULTS = 하얀 숲의 루시아
	CASE "略称"
RESULTS = 루시아
	CASE "カテゴリ"
RESULTS:0 = 하얀 숲
ENDSELECT


@CARD_1932(参照先)

#DIMS DYNAMIC 参照先
VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE "종류"
		RETURN 효과モン
	CASE "속성"
		RETURN 빛속성
	CASE "종족"
		RETURN 마법사족
	CASE "レベル"
		RETURN 4
	CASE "攻撃力"
		RETURN 800
	CASE "守備力"
		RETURN 1500
	CASE "튜너"
		RETURN 1
	CASE "성별"
		RETURN 牝性
ENDSELECT
RETURN 0

@CARD_EXPLANATION_1932(종류)
#DIM DYNAMIC 종류

PRINTL 이 카드명의 ①②③의 효과는 각각 1턴에 1번밖에 사용할 수 없다.
CALLFORM CARD_ABLE(1,손패起動)
PRINTL ①: 자신 필드에 「하얀 숲」 몬스터가 존재할 경우 발동할 수 있다.
PRINTL 이 카드를 손패에서 특수 소환한다.
CALLFORM CARD_ABLE(2,필드起動)
PRINTL ②: 자신의 손패/필드에서 마법/함정 카드 1장을 묘지로 보내고 발동할 수 있다.
PRINTL 자신은 1장 드로우한다.
CALLFORM CARD_ABLE(3,묘지起動,フリー誘発)
PRINTL ③: 자신/상대 턴에, 이 카드가 묘지에 존재할 경우 발동할 수 있다.
PRINTL 자신의 필드/묘지에서 「하얀 숲」 S 몬스터 1장을 EX 덱으로 되돌리고,
PRINTL 이 카드의 효과를 무효로 하고 특수 소환한다.


@CARDSUMMON_AA_1932


@CARDCAN_1932(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC EX플래그

CALL CARD_NEGATE(듀얼리스트,종류,ゾーン,場所,1932)
SIF RESULT == 1
	RETURN 0

IF 종류 == 손패起動
	SIF 발동済_1(듀얼리스트,1932)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,1932,"손패")
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	효과範囲필드 = 1
	효과条件종류 = モン全般
	효과条件カテゴリ = 하얀 숲
	효과条件표시 = 0
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 필드起動
	SIF 발동済_2(듀얼리스트,1932)
		RETURN 0
	SIF メインフェイズ != 1
		RETURN 0
	SIF ターンプレイヤー != 듀얼리스트
		RETURN 0
	CALL FULLRESET
	효과範囲손패 = 1
	효과範囲마함 = 1
	효과範囲필드 = 1
	효과条件종류 = 마함全般
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ELSEIF 종류 == 묘지起動 || 종류 == フリー誘発
	SIF 발동済_3(듀얼리스트,1932)
		RETURN 0
	SIF ゾーン != "묘지"
		RETURN 0
	CALL CARD_SS_CHECK(듀얼리스트,1932,"묘지",EX플래그)
	SIF RESULT == 1
		RETURN 0
	CALL FULLRESET
	SIF 듀얼리스트 == "자신"
		효과範囲필드 = 1
	효과範囲묘지 = 1
	효과条件カテゴリ = 하얀 숲
	효과条件종류 = 싱크로
	CALL CARD_CHECK_ADV(듀얼리스트,종류,ゾーン,場所)
	SIF RESULT != 1
		RETURN 0
	RETURN 1
ENDIF



@CARDEFFECT_1932(듀얼리스트,종류,ゾーン,場所)
#DIMS DYNAMIC 듀얼리스트
#DIMS DYNAMIC 対面者 
#DIMS DYNAMIC ゾーン
#DIM DYNAMIC 종류
#DIM DYNAMIC 場所
#DIM DYNAMIC カウンタ
#DIM DYNAMIC 攻撃力修正

CALL 対面者판정(듀얼리스트)
対面者 = %RESULTS%


IF 종류 == 손패起動
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1932,1)
	IF RESULT == 0
		CALL FULLRESET
		CALL CARD_SP_SUMMON(듀얼리스트, "손패", 場所)
	ENDIF
ELSEIF 종류 == 필드起動
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1932,2)
	IF RESULT == 0
		;손패、場の마함を묘지に
		CALL FULLRESET
		효과範囲손패 = 1
		효과範囲마함 = 1
		효과範囲필드 = 1
		효과条件종류 = 마함全般
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		SIF RESULT == 1
			CALL CARDDRAW_BASIC(듀얼리스트,1)
	ENDIF
ELSEIF 종류 == 묘지起動 || 종류 == フリー誘発
	;カード발동確認
	CALL CARD_CHOICE(듀얼리스트,종류,ゾーン,場所,1932,3)
	IF RESULT == 0
		CALL FULLRESET
		SIF 듀얼리스트 == "자신"
			효과範囲필드 = 1
		효과範囲묘지 = 1
		효과条件カテゴリ = 하얀 숲
		효과条件종류 = 싱크로
		효과条件行先플래그 = EX덱行き
		CALL CARD_ADD_GY_ADV(듀얼리스트,ゾーン,場所)
		IF RESULT == 1
			CALL FULLRESET
			효과추가종류 = 効追無効
			CALL CARD_SP_SUMMON(듀얼리스트,"묘지",場所)
		ENDIF
	ENDIF
ENDIF
