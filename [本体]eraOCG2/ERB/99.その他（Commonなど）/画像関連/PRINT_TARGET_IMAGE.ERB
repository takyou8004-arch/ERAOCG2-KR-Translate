@SHOW_TARGET_IMAGE(캐릭터_ID,SPR値,isSquare=0)
	#DIM DYNAMIC 캐릭터_ID
	#DIM DYNAMIC SPR値
	#DIM SUCCESS
	#DIM DYNAMIC SRC_HEIGHT
	#DIM DYNAMIC SRC_WIDTH
	#DIM isSquare
	SUCCESS = 0
	DEBUGPRINTFORML 캐릭터_ID {캐릭터_ID}, SPR値 {SPR値}
	GDISPOSE SPR値

	IF CFLAG:TARGET:얼굴화상표시 == 1
		CALL SHOW_TARGET_IMAGE_FACE(캐릭터_ID,SPR値)
		IF RESULT == 1
			DEBUGPRINTFORML SHOW_TARGET_IMAGE_FACE
			SUCCESS = 1
			GOTO END_OF_GET_IMAGE
		ENDIF
	ENDIF
	; defaultRatio 832:1216
	; TRY FROM UFUFU/_BODY
	CALL SHOW_TARGET_IMAGE_BODY(캐릭터_ID,SPR値)
	IF RESULT == 1
		DEBUGPRINTFORML SHOW_TARGET_IMAGE_BODY
		SUCCESS = 1
		GOTO END_OF_GET_IMAGE
	ENDIF

	; TRY FROM CARD
	CALL SHOW_TARGET_IMAGE_CARD(캐릭터_ID,SPR値)
	IF RESULT == 1
		DEBUGPRINTFORML SHOW_TARGET_IMAGE_CARD
		SUCCESS = 1
		GOTO END_OF_GET_IMAGE
	ENDIF

	; TRY FROM FACE
	CALL SHOW_TARGET_IMAGE_FACE(캐릭터_ID,SPR値)
	IF RESULT == 1
		DEBUGPRINTFORML SHOW_TARGET_IMAGE_FACE
		SUCCESS = 1
		GOTO END_OF_GET_IMAGE
	ENDIF

	$END_OF_GET_IMAGE

	IF !SUCCESS
		; FAILED CREATE IMAGE
		DEBUGPRINTFORML FAILED GET IMAGE
		RETURN 0
	ENDIF

	SRC_HEIGHT = GHEIGHT(SPR値)
	SRC_WIDTH = GWIDTH(SPR値)

	SPRITEDISPOSE @"{SPR値}_TARGET"
	IF isSquare
		SPRITECREATE @"{SPR値}_TARGET", SPR値, 0, 0, SRC_WIDTH, SRC_HEIGHT
	ELSE
		LOCAL = SRC_HEIGHT *416 /1216
		SPRITECREATE @"{SPR値}_TARGET", SPR値, SRC_WIDTH/2 -LOCAL, 0, LOCAL*2, SRC_HEIGHT   
	ENDIF


@SHOW_TARGET_IMAGE_BODY(캐릭터_ID,SPR値)
	#DIM DYNAMIC 캐릭터_ID
	#DIM DYNAMIC SPR値
	#DIM DYNAMIC SRC_HEIGHT
	#DIM DYNAMIC SRC_WIDTH
	#DIM DYNAMIC TOTAL
	#DIMS DYNAMIC 캐릭터衣装

	IF !EQUIP:캐릭터_ID:0
		TOTAL = ENUMFILES(@"RESOURCES/UFUFU/{NO:캐릭터_ID}", @"{NO:캐릭터_ID}_NUDE*", 0)
		SIF BASE:캐릭터_ID:만족 < 200
			TOTAL = ENUMFILES(@"RESOURCES/UFUFU/{NO:캐릭터_ID}", @"{NO:캐릭터_ID}_H_NUDE*", 0)
	ELSE
		CALLFORM FASHION_TYPE(캐릭터_ID)
		IF RESULT == 1
			캐릭터衣装 = RESULTS
			SELECTCASE 캐릭터衣装
			CASE "바니"
				TOTAL = ENUMFILES(@"RESOURCES/UFUFU/{NO:캐릭터_ID}", @"{NO:캐릭터_ID}_BUNY*", 0)
			CASE "수영복"
				TOTAL = ENUMFILES(@"RESOURCES/UFUFU/{NO:캐릭터_ID}", @"{NO:캐릭터_ID}_SWIM*", 0)
			CASE "메이드"
				TOTAL = ENUMFILES(@"RESOURCES/UFUFU/{NO:캐릭터_ID}", @"{NO:캐릭터_ID}_MAID*", 0)
			CASE "현대복"
				TOTAL = ENUMFILES(@"RESOURCES/UFUFU/{NO:캐릭터_ID}", @"{NO:캐릭터_ID}_MODN*", 0)
			CASEELSE
				TOTAL = ENUMFILES(@"RESOURCES/UFUFU/{NO:캐릭터_ID}", @"{NO:캐릭터_ID}_BODY*", 0)
			ENDSELECT
			IF TOTAL <= 0
				TOTAL = ENUMFILES(@"RESOURCES/UFUFU/{NO:캐릭터_ID}", @"{NO:캐릭터_ID}_BODY*", 0)
				SIF BASE:캐릭터_ID:만족 < 200
					TOTAL = ENUMFILES(@"RESOURCES/UFUFU/{NO:캐릭터_ID}", @"{NO:캐릭터_ID}_H_BODY*", 0)
			ENDIF
		ELSE
			TOTAL = ENUMFILES(@"RESOURCES/UFUFU/{NO:캐릭터_ID}", @"{NO:캐릭터_ID}_BODY*", 0)
			SIF BASE:캐릭터_ID:만족 < 200
				TOTAL = ENUMFILES(@"RESOURCES/UFUFU/{NO:캐릭터_ID}", @"{NO:캐릭터_ID}_H_BODY*", 0)
		ENDIF
	ENDIF
	IF TOTAL <= 0
		RETURN 0
	ENDIF
	GCREATEFROMFILE SPR値, SUBSTRING(RESULTS:(RAND:TOTAL), 10, -1)    
	SRC_HEIGHT = GHEIGHT(SPR値)
	SRC_WIDTH = GWIDTH(SPR値)
	IF SRC_HEIGHT == 0 || SRC_WIDTH == 0
		; FAILED LOADING
		RETURN 0
	ENDIF
	RETURN 1


@SHOW_TARGET_IMAGE_CARD(캐릭터_ID,SPR値)
	#DIM DYNAMIC 캐릭터_ID
	#DIM DYNAMIC SPR値
	#DIM TOTAL
	#DIM DYNAMIC SRC_HEIGHT
	#DIM DYNAMIC SRC_WIDTH

	TOTAL = (ENUMFILES("RESOURCES/DUEL", @"{NO:캐릭터_ID}_*",0) || ENUMFILES("RESOURCES/DUEL", @"%NAME:캐릭터_ID%_*",0))
	IF !TOTAL
		RETURN 0
	ENDIF
	SPLIT RESULTS, "\\", RESULTS
	GCREATEFROMFILE SPR値, "DUEL/"+RESULTS:(RESULT-1)

	SRC_HEIGHT = GHEIGHT(SPR値)
	SRC_WIDTH = GWIDTH(SPR値)
	IF SRC_HEIGHT == 0 || SRC_WIDTH == 0
		; FAILED LOADING
		RETURN 0
	ENDIF
	RETURN 1


@SHOW_TARGET_IMAGE_FACE(캐릭터_ID,SPR値)
	#DIM DYNAMIC 캐릭터_ID
	#DIM DYNAMIC SPR値
	CALL SHOW_GRAPHICS(캐릭터_ID,SPR値,7)
	RETURN 1