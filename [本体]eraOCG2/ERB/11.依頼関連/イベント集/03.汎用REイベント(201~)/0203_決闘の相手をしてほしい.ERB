@IRAI_EVENT_203(参照先)
#DIMS DYNAMIC 参照先
#DIM DYNAMIC 대전상대

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE  "名前"
		RESULTS = (CP3)듀얼의 상대를 해줬으면 한다。(반복수주 가능)
	CASE "이벤트種"
		RETURN 이벤트_ＲＥ
	CASE "依頼先"
		RETURN 場所_学校内
	CASE "詳細"
		PRINTL 시험을 위해 덱을 조정했으니 상대해 주었으면 하는 것 같다.
		PRINTL 아카데미아로 향하여, 학생을 상대하자!
		PRINTL 승패는 상관없으니 마음껏 겨뤄봅시다!
		PRINTL
		PRINTL 보수: 2000DP

	CASE "条件"
		SIF 依頼_全完了횟수() == 0 && FLAG:게임모드 == 1
			RETURN 0
		RETURN 1
ENDSELECT

RETURN 0

@IRAI_ACT_203(参照)
#DIM DYNAMIC 参照
#DIM DYNAMIC 대전상대



IF TIME_ZONE() >= T_夜時間
	PRINTFORMW 역시 이런 시간이라서 아무도 없는 것 같다……
	PRINTFORMW 내일 좀 이른 시간에 다시 받아보자……
	RETURN 依頼失敗
ENDIF


$INPUTLOOP
대전상대 = RAND:CHARANUM
IF TALENT:대전상대:연령 == -1 && TALENT:대전상대:듀얼리스트 >= 1 && 대전상대 != 0
	TARGET = 대전상대
ELSE
	GOTO INPUTLOOP
ENDIF
PRINTFORMW 약속 장소에서 기다리고 있으니, 의뢰주인 %NAME:TARGET%가 나타났다.
CALL SYOTAIMEN(TARGET)
PRINTFORMW 가볍게 인사를 마친 후, 역시 아카데미아 학생이라고 해야 할까,
PRINTFORMW 디스크를 들고, 듀얼 자세를 취했다.

CALL CHOICE_DUEL_TYPE
;PRINTFORML 리ザルト{RESULT}
IF RESULT == 0
	PRINTFORML 아직 준비가 안 되어 있어서 일단 기다려 달라고 하게 되었습니다
	RETURN 1
ENDIF
IF RESULT == 1
	PRINTFORMW %CALLNAME:TARGET%에게 승리하자, 조금 아쉬워하면서도 %CALLNAME:MASTER%에게 감사하며,
ELSE
	PRINTFORMW %CALLNAME:TARGET%가 승리하자, 만족스러운 미소를 지으며 %CALLNAME:MASTER%에게 감사하며,
ENDIF

PRINTFORMW 의뢰료로 2000DP를 받았습니다.
FLAG:듀얼포인트 += 2000
CALL SOURCE_CALC_환락(대전상대, MASTER, 500)
TIME += 60
RETURN 2

