@IRAI_EVENT_201(参照先)
#DIMS DYNAMIC 参照先
#DIM DYNAMIC 대상캐릭터

VARSET RESULT
VARSET RESULTS

SELECTCASE 参照先
	CASE  "名前"
		;표시할 타이틀
		RESULTS = (CP1)실체화한 몬스터를 쫒아라！(반복수주 가능)
	CASE "이벤트種"
		;이벤트의 종류
		;이벤트_物語：클리어할 때까지 플레이할 수 있는 이벤트, 메인 스토리에 관련된다면 여기
		;이벤트_通常：클리어할 때까지 플레이할 수 있는 이벤트, 메인 스토리에 관련되지 않는다면 여기
		;이벤트_ＲＥ：몇 번이고 도전할 수 있는 이벤트, 완료 상태가 다음날 리셋된다.
		RETURN 이벤트_ＲＥ
	CASE "依頼先"
		;이벤트가 발생하는 장소, 장소명에 관해서는 DIM.ERH를 확인（ERB의 99.기타 내에 있다）
		;RETURN 即時開始으로 받은 후 바로 이벤트를 개시할 수도 있다
		RETURN 場所_繁華街
	CASE "詳細"
		;단순한 설명문, 여기에 필요한 정보를 쓴다：
		PRINTL 인터넷 상에서 실체화된 몬스터의 목격 정보가 잇따르고 있다.
		PRINTL 번화가로 향하여 실체화된 몬스터를 찾아보자!
		PRINTL
		PRINTL
		PRINTL 보수: 확보 몬스터
	CASE "条件"
		;아래의 依頼_完了횟수:○○는, ○○에 들어가는 ID의 의뢰를 보고, 그것이 클리어되지 않은 상태라면 받을 수 없다고 돌려준다
		RETURN 1
ENDSELECT

RETURN 0

@IRAI_ACT_201(参照)
#DIM DYNAMIC 参照
#DIM DYNAMIC 이벤트主

CALL RECOVER_PERMIL(MASTER,-50,"체력")
CALL RECOVER_PERMIL(MASTER,-100,"기력")

PRINTFORMW 눈을 크게 뜨고 주위를 둘러보며 인간으로 변해 있는 몬스터가 없는지 확인했습니다.
PRINT .
WAIT
PRINT .
WAIT
PRINT .
WAIT
PRINTFORML ！！

이벤트主 = RAND:CHARANUM
TARGET = 이벤트主
IF TALENT:이벤트主:듀얼리스트 >= 1
	PRINTFORMW 아니…… 그냥 인간인 것 같다.
	RETURN 1
ELSEIF TALENT:이벤트主:외부자 >= 1
	PRINTFORMW ……아니, 착각이었던 것 같다.
	RETURN 1
ELSEIF CFLAG:이벤트主:소유함 == 1
	PRINTFORML 아무래도 인터넷을 떠들썩하게 했던 것은, 몰래 당신의 컬렉션에서 빠져나와 있었던
	PRINTFORMW %CALLNAME:이벤트主%였던 것 같다, 이제 돌아가자고 말하자 아쉬운 듯한 얼굴로 카드로 돌아갔다.
	
	RETURN 2
ENDIF
PRINTL
PRINTFORMW 찾았다! %CALLNAME:이벤트主%다!
PRINTFORMW 보고 있다는 것을 알아챈 %NAME:이벤트主%는 그 자리에서 도망치려 하지만, 궁지에 몰려…….
PRINTFORMW 단념한 것인지 마지막 발버둥으로 듀얼을 걸어왔다.

CALL CHOICE_DUEL_TYPE

IF RESULT == 1
	TIME += 60
	CALL CARD_GET_CHARA(이벤트主)
PRINTFORMW %CALLNAME:이벤트主%를 쓰러뜨리자, 카드의 모습으로 돌아갔다!
	RETURN 2
ELSE
	PRINTFORML 으악!?
	PRINTFORML …………！！
	PRINTFORML 아무래도 기절한 사이에 도망쳐 버린 것 같다…….
	CALL RECOVER_PERMIL(MASTER,-500,"체력")
	CALL RECOVER_PERMIL(MASTER,-2000,"기력")
	TIME += 180
	RETURN 3
ENDIF
RETURN 1



